  JMP Main ;Skip over TSR Code to Main Program


;==============================================================================
;NOTE: This program is written for and compiled with Eric Isaacson's A386
;      assembly language compiler, using Options +P3 -F (80386+ CPU and
;      no FPU).
;==============================================================================


;In DOCS:

;No way to "Disable" entire program, only individual devices.
;  Must Uninstall and then Reinstall/Reinitialize.
;Digital joystick BIOS recognition call only works if a Joystick is installed
;  and Enabled
;Can't use "-" for Switches, since use it for Ranges in Ignore Switch.
;Can't find good Multi-Joystick Interface.  May need to create one in final
;    release of program, but won't do it unless there is a possiblity of
;    some program (besides JOYKEYS) actually using it.  A "good" API
;    would include multiple joysticks, multiple interface types (standard
;    Game Ports, Parallel Ports, Serial ports, USB, remote/internet control,
;    Keyboard, etc.), multiple input control types (control "groups",
;    self-centering axes, self-closing axes/Throttles, non-return Axes/Sliders,
;    polar/rotating axes, 4-way/8-Way D-Pads/hat switches,
;    buttons/button groups, toggle/selection switches, and anything else that
;    might be used as an put, including things like body gloves, flight
;    helmets, etc.).  The API would also need include Force Feedback (a
;    standard way to send data to the Joystick).  MUCH complication involved
;    here -- to much to handle right now.
;  MULTIJOY handles up to 6 simple joysticks (two axes and two buttons each).
;    Not enough joysticks, buttons, or axes to be really useful.  Doesn't
;    define how to have multiple drivers in memory at same time (e.g., keyboard,
;    prallel, standard, USB).
;  SGDI (from Art of Assembly Language) doesn't handle multiple joysticks.
;    Does allow several Buttons and Axes (Pots), but doesn't have a way
;    to logically "combine" buttons or axes (such as 15-button protocol or
;    4-way/8-way hat switch).  Requires Axes return values to be "normalized",
;    which is a problem on real joysticks without requiring the user to pan
;    the axes.  Doesn't define how to have multiple drivers in memory at
;    same time.  In spite of severe shortcomings, maybe the best choice
;    to start with to extend into an appopriate API.  Don't know of any
;    programs that actually use it, though there might be some.
;  GrIP is proprietary, not clearly defined.  Really designed as an SDK,
;    not an API.  "Natively" allows Protected Mode and Real Mode Interfaces.
;    Does allow multiple joysticks, Defines Axes, Buttons, POV Hats,
;    & "Velocity-sensitive Buttons" (whatever those are).  POV Hats are always
;    4-way???)
;Gas Pedal is 5th Axis, not third, even though it appears third in the
;  coordinate list (BX).
;We tried to "automate" provisioning, but turned out to be harder to
;  understand than just straight mappings.  Some sort of GUI could make
;  this easier, but even that won't make it "easy" to understand with
;  multiple control types and multiple joysticks.  No true standards, too
;  many different kinds of joysticks (even in Windows, requires a custom
;  driver for each joystick with graphics, no such things as a "universal"
;  joystick driver in Windows, except for simple 2-axis, 2-button
;  analog joysticks.  Definitely need to make youself some batch files/
;  environment variables, since defauls will most likely not be what you want!
;Mention Re-Enable somehow


;哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
;EQUATE/STRUCTURE/MACRO DEFINITIONS
;苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘

;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;EQUATES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;==============================================================================
;TSR Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Size of our TSR Stack
  ;----------------------------------------------------------------------------
  StackSize EQU 256

  ;----------------------------------------------------------------------------
  ;Special Interrupt 2Fh (TSR Multiplex) Function Numbers we monitor/use
  ;----------------------------------------------------------------------------
  MuxInstallChk   EQU 0 ;Handle # Install Check
  MuxGetProgName  EQU 1 ;Get Program Name
  MuxGetProgVer   EQU 2 ;Get Program Version
  MuxGetAuthor    EQU 3 ;Get Program Author
  MuxInstallVIO   EQU 4 ;Install I/O Virtualization
  MuxUninstallVIO EQU 5 ;Uninstall I/O Virtualization


;==============================================================================
;Miscellaneous
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Special Codes
  ;----------------------------------------------------------------------------
  No  EQU  0 ;Miscellaneous Yes/No tests
  Yes EQU -1 ;      "        "  "    "


;==============================================================================
;CPU Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Bit masks in the CPU Flags Register
  ;----------------------------------------------------------------------------
  CarryFlag  EQU 0001h ;Carry Flag
  ParityFlag EQU 0004h ;Parity Flag
  AuxFlag    EQU 0010h ;Auxiliary Flag
  ZeroFlag   EQU 0040h ;Zero Flag
  SignFlag   EQU 0080h ;Sign Flag
  TrapFlag   EQU 0100h ;Trap Flag
  IntFlag    EQU 0200h ;Interrupt Flag
  DirFlag    EQU 0400h ;Direction Flag
  OFFlag     EQU 0800h ;Overflow Flag

  ;----------------------------------------------------------------------------
  ;Special CPU OpCodes we need to concern ourselves with
  ;----------------------------------------------------------------------------
  OpCodeIntXX    EQU 0CDh ;Int XX
  OpCodeRETF     EQU 0CBh ;RETF
  OpCodeJMPShort EQU 0EBh ;JMP Short (+128/-127 bits max)


;==============================================================================
;CONsole/Screen Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Special ASCII Characters
  ;----------------------------------------------------------------------------
  Tab       EQU  9 ;Tab
  LF        EQU 10 ;Line Feed
  FormFeed  EQU 12 ;FormFeed
  CR        EQU 13 ;Carriage Return
  EOF       EQU 26 ;End-of-File
  Quote2    EQU 34 ;Double quote
  Quote1    EQU 39 ;Single quote
  BackSlash EQU 92 ;BackSlash (\)

  ;----------------------------------------------------------------------------
  ;Special Keys that can be pressed during Pause routine
  ;----------------------------------------------------------------------------
  CtrlC  EQU  3 ;Control-C Keystroke
  Escape EQU 27 ;Escape Keystroke


;==============================================================================
;DOS Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;;Minimum DOS Version required to use this program
  ;----------------------------------------------------------------------------
  MinDOSVer EQU 0300h

  ;----------------------------------------------------------------------------
  ;DOS Memory Allocation Strategies
  ;----------------------------------------------------------------------------
  StrategyLowFirst     EQU 00h ;First Fit, Low Memory Only
  StrategyLowBest      EQU 01h ;Best Fit, Low Memory Only
  StrategyLowLast      EQU 02h ;Last Fit, Low Memory Only
  ;For DOS 3&4, anything >2 is Last Fit Low Memory
  ;Below are for DOS 5+ Only
  StrategyHighFirst    EQU 40h ;First Fit, High Memory Only
  StrategyHighBest     EQU 41h ;Best Fit, High Memory Only
  StrategyHighLast     EQU 42h ;Last Fit, High Memory Only
  StrategyHighLowFirst EQU 80h ;First Fit, Try High Memory then Low Memory
  StrategyHighLowBest  EQU 81h ;Best Fit, Try High Memory then Low Memory
  StrategyHighLowLast  EQU 82h ;Last Fit, Try High Memory then Low Memory

  ;----------------------------------------------------------------------------
  ;UMB Link States
  ;----------------------------------------------------------------------------
  UMBLinkNo  EQU 00h ;UMB's are not part of DOS Memory Chain
  UMBLinkYes EQU 01h ;UMB's are in DOS Memory Chain (DOS=HIGH,UMB in CONFIG.SYS)

  ;----------------------------------------------------------------------------
  ;Sizes of various DOS data structures
  ;----------------------------------------------------------------------------
  FCBSize     EQU  44 ;Size of DOS File Control Block
  CmdTailMax  EQU 128 ;Maximum size of a DOS Command Tail
  MaxPathSize EQU  64 ;Maximum Size of a DOS Path/FileName String

  ;----------------------------------------------------------------------------
  ;Special "hard-coded" DOS Device Handles.
  ;These Handles are permanently hard-coded in DOS, and are always open.
  ;There is no need to issue Find, Open, or Close calls to these
  ;  devices - you can just use them at will.
  ;----------------------------------------------------------------------------
  StdInHandle  EQU 0 ;Standard Input Device Handle
  StdOutHandle EQU 1 ;Standard Output Device Handle
  StdErrHandle EQU 2 ;Standard Error Device Handle
  StdAuxHandle EQU 3 ;Standard Auxiliary Device Handle (COM1 by default)
  StdPrnHandle EQU 4 ;Standard Printer Device Handle (LPT1 by default)

  ;----------------------------------------------------------------------------
  ;Codes returned by INT 21h, Function 4400h (Get Device Information)
  ;----------------------------------------------------------------------------
  ;If bit 7 is Set (is a Device):
  DvcInfoIsStdIn    EQU 0001h ;STDIN
  DvcInfoIsStdOut   EQU 0002h ;STDOUT
  DvcInfoIsNUL      EQU 0004h ;NUL
  DvcInfoIsClock    EQU 0008h ;CLOCK$
  DvcInfoInt29h     EQU 0010h ;Uses Int 29h (Special Device)
  DvcInfoBinary     EQU 0020h ;Binary (Raw) Mode
  DvcInfoEOF        EQU 0040h ;EOF on Input
  DvcInfoIsDevice   EQU 0080h ;Is a Device (not a File)
  DvcInfoKEYB       EQU 0100h ;Unknown (set by DOS 6.2x KEYB program)
  DvcInfoOpenClose  EQU 0800h ;Supports Open/Close calls
  DvcInfoOutputBusy EQU 2000h ;Supports Output until Busy
  DvcInfoIOCTL      EQU 4000h ;Can process IOCTL requests (INT 21h, Func 4402h)

  ;If bit 7 is Clear (is a File):
  DvcInfoDriveMask    EQU 003Fh ;Drive number (0 = A:)
  DvcInfoNotWritten   EQU 0040h ;File has not been written (Dirty Buffer?)
  ;Bit 7 = Clear!
  DvcInfoInt24h       EQU 0100h ;Generate Int 24h on Errors (DOS 4 only)
  DvcInfoNotRemovable EQU 0800h ;Media Not Removable
  DvcInfoNoDateTime   EQU 4000h ;Don't set File Date/Time on Closing (DOS 3.0+)
  DvcInfoRemote       EQU 8000h ;File is Remote (DOS 3.0+)


;==============================================================================
;Joystick BIOS Functions Related (Int 15h)
;==============================================================================

  ;----------------------------------------------------------------------------
  ;INT 15h Subfunction (in AH) for Joystick Calls
  ;----------------------------------------------------------------------------
  Int15JoyStickCall EQU 84h

  ;----------------------------------------------------------------------------
  ;INT 15h Error Codes (returned in AH)
  ;----------------------------------------------------------------------------
  I15ErrInvalidCmd EQU 80h ;Invalid Command
  I15ErrFuncNoSupt EQU 86h ;Function not Supported

  ;----------------------------------------------------------------------------
  ;Maximum number of Control Entities we can have on a Joystick
  ;A "Control Entity" is a contiguous series of Buttons or Sliders or Axes
  ;  in the USB Report.  For example, there may be two or three "groups"
  ;  of Buttons in different places in the Report.
  ;There are so many different kinds of controls that can be on a joystick
  ;  and so many different formats that the data can come in, that we need
  ;  a highly-flexible way to handle it all.
  ;----------------------------------------------------------------------------
  MaxControls EQU 16

  ;----------------------------------------------------------------------------
  ;Maximum Control Index Number in Translation Table
  ;----------------------------------------------------------------------------
  MaxXlatIndex EQU 99

  ;----------------------------------------------------------------------------
  ;Temporary Bit-masks needed while Cooking ThrustMaster Coordinates
  ;Cooking ThrustMaster coordinates is complicated, since we are converting
  ;  8 directions into 4, and some of the coordinates can be bad (0).
  ;----------------------------------------------------------------------------
  XInvalid EQU 01h ;X Direction = 0
  XLeft    EQU 04h ;X Direction = Left
  XRight   EQU 08h ;X Direction = Right
  YInvalid EQU 10h ;Y Direction = 0
  YUp      EQU 40h ;Y Direction = Up
  YDown    EQU 80h ;Y Direction = Down


;==============================================================================
;Game Port Related
;==============================================================================

  MinPort EQU 0200h ;Minimum Game Port I/O Address
  MaxPort EQU 020Fh ;Maximum Game Port I/O Address


;==============================================================================
;EMM386/386MAX I/O Virtualization Related (INT 2Fh, Function 4A15h)
;EMM386 allows up to 6 Handlers, but supposedly (haven't tested this yet)
;  can only uninstall 5.  The 6th never uninstalls correctly.
;==============================================================================

  ;----------------------------------------------------------------------------
  ;AX Register Value on Call
  ;----------------------------------------------------------------------------
  VirtIOFuncAX   EQU 4A15h ;Value in AX for I/O Virtualization call

  ;----------------------------------------------------------------------------
  ;BX Register Values on Call
  ;----------------------------------------------------------------------------
  VirtIOBXInst   EQU 0000h ;Value in BX for Install Function
  VirtIOBXUninst EQU 0001h ;Value in BX for Uninstall Function
  VirtIOBXCapab  EQU 0003h ;Value in BX for Get Capabilities (386MAX Only)

  ;----------------------------------------------------------------------------
  ;ECX Register Values on Protected Mode Call
  ;----------------------------------------------------------------------------
  PMCallCXOut     EQU 0000_0004h ;Call is OUT DX,Ax (not IN Ax,DX)
  PMCallCXSize    EQU 0000_0003h ;Size of Call (386MAX Only)
    PMCallCXDWord EQU 0000_0002h ;DWord (IN EAX,DX or OUT DX,EAX)
    PMCallCXWord  EQU 0000_0001h ;DWord (IN AX,DX or OUT DX,AX)
    PMCallCXByte  EQU 0000_0000h ;DWord (IN AL,DX or OUT DX,AL)

  ;----------------------------------------------------------------------------
  ;Minimum I/O Port Number allowed
  ;----------------------------------------------------------------------------
  MinVIOPortNum EQU 100h ;386MAX allows any Port?

  ;----------------------------------------------------------------------------
  ;Error Codes returned (386MAX Only)
  ;----------------------------------------------------------------------------
    VIOErrBadFunc   EQU 8000h ;Invalid Function
    VIOErrEDXHiLo   EQU 8001h ;EDX.HI < EDX.LO
    VIOErrCSNeqDS   EQU 8002h ;CS <> DS
    VIOErrCXIs0     EQU 8003h ;CX = 0
    VIOErrNoHandles EQU 8004h ;Too many I/O Handlers already installed
                              ;  6 Max in EMM386, 32 in 386MAX
    VIOErrPortMin   EQU 8005h ;Port Number < Minimum (100h)
    VIOErrPortLow   EQU 8006h ;Dispatch Table I/O Port < EDX.LO
    VIOErrPortHi    EQU 8007h ;Dispatch Table I/O Port > EDX.HI
    VIOErrPortInUse EQU 8008h ;I/O Port already Virtualized
    VIOErrPortUsed  EQU 8009h ;I/O Port used by Memory Manager
    VIOErrBadHandle EQU 800Ah ;Invalid Handle Number
    VIOErrHandleRmv EQU 800Bh ;Handle already removed
    VIOErrPortRmv   EQU 800Ch ;I/O Port already removed


;==============================================================================
;USB Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Maximum number of Devices we can control
  ;----------------------------------------------------------------------------
  MaxDevices EQU 8

  ;----------------------------------------------------------------------------
  ;Maximum number of entries in Bad (incompatible) Device Table
  ;----------------------------------------------------------------------------
  MaxBadDevices EQU 12

  ;----------------------------------------------------------------------------
  ;Maximum number of Raw bytes we will allow to be returned by USB Joystick
  ;----------------------------------------------------------------------------
  MaxJDataSize EQU 32

  ;----------------------------------------------------------------------------
  ;General
  ;----------------------------------------------------------------------------
  Int14RequestSize EQU 64 ;Size of Int 14h USB Request Data Structure

  ;----------------------------------------------------------------------------
  ;Maximum Descriptor Sizes
  ;NOTE: MaxDescriptorSize must be big enough to include all Config,
  ;        Interface, End-Point, and any Class-specific Descriptors
  ;        that may be included with any multiple-interface Device.
  ;      Audio and Video Interface Devices, for example, generally
  ;        include an HID Interface to further "describe" the Device.
  ;        Since we don't know ahead of time whether the HID Interface
  ;        is a Joystick/GamePad or not (and it usually isn't with
  ;        and Audio or Video Interface Device), we have to download
  ;        the entire set of Descriptors and decipher them before
  ;        we can tell whether we should control it or not.
  ;      The Descriptor sizes for Audio and Video Interface devices
  ;        are usually VERY long, much longer than "normal" devices.
  ;        The standard "Get Configuration" Request downloads all of
  ;        the Class-specific Descriptors in addition to the general
  ;        Descriptors, and Audio and Video Interface Devices can have
  ;        a LOT of Class-Specific Descriptors.
  ;      I wish the designers of the Audio and Video Interface Specs
  ;        would have done what the HID designers did, and didn't
  ;        include ALL of the Class-specific data along with the general
  ;        Device data.
  ;----------------------------------------------------------------------------
  MaxDescriptorSize EQU 800 ;Max size of Device/Config/HID Descriptor
  MaxReportSize     EQU 600 ;Max size of Report Descriptor

  ;----------------------------------------------------------------------------
  ;USB Descriptor Types
  ;----------------------------------------------------------------------------
  DescrTypeDevice    EQU 01h ;Device
  DescrTypeConfig    EQU 02h ;Configuration
  DescrTypeString    EQU 03h ;String
  DescrTypeInterface EQU 04h ;Interface
  DescrTypeEndPoint  EQU 05h ;EndPoint
  DescrTypeDvcQual   EQU 06h ;Device Qualifier
  DescrTypeOtherSpd  EQU 07h ;Other Speed Configuration
  DescrTypeIntfPower EQU 08h ;Interface Power
  DescrTypeOnTheGo   EQU 09h ;On-the-Go
  DescrTypeDebug     EQU 0Ah ;Debug
  DescrTypeIntfAssoc EQU 0Bh ;Interface Association
  ;Types 12 - 17 (0Ch - 11h) are added for Wireless USB
  DescrTypeWSecurity EQU 0Ch ;Wireless USB Security
  DescrTypeWKey      EQU 0Dh ;Wireless USB Key
  DescrTypeWEncrType EQU 0Eh ;Wireless USB Encryption Type
  DescrTypeWBOS      EQU 0Fh ;Wireless USB Binary-device Object Store
  DescrTypeWDvcCap   EQU 10h ;Wireless USB Device Capability
  DescrTypeWEndPtCmp EQU 11h ;Wireless USB Endpoint Companion

  ;HID Class-Specific Descriptor Types
  DescrTypeHID         EQU 21h ;Human Interface Device
  DescrTypeHIDReport   EQU 22h ;Report (from HID)
  DescrTypeHIDPhysical EQU 23h ;Physical Descriptor (from HID)

  ;----------------------------------------------------------------------------
  ;Values in SRRequest for HID Devices
  ;----------------------------------------------------------------------------
  HIDRQGetReport   EQU  1 ;Get Report
  HIDRQGetIdle     EQU  2 ;Get Idle Factor
  HIDRQGetProtocol EQU  3 ;Get Protocol Type
  HIDRQSetReport   EQU  9 ;Set Report
  HIDRQSetIdle     EQU 10 ;Set Idle Factor

  ;----------------------------------------------------------------------------
  ;Data for GetReport/SetReport
  ;----------------------------------------------------------------------------
  HIDReportTypeNone    EQU 0 ;None
  HIDReportTypeInput   EQU 1 ;Input
  HIDReportTypeOutput  EQU 2 ;Output
  HIDReportTypeFeature EQU 3 ;Feature

  ;----------------------------------------------------------------------------
  ;USB-Defined Device and Interface Classes, SubClasses, and Protocols
  ;----------------------------------------------------------------------------
  DvcClassNone     EQU 0 ;Device Class = None
  DvcSubClassNone  EQU 0 ;Device SubClass = None
  DvcProtocolNone  EQU 0 ;Device Protocol = None
  IntfClassHID     EQU 3 ;Interface Class = HID (Human Interface Device)
  IntfSubClassNone EQU 0 ;Interface SubClass = None
  IntfProtocolNone EQU 0 ;Interface Protocol = None

  ;----------------------------------------------------------------------------
  ;Possible Error Codes to return to calling program on completion of TD
  ;----------------------------------------------------------------------------
  TDStsOK             EQU 0000h ;ACK Received (TD completed OK / no errors)
  TDStsNAKReceived    EQU 0001h ;NAK Received
  TDStsStalled        EQU 0002h ;TD is Stalled
  TDStsTimeout        EQU 0004h ;TD has timed out (Bulk/Control)
  TDStsOverDue        EQU 0008h ;TD is OverDue (Int/Isoch)
  TDStsShortPacket    EQU 0010h ;TD Short Packet Detected
  TDStsBabbleDetected EQU 0020h ;Babble Detected
  TDStsCRCTOReceived  EQU 0040h ;CRC/TimeOut Error Received
  TDStsBitStuffError  EQU 0080h ;Rx Data contained > 6 ones in a row
  TDStsDataBuffErr    EQU 0100h ;Data Buffer Error
  TDStsControlSetup   EQU 1000h ;Error actually occurred during Control Setup
  TDStsDvcRemoved     EQU 2000h ;Device removed while TD was in progress
  TDStsLargeCallErr   EQU 8000h ;Error during Large Call

  ;----------------------------------------------------------------------------
  ;Statuses sent by Host to USB Device Owners
  ;----------------------------------------------------------------------------
  ;Device Connect/Disconnect
  OwnerCallNewDvc   EQU 0001h ;A new Matching Device has been attached
  OwnerCallDvcDisc  EQU 0002h ;Registered Device has been disconnected
  OwnerCallDvc0Disc EQU 0003h ;Matching Unregistered Device Disconnected
  OwnerCallDontLook EQU 0007h ;Stop Looking for New Devices for ~5 seconds
  ;Device Enable/Disable/Reset/Suspend/Resume/Power
  OwnerCallDvcEnable   EQU 0008h ;Device Enabled
  OwnerCallDvcDisable  EQU 0009h ;Device Disabled
  OwnerCallDvcReset    EQU 000Ah ;Device Resetting
  OwnerCallDvcSuspend  EQU 000Bh ;Device Suspended
  OwnerCallDvcResume   EQU 000Ch ;Device Resumed
  OwnerCallDvcPwrOn    EQU 000Dh ;Device Power On
  OwnerCallDvcPwrOff   EQU 000Eh ;Device Power Off
  OwnerCallDvcPwrReset EQU 000Fh ;Device Power Reset
  ;Host Connect/Disconnect/Error
  OwnerCallNewHost     EQU 0011h ;A new Host Driver has been Installed
  OwnerCallHostDisc    EQU 0012h ;An existing Host Driver has been Uninstalled
  OwnerCallHostHWErr   EQU 0017h ;Host System/Processor Error (Host will Reset)
  ;Host Run/Stop/Reset/Suspend/Resume
  OwnerCallHostRun     EQU 0018h ;Host Running
  OwnerCallHostStop    EQU 0019h ;Host Stopped
  OwnerCallHostReset   EQU 001Ah ;Host Reset
  OwnerCallHostSuspend EQU 001Bh ;Host Suspended
  OwnerCallHostResume  EQU 001Ch ;Host Resumed
  OwnerCallHostDebug   EQU 001Dh ;Host in Debug Mode
  OwnerCallHostSST     EQU 001Eh ;Host Single-Stepped
  ;Timing Changes
  OwnerCallTimingChg   EQU 0021h ;Timing Change on Host
  ;Upstream Device Changes
  OwnerCallDvcDiscUS     EQU (OwnerCallDvcDisc OR 0080h)     ;Dvc Disc Upstream
  OwnerCallDvcEnableUS   EQU (OwnerCallDvcEnable OR 0080h)   ;Dvc Enable Upstream
  OwnerCallDvcDisableUS  EQU (OwnerCallDvcDisable OR 0080h)  ;Dvc Disable Upstream
  OwnerCallDvcResetUS    EQU (OwnerCallDvcReset OR 0080h)    ;Dvc Reset Upstream
  OwnerCallDvcSuspendUS  EQU (OwnerCallDvcSuspend OR 0080h)  ;Dvc Suspend Upstream
  OwnerCallDvcResumeUS   EQU (OwnerCallDvcResume OR 0080h)   ;Dvc Resume Upstream
  OwnerCallDvcPwrOnUS    EQU (OwnerCallDvcPwrOn OR 0080h)    ;Dvc PwrOn Upstream
  OwnerCallDvcPwrOffUS   EQU (OwnerCallDvcPwrOff OR 0080h)   ;Dvc PwrOff Upstream
  OwnerCallDvcPwrResetUS EQU (OwnerCallDvcPwrReset OR 0080h) ;Dvc PwrReset Upstream


;==============================================================================
;USB Report Descriptor Related
;  HID Reports Start with a 1-byte Tag, followed by 0 or more bytes of Data
;  Bits 0-1 are actual size (0-3 bytes of data following the Tag)
;  Bits 2-3 are Tag Type: 0h = Main, 4h = Global, 8h = Local, Ch = Reserved
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Miscellaneous
  ;----------------------------------------------------------------------------
  RptTagLongData EQU 00FCh ;Long Data Item (more than 3 bytes)
                           ;Lower 2 bits are always 10h (Size of 2)
                           ;Next byte is Actual Data Size (0-255)
                           ;Next Byte is Tag (Description)
                           ;Followed by up to 255 bytes of "real" data

  ;----------------------------------------------------------------------------
  ;Main
  ;----------------------------------------------------------------------------
  RptTagInput              EQU   80h ;Input
  RPtTagOutput             EQU   90h ;Output
  RptTagFeature            EQU  0B0h ;Feature
    ;These apply to Input, Output, and Feature
    RptMainIOFConstant     EQU   01h ;Constant
     RptMainIOFData        EQU   00h ; Data
    RptMainIOFVariable     EQU   02h ;Variable
     RptMainIOFArray       EQU   00h ; Array
    RptMainIOFRelative     EQU   04h ;Relative
     RptMainIOFAbsolute    EQU   00h ; Absolute
    RptMainIOFWrap         EQU   08h ;Wrap
     RptMainIOFNoWrap      EQU   00h ; No Wrap
    RptMainIOFNonLinear    EQU   10h ;Non-Linear
     RptMainIOFLinear      EQU   00h ; Linear
    RptMainIOFNoPrefer     EQU   20h ;No Preferred State
     RptMainIOFPrefer      EQU   00h ; Preferred State
    RptMainIOFNull         EQU   40h ;Null State
     RptMainIOFNoNull      EQU   00h ; No Null State
    RptMainIOFVolatile     EQU   80h ;Volatile
     RptMainIOFNonVolatile EQU   00h ; Non-Volatile
    RptMainIOFBuffered     EQU  100h ;Buffered Bytes
     RptMainIOFBitField    EQU  000h ; Bit-Field

  RptTagCollection         EQU  0A0h ;Collection
    RptMainColPhysical     EQU   00h ;Physical (Group of Axes)
    RptMainColApplication  EQU   01h ;Application (Mouse/Keyboard)
    RptMainColLogical      EQU   02h ;Logical (interrelated Data)

  RptTagEndCollection      EQU  0C0h ;End Collection

  ;----------------------------------------------------------------------------
  ;Global
  ;----------------------------------------------------------------------------
  RptTagUsagePage          EQU   04h ;Usage Page
    RptGlbUPGenericDeskTop EQU   01h ;Generic Desktop
      RptGDPointer         EQU   01h ;Pointing Device
      RptGDMouse           EQU   02h ;Mouse
      ;03h = Reserved
      RptGDJoystick        EQU   04h ;Joystick
      RptGDGamePad         EQU   05h ;GamePad
      RptGDKeyboard        EQU   06h ;Keyboard
      RptGDKeyPad          EQU   07h ;KeyPad
      RptGDMultiAxisCtl    EQU   08h ;Multi-Axis Controller
       ;09h - 2Fh = Reserved
      RptGDXAxis           EQU   30h ;X-axis
      RptGDYAxis           EQU   31h ;Y-axis
      RptGDZAxis           EQU   32h ;Z-axis
      RptGDRotateX         EQU   33h ;Rotation around X-axis
      RptGDRotateY         EQU   34h ;Rotation around Y-axis
      RptGDRotateZ         EQU   35h ;Rotation around Z-axis
      RptGDSlider          EQU   36h ;Slider
      RptGDDial            EQU   37h ;Dial
      RptGDWheel           EQU   38h ;Wheel
      RptGDHatSwitch       EQU   39h ;Hat-Switch
      RptGDCountedBuff     EQU   3Ah ;Counted Buffer
      RptGDByteCount       EQU   3Bh ;Byte Count
      RptGDMotionWakeup    EQU   3Ch ;Wakeup on Motion
      RptGDStart           EQU   3Dh ;Start
      RptGDSelect          EQU   3Eh ;Select
      ;3Fh Reserved
      RptGDVectorX         EQU   40h ;X-Axis Vector
      RptGDVectorY         EQU   41h ;Y-Axis Vector
      RptGDVectorZ         EQU   42h ;Z-Axis Vector
      RptGDVectorBRX       EQU   43h ;X-Axis Vector, Relative to the Body
      RptGDVectorBRY       EQU   44h ;Y-Axis Vector, Relative to the Body
      RptGDVectorBRZ       EQU   45h ;Z-Axis Vector, Relative to the Body
      RptGDVectorNoD       EQU   46h ;Non-Directional Vector (e.g., Tempurature)
      ;47h = ??
      RptGDResMult         EQU   48h ;Resolution Multiplier
      ;49h-7Fh = Reserved
      RptGDSysCtl          EQU   80h ;System Control
      RptGDSysPowerDown    EQU   81h ;System Power Down
      RptGDSysSleep        EQU   82h ;System Sleep
      RptGDSysWakeUp       EQU   83h ;System Wake Up
      RptGDSysMenuContext  EQU   84h ;System Menu Context
      RptGDSysMenuMain     EQU   85h ;System Menu Main
      RptGDSysMenuApp      EQU   86h ;System Menu Application
      RptGDSysMenuHelp     EQU   87h ;System Menu Help
      RptGDSysMenuExit     EQU   88h ;System Menu Exit
      RptGDSysMenuSelect   EQU   89h ;System Menu Select
      RptGDSysMenuRight    EQU   8Ah ;System Menu Right
      RptGDSysMenuLeft     EQU   8Bh ;System Menu Left
      RptGDSysMenuUp       EQU   8Ch ;System Menu Up
      RptGDSysMenuDown     EQU   8Dh ;System Menu Down
      RptGDSysColdStart    EQU   8Eh ;System Cold Restart
      RptGDSysWarmStart    EQU   8Fh ;System Warm Restart
      RptGDDPadUp          EQU   90h ;D-Pad Up
      RptGDDPadDown        EQU   91h ;D-Pad Down
      RptGDDPadRight       EQU   92h ;D-Pad Right
      RptGDDPadLeft        EQU   93h ;D-Pad Left
      ;94h - 9Fh = Reserved
      RptGDSysDock         EQU  0A0h ;System Dock
      RptGDSysUndock       EQU  0A1h ;System Undock
      RptGDSysSetup        EQU  0A2h ;System Setup
      RptGDSysBreak        EQU  0A3h ;System Break
      RptGDSysDbgBrk       EQU  0A4h ;System Debugger Break
      RptGDSysAppBrk       EQU  0A5h ;Application Break
      RptGDSysAppDbgBrk    EQU  0A6h ;Application Debugger Break
      RptGDSysMute         EQU  0A7h ;System Speaker Mute
      RptGDSysHibernate    EQU  0A8h ;System Hibernate
      ;A9h - AFh = Reserved
      RptGDSysDispInvt     EQU  0B0h ;System Display Invert
      RptGDSysDispInt      EQU  0B1h ;System Display Internal
      RptGDSysDispExt      EQU  0B2h ;System Display External
      RptGDSysDispBoth     EQU  0B3h ;System Display Both
      RptGDSysDispDual     EQU  0B4h ;System Display Dual
      RptGDSysDispTgl      EQU  0B5h ;System Display Toggle Internal/External
      RptGDSysDispSwap     EQU  0B6h ;System Display Swap Primary/Secondary
      RptGDSysDispAuto     EQU  0B7h ;System Display LCD Autoscale
      ;B8h - FFFFh = Reserved

    RptGlbUPSimulationCtl  EQU   02h ;Simulation Controls
      RptSimFlightSim      EQU   01h ;Flight Simulation Device
      RptSimAutoSim        EQU   02h ;Automobile Simulation Device
      RptSimTankSim        EQU   03h ;Tank Simulation Device
      RptSimSpaceShipSim   EQU   04h ;Spaceship Simulation Device
      RptSimSubmarineSim   EQU   05h ;Submarine Simulation Device
      RptSimSailingSim     EQU   06h ;Sailing Simulation Device
      RptSimMotorcycleSim  EQU   07h ;Motorcycle Simulation Device
      RptSimSportsSim      EQU   08h ;Sports Simulation Device
      RptSimAirplaneSim    EQU   09h ;Airplane Simulation Device
      RptSimHelicopterSim  EQU   0Ah ;Helicopter Simulation Device
      RptSimMagicCarpetSim EQU   0Bh ;Magic Carpet Simulation Device
      RptSimBicycleSim     EQU   0Ch ;Bicycle Simulation Device
      ;0Dh - 1Fh = Reserved
      RptSimFlightCtlStick EQU   20h ;Flight Control Stick
      RptSimFlightStick    EQU   21h ;Flight Stick
      RptSimCyclicCtl      EQU   22h ;Cyclic Control
      RptSimCyclicTrim     EQU   23h ;Cyclic Trim
      RptSimFlightYoke     EQU   24h ;Flight Yoke
      RptSimTrackCtl       EQU   25h ;Track Control
      ;26h - AFh = Reserved
      RptSimAileron        EQU  0B0h ;Aileron
      RptSimAileronTrim    EQU  0B1h ;Aileron Trim
      RptSimAntiTorqueCtl  EQU  0B2h ;Anti-Torque Control
      RptSimAutoPilot      EQU  0B3h ;Autopilot Enable
      RptSimChaffRelease   EQU  0B4h ;Chaff Release
      RptSimCollectiveCtl  EQU  0B5h ;Collective Control
      RptSimDiveBrake      EQU  0B6h ;Dive Brake
      RptSimElecCtrMsrs    EQU  0B7h ;Electronic Countermeasures
      RptSimElevator       EQU  0B8h ;Elevator
      RptSimElevatorTrim   EQU  0B9h ;Elevator Trim
      RptSimRudder         EQU  0BAh ;Rudder
      RptSimThrottle       EQU  0BBh ;Throttle
      RptSimFlightComm     EQU  0BCh ;Flight Communications
      RptSimFlareRelease   EQU  0BDh ;Flare Release
      RptSimLandingGear    EQU  0BEh ;Landing Gear
      RptSimToeBrake       EQU  0BFh ;Toe Brake
      RptSimTrigger        EQU  0C0h ;Trigger
      RptSimWeaponsArm     EQU  0C1h ;Weapons Arm
      RptSimWeaponsSelect  EQU  0C2h ;Weapons Select
      RptSimWingFlaps      EQU  0C3h ;Wing Flaps
      RptSimAccelerator    EQU  0C4h ;Accelerator
      RptSimBrake          EQU  0C5h ;Brake
      RptSimClutch         EQU  0C6h ;Clutch
      RptSimShifter        EQU  0C7h ;Shifter
      RptSimSteering       EQU  0C8h ;Steering
      RptSimTurretDir      EQU  0C9h ;Turret Direction
      RptSimBarrelElev     EQU  0CAh ;Barrel Elevation
      RptSimDivePlane      EQU  0CBh ;Dive Plane
      RptSimBallast        EQU  0CCh ;Ballast
      RptSimBicycleCrank   EQU  0CDh ;Bicycle Crank
      RptSimHandlebars     EQU  0CEh ;Handle Bars
      RptSimFrontBrake     EQU  0CFh ;Front Brake
      RptSimRearBrake      EQU  0D0h ;Rear Brake
      ;D1h - FFFFh = Reserved

    RptGlbUPVRCtl          EQU   03h ;Virtual Reality? Controls
    RptGlbUPSportCtl       EQU   04h ;Sport Controls
    RptGlbUPGameCtl        EQU   05h ;Game Controls
      RptGame3DCtl         EQU   01h ;3D game Controller
      RptGamePinball       EQU   02h ;Pinball Device
      RptGameGun           EQU   03h ;Gun Device
      ;04h - 1Fh = Reserved
      RptGamePOV           EQU   20h ;Point-of-View
      RptGameTurn          EQU   21h ;Turn Right/Left
      RptGamePitch         EQU   22h ;Pitch Right/Left
      RptGameRoll          EQU   23h ;Roll Forward/Backward
      RptGameMoveX         EQU   24h ;Move Right/Left
      RptGameMoveY         EQU   25h ;Move Forward/Backward
      RptGameMoveZ         EQU   26h ;Move Up/Down
      RptGameLeanX         EQU   27h ;Lean Right/Left
      RptGameLeanY         EQU   28h ;Lean Forward/Backward
      RptGamePOVHeight     EQU   29h ;Height of Point-of-View
      RptGameFlipper       EQU   2Ah ;Flipper
      RptGameFlipper2      EQU   2Bh ;Secondary Flipper
      RptGameBump          EQU   2Ch ;Bump
      RptGameNewgame       EQU   2Dh ;New Game
      RptGameShootball     EQU   2Eh ;Shoot ball
      RptGamePlayer        EQU   2Fh ;Player
      RptGameGunBolt       EQU   30h ;Gun Bolt
      RptGameGunClip       EQU   31h ;Gun Clip
      RptGameGunSelector   EQU   32h ;Gun Selector
      RptGameGunSingleShot EQU   33h ;Gun Single Shot
      RptGameGunBurst      EQU   34h ;Gun Burst Shot
      RptGameGunAutomatic  EQU   35h ;Gun Automatic Shot
      RptGameGunSafety     EQU   36h ;Gun Safety
      RptGamePadFireJump   EQU   37h ;GamePad Fire/Jump
      ;38h = ???
      RptGamePadTrigger    EQU   39h ;GamePad Trigger
      ;3Ah - FFFFh = Reserved

    ;06h = ??
    RptGlbUPKeyboard       EQU   07h ;Keyboard/Keypad (Usage Type = Selector)
    RptGlbUPLED            EQU   08h ;LEDs (Light Emitting Diodes)
    RptGlbUPButton         EQU   09h ;Buttons
    RptGlbUPOrdinal        EQU   0Ah ;Ordinal
    RptGlbUPConsumerDvc    EQU   0Ch ;Consumer Devices
    RptGlbUPPID            EQU   0Fh ;Physical Interface Device (Force Feedback)

  RptTagLogicalMin         EQU   14h ;Logical Minumum
  RptTagLogicalMax         EQU   24h ;Logical Maximum
  RptTagPhysicalMin        EQU   34h ;Physical Minimum
  RptTagPhysicalMax        EQU   44h ;Physical Maximum
  RptTagUnitExponent       EQU   54h ;Unit Exponent (base 10)
  RptTagUnit               EQU   64h ;Units

  RptTagReportSize         EQU   74h ;Size of Report Field in (bits)
  RptTagReportID           EQU   84h ;Report ID
  RptTagReportCount        EQU   94h ;Report Count
  RptTagPush               EQU  0A4h ;Push Global State Table onto the Stack
  RptTagPop                EQU  0B4h ;Pop Global State Table from the Stack

  ;----------------------------------------------------------------------------
  ;Local
  ;----------------------------------------------------------------------------
  RptTagUsage              EQU   08h ;Usage
  RptTagUsageMin           EQU   18h ;Usage Minimum
  RptTagUsageMax           EQU   28h ;Usage Maximum
  RptTagDesignatorIndex    EQU   38h ;Designator Index (Body Part)
  RptTagDesignatorMin      EQU   48h ;Designator Minimum
  RptTagDesignatorMax      EQU   58h ;Designator Maximum
  RptTagStringIndex        EQU   78h ;String Index
  RptTagStringMin          EQU   88h ;String Minimum
  RptTagStringMax          EQU   98h ;String Maximum
  RptTagDelimiter          EQU  0A8h ;1 = Open Set, 2 = Close Set


;==============================================================================
;USBJSTIK Related (Program-specific)
;==============================================================================

  MinCoord  EQU   1 ;Value returned when coordinate is Up/Left
  DefCenter EQU  50 ;Default Value returned when coordinate is Centered
  MinCenter EQU  10 ;Minimum Center Coordinate allowed
  MaxCenter EQU 200 ;Maximum Center Coordinate allowed


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;STRUCTURES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;==============================================================================
;TSR Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;This is a Structure to use at the beginning of Interrupt Routines in
  ;  TSR's.  Using this header, along with the appropriate code,
  ;  will allow the installation and removal of TSR's (actually,
  ;  the redirected interrupt vectors of a TSR) in any order.
  ;Note that the real Interrupt code MUST IMMEDIATELY follow this Header!!
  ;----------------------------------------------------------------------------
  IntHdr STRUC
    HdwreRst  DB      0CBh ;Hardware Reset Routine (CBh = RETF)
    CodeJmp   DW    010EBh ;Jump to real Interrupt Handler (JMP ENDS)
    OldVector DD        ?  ;Old interrupt Vector
    Signature DW    0424Bh ;Signature for this type of Interrupt routine
    EOIFlag   DB        0  ;=80h if we issue End-of-Interrupt, else 0
    HdwreJmp  DW    0F4EBh ;Jump to Hardware Reset Routine (JMP HdwreRst)
    FutureUse DB 7 DUP (0) ;Reserved for future use
   ENDS
  IntOfst EQU OFFSET CodeJmp ;Offset from beginning of our Structure


;==============================================================================
;DOS Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Program Segment Prefix
  ;----------------------------------------------------------------------------
  PSPStruc      STRUC
    PSPDOSExit     DW       20CDh ;Int 20h Call
    PSPNextSegment DW          ?  ;Segment of First Byte of Memory beyond Program
    PSPCPMCall5    DB   6 DUP (?) ;CPM Far Call 5
    PSPInt22Addr   DD          ?  ;Stored Int 22h Handler
    PSPInt23Addr   DD          ?  ;Stored Int 23h (Ctrl-Break) Handler
    PSPInt24Addr   DD          ?  ;Stored Int 23h (Critical Error) Handler
    PSPParentPSP   DW          ?  ;Segment of Parent PSP
    PSPJFT         DB  20 DUP (?) ;Job File Table (20 File Handles)
    PSPEnvirSeg    DW          ?  ;Environment Segment for PSP
    PSPSSSPOnEntry DD          ?  ;Processes SS:SP on Entry to last Int 21h Call
    PSPJFTSize     DW          ?  ;Number of entries in JFT (default = 20)
    PSPJFTPointer  DD          ?  ;Pointer to JFT (Default = PSP:0018h)
    PSPPreviousPSP DD          ?  ;Pointer to previous PSP (Default = FFFF:FFFF)
    PSPFiller1     DB  18 DUP (?) ;Misc stuff used by SHARE, Windows, etc.
                                  ;Memory Below here available to TSR!
    PSPFiller2     DB   2 DUP (?) ;Unused in DOS <= 6.00
    PSPInt21       DW       21CDh ;Int 21h Call
    PSPRETF        DB        0CBh ;RETF Call
    PSPFiller3     DB   9 DUP (?) ;Can be used to make Extended FCB from FCB1
    PSPFCB1        DB  16 DUP (?) ;First File Control Block
    PSPFCB2        DB  16 DUP (?) ;Second File Control Block
    PSPFiller4     DB   4 DUP (?) ;Unused
    PSPCmdLineLen  DB          ?  ;Command Line Length & DTA
    PSPCmdLine     DB 127 DUP (?) ;Command Line String
   ENDS
  PSPStrucSize EQU (TYPE PSPStruc) ;Should be 256 bytes

  ;----------------------------------------------------------------------------
  ;Memory Control Block
  ;----------------------------------------------------------------------------
  MCBStruc STRUC
    MCBID              DB        ?  ;Memory Control Block ID
      MCBIDValid      EQU       'M' ;Memory Block is Valid
      MCBIDLastInList EQU       'Z' ;Memory Block is Last in List
    MCBOwnerID         DW        ?  ;PSP Segment of Owner
                                    ;   0 = Owns Self
                                    ;   6 = DR-DOS XMS UMB
                                    ;   7 = DR-DOS Excluded Upper Memory
                                    ;   8 = Belongs to DOS
                                    ;FFFx = 386MAX
    MCBSize            DW        ?  ;Size in Paragraphs
    MCBFiller          DB 3 DUP (?) ;Filler
    MCBOwnerName       DB 8 DUP (?) ;Owner Name
   ENDS
  MCBStrucSize EQU (TYPE MCBStruc) ;Should be 16 bytes

  ;----------------------------------------------------------------------------
  ;Structure needed to Initialize DOS EXEC Function
  ;----------------------------------------------------------------------------
  ParamBlockStruc STRUC
    PBEnvirSegment   DW ? ;Environment Segment to use (0 = Copy Callers Segment)
    PBCmdTailPtr     DD ? ;Pointer to Command Tail
    PBFCB1Offset     DD ? ;Pointer to File Control Block #1
    PBFCB2Offset     DD ? ;Pointer to File Control Block #2
    PBSSSPOnReturn   DD ? ;Holds Childs Initial SS:SP on Return (if AL = 01)
    PBCSIPOnReturn   DD ? ;Holds Entry Point (CS:IP) on Return (if AL = 01)
   ENDS
  ParamBlockSize EQU (TYPE ParamBlockStruc) ;Size of EXEC Parameter Block

  ;----------------------------------------------------------------------------
  ;DTA (Disk Transfer Area) after Find First Matching File (INT 21h, Func 4Eh)
  ;The first several bytes of the DTA are DOS version specific, and
  ;  we don't care about that data anyway.  We only care about the data
  ;  that is documented and the same across all versions of DOS.
  ;----------------------------------------------------------------------------
  FindFirstStruc   STRUC            ;Find First Matching File
    FFVerSpecific     DB 21 DUP (0) ;DOS Version-specific Data
    FFAttribs         DB         ?  ;File Attributes
      FAttrReadOnly  EQU        01h ;Read-Only
      FAttrHidden    EQU        02h ;Hidden
      FAttrSystem    EQU        04h ;System
      FAttrVolume    EQU        08h ;Volume Label
      ;Bit 3 = Execute-only (Novell Netware)
      FAttrDirectory EQU        10h ;Directory
      FAttrArchive   EQU        20h ;Archive
      FAttrUnused    EQU        40h ;Unused
                                    ;DOS 3+ returns this bit set if file
                                    ;  is actually a character device
                                    ;DOS Window underneath Windows XP doesn't
      ;Bit 7 = Delete Pending (OpenDOS, NovellDOS)
      ;      = Shareable (Novell Netware)
   FFTime            DW         ?  ;File Creation Time
    FFDate            DW         ?  ;File Creation Date
    FFSize            DD         ?  ;File Size
    FFName            DB 13 DUP (?) ;ASCIIZ File Name & Extension
   ENDS

   FAttrToUse EQU FAttrHidden+FAttrSystem
     ;Attributes to use in DOS's Find First Matching File Function
     ;DOS ignores the Read-Only and and Archive bits, so they don't matter


;==============================================================================
;USB/Joystick-Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Structure to Translate Raw USB Data into individual Entities
  ;----------------------------------------------------------------------------
  ControlStruc          STRUC
    ControlType            DB  ?  ;Type of Control
      CTNone              EQU 00h ;Empty Entry
      CTButton            EQU 01h ;Button
      ;Need 2-Way and/or 4-way Hat-Switch?
      CTDPad              EQU 02h ;D-Pad or 8-Way Hat Switch
      CTSlider            EQU 03h ;Slider (Loose Axis)
      CTThrottle          EQU 04h ;Throttle (Self-Closing Axis)
      CTAxis              EQU 05h ;Standard Axis (Self-Centering Axis)
      ;Need 4-way Hat-Switch?

    ControlQty             DB  ?  ;Number of Controls in a row (no spaces!)
    ControlOffset          DB  ?  ;Offset (in Bits) to beginning of data
    ControlNumBits         DB  ?  ;Number of bits of Data per Control
    ControlMin             DW  ?  ;Minimum Control value
    ControlMax             DW  ?  ;Maximum Control value
   ENDS
  ControlStrucSize EQU (TYPE ControlStruc)

  ;----------------------------------------------------------------------------
  ;Structure to store data for each USB Joystick
  ;----------------------------------------------------------------------------
  DeviceInfoStruc  STRUC ;Structure to Store Device Data
    DIFlags           DB  ?  ;Misc Yes/No Flags
      DIFlagInUse    EQU 01h ;This entry currently being used?
    DIHostIndex       DB  ?  ;Host Index
    DIDvcAddress      DB  ?  ;Device Address
    DIEndPoint        DB  ?  ;End Point Number
    DIInterfaceNum    DB  ?  ;Interface Number
    DIFiller          DB  ?  ;To align at even addresses
    DIControlsData    DB (ControlStrucSize*MaxControls) DUP (0)
                             ;Controls Translation Data
    DIReportID        DB  ?  ;Report ID (if needed)
    DIReportIDData    DB  ?  ;Report ID from Data (if provided)
                             ;  MUST IMMEDIATELY precede DIRawData!!!
;    DIRawData         DB  8 DUP (0) ;Raw Device Data
    DIRawData         DB MaxJDataSize DUP (0) ;Raw Device Data
    DIRegHandle       DW  ?  ;Handle Number of Owner Registry
    DIIntHandle       DW  ?  ;Handle number of Periodic Interrupt
   ENDS
  DeviceInfoStrucSize EQU (TYPE DeviceInfoStruc) ;Size of Structure

  ;----------------------------------------------------------------------------
  ;Structure to keep track of Translations between USB Controls and
  ;  "standard" Joystick Controls
  ;We'll need one of these Structures for each Analog Joystick Value
  ;  (AX, AY, BX, BY, G, B1-B15)
  ;We originally tried to "automate" things more by trying ignore/keep
  ;  "skipping" controls, but it turned out to be more confusing and
  ;  less flexible, especially for "complicated" joysticks.  Can't think of
  ;  a good way to provide mapping facilities, short of a GUI.  A GUI itself
  ;  would be EXTREMELY complicated, since we may or may not be dealing with
  ;  multiple joysticks, with different nubmers and types of controls.
  ;If we ever get into Force Feedback, we're really talking about complicated!
  ;----------------------------------------------------------------------------
  XlatStruc   STRUC
    XlatJIndex   DB ? ;Joystick Index Number (0-7)
    XlatCType    DB ? ;Control Type (Axis, Button, etc., 255 = N/A)
    XlatCIndex   DB ? ;Control Index (0-100, 255 = N/A)
    XlatCIndex2  DB ? ;Control Index #2 (-1 if N/A)
                      ;  If simulating Axis with two buttons, is second
                      ;    (Right/Down) Button Index
      XlatDPadX EQU 0 ;If simulating Axis with D-Pad Axis, 0 = D-Pad X Axis
      XlatDPadY EQU 1 ;If simulating Axis with D-Pad Axis, 1 = D-Pad X Axis
   ENDS
  XlatStrucSize EQU (TYPE XlatStruc)

  ;----------------------------------------------------------------------------
  ;This structure keeps track of Bad (incompatible) Devices
  ;----------------------------------------------------------------------------
  BadDeviceStruc    STRUC
    BDFlags            DB  ?  ;Misc Yes/No Flags
      BDFlagInUse     EQU 01h ;This entry currently being used?
    BDInterfaceNum     DB  ?  ;Interface Number of Bad Device
    BDHostIndex        DB  ?  ;Host Index of Bad Device
    BDDvcAddress       DB  ?  ;Device Address of Bad Device
    BDFiller           DB  ?  ;To align data at even addresses
    BDNewDeviceStage   DB  ?  ;Bad Device Stage
    BDNewDeviceError   DW  ?  ;Bad Device Error Code
   ENDS
  BadDeviceStrucSize EQU (TYPE BadDeviceStruc)

  ;----------------------------------------------------------------------------
  ;This is the format for a request sent to Int 14h to Send/Receive Data
  ;It contains the type of request, etc.
  ;----------------------------------------------------------------------------
  Int14RequestStruc         STRUC      ;Structure for an Int 14 Request
    I14RRequestType            DB   ?  ;Type of Request
      I14RRTHostClass         EQU  00h ;Host/System/OS Class
        I14RRTGetHostSWInfo   EQU  01h ;Get Host Software Info
        I14RRTGetHostHWInfo   EQU  02h ;Get Host Hardware Info
        I14RRTGetHostVendInfo EQU  03h ;Get Host Vendor Info
        I14RRTGetHostStatus   EQU  04h ;Get Current Host Status
        I14RRTHostRun         EQU  08h ;Start/Run/Resume Host
        I14RRTHostStop        EQU  09h ;Stop Host
        I14RRTHostReset       EQU  0Ah ;Reset Host
        I14RRTHostSuspend     EQU  0Bh ;Global Suspend Host
        I14RRTHostResume      EQU  0Ch ;Force Global Resume on Host
      I14RRTTimingClass       EQU  10h ;Frame Timing Class
        I14RRTRegTmgOwner     EQU  11h ;Register as Timing Owner
        I14RRTUnRegTmgOwner   EQU  12h ;UnRegister as Timing Owner
        I14RRTIncTiming       EQU  13h ;Increment (Slow Down) Frame Timing
        I14RRTDecTiming       EQU  14h ;Decrement (Speed Up) Frame Timing
        I14RRTChangeTiming    EQU  15h ;Change Frame Timing (by Large Amount)
      I14RRTHubClass          EQU  20h ;Hub Class
        I14RRTGetDvcHubInfo   EQU  21h ;Get Hub Info for Device
        I14RRTNewDvcConn      EQU  24h ;Hub has Detected new Device
        I14RRTDvcDisc         EQU  25h ;Device has been Disconnected
        I14RRTSendHubChar     EQU  27h ;Send Hub Characteristics to Host
        I14RRTEnableHubPort   EQU  28h ;Enable Device given Hub & Port
        I14RRTDisableHubPort  EQU  29h ;Disable Device given Hub & Port
        I14RRTResetHubPort    EQU  2Ah ;Reset Device given Hub & Port
        I14RRTSuspendHubPort  EQU  2Bh ;Suspend Device given Hub & Port
        I14RRTResumeHubPort   EQU  2Ch ;Resume Device given Hub & Port
        I14RRTPwrOnHubPort    EQU  2Dh ;Power On Device given Hub & Port
        I14RRTPwrOffHubPort   EQU  2Eh ;Power Off Device given Hub & Port
        I14RRTPwrResetHubPort EQU  2Fh ;Power Reset Device given Hub & Port
      I14RRTTPowerClass       EQU  30h ;Power Class
        I14RRTGetDvcPowerInfo EQU  31h ;Get Power Info for Device
        I14RRTGetHubPowerDraw EQU  32h ;Get Power Draw for Hub
        I14RRTPwrOnDevice     EQU  3Dh ;Power On Device given Dvc Addr
        I14RRTPwrOffDevice    EQU  3Eh ;Power Off Device given Dvc Addr
        I14RRTPwrResetDevice  EQU  3Fh ;Power Reset Device given Dvc Addr
      I14RRTDeviceClass       EQU  40h ;Device Class
        I14RRTGetDvcClassInfo EQU  41h ;Get Device Class Info
        I14RRTGetDvcVendInfo  EQU  42h ;Get Device Vendor Info
        I14RRTGetDvcStatus    EQU  43h ;Get Device Status
        I14RRTEnableDevice    EQU  48h ;Enable/Resume Device given Dvc Addr
        I14RRTDisableDevice   EQU  49h ;Disable Device given Dvc Addr
        I14RRTResetDevice     EQU  4Ah ;Reset Device given Dvc Addr
        I14RRTSuspendDevice   EQU  4Bh ;Suspend Device given Dvc Addr
        I14RRTResumeDevice    EQU  4Ch ;Resume Device given Dvc Addr
      I14RRTConfigClass       EQU  50h ;Configuration Class
        I14RRTConfigingIntf   EQU  51h ;Driver is Configuring an Interface
        I14RRTConfigIntfDone  EQU  52h ;Interface Configuration is Complete
        I14RRTSetNewConfig    EQU  58h ;Set/Change Device Config Value
      I14RRTInterfaceClass    EQU  60h ;Interface Class
        I14RRTFindRegIntf     EQU  62h ;Look for Registered Interface
        I14RRTFindUnRegIntf   EQU  63h ;Look for Unregistered Interface
        I14RRTRegIntfOwner    EQU  64h ;Register as Interface Owner
        I14RRTUnRegIntfOwner  EQU  65h ;Unregister as Interface Owner
        I14RRTIntfDontLook    EQU  68h ;Existing Interface Owner Don't Look
      I14RRTAltIntfClass      EQU  70h ;Alternate Interface Class
        I14RRTGetAltIntfInfo  EQU  71h ;Get Alternate Interface Info
      I14RRTEndPointClass     EQU  80h ;End Point Class
        I14RRTGetDataX        EQU  81h ;Get Current DataX Value
        I14RRTIncDataX        EQU  88h ;Increment/Toggle DataX Value
      I14RRTPacketClass       EQU  90h ;Packet Class
        I14RRTDoIsoch         EQU  94h ;Schedule Isochronous Transaction
        I14RRTDoInterruptPer  EQU  95h ;Schedule Periodic Interrupt
        I14RRTDoControl       EQU  96h ;Schedule Control/Setup Request
        I14RRTDoBulk          EQU  97h ;Schedule Bulk Transaction
        I14RRTDoInterrupt1T   EQU  98h ;Schedule One-Time Interrupt
        I14RRTCloseHandle     EQU  9Ch ;Close/Remove Scheduled Transaction
        I14RRTChangeIntPer    EQU  9Dh ;Change Periodicity of Interrupt
        I14RRTGetTransStatus  EQU  9Fh ;Get Status of Packet Transaction
      I14RRTMiscClass         EQU 0A0h ;Miscellaneous Class
        I14RRTLargeCallDone   EQU 0A1h ;Large (Complicated) Call Complete
        I14RRTBeepSpeaker     EQU 0AFh ;Beep the Speaker
      I14RRTInterHostClass    EQU 0E0h ;Inter-Host Communication Class
      I14RRTInternalClass     EQU 0F0h ;Host Internal/Troubleshooting Class
    I14RFlags                  DB   ?  ;Bit-level flags
      I14RFlagIn              EQU  01h ;In Direction
      I14RFlagLowSpeed        EQU  02h ;Low-Speed Device
      I14RFlagHiSpeed         EQU  04h ;High-Speed Device
      I14RFlagNoRetries       EQU  10h ;No Auto Retries for Control
      I14RFlagShortPktOK      EQU  20h ;No Retries for Short Packets
      I14RFlagSpecificFrame   EQU  40h ;Use Specific Frame Number
      I14RFlagAddrIsPhys      EQU  80h ;Data Address is Physical
    I14RHostIndex              DB   ?  ;Host Index
    I14RDeviceAddress          DB   ?  ;USB Device Address
      I14RHubAddress          EQU (OFFSET I14RDeviceAddress)
    I14REndPoint               DB   ?  ;EndPoint or Port or Alt Interface
      I14RHubPort             EQU (OFFSET I14REndPoint)
      I14RAltInterface        EQU (OFFSET I14REndPoint)
    I14RConfigValue            DB   ?  ;Configuration Value
      I14RCloseID             EQU (OFFSET I14RConfigValue)
      I14RDataX               EQU (OFFSET I14RConfigValue)
    I14RInterfaceNum           DB   ?  ;Interface Number
    I14RSearchIndex            DB   ?  ;Search Index
    I14RVendorID               DW   ?  ;Vendor ID
    I14RProductID              DW   ?  ;Product ID
    I14RDvcClass               DB   ?  ;Device Class
    I14RDvcSubClass            DB   ?  ;Device SubClass
    I14RDvcProtocol            DB   ?  ;Device Protocol
    I14RIntfClass              DB   ?  ;Interface Class
    I14RIntfSubClass           DB   ?  ;Interface SubClass
    I14RIntfProtocol           DB   ?  ;Interface Protocol
    I14RRequestHandle          DW   ?  ;Request Handle Number
    I14RPeriodicity            DW   ?  ;Interrupt Periodicity/Duration
      I14RBeepFrequency       EQU (OFFSET I14RPeriodicity)
    I14RTimeout                DW   ?  ;Transaction Time Out Value
    I14RDataAddress            DD   ?  ;Data Address
    I14RDataSize               DW   ?  ;Size of Data (Bytes)
    I14RCallBackAddr           DD   ?  ;Call Back Address (CS:IP format)
      I14RLargeCallRtnCode    EQU (OFFSET I14RCallBackAddr)
    I14RUserPktID              DW   ?  ;User Packet ID
    I14RSetupReqData           DQ   ?  ;Setup Request Data (8 bytes)
    I14RFrameTiming            DW   ?  ;Frame Timing Value (def = 12000)
      I14RFrameIndex          EQU (OFFSET I14RFrameTiming)
                                       ;Starting Frame of Isoch Schedule
    I14RIsochSchedAddr         DD   ?  ;Address of Isoch Schedule
    I14RFiller  DB (Int14RequestSize-$) DUP (0)

   ENDS

  ;----------------------------------------------------------------------------
  ;Device Request is a structure containing data required by a USB Setup Packet
  ;----------------------------------------------------------------------------
  SetupRequest           STRUC     ;Structure for a Standard Setup Pkt
    SRRequestType           DB  ?  ;Request Type
      SRRTOut              EQU 00h ;0=Out (Host to Device)
      SRRTIn               EQU 80h ;1=In (Device to Host)
      SRRTTypeMask         EQU 60h ;Mask for Type
        SRRTTypeStandard   EQU 00h ;Type 0 = Standard
        SRRTTypeClass      EQU 20h ;Type 1 = Class
        SRRTTypeVendor     EQU 40h ;Type 2 = Vendor
                                   ;Type 3 = Reserved
      SRRTRecipientMask    EQU 1Fh ;Mask for Recipient Type
        SRRTRecipDevice    EQU 00h ;Recipient Type 0 = Device
        SRRTRecipInterface EQU 01h ;Recipient Type 1 = Interface
        SRRTRecipEndPoint  EQU 02h ;Recipient Type 2 = EndPoint
        SRRTRecipOther     EQU 03h ;Recipient Type 3 = Other
                                   ;Types 4-31 = Reserved
    SRRequest               DB  ?  ;Specific Request
      SRRQGetStatus        EQU  0  ;Request  0 = Get Status
      SRRQClearFeature     EQU  1  ;Request  1 = Clear Feature
      SRRQGetState         EQU  2  ;Request  2 = Get State
      SRRQSetFeature       EQU  3  ;Request  3 = Set Feature
                                   ;Request  4 = Reserved
      SRRQSetAddress       EQU  5  ;Request  5 = Set Address
      SRRQGetDescriptor    EQU  6  ;Request  6 = Get Descriptor
      SRRQSetDescriptor    EQU  7  ;Request  7 = Set Descriptor
      SRRQGetConfig        EQU  8  ;Request  8 = Get Configuration
      SRRQSetConfig        EQU  9  ;Request  9 = Set Configuration
      SRRQGetAltInterface  EQU 10  ;Request 10 = Get Alternate Interface
      SRRQSetAltInterface  EQU 11  ;Request 11 = Set Alternate Interface
      SRRQSynchFrame       EQU 12  ;Request 12 = Synch Frame
    SRValue                 DW  ?  ;Value depends on Request Type
    SRIndex                 DW  ?  ;Index depends on Request Type
      ;If EndPoint,  Bits 3:0 = EndPoint Number
      ;              Bit    7 = 1 if IN EndPoint, 0 if OUT EndPoint
      ;              All other bits are 0
      ;If Interface, Bits 7:0 = Interface Number (low word)
      ;              All other bits are 0 (high word)
    SRLength                DW  ?  ;Length of Data (if there is Data)

   ENDS
  SetupRequestSize EQU TYPE (SetupRequest)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get Device Descriptor
  ;----------------------------------------------------------------------------
  DeviceDescriptor   STRUC    ;Structure for a Device Descriptor
    DDLength            DB  ? ;Size of Descriptor in bytes
    DDDescrType         DB DescrTypeDevice
    DDUSBRelease        DW  ? ;USB Compliance  bcd (0210h=v2.10)
    DDDvcClass          DB  ? ;Device Class (defined by USB-IF)
    DDDvcSubClass       DB  ? ;Device SubClass (defined by USB-IF)
    DDDvcProtocol       DB  ? ;Device Protocol (defined by USB-IF)
    DDMaxPktSize        DB  ? ;Max Packet Size for EndPoint 0
                              ;  MUST be 8, 16, 32, or 64
    DDVendorID          DW  ? ;Vendor ID (defined by USB-IF)
    DDProductID         DW  ? ;Product ID (defined by manufacturer)
    DDDeviceRelease     DW  ? ;Device Release Number (bcd)
    DDManufacturerIndx  DB  ? ;Index of Manufacturer String
    DDProductIndx       DB  ? ;Index of Product String
    DDSerialNumberIndx  DB  ? ;Index of Serial Number String
    DDNumConfigs        DB  ? ;Number of possible Configurations
   ENDS
  DeviceDescriptorSize EQU TYPE (DeviceDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get Configuration Descriptor
  ;----------------------------------------------------------------------------
  ConfigDescriptor     STRUC     ;Structure for a Config Descriptor
    CDLength              DB  ?  ;Size of Descriptor in bytes
    CDDescrType           DB DescrTypeConfig
    CDTotalLength         DW  ?  ;Total length of data returned for
                                 ;  this configuration (ALL data)
    CDNumInterfaces       DB  ?  ;Number of Interfaces in this Config
    CDConfigValue         DB  ?  ;Value to use in SetRequest to
                                 ;  set this configuration
    CDConfigurationIndx   DB  ?  ;Index of Configuration String
    CDAttributes          DB  ?  ;Configuration Attributes
      CDAttrBit7         EQU 80h ;Bit 7 must be set to 1
      CDAttrSelfPowered  EQU 40h ;Self Powered
      CDAttrRemoteWakeup EQU 20h ;Remote Wakeup Supported
      CDAttrBatteryPower EQU 10h ;Battery Powered
      ;Bits 3:0 = Reserved (0)
    CDMaxBusPower         DB  ?  ;Maximum Power Consumption FROM BUS
                                 ;  in 2mA units (*2 to get mA)
   ENDS
  ConfigDescriptorSize EQU TYPE (ConfigDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get Interface Descriptor
  ;----------------------------------------------------------------------------
  InterfaceDescriptor   STRUC     ;Structure for an Interface Descr
    IDLength               DB  ?  ;Size of Descriptor in bytes
    IDDescrType            DB DescrTypeInterface
    IDInterfaceNumber      DB  ?  ;Interface Number (0-based Index)
    IDAlternateSetting     DB  ?  ;Value used to select this alternate
                                  ;  setting for the Interface Number
    IDNumEndPoints         DB  ?  ;Number of Interfaces, excl 0
    IDInterfaceClass       DB  ?  ;Interface Class (defined by USB-IF)
    IDInterfaceSubClass    DB  ?  ;Interface SubClass (def by USB-IF)
    IDInterfaceProtocol    DB  ?  ;Interface Protocol (def by USB-IF)
    IDInterfaceIndx        DB  ?  ;Index of Interface String
   ENDS
  InterfaceDescriptorSize EQU TYPE (InterfaceDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get EndPoint Descriptor
  ;----------------------------------------------------------------------------
  EndPointDescriptor    STRUC     ;Structure for an EndPoint Descr
    EDLength               DB  ?  ;Size of Descriptor in bytes
    EDDescrType            DB DescrTypeEndPoint
    EDEndPointNumber       DB  ?  ;EndPoint Number
      EDDirectionIn       EQU 80h ;Direction = In (Ignored in Control EP's)
      ;Bits 6:4 = Reserved (0)
      EDNumberMask        EQU 0Fh ;EndPoint Number (0-15)
    EDAttributes           DB  ?  ;EndPoint attributes
      ;Bits 7:6 = Reserved (0)
      EDUsageTypeMask     EQU 30h ;Bits 5:4 = Usage Type
      EDUsageTypeSft      EQU  4  ;Bits to Shift by
        EDAttrImplicitFB  EQU 20h ;2 = Implicit Feedback Data EndPoint
        EDAttrFeedback    EQU 10h ;1 = Feedback EndPoint
        EDAttrData        EQU 00h ;0 = Data EndPoint
      EDSynchTypeMask     EQU 0Ch ;Bits 3:2 = Synchronization Type
      EDSynchTypeSft      EQU  2  ;Bits to Shift By
        EDAttrSynch       EQU 0Ch ;3 = Synchronous
        EDAttrAdaptive    EQU 08h ;2 = Adaptive
        EDAttrAsynch      EQU 04h ;1 = Asynchronous
      EDAttrTransTypeMask EQU 03h ;Bits 1:0 = Transfer Type
        EDAttrInterrupt   EQU 03h ;3 = Interrupt
        EDAttrBulk        EQU 02h ;2 = Bulk
        EDAttrIsoch       EQU 01h ;1 = Isochronous
        EDAttrControl     EQU 00h ;0 = Control
    EDMaxPktSize           DW  ?  ;Max Packet Size EndPoint can handle
    EDPollInterval         DB  ?  ;Polling Interval for Data Transfers
    ;The following two bytes only appear in some Isochronous Applications
    EDRefreshRate          DB  ?  ;Refresh Rate (1-9, power of 2 mS)
    EDSynchEndPoint        DB  ?  ;Synchronization EndPoint
   ENDS
  EndPointDescriptorSize EQU TYPE (EndPointDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by an HID (Human Interface Device) Descriptor
  ;----------------------------------------------------------------------------
  HIDDescriptor       STRUC     ;Structure for an HID Descriptor
    HIDDLength           DB ?   ;Size of Descriptor in bytes
    HIDDDescrType        DB DescrTypeHID
    HIDDClassRelease     DW ?   ;HID Class Release Spec (BCD)
    HIDDCountryCode      DB ?   ;Country Code
                                ;0 = None/NA, 21h = US
    HIDDNumClasses       DB ?   ;Number of Class Descriptors
    HIDDDescriptorType   DB ?   ;Type of Class Descriptor
                                ;DescrTypeHIDReport (22h) or
                                ;DescrTypeHIDPhysical (23h)
    HIDDDescriptorLength DW ?   ;Length of Class Descriptor
     ;Repeat Type and Length for each Class Descriptor
   ENDS
  HIDDescriptorSize EQU 9
   ;HIDDescriptorSize EQU TYPE (HIDDescriptor)


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;MACROS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

  SwapDSES MACRO ;Exchange DS & ES with each other
    PUSH DS
    PUSH ES
    POP  DS
    POP  ES
   #EM

  JUMP MACRO
    JMP SHORT #1
   #EM


;哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
;TSR DATA
;苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘

;==============================================================================
;TSR Related
;==============================================================================

  ;------------------------------------------------------------------------------
  ;INT2F Data (this needs to be the first data at the top of the file)
  ;If the user does a "TYPE USBJSTIK.COM" at the command line, this will
  ;  just show the program name and version, instead of sending all kinds of
  ;  annoying happy faces and BELs to the screen the way most programs do.
  ;This is also the same data that is used to test if the program is
  ;  already installed in memory.
  ;------------------------------------------------------------------------------
  NoType1:     DB               CR  ;To handle TYPE from command line
  ProgName:    DB        'USBJSTIK' ;Program Name
  ProgNameLen EQU       $-ProgName  ;Length of name
  ProgVerTxt:  DB           ' 0.11' ;Program version in ASCII
  Author:      DB '  Bret Johnson' ;Authors name
  AuthorLen   EQU         $-Author  ;Length of Author
  NoType2:     DB              EOF  ;To handle TYPE from command line
  ProgVer      DW             0011h ;Program version as a word
  HandleNum    DB              0C0h ;TSR multiplex handle number

  ;------------------------------------------------------------------------------
  ;Miscellaneous Yes/No TSR Flags
  ;------------------------------------------------------------------------------
  TSRFlags         DB 00h ;Miscellaneous Yes/No Flags #1
    Int14ReqInUse EQU 01h ;Int 14h Structure currently in use?
    NoBeep        EQU 02h ;Don't Beep the Speaker
    DisableSts    EQU 04h ;Disabled while doing Status
    Buttons15     EQU 08h ;Use 15-button Protocol
                          ;  Only applies if Joysticks1 = True
    ThrustMaster  EQU 10h ;Use Thrustmaster protocol for HatSwitch
                          ;  Only applies if Joysticks1 = True
    NoBeep1Time   EQU 20h ;No Beep this One Time

  TSRFlags2      DB 00h ;Miscellaneous Yes/No Flags #2
    Is80386     EQU 01h ;CPU is an 80386+
    IsWindowsNT EQU 02h ;Running underneath Windows NT
    SameTick    EQU 04h ;IN's are in same Timer Tick as original OUT
                        ;The algorithm we use here assumes that interrupts
                        ;  are disabled while the calling program is attempting
                        ;     to read the joystick data, so that an INT 08 (Timer
                        ;  Tick) cannot issued in the middle of it.  There's
                        ;  no way a program can get an accurate reading if it
                        ;  doesn't disable interrupts, so this should be a
                        ;  safe assumption.


;==============================================================================
;Stack Related
;==============================================================================
  StackInUse DB 0 ;Counter to keep track of "setup stack" calls
  OldSS      DW ? ;Original Stack Segment
  OldSP      DW ? ;Original Stack Pointer
  TempBX     DW ? ;Temporary Storage for BX while setting up Stack
  TempFlags  DW ? ;Temporary Storage for Flags while setting up Stack


;==============================================================================
;Joystick Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Maximum Value to Use while Normalizing
  ;----------------------------------------------------------------------------
  NormalizeMax DW (DefCenter*2)

  ;----------------------------------------------------------------------------
  ;Percentage of Movement (from Center) to be considered Moved
  ;  Only applies when simulating ThrustMaster Hat Switch with real Axes
  ;----------------------------------------------------------------------------
  MinPercent  EQU 20 ;Minimum allowed
  MaxPercent  EQU 80 ;Maximum allowed
  MovePercent  DW 50 ;Default

  ;----------------------------------------------------------------------------
  ;Button Mask to use for INT 15h or I/O Virtualization
  ;----------------------------------------------------------------------------
  BtnMask DB ? ;Buttons are in high nibble (1 = not pressed, 0 = pressed)
               ;  Low nibble is always 0 in here!

  ;----------------------------------------------------------------------------
  ;Temporary Coordinate Variable storage while processing Int 15h
  ;----------------------------------------------------------------------------
  AXValue DW ? ;Coordinate Value to return in AX
  BXValue DW ? ;Coordinate Value to return in BX
  CXValue DW ? ;Coordinate Value to return in CX
  DXValue DW ? ;Coordinate Value to return in DX
  GXValue DW ? ;Temporary Gas Pedal value


;==============================================================================
;Related to "Translation" between USB Controls & Analog Joystick Controls
;==============================================================================

  NumXlats EQU 20 ;Number of Translation entries

  AXXlat:  DB (XlatStrucSize) DUP (-1) ;AX Translation Structure
  AYXlat:  DB (XlatStrucSize) DUP (-1) ;AY Translation Structure
  BXXlat:  DB (XlatStrucSize) DUP (-1) ;BX Translation Structure
  BYXlat:  DB (XlatStrucSize) DUP (-1) ;BY Translation Structure
  AZXlat:  DB (XlatStrucSize) DUP (-1) ;Gas Pedal Translation Structure
  A01Xlat: DB (XlatStrucSize) DUP (-1) ;Button 01 Translation Structure
  A02Xlat: DB (XlatStrucSize) DUP (-1) ;Button 02 Translation Structure
  A03Xlat: DB (XlatStrucSize) DUP (-1) ;Button 03 Translation Structure
  A04Xlat: DB (XlatStrucSize) DUP (-1) ;Button 04 Translation Structure
  A05Xlat: DB (XlatStrucSize) DUP (-1) ;Button 05 Translation Structure
  A06Xlat: DB (XlatStrucSize) DUP (-1) ;Button 06 Translation Structure
  A07Xlat: DB (XlatStrucSize) DUP (-1) ;Button 07 Translation Structure
  A08Xlat: DB (XlatStrucSize) DUP (-1) ;Button 08 Translation Structure
  A09Xlat: DB (XlatStrucSize) DUP (-1) ;Button 09 Translation Structure
  A10Xlat: DB (XlatStrucSize) DUP (-1) ;Button 10 Translation Structure
  A11Xlat: DB (XlatStrucSize) DUP (-1) ;Button 11 Translation Structure
  A12Xlat: DB (XlatStrucSize) DUP (-1) ;Button 12 Translation Structure
  A13Xlat: DB (XlatStrucSize) DUP (-1) ;Button 13 Translation Structure
  A14Xlat: DB (XlatStrucSize) DUP (-1) ;Button 14 Translation Structure
  A15Xlat: DB (XlatStrucSize) DUP (-1) ;Button 15 Translation Structure


;==============================================================================
;EMM386/386MAX I/O Virtualization Related (INT 2Fh, Function 4A15h)
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Handle Number for our Virtual I/O Handler
  ;----------------------------------------------------------------------------
  VirtIOHandle DW -1 ;-1 indicates Virtual I/O not installed

  ;----------------------------------------------------------------------------
  ;I/O Virtualization Dispatch Table
  ;The table contains one entry for each port we actually need to virtualize
  ;The table can have "holes" in it -- it does not need to include an entry
  ;  for every port in the EDXPortRange, just the ones that need virtualization.
  ;This table must remain in memory the whole time I/O is virtualized.  The
  ;  Memory Manager doe snot copy the table anywhere else, but refers to it
  ;  each time an I/i port is accessed.
  ;----------------------------------------------------------------------------
  VirtIOTblSize EQU 1 ;Number of entries in table (each entry is 2 words)
  VirtIOTable:
    DW 0201h        ;Port Number
       DW PMCode201 ;Offset of PM Code to call when Port is Accessed

  ;----------------------------------------------------------------------------
  ;Programmable Interval Timer Related
  ;----------------------------------------------------------------------------
  StartTimer  DW ? ;PIT value when OUT DX,AL is issued
  VirtIOTicks DB ? ;Need to make sure we don't span multiple timer ticks
                   ;  with our PIT calculations


;==============================================================================
;USB Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Handle Number of New Device (Device 0) Registry
  ;----------------------------------------------------------------------------
  Reg0Handle DW ?

  ;----------------------------------------------------------------------------
  ;Int08 Countdown Timers
  ;----------------------------------------------------------------------------
  Int08Counter    DB 5 ;Countdown Timer to periodically check for new Devices
                       ;Initial Delay is 5 timer ticks (about 1/4 second)
                       ;Delays after initial one will be about 1 second
                       ;Setting to 0 Disables the program
  DontLookCounter DW 0 ;Countdown Timer to not Look for New Devices

  ;----------------------------------------------------------------------------
  ;Calls that the USB Host Driver can send to us after we're "registered"
  ;Our code is called with this function code in AX
  ;----------------------------------------------------------------------------
  OwnerCallFunctionTbl:
   ;Device Connect/Disconnect
    DB OwnerCallNewDvc        ;New Device Connected
     DW DeviceAdded
    DB OwnerCallDvcDisc       ;Existing Device Disconnected
     DW DeviceDisconnected
    DB OwnerCallDvc0Disc      ;Unregistered Device Disconnected
     DW Device0Disconnected
    DB OwnerCallDontLook      ;Don't Look for New Interface Temporarily
     DW StartDontLook
   ;Device Enable/Disable/Reset/Suspend/Resume/PwrOn/PwrOff/PwrReset
    DB OwnerCallDvcEnable     ;Device Enabled
     DW DeviceEnabled
    DB OwnerCallDvcDisable    ;Device Disabled
     DW DeviceDisabled
    DB OwnerCallDvcReset      ;Device Reset
     DW DeviceReset
    DB OwnerCallDvcSuspend    ;Device Suspend
     DW DeviceSuspend
    DB OwnerCallDvcResume     ;Device Resume
     DW DeviceResume
    DB OwnerCallDvcPwrOn      ;Device Power On
     DW DevicePowerOn
    DB OwnerCallDvcPwrOff     ;Device Power Off
     DW DevicePowerOff
    DB OwnerCallDvcPwrReset   ;Device Power Reset
     DW DevicePowerReset
   ;Host Connect/Disconnect
    DB OwnerCallNewHost       ;New Host has been Added
     DW NewHostAdded
    DB OwnerCallHostDisc      ;Existing Host Disconnected
     DW HostDisconnected
    DB OwnerCallHostHWErr     ;Host System/Processor Error
     DW HostHWErr
   ;Host Run/Stop/Reset/Suspend/Resume/Debug/SingleStep
    DB OwnerCallHostRun       ;Host Running
     DW HostRun
    DB OwnerCallHostStop      ;Host Stopped
     DW HostStop
    DB OwnerCallHostReset     ;Host Reset
     DW HostReset
    DB OwnerCallHostSuspend   ;Host Suspended
     DW HostSuspend
    DB OwnerCallHostResume    ;Host Resumed
     DW HostResume
    DB OwnerCallHostDebug     ;Host in Debug Mode
     DW HostDebug
    DB OwnerCallHostSST       ;Host Single-Stepped
     DW HostSST
   ;Timing Changes
    DB OwnerCallTimingChg     ;Host Timing Changed
     DW TimingChange
   ;Upstream Device Changes
    DB OwnerCallDvcDiscUS     ;Device Disconnect Upstream
     DW DeviceDisconnectedUS
    DB OwnerCallDvcEnableUS   ;Device Enable Upstream
     DW DeviceEnabledUS
    DB OwnerCallDvcDisableUS  ;Device Disable Upstream
     DW DeviceDisabledUS
    DB OwnerCallDvcResetUS    ;Device Reset Upstream
     DW DeviceResetUS
    DB OwnerCallDvcSuspendUS  ;Device Suspend Upstream
     DW DeviceSuspendUS
    DB OwnerCallDvcResumeUS   ;Device Resume Upstream
     DW DeviceResumeUS
    DB OwnerCallDvcPwrOnUS    ;Device Power On Upstream
     DW DevicePowerOnUS
    DB OwnerCallDvcPwrOffUS   ;Device Power Off Upstream
     DW DevicePowerOffUS
    DB OwnerCallDvcPwrResetUS ;Device Power Reset Upstream
     DW DevicePowerResetUS
   ;End of Table
    DB -1                     ;End of Table
     DW -1

  ;----------------------------------------------------------------------------
  ;Table to keep track of individual Devices
  ;----------------------------------------------------------------------------
  EVEN 2
  DeviceInfoTable:
    DB (MaxDevices*DeviceInfoStrucSize) DUP (0)

  ;----------------------------------------------------------------------------
  ;This table will keep track of Host Indexes and Address of incompatible
  ;  Devices that we find.
  ;If we don't keep track of them, we will get caught in an endless
  ;  loop of looking at the bad one every time we check (if it happens
  ;  to be the first one), and will never skip over it and be
  ;  able to find a "good" (compatible) Device.
  ;----------------------------------------------------------------------------
  EVEN 2
  BadDeviceTable:
    DB (MaxBadDevices*BadDeviceStrucSize) DUP (0)

  ;----------------------------------------------------------------------------
  ;Offset in InfoTable of Last Device we found (needed to print Descriptors)
  ;----------------------------------------------------------------------------
  LastDeviceFound DW DeviceInfoTable

  ;----------------------------------------------------------------------------
  ;Stage & Error Code where New Device search failed (for troubleshooting)
  ;For even stages < 200, the Error is a TD Status Error Code.
  ;  These are the ones generally caused by Devices that don't respond
  ;  like they're expected to.
  ;For odd stages < 200, the Error is an INT 14h Call Error Code.
  ;  These are generally cause by faulty code, and should not appear
  ;  in final, bug-less released of program.
  ;For stages >= 200, the "Error" is not related to an INT 14h or TD at all.
  ;  These are other kinds of Errors, usually caused by a value in one
  ;    of the Descriptors that we don't undertand how to handle.  The
  ;    "Error Code" in these instances is usually meaningless.
  ;Categorizing the Error Codes like this allows us to easily write a more
  ;  complete and meaningful description of the Bad Device Error.  It is
  ;  still necessary to look at the source code to REALLY figure out what's
  ;  going on, but the information provided is better than just writing
  ;  the raw Error Code numbers.
  ;----------------------------------------------------------------------------
  NewDeviceStage DB ?
  NewDeviceError DW ?

  ;----------------------------------------------------------------------------
  ;Data Structures needed to issue Requests to the USB Host
  ;----------------------------------------------------------------------------
  EVEN 2
  Int14Request:
    DB Int14RequestSize DUP (0) ;New Devices
  Int14Request2:
    DB Int14RequestSize DUP (0) ;Everything else

  ;----------------------------------------------------------------------------
  ;Storage Area for Data returned by USB Descriptors and Reports
  ;----------------------------------------------------------------------------
  DvcDescrData:
    DB DeviceDescriptorSize DUP (?) ;Device Descriptor Data
  DescriptorData:
    DB MaxDescriptorSize    DUP (?) ;Configuration Descriptor Data
  ReportData:
    DB MaxReportSize        DUP (?) ;Report Descriptor Data


;==============================================================================
;Data needed to Decipher an HID Report Descriptor
;==============================================================================
  UsagePage    DW ?
  Usage        DW ?
  Collection   DW ?
  ReportSize   DW ?
  ReportCount  DW ?
  Input        DW ?
  UsageMin     DW ?
  UsageMax     DW ?
  LogicalMin   DW ?
  LogicalMax   DW ?
  PhysicalMin  DW ?
  PhysicalMax  DW ?
  Unit         DW ?
  UnitExponent DW ?
  RptVarsSize  EQU ($-(OFFSET UsagePage))/2

  IOFValue     DW ? ;Input/Out/Feature value

  TagLookupTbl:
    DB RptTagUsagePage
     DW OFFSET UsagePage
    DB RptTagUsage
     DW OFFSET Usage
    DB RptTagCollection
     DW OFFSET Collection
    DB RptTagReportSize
     DW OFFSET ReportSize
    DB RptTagReportCount
     DW OFFSET ReportCount
    DB RptTagInput
     DW OFFSET Input
    DB RptTagUsageMin
     DW OFFSET UsageMin
    DB RptTagUsageMax
     DW OFFSET UsageMax
    DB RptTagLogicalMin
     DW OFFSET LogicalMin
    DB RptTagLogicalMax
     DW OFFSET LogicalMax
    DB RptTagPhysicalMin
     DW OFFSET PhysicalMin
    DB RptTagPhysicalMax
     DW OFFSET PhysicalMax
    DB RptTagUnit
     DW OFFSET Unit
    DB RptTagUnitExponent
     DW OFFSET UnitExponent
    DB RptTagEndCollection  ;This one doesn't matter!
     DW 0
    DB -1                   ;End Of Table
     DW -1


;==============================================================================
;USB Joystick/HID Class-Specific Control Requests
;==============================================================================
  EVEN 2

  ;----------------------------------------------------------------------------
  ;Get Report Descriptor
  ;----------------------------------------------------------------------------
  GetReportDescrReq:
    DB SRRTIn+SRRTTypeStandard+SRRTRecipInterface
    DB SRRQGetDescriptor
    DB ? ;Report ID
    DB DescrTypeHIDReport
    DB ? ;Interface Number
    DB 0
    DW ? ;Number of Bytes

  I14RGetReportDescrRptID EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RGetReportDescrIntf  EQU I14RSetupReqData.SRIndex[0] ;Byte
  I14RGetReportDescrSize  EQU I14RSetupReqData.SRLength   ;Word

  ;----------------------------------------------------------------------------
  ;Set Idle Rate
  ;  Silences Report on Interrupt In EndPoint until new event or specified time
  ;  Recommended DEFAULT idle rate is 500ms for keyboards (delay before
  ;    first rpt) and infinity for joysticks and mice
  ;----------------------------------------------------------------------------
  SetHIDIdleReq:
    DB SRRTOut+SRRTTypeClass+SRRTRecipInterface
    DB HIDRQSetIdle
    DB ? ;Report ID (0 = AllReports)
    DB ? ;Duration in 4ms increments (+/- 10% + 2ms)
         ;0 = infinite (only report changes)
         ;If less than polling rate, polling rate is used
    DB ? ;Interface Number
    DB 0
    DW 0

  I14RSetHIDIdleRptID    EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RSetHIDIdleDuration EQU I14RSetupReqData.SRValue[1] ;Byte
  I14RSetHIDIdleIntf     EQU I14RSetupReqData.SRIndex[0] ;Byte


;==============================================================================
;USB General-Purpose Control Requests
;==============================================================================
  EVEN 2

  ;----------------------------------------------------------------------------
  ;Get Device Descriptor
  ;----------------------------------------------------------------------------
  GetDvcDescrReq:
    DB SRRTIn+SRRTTypeStandard+SRRTRecipDevice
    DB SRRQGetDescriptor
    DB 0
    DB DescrTypeDevice
    DW 0
    DW DeviceDescriptorSize

  ;----------------------------------------------------------------------------
  ;Get Configuration Descriptor
  ;----------------------------------------------------------------------------
  GetCfgDescrReq:
    DB SRRTIn+SRRTTypeStandard+SRRTRecipDevice
    DB SRRQGetDescriptor
    DB 0 ;Configuration Value to Get (always 0?)
    DB DescrTypeConfig
    DW 0
    DW ConfigDescriptorSize ;Size will change for a Complete Config

  I14RGetCfgDescrValue EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RGetCfgDescrSize  EQU I14RSetupReqData.SRLength   ;Word

  ;----------------------------------------------------------------------------
  ;Set Alternate Interface
  ;----------------------------------------------------------------------------
  SetAltIntfReq:
    DB SRRTOut+SRRTTypeStandard+SRRTRecipInterface
    DB SRRQSetAltInterface
    DB ? ;Alternate Value to set Interface to
    DB 0
    DB ? ;Interface to Set
    DB 0
    DW 0

  I14RSetAltIntfAltIntf EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RSetAltIntfIntf    EQU I14RSetupReqData.SRIndex[0] ;Byte


;哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
;TSR CODE
;苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘

;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO HANDLE INT 2Fh (TSR Multiplex)
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;HAVE INTERRUPT 2Fh RUN THIS PROGRAM EVERY TIME IT OCCURS
;Inputs:  CS = TSR Data Area
;         AH = Function (Handle) number, C0h-FFh
;         AL = Subfunction number
;         (If MuxGetProgName): DI = 0
;Outputs: (AL = MuxInstallChk):  AL = FFh if installed
;         (AL = MuxGetProgName): ES:[DI] = Pointer to ProgName
;         (AL = MuxGetProgVer):  AX = ProgVer (Program version number, AH=Major)
;         (AL = MuxGetAuthor):   ES:[DI] = Pointer to Author
;         (AL = MuxInstallVIO:   Nothing
;         (AL = MuxUninstallVIO: Nothing
;Changes:
;------------------------------------------------------------------------------
Int2FHdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int2F:
  STI                               ;Enable Interrupts
  CMP  AH,CS:HandleNum              ;Is this our handle?
  JE   Int2FInstall                 ;If so, jump to handle it
  CMP  W CS:Int2FHdr.OldVector[2],0 ;Is the Old Vector valid?
  JE   Int2FDone                    ;If not, handle it
  JMP  D CS:Int2FHdr.OldVector      ;If so, go to the next one in the chain
Int2FDone:                          ;Invalid vector before us
  IRET

Int2FInstall:
  CMP  AL,MuxInstallChk  ;Is it Install Check?
  JNZ  Int2FProgName     ;If not, try the next possibility
  MOV  AL,-1             ;Mark the flag byte
  IRET

Int2FProgName:
  CMP  AL,MuxGetProgName ;Is it Get Program Name?
  JNE  Int2FProgVer      ;If not, try the next possibility
  OR   DI,DI             ;Does DI=0?
  JNZ >N90               ;If not, it's not for us
  MOV  ES,CS             ;If so, point ES:[DI]
  MOV  DI,ProgName       ;  at ProgName
N90:                     ;Done
  IRET

Int2FProgVer:
  CMP  AL,MuxGetProgVer ;Is it Get Program Version?
  JNE  Int2FAuthor      ;If not, try the next one
  MOV  AX,CS:ProgVer    ;If so, do it
  IRET

Int2FAuthor:
  CMP  AL,MuxGetAuthor ;Is it Get Author?
  JNE  Int2FInstallVIO ;If not, try the next one
  MOV  ES,CS           ;If so, point ES:[DI]
  MOV  DI,Author       ;  at Author
  IRET

Int2FinstallVIO:
  CMP  AL,MuxInstallVIO      ;Is it Virtualize I/O?
  JNE  Int2FUninstallVIO     ;If not, try the next one
  PUSH DS                    ;If so, save used registers
  MOV  DS,CS                 ;CS = DS = TSR Data Area
  TEST TSRFlags2,Is80386     ;Is the CPU at least an 80386?
  JZ  >V90                   ;If not, we're done
  TEST TSRFlags2,IsWindowsNT ;Are we running underneath Windows NT?
  JNZ >V90                   ;If not, we're done
  CALL InstallVIO            ;Do what we need to do
V90:                         ;Done
  POP  DS                    ;Restore used registers
  IRET

Int2FUninstallVIO:
  CMP  AL,MuxUninstallVIO ;Is it Uninstall Virtual I/O?
  JNE >U90                ;If not, we're done
  PUSH DS                 ;If so, save used registers
  MOV  DS,CS              ;CS = DS = TSR Data Area
  CALL UninstallVIO       ;Do what we need to do
  POP  DS                 ;Restore used registers
U90:                      ;Done
  IRET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO LOOK FOR NEW USB DEVICES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;CHECK AND SEE IF ANY NEW DEVICES HAVE BEEN CONNECTED, AND HANDLE THEM
;Inputs:  DS = ES = Our Data Area
;         CLD already issued
;Outputs:
;Changes: Looks for new, unowned (unconfigured) Devices
;         If one is found, it is Configured and pointed at us
;NOTES:   When we first install this program into memory, we Registered with
;           the USB Drivers that we wanted to be informed as soon as a new
;           Device was attached.  We also poll the hosts periodically for a
;           new Device, just in case we somehow missed the Host informing us.
;         Whether this is in response to the Host telling us, or whether we
;           polled to find out, this section of code is called.
;         This section of code is actually a series of short pieces of code,
;           which are disjointed in time.  One section schedules a USB Request
;           with the Host Driver, and gives the Host Driver the address
;           of the NEXT section of code to perform when the Request is
;           completed.
;         Requests sent to a USB Host can take anywhere from a few
;           milliseconds to several hundred milliseconds to complete.
;         It is very bad for a TSR to just sit around waiting for something
;           to happen, especially when it can be a long time in coming.
;         This back-and-forth approach between the Host Driver and the Device
;           Driver makes the TSR run pretty efficiently.
;------------------------------------------------------------------------------
LookForNewDevice:
  CALL FindEmptyInfoBX              ;Look for an empty Info Table Entry (BX)
  JZ  >L901                         ;If none, just quit
  TEST TSRFlags,Int14ReqInUse       ;Is the Request Structure already in use?
  JNZ >L901                         ;If so, just quit
  OR   TSRFlags,Int14ReqInUse       ;If not, mark it as in use now
  MOV  SI,Int14Request              ;Point at our Int 14 Structure
  MOV  [SI].I14RRequestType,I14RRTFindUnRegIntf ;Request Type = Find Interface
  MOV  [SI].I14RHostIndex,-1        ;Any/All Hosts
  MOV  [SI].I14RSearchIndex,0       ;Start with Search Index 0
L10:                                ;Loop to here to find Device
  CALL DoInt14CallCXDXSI            ;Do it (uses DS:SI, CH = Addr, CL = Host,
                                    ;  DL = Interface, DH = Alt Interface)
  JC  >L80                          ;If no device, we're done
  CALL TestBadDevice                ;If one there, is it in our Bad List?
  JC  >L20                          ;If not, continue
  INC  [SI].I14RSearchIndex         ;If so, Increment the Search Index
  JMP  L10                          ;Keep looking
L20:                                ;Found a possible Device!
  MOV  [SI].I14RRequestType,I14RRTConfigingIntf ;Request Type = Configing
  MOV  W [SI].I14RHostIndex,CX      ;Store Host Index & Device Address
  CALL DoInt14CallSaveAllSI         ;Do it
  JNC >L30                          ;If OK, continue
  MOV  Int08Counter,2               ;If not, Look again Soon
  JMP >L80                          ;Done for now
L901:                               ;To avoid JMP > 128
  JMP >L90
L30:                                ;OK to Configure Interface
  MOV  NewDeviceStage,0             ;Stage
  MOV  NewDeviceError,0             ;  Zero
  OR   [BX].DIFlags,DIFlagInUse     ;Mark Info Table entry as in use
  MOV  W [BX].DIHostIndex,CX        ;Store Host Index & Device Address
  MOV  [BX].DIInterfaceNum,DL       ;Store Interface Number
  MOV  [SI].I14RUserPktID,BX        ;Set User Pkt ID to Info Table Offset
  MOV  [SI].I14RRequestType,I14RRTDoControl ;Request Type = Control
  MOV  [SI].I14RInterfaceNum,DL     ;Store Interface Number
  MOV  [SI].I14REndPoint,0          ;EndPoint 0
  MOV  CX,SetAltIntfReq             ;Copy
  CALL CopySetupReqToInt14SI        ;  Request Pkt
  MOV  B [SI].I14RSetAltIntfIntf,DL          ;Store Interface in Request
  MOV  B [SI].I14RSetAltIntfAltIntf,DH       ;Store Alt Interface in Request
  MOV  W [SI].I14RCallBackAddr[0],AltIntfSet ;Go here when done
  OR   [SI].I14RFlags,I14RFlagNoRetries ;No retries
  CALL DoInt14CallAXSI              ;Do it
  JNC >L90                          ;If it worked, we're done
  MOV  NewDeviceStage,1             ;Update Stage
  CALL FinishUpBadDevice            ;If not, mark it as bad
  JMP >L90                          ;Done
L80:                                ;No New Devices
  AND  TSRFlags,(NOT Int14ReqInUse) ;Mark the request as available
L90:                                ;Done
  RET                               ;NOT RETF here!

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;NOTE: Some Devices will not accept a Set Alternate Interface Request,
;        so an error here does not necessarily indicate a bad Device!
;------------------------------------------------------------------------------
AltIntfSet:
  MOV  NewDeviceStage,10     ;Update Stage
  CALL ClearDescriptorData   ;Clear out the data buffer
  MOV  SI,Int14Request       ;Point at Int14 Request Structure
  AND  [SI].I14RFlags,(NOT I14RFlagNoRetries) ;Allow retries again
  MOV  CX,GetDvcDescrReq     ;Copy
  CALL CopySetupReqToInt14SI ;  Request Pkt
  MOV  W [SI].I14RDataAddress[0],DvcDescrData    ;Put data here
  MOV  W [SI].I14RCallBackAddr[0],GotDeviceDescr ;Go here when done
  CALL DoInt14CallAXSI       ;Do it
  JNC >I90                   ;If it worked, we're done
  MOV  NewDeviceStage,11     ;Update Stage
I70:                         ;Error
  CALL FinishUpBadDevice     ;Mark it as Bad
I90:                         ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
GotDeviceDescr:
  MOV  NewDeviceStage,20     ;Update Stage
  JNZ >D70                   ;If Error, handle it
  MOV  SI,Int14Request       ;Point at Int14 Request Structure
  MOV  CX,GetCfgDescrReq     ;Copy
  CALL CopySetupReqToInt14SI ;  Request Pkt
  MOV  W [SI].I14RDataAddress[0],DescriptorData  ;Put data here
  MOV  W [SI].I14RCallBackAddr[0],GotConfigDescr ;Go here when done
  CALL DoInt14CallAXSI       ;Do it
  JNC >D90                   ;If it worked, we're done
  MOV  NewDeviceStage,21     ;Update Stage
D70:                         ;Error
  CALL FinishUpBadDevice     ;Mark it as Bad
D90:                         ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
GotConfigDescr:
  MOV  NewDeviceStage,30     ;Update Stage
  JNZ >O70                   ;If Error, handle it
  MOV  NewDeviceStage,31     ;Update Stage
  MOV  SI,Int14Request       ;Point at Int14 Request Structure
  MOV  DI,DescriptorData     ;Point at Config Descriptor Data
  MOV  AX,[DI].CDTotalLength ;Get length of entire Descriptor
  CMP  AX,MaxDescriptorSize  ;Will it fit in our buffer?
  JAE >O70                   ;If not, error
  MOV  W [SI].I14RGetCfgDescrSize,AX ;Store the number of bytes
  MOV  W [SI].I14RCallBackAddr[0],GotCompleteConfig ;Go here when done
  CALL ClearDescriptorData   ;Clear out the data buffer
  CALL DoInt14CallAXSI       ;Do it
  JNC >O90                   ;If it worked, we're done
  MOV  NewDeviceStage,32     ;Update Stage
O70:                         ;Error
  CALL FinishUpBadDevice     ;Mark it as Bad
O90:                         ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
GotCompleteConfig:
  MOV  NewDeviceStage,40               ;Update Stage
  JNZ >C70                             ;If Error, handle it
  MOV  DI,DescriptorData               ;Point at Config Descriptor Data
  MOV  SI,Int14Request                 ;Point at Int14 Request Structure
  MOV  CX,SetHIDIdleReq                ;Copy
  CALL CopySetupReqToInt14SI           ;  Request Pkt
  MOV  AL,[SI].I14RInterfaceNum        ;Set
  MOV  B [SI].I14RSetHIDIdleIntf,AL    ;  Interface Number
  ;MOV  B [SI].I14RSetHIDIdleRptID,0    ;Report ID 0 (All Reports)
  MOV  B [SI].I14RSetHIDIdleDuration,0 ;Duration 0 (only report changes)
  MOV  W [SI].I14RCallBackAddr[0],GetReportDescr ;Go here when done
  OR   [SI].I14RFlags,I14RFlagNoRetries ;No retries
  CALL DoInt14CallAXSI                 ;Do it
  JNC >C90                             ;If it worked, we're done
  MOV  NewDeviceStage,41               ;Update Stage
C70:                                   ;Error
  CALL FinishUpBadDevice               ;Mark it as Bad
C90:                                   ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;At least one joystick responds with a Stall to the previous Request (Set HID
;  Idle Rate), but the joystick works OK anyway!
;Therefore, we will NOT assume that an Error from the previous call indicates
;  a bad Joystick.
;------------------------------------------------------------------------------
GetReportDescr:
  MOV  NewDeviceStage,200                   ;Update Stage
  MOV  SI,Int14Request                      ;Point at Int14 Request Structure
  AND  [SI].I14RFlags,(NOT I14RFlagNoRetries) ;Allow retries again
  MOV  DI,DescriptorData                    ;Point at Config Data
  MOV  BL,[BX].DIInterfaceNum               ;Interface Number to look for
R10:                                        ;Loop to find Interface Descriptor
  MOV  AL,DescrTypeInterface                ;Look for an Interface
  CALL FindDescrType                        ;  Descriptor (rtns AX, DI)
  JC  >R70                                  ;If none found, Error
  CMP  [DI].IDInterfaceNumber,BL            ;Is it the Correct Interface?
  JE  >R20                                  ;If so, continue
  ADD  DI,AX                                ;If not, point at the next Descr
  JMP  R10                                  ;Keep looking
R20:                                        ;Found the Interface Descriptor
  MOV  NewDeviceStage,201                   ;Update Stage
  MOV  AL,DescrTypeHID                      ;Look for an HID
  CALL FindDescrType                        ;  Descriptor (rtns AX, DI)
  JC  >R70                                  ;If none found, Error
  MOV  NewDeviceStage,202                   ;Update Stage
  MOV  AX,[DI].HIDDDescriptorLength         ;Get Report Size
  CMP  AX,MaxReportSize                     ;Is it too big for our buffer?
  JAE >R70                                  ;If so, error
  MOV  CX,GetReportDescrReq                 ;Copy
  CALL CopySetupReqToInt14SI                ;  Request Pkt
  MOV  W [SI].I14RGetReportDescrSize,AX     ;Store number of bytes
  MOV  B [SI].I14RGetReportDescrIntf,BL     ;Store Interface Number
  ;MOV  B [SI].I14RGetReportDescrRptID,0     ;Report ID 0 (all reports)
  MOV  W [SI].I14RDataAddress[0],ReportData ;Where to put data
  MOV  W [SI].I14RCallBackAddr[0],SetupInts ;Go here when done
  CALL ClearReportData                      ;Clear out Report Data area
  MOV  NewDeviceStage,51                    ;Update Stage
  CALL DoInt14CallAXSI                      ;Do it
  JNC >R90                                  ;If it worked, we're done
R70:                                        ;Error
  CALL FinishUpBadDevice                    ;Mark it as Bad
R90:                                        ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
SetupInts:
  MOV  NewDeviceStage,60            ;Update Stage
  JNZ >I701                         ;If error, mark it as bad
  MOV  NewDeviceStage,210           ;Update Stage
  MOV  SI,Int14Request              ;Point SI at Int14 Request
  CALL DecipherReportDescr          ;Get Device Info from Report
  JC  >I701                         ;If invalid, quit
  MOV  NewDeviceStage,211           ;Update Stage
  MOV  DI,DescriptorData            ;Point DI at Config Data
  MOV  CL,[BX].DIInterfaceNum       ;Interface Number to look for
I10:                                ;Loop to find Interface Descr
  MOV  AL,DescrTypeInterface        ;Look for an Interface
  CALL FindDescrType                ;  Descriptor (rtns AX, DI)
  JC  >I701                         ;If none found, Error
  MOV  NewDeviceStage,212           ;Update Stage
  CMP  [DI].IDInterfaceNumber,CL    ;Is it the Correct Interface?
  JE  >I20                          ;If so, continue
  ADD  DI,AX                        ;If not, point at the next Descr
  JMP  I10                          ;Keep looking
I20:                                ;Found the Interface Descriptor
  MOV  AL,DescrTypeEndPoint         ;Look for an EndPoint
  CALL FindDescrType                ;  Descriptor (rtns AX, DI)
  JC  >I701                         ;If none found, Error
  MOV  NewDeviceStage,213           ;Update Stage
  MOV  [SI].I14RRequestType,I14RRTDoInterruptPer ;Periodic Interrupt
  MOV  [SI].I14RFlags,I14RFlagIn    ;Direction = In
  XOR  AH,AH                        ;Get the
  MOV  AL,[DI].EDPollInterval       ;  Poll Interval
  CMP  AL,10                        ;Is it OK?
  JAE >I25                          ;If so, continue
  MOV  AL,10                        ;If not, make it OK
I25:                                ;AX contains Periodicity
  MOV  [SI].I14RPeriodicity,AX      ;Store the Periodicity
  MOV  NewDeviceStage,214           ;Update Stage
  MOV  AX,[DI].EDMaxPktSize         ;Get Data Size
  CMP  AX,MaxJDataSize              ;Is it OK?
  JA  >I701                         ;If not, Error
  MOV  [SI].I14RDataSize,AX         ;If OK, store it
  MOV  AL,[DI].EDEndPointNumber     ;Get EP Address & Direction
  TEST AL,EDDirectionIn             ;Is it In (it better be!)
  JNZ >I30                          ;If so, continue
I701:                               ;To avoid JMP > 128
  JMP >I70                          ;Error
I30:
;  MOV  NewDeviceStage,214           ;Update Stage
  MOV  NewDeviceStage,215           ;Update Stage
  AND  AL,EDNumberMask              ;Store the
  MOV  [SI].I14REndPoint,AL         ;  EndPoint
  MOV  [BX].DIEndPoint,AL           ;  Number
  LEA  AX,[BX].DIRawData            ;Where to
  MOV  W [SI].I14RDataAddress[0],AX ;  put data
  CMP  [BX].DIReportID,0            ;Is there a Report ID?
  JE  >I40                          ;If not, Data Address is OK
  DEC  W [SI].I14RDataAddress[0]    ;If so, Data will include ID
I40:                                ;Data Address is OK
  MOV  W [SI].I14RCallBackAddr[0],DeviceChangeCode ;Code to call
  MOV  NewDeviceStage,73            ;Update Stage
  CALL DoInt14CallAXCXSI            ;Do it
  JC  >I70                          ;If error, quit
  MOV  [BX].DIIntHandle,CX          ;Save the Interrupt Handle
  MOV  LastDeviceFound,BX           ;Mark this as the Last Device
  MOV  [SI].I14RRequestType,I14RRTRegIntfOwner ;Register as Owner
  MOV  W [SI].I14RCallBackAddr[0],DvcOwnerFarCall ;Owner Address
  CALL DoInt14CallAXCXSI            ;Do it
  MOV  [BX].DIRegHandle,CX          ;Store the Registry Handle
  JNC >I80                          ;If it worked, we're done
  MOV  NewDeviceStage,75            ;Update Stage
  MOV  AX,[BX].DIIntHandle          ;If not,
  MOV  [SI].I14RRequestHandle,AX    ;  we need
  MOV  [SI].I14RRequestType,I14RRTCloseHandle  ;  to stop the
  CALL DoInt14CallAXSI              ;  Periodic Interrupt
I70:                                ;Error (DX contains orig BX)
  CALL FinishUpBadDevice            ;Mark it as Bad
  JMP >I90                          ;Done
I80:                                ;Device is OK
  MOV  [SI].I14RRequestType,I14RRTConfigIntfDone ;Request = Done Configuring
  CALL DoInt14CallSaveAllSI         ;Do it
  CMP  Int08Counter,0               ;Disabled?
  JE  >I85                          ;If so, quit
  MOV  Int08Counter,2               ;Look for another Device soon
I85:                                ;Done for now
  AND  TSRFlags,(NOT Int14ReqInUse) ;Mark the request as available
I90:                                ;Done
  RETF


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SUPPORT FUNCTIONS NEEDED BY LOOKFORNEWDEVICE PROCESS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;FINISH UP A BAD DEVICE ENTRY
;Inputs:  BX = Info Table Offset
;         (Info Table Entry contains Device Data)
;         DS = ES = TSR Data Area
;         Int14Request (contains Host/Address/Interface Data)
;Outputs: Resets DeviceInfoTable Entry
;         Marks Int14Request as available (not in use any more)
;         Updates NewDeviceStage & NewDeviceError
;         Puts Device info in BadDevice Table
;         Issues Request to Beep the Speaker
;Changes: DI,SI
;------------------------------------------------------------------------------
FinishUpBadDevice:
  MOV  SI,Int14Request               ;Point at Int14 Request Struc
  MOV  NewDeviceError,AX             ;Store Error Code
  CALL ResetDvcEntryBX               ;Reset the Dvc Info Entry that we Reserved
  MOV  [SI].I14RRequestType,I14RRTGetDvcStatus ;Request = Get Dvc Status
                                               ;(Make sure Device is still
                                               ;  plugged in)
  CALL DoInt14CallSaveAllSI          ;Issue the Request
  JC  >B10                           ;If error (no Device), don't add to Bad Tbl
  CALL AddBadDevice                  ;Add it to Bad Table
B10:                                 ;Bad Device added to table, if appropriate
  TEST TSRFlags,(NoBeep+NoBeep1Time) ;Are we allowed to Beep the Speaker
  JNZ >B20                           ;If not, skip down
  MOV  [SI].I14RRequestType,I14RRTBeepSpeaker ;Want to beep the Speaker
  MOV  [SI].I14RHostIndex,-1         ;Any Host
  MOV  B [SI].I14RTimeout,3          ;Beep length = 3 DeciSeconds
  MOV  W [SI].I14RBeepFrequency,300  ;Frequency = 300Hz
  CALL DoInt14CallSaveAllSI          ;Do it
  MOV  B [SI].I14RTimeOut,0          ;Reset Timeout Value
B20:                                 ;Done handling Beep
  AND  TSRFlags,(NOT NoBeep1Time)    ;Mark as OK to Beep next time
  MOV  [SI].I14RRequestType,I14RRTConfigIntfDone ;Request = Done Configuring
  CALL DoInt14CallSaveAllSI          ;Do it
  CMP  Int08Counter,0                ;Disabled?
  JE  >B90                           ;If so, Done
  MOV  Int08Counter,2                ;Look for another Device soon
B90:                                 ;Done
  AND  TSRFlags,(NOT Int14ReqInUse)  ;Mark the request as available
  RET

;------------------------------------------------------------------------------
;CLEAR OUT THE OLD DATA FROM THE DESCRIPTOR DATA BUFFER
;Inputs:  ES = TSR Data Area
;         CLD already issued
;Outputs:
;Changes: DescriptorData becomes all zeroes
;------------------------------------------------------------------------------
ClearDescriptorData:
  PUSH AX,CX,DI                 ;Save used registers
  MOV  DI,DescriptorData        ;Point at buffer
  MOV  CX,(MaxDescriptorSize/2) ;Number of words to write
  XOR  AX,AX                    ;Write zeroes
  REP  STOSW                    ;Do it
  POP  DI,CX,AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CLEAR OUT THE OLD DATA FROM THE REPORT DATA BUFFER
;Inputs:  ES = TSR Data Area
;Outputs:
;Changes: ReportData becomes all zeroes
;------------------------------------------------------------------------------
ClearReportData:
  PUSH AX,CX,DI             ;Save used registers
  MOV  DI,ReportData        ;Point at buffer
  MOV  CX,(MaxReportSize/2) ;Number of words to write
  XOR  AX,AX                ;Write zeroes
  REP  STOSW                ;Do it
  POP  DI,CX,AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND A PARTICULAR TYPE OF DESCRIPTOR IN THE COMPLETE CONFIGURATION DESCRIPTOR
;Inputs:  DS = ES = TSR Data Area
;         [DI] = Pointer to start/middle of Configuration Descriptor
;          AL  = Descriptor Type to find
;Outputs: CF = Set if OK (Descriptor Found)
;              [DI] points at beginning of Descriptor
;               AX = Descriptor Length
;            = Clear if Error (Descriptor not found)
;              AX, DI unchanged
;Changes:
;------------------------------------------------------------------------------
FindDescrType:
  PUSH BX,SI               ;Save used registers
  MOV  SI,DI               ;Point SI at the starting Pointer
  XOR  BH,BH               ;Initialize high byte of Size to 0
Y10:                       ;Loop here to find Descriptor
  MOV  BL,[SI].DDLength    ;Put the Length in BL
  OR   BL,BL               ;Valid Descriptor?
  JZ  >Y70                 ;If not, Error
  CMP  AL,[SI].DDDescrType ;The Descriptor we're looking for?
  JE  >Y80                 ;If so, we're done
  ADD  SI,BX               ;If not, point at next Descriptor
  CMP  SI,(DescriptorData+MaxDescriptorSize) ;Gone past the end of the buffer?
  JBE  Y10                 ;If not, keep looking
Y70:                       ;Error
  STC                      ;Set Error Flag
  JMP >Y90                 ;Done
Y80:                       ;OK
  MOV  DI,SI               ;Update Pointer
  MOV  AX,BX               ;Put length in AX
  CLC                      ;Set OK Flag
Y90:                       ;Done
  POP  SI,BX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A REQUEST (CONTROL) STRUCTURE INTO THE INT14 STRUCTURE
;Inputs:  [CX] = Request Structure to Copy
;          ES  = TSR Data Area (contains both Request & Int14 Request)
;         [DI] = Int14 Request Structure to copy it to
;      or [SI] = Int14 Request Structure to copy it to
;Outputs:
;Changes: Int14 Structure contains Request Structure Data
;------------------------------------------------------------------------------
CopySetupReqToInt14SI:
  PUSH DI                    ;Save used registers
  MOV  DI,SI                 ;Put pointer in DI
  CALL CopySetupReqToInt14DI ;Copy it
  POP  DI                    ;Restore used registers
  RET

CopySetupReqToInt14DI:
  PUSH CX,DI,SI,DS                ;Save used registers
  MOV  DS,ES                      ;Point DS at correct Data Area
  ADD  DI,OFFSET I14RSetupReqData ;Point at correct part of structure
  MOV  SI,CX                      ;Point at structure to copy
  MOV  CX,(SetupRequestSize/2)    ;Number of words to copy
  REP  MOVSW                      ;Copy it
  POP  DS,SI,DI,CX                ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO DECIPHER THE REPORT DESCRIPTOR
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;DECIPHER THE REPORT DESCRIPTOR TO DETERMINE THE JOYSTICK CHARACTERISTICS
;Inputs:  ReportData
;         DS = ES = TSR Data Area
;         BX = Info Table Pointer
;Outputs: CF = Clear if OK (found valid Data)
;            = Set if Error (not a valid Joystick/GamePad)
;         UsagePage, Usage, Collection, ReportSize, ReportCount, Input,
;         UsageMin, UsageMax, LogicalMin, LogicalMax, PhysicalMin, PhysicalMax
;Changes:
;NOTES: To keep this code simple, we will assume that the Joystick and the
;         Report Descriptor don't do or have anything really weird (like
;         PUSH's and POP's).
;------------------------------------------------------------------------------
DecipherReportDescr:
  PUSH AX,BX,DI,SI,BP                ;Save used registers
  CALL ResetRptVars                  ;Initialize Report Variables
  MOV  SI,ReportData                 ;Point at Data
  MOV  BP,(ReportData+MaxReportSize) ;Point at end of data
  CALL LookForJoystick               ;Is it a Joystick or GamePad?
  JC  >D70                           ;If not, just quit
  LEA  DI,[BX].DIControlsData        ;If so, point at Control Struc
  MOV  BH,MaxControls                ;Initialize Control Counter
  XOR  BL,BL                         ;Starting Bit Offset = 0
D10:                                 ;Loop to here for each Ctl Grp
  CALL FindNextIOFTag                ;Scan thru the next Ctl Grp
  JC  >D80                           ;If done, just quit
  CMP  DL,RPtTagInput                ;Is it Input?
  JNE  D10                           ;If not, just continue
  MOV  IOFValue,AX                   ;If OK, store value
  CALL CalcControlType               ;See what kind of Control it is
  JC   D10                           ;If unknown, just continue
  CALL FillControlEntry              ;If OK, fill in the Table Entry
  JC   D10                           ;If invalid, try the next one
  ADD  DI,ControlStrucSize           ;Point at next Control
  DEC  BH                            ;Decrement the Counter
  JNZ  D10                           ;If not 0 yet, keep looking
  JMP >D80                           ;Done
D70:                                 ;Error
  STC                                ;Set Error Flag
  JMP >D90                           ;Done
D80:                                 ;OK
  CLC                                ;Set OK Flag
D90:                                 ;Done
  POP  BP,SI,DI,BX,AX                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESET THE TEMPORARY REPORT VARIABLES TABLE TO ALL ZEROES
;Inputs:  ES = TSR Data Area
;         CLD already issued
;Outputs:
;Changes: All temporary report variables (UsagePage, Usage, etc)
;------------------------------------------------------------------------------
ResetRptVars:
  PUSH AX,CX,DI            ;Save used registers
  MOV  DI,OFFSET UsagePage ;Point at Table
  XOR  AX,AX               ;Fill with zeroes
  MOV  CX,RptVarsSize      ;Number of words to write
  REP  STOSW               ;Do it
  POP  DI,CX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SEE IF THE HID REPORT DESCRIPTOR IS FOR A GAMEPAD OR A JOYSTICK
;Inputs:  [SI] = Report Descriptor
;         [DI] = End of Report Descriptor
;Outputs: CF = Clear if it is a Josytcik or a GamePad
;            = Set if not
;Changes: SI, UsagePage, Usage, Collection
;NOTES:   According to the USB Spec, this should be at the top of the Descr:
;           Usage Page = Generic DeskTop, Usage = Joystick or GamePad,
;           Collection = Application, Usage = Pointer, Collection = Physical
;         We look for those exat things in that exact order.
;         If it's not there, we reject it as not being a Joystick or GamePad!

;         Well, things have changed.  A Logitech Precision GamePad (and
;           possibly other josyticks/gamepads as well) do not follow the
;           standards.  Well, knock me over with a feather!
;         The Logitech Precision Joystick has the first three items,
;           but the the Usage = Pointer & Collection = Physical.
;           So, we'll not check for those any more.
;------------------------------------------------------------------------------
LookForJoystick:
  PUSH AX                         ;Save used registers
J10:                              ;Look for Generic DeskTop Usage Page
  MOV  AL,RptTagUsagePage         ;Look for
  CALL LookForReportTag           ;  Usage Page
  JC  >J70                        ;If Error, Quit
  CMP  AL,RptGlbUPGenericDeskTop  ;Is it Generic DeskTop?
  JNE >J70                        ;If not, Quit
  MOV  UsagePage,AX               ;If OK, Store the Usage Page
J20:                              ;Look for Joystick or GamePad Usage
  MOV  AL,RptTagUsage             ;Look for
  CALL LookForReportTag           ;  Usage
  JC  >J70                        ;If Error, Quit
  OR   TSRFlags,NoBeep1Time       ;Don't Beep if not a GamePad or Joystick
  CMP  AL,RptGDJoystick           ;Is it Joystick?
  JE  >J25                        ;If so, continue
  CMP  AL,RptGDGamePad            ;Is it GamePad?
  JNE >J70                        ;If not, Quit
J25:                              ;Is Joystick or GamePad
  AND  TSRFlags,(NOT NoBeep1Time) ;Allow Beeps again
  MOV  Usage,AX                   ;Store the Usage
J30:                              ;Look for Physical Collection
  MOV  AL,RptTagCollection        ;Look for
  CALL LookForReportTag           ;  Collection
  JC  >J70                        ;If Error, Quit
  CMP  AL,RptMainColApplication   ;Is it Application?
  JNE >J70                        ;If not, Quit
  MOV  Collection,AX              ;If OK, Store the Collection
;J40:                              ;Look for Pointer Usage
;  MOV  AL,RptTagUsage             ;Look for
;  CALL LookForReportTag           ;  Usage
;  JC  >J70                        ;If Error, Quit
;  CMP  AL,RptGDPointer            ;Is it Pointer?
;  JNE >J70                        ;If not, Quit
;  MOV  Usage,AX                   ;Store the Usage
;J50:                              ;Look for Physical Collection
;  MOV  AL,RptTagCollection        ;Look for
;  CALL LookForReportTag           ;  Collection
;  JC  >J70                        ;If Error, Quit
;  CMP  AL,RptMainColPhysical      ;Is it Physical?
;  JNE >J70                        ;If not, Quit
;  MOV  Collection,AX              ;If OK, Store the Collection
  JMP >J80                        ;Done
J70:                              ;Error
  STC                             ;Set Error Flag
  JMP >J90                        ;Done
J80:                              ;OK
  CLC                             ;Set OK Flag
J90:                              ;Done
  POP  AX                         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;LOOK FOR A SPECIFIC REPORT TAG IN THE REPORT DESCRIPTOR
;Inputs:  [SI] = Report Pointer
;         [BP] = End of Report Data Area
;         AL   = Report Tag to Look for
;Outputs: CF = Clear if Tag was found
;              AX  = Data Word immediately following the Tag
;                    AH = 0 if it is just a byte!
;             [SI] = Points to next Tag after the one found
;            = Set if Tag was not Found
;              AX = 0
;              SI >= BP
;Changes:
;NOTES: This works under the premise that Data is at most two bytes
;         after a Report Tag (which all Joystick Data should be!)
;------------------------------------------------------------------------------
LookForReportTag:
  PUSH BX,CX        ;Save used registers
  XOR  CH,CH        ;Size Adder is always a Byte
  MOV  BL,AL        ;Save Tag we're looking for
L10:                ;Loop to here to find the Tag
  LODSB             ;Get the next Tag
  OR   AL,AL        ;Legitimate Tag?
  JZ  >L70          ;If not, error
  MOV  CL,AL        ;Get the
  AND  CL,03h       ;  Data Size
  AND  AL,(NOT 03h) ;Mask out the Size so we can test it
  CMP  AL,BL        ;Is it the Tag we're looking for?
  JE  >L80          ;If so, we're done
  ADD  SI,CX        ;If not, point at the next one
  CMP  SI,BP        ;Are we past the end of the buffer?
  JB   L10          ;If not,I keep looking
L70:                ;Error
  XOR  AX,AX        ;AX = 0
  STC               ;Set Error Flag
  JMP >L90          ;Done
L80:                ;Found it
  XOR  AX,AX        ;Assume 0 bytes of data
  CMP  CL,1         ;How many data bytes are there?
  JB  >L85          ;If 0 bytes, handle it
  JA  >L82          ;If 2, handle it
  LODSB             ;If one, get the Data
  JMP >L85          ;Done
L82:                ;Data is two bytes
  LODSW             ;Get the Data
L85:                ;Have Data
  CLC               ;Set OK Flag
L90:                ;Done
  POP  CX,BX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;LOOK FOR THE NEXT INPUT, OUTPUT, OR FEATURE TAG IN THE REPORT DESCRIPTOR
;Inputs:  [SI] = Current pointer
;          BP  = End of Data Area
;Outputs: CF = Clear if valid data is found
;              Temp variables (UsagePage, Usage, etc.) updated appropriately
;              DL = Input/Output/Feature Tag (without the Size)
;              AX = Data Byte/Word after the Tag
;            = Set if no more data
;Changes: SI
;NOTES: This assumes that Tags only contain byte or word data, not DWord data.
;         If it ever does, we will be screwed up!
;       There's no legitimate reason a device would have DWord data, though, other
;         than the fact that it's possible to do.
;------------------------------------------------------------------------------
FindNextIOFTag:
  PUSH BX,CX               ;Save used registers
  XOR  CH,CH               ;Size comes in bytes
I10:                       ;Loop to here for each non-IOF
  CMP  SI,BP               ;End of Data Area?
  JAE >I70                 ;If so, quit
  LODSB                    ;Get the Tag
  OR   AL,AL               ;End of Data?
  JZ  >I70                 ;If so, quit
  MOV  CL,AL               ;Put Data Size
  AND  CL,03h              ;  in CX
;  CMP  CL,03h              ;Is it 4 bytes of data?
;  JE  >I15                 ;If so, skip it
  AND  AL,(NOT 03h)        ;Take size out of Tag
  CMP  AL,RptTagInput      ;Input?
  JE  >I50                 ;If so, we're done
  CMP  AL,RptTagOutput     ;Output?
  JE  >I50                 ;If so, we're done
  CMP  AL,RptTagFeature    ;Feature?
  JE  >I50                 ;If so, we're done
  MOV  BX,TagLookupTbl     ;Convert Tag to
  CALL SearchByteToWordTbl ;  a Pointer
  JNC >I20                 ;If Tag is recognized, handle it
;I15:                       ;Done with Tag
  ADD  SI,CX               ;If not recognized, Update Pointer
  JMP >I30                 ;Done with this one
I20:                       ;Valid tag
  CMP  CL,1                ;How big is the data?
  JB  >I30                 ;If 0 bytes, just continue
  JA  >I25                 ;If 2 bytes, handle it
  LODSB                    ;If one byte, get it
  XOR  AH,AH               ;Set high byte to 0
;  MOV  [BX],AX             ;  store it
;  JMP >I30                 ;Done
  JMP >I28                 ;Jump to store it
I25:                       ;Is two bytes long
  LODSW                    ;Get teh Data
I28:                       ;AX = Data to Store
  MOV  [BX],AX             ;Store the Data
I30:                       ;Have Data updated
  JMP  I10                 ;Keep looking
I50:                       ;Have IOF
  MOV  DL,AL               ;Put Tag in DL
  XOR  AX,AX               ;Assume Byte
  CMP  CL,1                ;How big is the data?
  JB  >I80                 ;If 0 bytes, we're done
  JA  >I55                 ;If two bytes, Handle it
  LODSB                    ;If one byte, get the Data
  JMP >I80                 ;Done
I55:                       ;Two-byte Tag
  LODSW                    ;Get the Data
  JMP >I80                 ;Done
I70:                       ;End of Data
  STC                      ;Set flag
  JMP >I90                 ;Done
I80:                       ;Have valid data
  CLC                      ;Set flag
I90:                       ;Done
  POP  CX,BX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE THE TYPE OF CONTROL DESCRIBED BY THE HID REPORT DESCRIPTOR
;Inputs:  UsagePage, Usage
;Outputs: CF = Clear if Valid data
;              AL = Control Type (Button, D-Pad, etc.)
;            = Set if Error (Unknown Type)
;              AL = 0
;Changes:
;------------------------------------------------------------------------------
CalcControlType:
  PUSH BX                        ;Save used regsiters
  MOV  BL,B UsagePage[0]         ;Get Usage Page
  MOV  AL,B Usage[0]             ;Get Usage
  CMP  BL,RptGlbUPGenericDeskTop ;Is it Generic Desktop?
  JE  >C10                       ;If so, handle it
  CMP  BL,RptGlbUPSimulationCtl  ;Is it Simulation Control?
  JE  >C40                       ;If so, handle it
  CMP  BL,RptGlbUPGameCtl        ;Is it Game Control?
  JE  >C50                       ;If so, handle it
  CMP  BL,RptGlbUPButton         ;Is it Button?
  JE  >C60                       ;If so, handle it
  JMP >C701                      ;If not, we don't know what it is!
C10:                             ;Generic Desktop
  MOV  BL,CTAxis                 ;Assume Axis
  CMP  AL,RptGDXAxis             ;Axis?
  JB  >C701                      ;If not, Error
  CMP  AL,RptGDRotateZ           ;Axis?
  JBE >C801                      ;If so, Done
  MOV  BL,CTSlider               ;Assume Slider
  CMP  AL,RptGDSlider            ;Slider?
  JE  >C801                      ;If so, Done
  CMP  AL,RptGDDial              ;Dial?
  JE  >C801                      ;If so, Done
  MOV  BL,CTDPad                 ;Assume D-Pad/Hat Switch
  CMP  AL,RptGDHatSwitch         ;Hat Switch?
  JE  >C801                      ;If so, Done
  CMP  AL,RptGDDPadUp            ;D-Pad?
  JB  >C15                       ;If not, continue
  CMP  AL,RptGDDPadLeft          ;D-Pad?
  JBE >C801                      ;If so, Done
C15:                             ;Not D-Pad/HatSwitch
  MOV  BL,CTButton               ;Assume Button
  CMP  AL,RptGDStart             ;Button?
  JB  >C70                       ;If not, Error
  CMP  AL,RptGDSelect            ;Button?
  JBE >C801                      ;If so, Done
  JMP >C701                      ;If none of these, Error
C40:                             ;Simulation Controls
  MOV  BL,CTAxis                 ;Assume Axis
  CMP  AL,RptSimFlightCtlStick   ;Axis?
  JB  >C701                      ;If not, Error
  CMP  AL,RptSimFlightStick      ;Axis?
  JBE >C801                      ;If so, Done
  MOV  BL,CTThrottle             ;Assume Throttle
  CMP  AL,RptSimThrottle         ;Throttle?
  JE  >C801                      ;If so, Done
C701:
  JMP >C70                       ;If none of these, Error
C801:
  JMP >C80
C50:                             ;Game Controls
  MOV  BL,CTDPad                 ;Assume D-Pad/HatSwitch
  CMP  AL,RptGamePOV             ;Hat Switch?
  JE  >C80                       ;If so, done
  MOV  BL,CTAxis                 ;Assume Axis
  CMP  AL,RptGameTurn            ;Axis?
  JB  >C70                       ;If not, Error
  CMP  AL,RptGamePOVHeight       ;Axis?
  JBE >C80                       ;If so, continue
  MOV  BL,CTButton               ;Assume Button
  CMP  AL,RptGameFlipper         ;Button?
  JBE >C70                       ;If not, Error
  CMP  AL,RptGamePadTrigger      ;Button?
  JBE >C80                       ;If so, Done
  JMP >C70                       ;If none of these, Error
C60:                             ;Buttons
  MOV  BL,CTButton               ;Always a Button!
  JMP >C80                       ;Done
C70:                             ;Error
  XOR  AL,AL                     ;Set
  STC                            ;  Error Flags
  JMP >C90                       ;Done
C80:                             ;OK
  MOV  AL,BL                     ;Set return byte
  CLC                            ;Set OK flag
C90:                             ;Done
  POP  BX                        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FILL IN THE CONTROL TABLE ENTRY WITH APPROPRIATE DATA
;Inputs:  DS = ES = TSR Data Area
;         AL   = Control Type (Button, DPad, etc.)
;         BL   = Current Bit Offset Counter
;         [DI] = Table entry to fill in
;         Temp Report variables containing Data (UsagePage, Usage, etc.)
;Outputs: CF = Clear if OK
;              Fills in Table Entry at [DI]
;              Updates BL
;            = Set if Error
;Changes:
;------------------------------------------------------------------------------
FillControlEntry:
  PUSH AX,CX,DX               ;Save used registers
  MOV  AH,B ReportCount[0]    ;Get Number of Controls
  MOV  DL,B ReportSize[0]     ;Get Report Size
  CMP  AL,CTButton            ;Possible Button?
  JNE >E80                    ;If not, just store it
E10:                          ;Possible Button
  CMP  DL,1                   ;Is it really a button?
  JNE >E70                    ;If not, error
  CMP  IOFValue,RptMainIOFConstant ;Is it a constant?
  JNE >E80                    ;If so, Error
E70:                          ;Error
  STC                         ;Set Error Flag
  JMP >E90                    ;Done
E80:                          ;OK
  MOV  [DI].ControlType,AL    ;Store Control Type
  MOV  [DI].ControlQty,AH     ;Store Control Quantity
  MOV  [DI].ControlOffset,BL  ;Store Control Offset
  MOV  [DI].ControlNumBits,DL ;Store number of Bits per Entry
  CALL FillMinMax             ;Store Minimum and Maximum
  CLC                         ;Set Flag
E90:                          ;Done
  CALL CalcNextOffset         ;Update the Offset
  POP  DX,CX,AX               ;Restore used registers
  RET


;------------------------------------------------------------------------------
;FILL IN THE MIN/MAX VALUES IN THE CONTROL TABLE
;Inputs:  DS = ES = TSR Data Area
;         [DI] = Control Table entry to fill in
;         Temp Report variables containing Data (UsagePage, Usage, etc.)
;Outputs: Fills in ControlMin, ControlMax
;Changes:
;NOTE: All Joysticks are SUPPOSED to return the values stated as the Logical
;        Minimum and Logical Maximum in the Report Descriptor.  At least one
;        joystick (the one I have made by Inland), doesn't.  It returns
;        the values defined by Physical Min and Physical Max.  We try to test
;        for this possibility by assuming that if the Logical Min is
;        Negative, the Physical Min is 0, and the Physical Max is more than
;        the Physical Min, it is a joystick that needs to use the
;        Physical values instead of the Logical Values.  This hopefully won't
;        cause problems with any joysticks that work like they're supposed to.
;------------------------------------------------------------------------------
FillMinMax:
  PUSH AX,BX,CX,DX        ;Save used registers
  MOV  AX,LogicalMin      ;Get Logical Min
  MOV  BX,LogicalMax      ;  and Logical Max
  CMP  AX,BX              ;Is Logical Min less than Max?
  JB  >L80                ;If so, use Logical Values
  OR   AH,AH              ;Does AX have any high bits set?
  JNZ >L10                ;If so, it's already a word
  CBW                     ;If not, sign-extend AL to a Word
L10:                      ;AX is a complete word
  MOV  CX,PhysicalMin     ;Get Physical Min
  MOV  DX,PhysicalMax     ;  and Physical Max
  OR   CX,CX              ;Is the Physical Min 0?
  JNZ >L80                ;If not, we'll assume things are OK
  SUB  DX,CX              ;Calculate the difference between Physical Min and Max
  JZ  >L80                ;If no difference, we're done
  MOV  CX,BX              ;Calculate the difference between
  SUB  CX,AX              ;  Logical Min and Max
  CMP  CX,DX              ;Are the differences the same?
  JNE >L80                ;If not, use the Logical Values
  MOV  AX,PhysicalMin     ;If so, use Physical Min
  MOV  BX,PhysicalMax     ;  and Physical Max
L80:                      ;Done
  MOV  [DI].ControlMin,AX ;Store Min
  MOV  [DI].ControlMax,BX ;  and Max
  POP  DX,CX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE AND STORE THE OFFSET OF THE NEXT CONTROL ENTRY
;Inputs:  BL = Current Offset
;         ReportCount, ReportSize
;Outputs: BL = BL + (ReportCount * ReportSize)
;         Preserves Flags!
;Changes:
;------------------------------------------------------------------------------
CalcNextOffset:
  PUSHF                    ;Save flags
  PUSH AX                  ;Save used registers
  MOV  AL,B ReportCount[0] ;Get Report Count
  MUL  B ReportSize[0]     ;Multiply by Report Size
  ADD  BL,AL               ;Add it to the Offset
  POP  AX                  ;Restore used registers
  POPF                     ;Restore Flags
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO HANDLE JOYSTICK MOVEMENTS AND BUTTONS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;THIS CODE IS PROCESSED EVERY TIME AN INTERRUPT COMES FROM THE DEVICE
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Offset (contains DeviceData)
;Outputs: Processes "Raw" Joystick data into an appropriate format
;Changes: Lots of things
;------------------------------------------------------------------------------
DeviceChangeCode:
  OR   CX,CX                  ;Did we get any data?
  JZ  >V90                    ;If not, quit
  MOV  AL,[BX].DIReportIDData ;Is it the Correct
  CMP  AL,[BX].DIReportID     ;  Report ID?
  JNE >V90                    ;If not, try the next possibility
  CALL DeviceChangeJoystick   ;If so, handle it
V90:                          ;Done
  RETF                        ;Quit

;------------------------------------------------------------------------------
;HANDLE THE DEVICE NOTIFICATION FOR THE JOYSTICK REPORT ID
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Offset (contains DeviceData)
;Outputs: Processes "Raw" Joystick data into an appropriate format
;Changes: Lots of things, including possibly all Registers
;------------------------------------------------------------------------------
DeviceChangeJoystick:
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO PROCESS NOTIFICATIONS FROM THE HOST DRIVER REGARDING DEVICE CHANGES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;HANDLE A NOTIFICATION FROM THE HOST DRIVER FOR SOMETHING RELATED TO THE DEVICE
;Inputs:   DS = ES = TSR Data Area
;          AX  = Device Owner Call Code (various possibilities)
;         [BX] = Info Table Offset
;          CX, DX = Varies by exact type of call
;Outputs: Depends on Call Type
;         Calls appropriate "subfunction"
;Changes: Nearly all registers
;------------------------------------------------------------------------------
DvcOwnerFarCall:
  MOV  DI,BX                   ;Point DI at the Info Table Entry
  MOV  BX,OwnerCallFunctionTbl ;Point at the Function table to search
  CALL SearchByteToWordTbl     ;Look for the function (in AL)
  JC  >H90                     ;If invalid function, quit
  CALL BX                      ;If OK, do it
H90:
  RETF

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST SAYING THAT A NEW DEVICE HAS BEEN ADDED
;Inputs:   DS = ES = TSR Data Area
;         [DI] = Info Table Entry
;          CL  = Host Index
;          CH  = Device Address
;          DL  = Interface
;          DH  = Alternate Interface
;Outputs:
;Changes: Prompts us to look for a new Device earlier than normal
;NOTES:   Originally, this routine directly called LookForNewDevice.
;         For some reason, this did not work with some Devices (they would
;           return bad descriptors).  This is probably due to the Devices not
;           being given enough time to "reorganize" themselves after the
;           enumeration process of the Host Driver, which downloads the same
;           descriptors we download again here.
;         So now, instead of calling LookForNewDevice directly, this routine
;           sets the Int08Counter to a small number so that we look for
;           the new Device after a small delay.  Doing it after a small
;           delay, instead of doing it immediately, seems to work better
;           (at least with some Devices).
;------------------------------------------------------------------------------
DeviceAdded:
  CMP  Int08Counter,0 ;Disabled?
  JE  >A90            ;If so, quit
  MOV  Int08Counter,2 ;If not, look for a new Device right away
A90:                  ;Done
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST SAYING THAT A DEVICE HAS BEEN DISCONNECTED
;Inputs:   DS = ES = TSR Data Area
;         [DI] = Info Table Entry
;          CL  = Host Index
;          CH  = Device Address
;Outputs:
;Changes: Removes Device from Configuration
;NOTES:   The Host Driver should automatically close the Periodic Interrupt
;         This is called if a REGISTERED Device has been disconnected.
;         This is also called when an Upstream device is Disconnected
;------------------------------------------------------------------------------
DeviceDisconnected:
  CALL ResetDvcEntryDI ;Delete entry from our table
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST SAYING THAT AN UNREGISTERED DEVICE IS DISCO'D
;Inputs:  DS = ES = TSR Data Area
;         CL = Host Index
;         CH = Device Address
;Outputs:
;Changes:
;NOTES: This is called if an UNREGISTERED Device has been disconnected,
;         so it will NOT be in DeviceInfoTable.  It may or may not be
;         in the BadDeviceTable.
;------------------------------------------------------------------------------
Device0Disconnected:
  CALL DeleteBadDevices ;Delete the Entries from Bad Device Table (Uses CX)
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST TO TEMPORARILY STOP LOOKING FOR DEVICES
;Inputs:   DS = ES = TSR Data Area
;         [DI] = Info Table Pointer
;          CL  = Host Index
;          CH  = Device Address
;          DL  = Interface Number
;          DH  = TimeOut Value to use (deciseconds)
;Outputs: DontLookCounter
;Changes:
;NOTES: This should only be called if a Device-specific Driver is trying
;         to take control of an already registered device from us.
;       We actually stop looking for ALL new Interfaces, rather than just
;         the one that is being requested.
;------------------------------------------------------------------------------
StartDontLook:
  PUSH AX,BX,DX,SI        ;Save used registers
  MOV  DontLookCounter,-1 ;Start not looking
  MOV  SI,Int14Request2   ;Point at Request Structure
  CALL RlsDvcNoResetDI    ;Release the Device, don't Reset it
  MOV  AL,DH              ;Put deciseconds in AL
  XOR  AH,AH              ;Multiply
  MOV  BX,18206           ;  deciseconds
  MUL  BX                 ;  by
  MOV  BX,10000           ;  1.8206
  DIV  BX                 ;  (number of timer ticks per decisecond)
  CMP  DX,5000            ;Do we need to round up?
  JB  >S90                ;If not, continue
  INC  AX                 ;If so, round up
S90:                      ;Done
  INC  AX                 ;Add one more Timer Tick for good measure
  MOV  DontLookCounter,AX ;Store the Counter Value
  POP  SI,DX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM A HOST SAYING THAT IT'S BEEN UNINSTALLED
;Inputs:   DS = ES = TSR Data Area
;         [DI] = Info Table Pointer
;          CL  = USB Host Index that isn't there any more
;Outputs:
;Changes: Removes appropriate Host Index & Devices from Tables
;------------------------------------------------------------------------------
HostDisconnected:
  PUSH BX,DI                    ;Save used registers
  MOV  DI,DeviceInfoTable       ;Point at table
  MOV  BL,MaxDevices            ;Number of table entries
D20:                            ;Loop to here for each table entry
  TEST [DI].DIFlags,DIFlagInUse ;Empty Table Entry?
  JZ  >D40                      ;If so, skip it
  CMP  [DI].DIHostIndex,CL      ;Is it the Host this Device is on?
  JNE >D40                      ;If not, skip it
  CALL ResetDvcEntryDI          ;If so, delete it from the Info Table
D40:                            ;Done with this table entry
  ADD  DI,DeviceInfoStrucSize   ;Point at the next Table Entry
  DEC  BL                       ;Decrement loop counter
  JNZ  D20                      ;If not done yet, keep looking
  POP  DI,BX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE VARIOUS REQUESTS FROM THE USB HOST DRIVER ABOUT UPSTREAM CHANGES
;Inputs:   DS = ES = TSR Data Segment
;          Other registers: Depends on Call
;Outputs:
;Changes: We don't care about any of these calls, since they don't directly
;           affect our program.  Any critical changes to upstream devices
;           are eventually reflected to us as a call directly addressing
;           one of our Devices, which is how we handle those things.
;------------------------------------------------------------------------------
DeviceEnabled:        ;Device Enabled
DeviceDisabled:       ;Device Disabled
DeviceReset:          ;Device Reset
DeviceSuspend:        ;Device Suspended
DeviceResume:         ;Device Resumed
DevicePowerOn:        ;Device Power On
DevicePowerOff:       ;Device Power Off
DevicePowerReset:     ;Device Power Reset
NewHostAdded:         ;New Host Added
HostHWErr:            ;Host System/Processor Error
HostRun:              ;Host Running
HostStop:             ;Host Stopped
HostReset:            ;Host Reset
HostSuspend:          ;Host Suspended
HostResume:           ;Host Resumed
HostDebug:            ;Host in Debug Mode
HostSST:              ;Host Single-Stepped
TimingChange:         ;Host Timing Change
DeviceDisconnectedUS: ;Device Disconnect Upstream
DeviceEnabledUS:      ;Device Enable Upstream
DeviceDisabledUS:     ;Device Disable Upstream
DeviceResetUS:        ;Device Reset Upstream
DeviceSuspendUS:      ;Device Suspend Upstream
DeviceResumeUS:       ;Device Resume Upstream
DevicePowerOnUS:      ;Device Power On Upstream
DevicePowerOffUS:     ;Device Power Off Upstream
DevicePowerResetUS:   ;Device Power Reset Upstream
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SUPPORT CODE TO RELEASE/RESET A DEVICE
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;COMPLETELY RELEASE A DEVICE THAT WE CURRENTLY OWN (POSSIBLY INCLUDING A RESET)
;Inputs:   DS = ES = TSR Data Area
;         [DI] = Device Info Entry to Release
;         [SI] = Int 14 Request to use
;Outputs: Releases Device Entry
;           Closes Periodic Handle
;           Unregisters as Interface Owner
;           Resets Device (if appropriate)
;           Resets Device Info Entry
;Changes:
;------------------------------------------------------------------------------
RlsDvcNoResetDI:
  PUSH BX                                        ;Save used registers
  XOR  BL,BL                                     ;Mark as No Reset
  JMP >R00                                       ;Do it
RlsDvcDoResetDI:
  PUSH BX                                        ;Save used registers
  MOV  BL,-1                                     ;Mark as a Reset
;  JMP >R00                                       ;Do it
R00:
  PUSH AX                                        ;Save used registers
  MOV  W [SI].I14RCallBackAddr[0],-1             ;Set CallBackAddr to nothing
  MOV  AX,W [DI].DIHostIndex                     ;Copy Host Index and
  MOV  W [SI].I14RHostIndex,AX                   ;  Device Address
  MOV  AX,[DI].DIIntHandle                       ;Copy
  MOV  [SI].I14RRequestHandle,AX                 ;  Interrupt Handle Number
  MOV  [SI].I14RRequestType,I14RRTCloseHandle    ;Request = Close Handle
  CALL DoInt14CallSaveAllSI                      ;Do it
  MOV  AX,[DI].DIRegHandle                       ;Copy
  MOV  [SI].I14RRequestHandle,AX                 ;  Registry Handle
  MOV  [SI].I14RRequestType,I14RRTUnRegIntfOwner ;Request = UnRegister
  CALL DoInt14CallSaveAllSI                      ;Do it
  OR   BL,BL                                     ;Need to Reset the Device?
  JZ  >R80                                       ;If not, we're done
  MOV  [SI].I14RRequestType,I14RRTResetDevice    ;Request = Reset
  CALL DoInt14CallSaveAllSI                      ;Do it
R80:                                             ;Done with Host
  CALL ResetDvcEntryDI                           ;Delete the Device Info Entry
  POP  AX                                        ;Restore used registers
  POP  BX                                        ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SUPPORT CODE FOR DEVICE INFO TABLE
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;RESET A DEVICE INFO TABLE ENTRY
;Inputs:  DS = ES = TSR Data Area
;        [BX] or [DI] = Info Table Entry to reset
;         CLD already issued
;Outputs:
;Changes: Resets (clears out) Info Table Entry
;NOTES:   This preserves the Disabled/Enabled status of
;           the Device, so that the user does not need to worry about
;           re-setting it after a temporary Disconnect or Host Reset.
;         Unlike most routines, this one preserves Flags across the Call!
;------------------------------------------------------------------------------
ResetDvcEntryBX:
  PUSH DI              ;Save used registers
  MOV  DI,BX           ;Point DI at the Table Entry
  CALL ResetDvcEntryDI ;Reset the Entry
  POP  DI              ;Restore used registers
  RET

ResetDvcEntryDI:
  PUSH AX,CX,DI ;Save used registers
  PUSHF         ;Save flags
  CLI           ;Disable interrupts
  XOR  AX,AX    ;Need to store zeroes
  MOV  CX,(DeviceInfoStrucSize/2) ;Number of words to store
  REP  STOSW    ;Reset the reset of the table
  POPF          ;Restore flags
  POP  DI,CX,AX ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO FIND/STORE ENTRIES IN VARIOUS STRUCTURE-BASED TABLES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;CONVERT A DEVICE INFO INDEX TO AN OFFSET
;Inputs:  ES = Data Segment where data is located
;         CL = Device Index, or BL = Device Index (if DIIndex2OffsetBL)
;Outputs: DI = Offset to Table Entry (Assuming ES is the segment)
;Changes:
;------------------------------------------------------------------------------
DIIndex2OffsetBL:
  PUSH CX             ;Save used registers
  MOV  CL,BL          ;Put Index in CL
  CALL DIIndex2Offset ;Do it
  POP  CX             ;Restore used registers
  RET

DIIndex2Offset:
  PUSH AX,CX,DX               ;Save used registers
  XOR  CH,CH                  ;Index is actually a byte
  MOV  DI,DeviceInfoTable     ;Offset to beginning of Table
  MOV  AX,DeviceInfoStrucSize ;Size of each entry
  MUL  CX                     ;Multiply Index by the size of each entry
  ADD  DI,AX                  ;Add the offset of the beginning of the table
  POP  DX,CX,AX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND AN EMPTY INFO TABLE ENTRY
;Inputs:  DS = ES = TSR Data Area
;Outputs: ZF = Clear if OK (found an empty Table entry)
;              [BX] = Table Entry
;            = Set if error (Table is full)
;               BX  = 0
;Changes:
;------------------------------------------------------------------------------
FindEmptyInfoBX:
  PUSH CX                       ;Save used registers
  MOV  BX,DeviceInfoTable       ;Point at first Table Entry
  MOV  CX,MaxDevices            ;Number of Table Entries
E10:                            ;Loop to here to find empty Table Entry
  TEST [BX].DIFlags,DIFlagInUse ;Empty Table entry?
  JZ  >E90                      ;If so, continue
  ADD  BX,DeviceInfoStrucSize   ;Point at next table entry
  LOOP E10                      ;Keep looking
  XOR  BX,BX                    ;Table is full!
E90:                            ;Done
  OR   BX,BX                    ;Set Return Flag
  POP  CX                       ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SUPPORT CODE FOR BAD DEVICE TABLE
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;ADD A BAD (INCOMPATIBLE) DEVICE HOST & ADDRESS TO THE TABLE
;Inputs:  DS:[SI] = Int 14 Structure containing Bad Device Data (Host/Addr/Intf)
;Outputs: Adds Host & Address to Bad Device Table
;Changes:
;------------------------------------------------------------------------------
AddBadDevice:
  PUSH AX,BX,CX                 ;Save used registers
  PUSHF                         ;Save Flags
  CLI                           ;Disable Interrupts
  MOV  BX,BadDeviceTable        ;Point at Table
  MOV  CX,MaxBadDevices         ;Number of Entries in Table
A20:                            ;Loop here to find empty table entry
  TEST [BX].BDFlags,BDFlagInUse ;This entry already used?
  JNZ >A50                      ;If so, skip it
  OR   [BX].BDFlags,BDFlagInUse ;If not, mark it as used
  MOV  AX,W [SI].I14RHostIndex  ;Store the Host Index
  MOV  W [BX].BDHostIndex,AX    ;  & Device Address
  MOV  AL,[SI].I14RInterfaceNum ;Store the
  MOV  [BX].BDInterfaceNum,AL   ;  Interface Number
  MOV  AX,NewDeviceError        ;Store the
  MOV  [BX].BDNewDeviceError,AX ;  Error Code
  MOV  AL,NewDeviceStage        ;Store the
  MOV  [BX].BDNewDeviceStage,AL ;  New Device Stage
  JMP >A90                      ;Done
A50:                            ;Entry not Empty
  ADD  BX,BadDeviceStrucSize    ;Point at next entry
  LOOP A20                      ;Keep going until we're done
A90:                            ;Done
  POPF                          ;Restore Flags
  POP  CX,BX,AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DELETE BAD (INCOMPATIBLE) DEVICE HOSTS & ADDRESSES FROM THE TABLE
;Inputs:  CL = Host Index, CH = Address
;Outputs: Deletes Host & Address from Bad Device Table
;Changes:
;NOTES: This is called as the result a a Device Disconnect, so we need to
;         delete all Interfaces of the Device.  There will probably only
;         be one Entry per Address, but we need to check all of them.
;------------------------------------------------------------------------------
DeleteBadDevices:
  PUSH AX,BX,CX,DX,DI            ;Save used registers
  PUSHF                          ;Save Flags
  MOV  AX,CX                     ;AL = Host Index, AH = Device Address
  MOV  DL,-1                     ;Don't care about the Interface Number
  CLI                            ;Disable Interrupts
D10:                             ;Loop here for each Table Entry
  CALL FindBadDeviceEntry        ;Any entries in the Table (returns BX)?
  JC  >D90                       ;If not, we're done
  PUSH AX                        ;If so, save used registers
  XOR  AX,AX                     ;Write zeroes
  MOV  CX,(BadDeviceStrucSize/2) ;Number of words to write
  MOV  DI,BX                     ;Point DI at Table Entry
  REP  STOSW                     ;Write them
  POP  AX                        ;Restore used registers
  JMP  D10                       ;Keep looking for more Entries
D90:                             ;Done
  POPF                           ;Restore Flags
  POP  DI,DX,CX,BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COMPARE A HOST AND ADDRESS TO SEE IF IT'S IN OUR BAD DEVICE TABLE
;Inputs:  CL = Host, CH = Address, DL = Interface, DH = Alt Interface
;Outputs: CF = Set if it's not in the Bad Device Table
;            = Clear if it is in Table
;Changes:
;NOTES: This ignores the Alt Interface Setting (we don't care about it).
;------------------------------------------------------------------------------
TestBadDevice:
  PUSH AX,BX              ;Save used registers
  MOV  AX,CX              ;AL = Host Index, AH = Device Address
                          ;DL = Interface Number
  CALL FindBadDeviceEntry ;Is it in the Table (returns BX)?
  POP  BX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND A MATCHING HOST/ADDRESS/INTERFACE ENTRY IN THE BAD DEVICE TABLE
;Inputs:  AL = Host Index to find
;         AH = Device Address to find
;         DL = Interface Number to find
;              if -1, we don't care about the Interface Number
;Outputs: CF = Clear if Entry Found
;              BX = Entry Offset
;            = Set if Entry not found
;              BX = Undefined
;Changes:
;------------------------------------------------------------------------------
FindBadDeviceEntry:
  PUSH CX                       ;Save used registers
  PUSHF                         ;Save flags
  MOV  BX,BadDeviceTable        ;Point at Table
  MOV  CX,MaxBadDevices         ;Number of Entries in Table
  CLI                           ;Disable Interrupts
E20:                            ;Loop here to find empty table entry
  TEST [BX].BDFlags,BDFlagInUse ;Valid Entry?
  JZ  >E50                      ;If not, skip it
  CMP  W [BX].BDHostIndex,AX    ;Do the Host Index & Device Address match?
  JNE >E50                      ;If not, skip it
  CMP  DL,-1                    ;Do we care about the Interface Number?
  JE  >E80                      ;If not, we've found it!
  CMP  [BX].BDInterfaceNum,DL   ;Does the Interface Number match?
  JE  >E80                      ;If so, we've found it!
E50:                            ;Go to next entry
  ADD  BX,BadDeviceStrucSize    ;Point at the next entry
  LOOP E20                      ;Keep looking
E70:                            ;Error
  POPF                          ;Restore Flags
  STC                           ;Set Not Found Flag
  JMP >E90                      ;Done
E80:                            ;OK
  POPF                          ;Restore Flags
  CLC                           ;Set Found Flag
E90:                            ;Done
  POP  CX                       ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO ISSUE INT 14h CALLS TO THE HOST DRIVER
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;ISSUE AN INT 14 CALL TO THE USB HOST
;Inputs:  DS:[DX] = Int 14 Request (filled with appropriate data)
;      OR DS:[DI]
;      OR DS:[SI]
;Outputs: CF = Set if error (host stalled, etc.)
;            = Clear if OK
;Changes: Sends the appropriate info to the USB Host via Int 14 call
;------------------------------------------------------------------------------
DoInt14CallSaveAllSI:       ;Save all changed registers
                            ;  [SI] = Int14 Structure
  PUSH DX                   ;Save used registers
  MOV  DX,SI                ;Point DX at the Data Offset
  CALL DoInt14CallSaveAllDX ;Do it
  POP  DX                   ;Restore used registers
  RET

DoInt14CallAXSI:     ;Save all changed registers except AX,
                     ;  [SI] = Int14 Structure
  PUSH DX            ;Save used registers
  MOV  DX,SI         ;Point DX at the Data Offset
  CALL DoInt14CallAX ;Do it
  POP  DX            ;Restore used registers
  RET

DoInt14CallCXDI:     ;Save all changed registers except CX,
                     ;  [DI] = Int14 Structure
  PUSH DX            ;Save used registers
  MOV  DX,DI         ;Point DX at the Data Offset
  CALL DoInt14CallCX ;Do it
  POP  DX            ;Restore used registers
  RET

DoInt14CallAXCXSI:     ;Save all changed registers except AX & CX
                       ;  [SI] = Int14 Structure
  MOV  DX,SI           ;Point DX at the Data Offset
  CALL DoInt14CallAXCX ;Do it
  RET

DoInt14CallCXDXSI:     ;Save all changed registers except CX & DX
                       ;  [SI] = Int14 Structure
  MOV  DX,SI           ;Point DX at the Data Offset
  CALL DoInt14CallCXDX ;Do it
  RET

DoInt14CallSaveAllDX: ;Save all changed registers
                      ;  [DX] = Int14 Structure
  PUSH AX,BX,CX,DX    ;Save registers that Int14 changes!
  CALL DoInt14Call    ;Do it
  POP  DX,CX,BX,AX    ;Restore used registers
  RET

DoInt14CallAX:
  PUSH BX,CX,DX    ;Save registers that Int14 changes, except AX!!
  CALL DoInt14Call ;Do it
  POP  DX,CX,BX    ;Restore used registers
  RET

DoInt14CallCX:
  PUSH AX,BX,DX    ;Save registers that Int14 changes, except CX!!
  CALL DoInt14Call ;Do it
  POP  DX,BX,AX    ;Restore used registers
  RET

DoInt14CallAXCX:
  PUSH BX,DX       ;Save registers that Int14 changes, except AX & CX!!
  CALL DoInt14Call ;Do it
  POP  DX,BX       ;Restore used registers
  RET

DoInt14CallCXDX:
  PUSH AX,BX       ;Save registers that Int14 changes, except CX & DX!!
  CALL DoInt14Call ;Do it
  POP  BX,AX       ;Restore used registers
  RET

;Int 14 returns various values in AX, BX, CX, & DX
DoInt14Call:
  STI           ;Enable Interrupts
  MOV  AX,5001h ;Set Function
  MOV  BX,'US'  ;Set
  MOV  CX,'B!'  ;  Registers
  STC           ;Preset Error Flag
  INT  14h      ;Do it
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO HANDLE INT 15h (BIOS Services)
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;HAVE INTERRUPT 15h (BIOS SERVICES) RUN THIS PROGRAM EVERY TIME IT OCCURS
;Inputs:
;Outputs: Emulates a Game Port with a Joystick Attached to it
;         Depending on Exact Call SubType (value in DX), will return
;           various values in AX, BX, CX and/or DX
;Changes:
;------------------------------------------------------------------------------
Int15Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int15:
  PUSH BP                          ;Save used registers
  MOV  BP,SP                       ;Point BP at the Stack
  PUSHF                            ;Save flags
  STI                              ;Enable interrupts
  CLD                              ;Go forward with string functions
  CALL MakeStack                   ;Set up our own stack space
  PUSH DS,ES                       ;Save used registers
  MOV  DS,CS                       ;Point DS
  MOV  ES,CS                       ;  and ES at our data area
  CMP  AH,Int15JoystickCall        ;Is it one of the Joystick functions?
  JNE  Int15Old                    ;If not, it's not for us
  TEST TSRFlags,DisableSts         ;Are we Disabled?
  JNZ  Int15Old                    ;If so, quit
  CALL GotAJoystick                ;Are there any Enabled Joysticks?
  JC   Int15Old                    ;If not, quit
  CMP  DX,-1                       ;Is it the Digital Install Check Function?
  JE  >F40                         ;If so, jump to handle it
  CMP  DX,1                        ;Is it Function 0 (Buttons) or 1 (Axes)?
  JE  >F30                         ;If Function 1, jump to handle it
  JA  >F70                         ;If neither Buttons nor Axes, Error
F20:                               ;Get Buttons
  AND  AL,0Fh                      ;Mask out old values in high nibble of AL
  OR   AL,BtnMask                  ;Mask in buttons to hoigh nibble of AL
  JMP >F80                         ;Done
F30:                               ;Get Axes
  CALL CopyXxValues                ;Get the Axes values (AX, BX, CX, DX)
  JMP >F80                         ;Done
F40:                               ;Digital Joystick Install Check
  INC  DX                          ;Set DX = 0 for the Return
  JMP >F80                         ;Done
F70:                               ;Error
  POP  ES,DS                       ;Restore used registers
  CALL ReturnStack                 ;Restore original Stack Space
  OR   B SS:[BP+6],CarryFlag       ;Set Carry Flag for the return
  MOV  AH,I15ErrFuncNoSupt         ;Set Error Code (Function not Supported)
  JMP >F90                         ;Done
F80:                               ;No Error
  POP  ES,DS                       ;Restore used registers
  CALL ReturnStack                 ;Restore original Stack Space
  AND  B SS:[BP+6],(NOT CarryFlag) ;Clear Carry Flag for the return
;  JMP >F90                         ;Done
F90:                               ;Done
  POPF                             ;Restore flags
  POP  BP                          ;Restore used registers
  IRET

Int15Old:
  POP  ES,DS                       ;Restore used registers
  CALL ReturnStack                 ;Restore original Stack Space
  POPF                             ;Restore flags
  POP  BP                          ;Restore used registers
  JMP  D CS:Int15Hdr.OldVector     ;Let the Old Interrupt Handler do it


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO HANDLE INT 08h (Timer Tick)
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;HAVE INTERRUPT 08 (TIMER) RUN THIS EVERY TIME IT OCCURS
;Inputs:
;Outputs: Searches for Newly attached Device
;Changes:
;------------------------------------------------------------------------------
Int08Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int08:
;  PUSHF                          ;Save flags
  PUSHF                          ;Call the
  CALL D CS:Int08Hdr.OldVector   ;  Old Interrupt Handler
  STI                            ;Enable interrupts
  CLD                            ;Go forward with string functions
  CALL MakeStack                 ;Set up our own Stack Space
  PUSH DI,SI,DS,ES               ;Save used registers
  MOV  DS,CS                     ;Point DS and
  MOV  ES,CS                     ;  ES at our data area
  AND  TSRFlags2,(NOT SameTick)  ;Mark as a new Timer Tick
  MOV  BtnMask,0F0h              ;Assume no valid
  CALL ResetxXValues             ;  joysticks installed
  CALL GotAJoystick              ;Are there any valid joysticks?
  JC  >E10                       ;If not, continue
  CALL GetButtons                ;Calculate Buttons status (BtnMask)
  CALL GetAxes                   ;Calculate Axes values (AXValue, etc.)
E10:                             ;Buttons & Coordinates calculated
  MOV  DI,OFFSET Int08Counter    ;Point DI at Int08Counter
  MOV  SI,OFFSET DontLookCounter ;Point BX at DontLookCounter
  CMP  W [SI],0                  ;Need to Decrement DontLookCounter?
  JE  >E20                       ;If not, continue
  DEC  W [SI]                    ;If so, Decrement it
  JNZ >E90                       ;If not 0 yet, quit
  CMP  B [DI],0                  ;Disabled?
  JE  >E90                       ;If so, quit
  MOV  B [DI],1                  ;If not, look for Device right away
E20:                             ;Done with DontLookCounter
  CMP  B [DI],0                  ;Are we supposed to look yet?
  JE  >E90                       ;If not, just quit
  DEC  B [DI]                    ;Decrement the Delay Counter
  JNZ >E90                       ;If not 0 yet, quit
  MOV  B [DI],182                ;If 0, reset for next time (approx 10 seconds)
  PUSH AX,BX,CX,DX,BP            ;Save used registers
  CALL LookForNewDevice          ;Look for a new Device
  POP  BP,DX,CX,BX,AX            ;Restore used registers
E90:                             ;Done
  POP  ES,DS,SI,DI               ;Restore used registers
  CALL ReturnStack               ;Restore Original Stack Space
  IRET
;  POPF                           ;Restore flags
;  JMP  D CS:Int08Hdr.OldVector   ;Jump to old Interrupt

;------------------------------------------------------------------------------
;TEST AND SEE IF THERE ARE ANY ENABLED JOYSTICKS INSTALLED
;Inputs:  DS = ES = TSR Data Area
;Outputs: CF = Clear if there is at least one Enabled Joystick
;            = Set if no enabled Joysticks
;Changes:
;------------------------------------------------------------------------------
GotAJoystick:
  PUSH BX,CX                    ;Save used registers
  MOV  BX,DeviceInfoTable       ;Point at the Table
  MOV  CX,MaxDevices            ;Number of Table Entries
G10:                            ;Loop to here for each table Entry
  TEST [BX].DIFlags,DIFlagInUse ;Is a Joystick installed here?
  JNZ >G80                      ;If so, we've got one
  ADD  BX,DeviceInfoStrucSize   ;Point at the next Table Entry
  LOOP G10                      ;Keep looking
G70:                            ;No Enabled Joysticks
  STC                           ;Set return flag
  JMP >G90                      ;Done
G80:                            ;At least one Enabled Joystick
  CLC                           ;Set return flag
G90:                            ;Done
  POP  CX,BX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE BUTTONS STATUS APPROPRIATE FOR THE INT 15h CALL
;Inputs:  DS = ES = TSR Data Area
;         BtnMask = F0h (No buttons pressed)
;Outputs: BtnMask (High Nibble = Buttons Status, Low Nibble = 0)
;Changes:
;------------------------------------------------------------------------------
GetButtons:
  PUSH AX,CX,DX,DI,SI    ;Save used registers
  MOV  DI,OFFSET BtnMask ;[DI] = BtnMask
;  MOV  B [DI],0F0h       ;Initialize Mask to no buttons pressed
  MOV  DH,TSRFlags       ;DH = TSRFlags
  MOV  SI,A01Xlat        ;[SI] = Button 01 Translation Table entry
  TEST DH,Buttons15      ;Using the 15-button protocol?
  JNZ >B30               ;If so, jump to handle it
B10:                     ;Using 4-button protocol
  MOV  CX,4              ;Need to get 4 buttons
  MOV  DL,(NOT 10h)      ;DL = Mask to use for BtnMask
B15:                     ;Loop to here for each button
  CALL GetControlData    ;Get the button data (uses [SI], rtns AX)
  JZ  >B20               ;If not pressed, continue
  AND  [DI],DL           ;If so, mask it in
B20:                     ;Button masked in, if appropriate
  SHL  DL,1              ;Shift the bit mask over
  ADD  SI,XlatStrucSize  ;Point at the next button
  LOOP B15               ;Keep going until we're done
  JMP >B90               ;Done
B30:                     ;Using 15 button protocol
  MOV  CX,15             ;Need to test 15 buttons
  MOV  DL,10h            ;DL = Button Mask
B35:                     ;Loop to here for each button
  CALL GetControlData    ;Get the button data (uses [SI], rtns AX)
  JZ  >B40               ;If not pressed, continue
  NOT  DL                ;If pressed,
  AND  [DI],DL           ;  set the button mask
  JMP >B90               ;done
B40:                     ;Button masked in, if appropriate
  ADD  DL,10h            ;Update the bit mask
  ADD  SI,XlatStrucSize  ;Point at the next button
  LOOP B35               ;Keep going until we're done
B90:                     ;Done
  POP  SI,DI,DX,CX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE AXES STATUS FOR THE INT 15h CALL
;Inputs:  DS = ES = TSR Data Area
;         AXValue ... DXValue = 0
;Outputs: AXValue, BXValue, CXValue, DXValue
;Changes:
;------------------------------------------------------------------------------
GetAxes:
  PUSH AX,CX,DX,DI,SI    ;Save used registers
  MOV  DI,OFFSET AXValue ;[DI] = AXValue
  MOV  DH,TSRFlags       ;DH = TSRFlags
  MOV  SI,AXXlat         ;[SI] = Axis AX Translation Table entry
  MOV  CX,4              ;Assume no ThrustMaster
  TEST DH,ThrustMaster   ;ThrustMaster Protocol?
  JZ  >X10               ;If not, continue
  INC  CX                ;If so, we need 5 axes
X10:                     ;CX = Number of Axes to get
  CALL GetControlData    ;Get the Axis data (uses [SI], rtns AX)
  STOSW                  ;Store the coordinate
  ADD  SI,XlatStrucSize  ;Point at the next Axis
  LOOP X10               ;Keep going until we're done
  TEST DH,ThrustMaster   ;ThrustMaster Protocol?
  JZ  >X90               ;If not, we're done
  CALL CookThrust        ;If so, calculate the ThrustMaster coordinates
X90:                     ;Done
  POP  SI,DI,DX,CX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE DATA FOR A PARTICULAR CONTROL
;Inputs:   DS = ES = TSR Data Area
;         [SI] = Translation Table entry to use
;                  (Contains RELATIVE Joystick Index, Control Index & Type)
;Outputs:  CF = Clear if OK
;               AX = Control Data
;               ZF = Set if AX = 0
;                  = Clear if AX <> 0
;             = Set if error (Invalid Translation table entry)
;               AX = 0
;               ZF = Set
;Changes:
;NOTES: If Control is a "Button Pair" (two buttons acting as an Axis),
;         this returns NormalizeMax/2 if neither or both buttons are pressed,
;         MinCoord if the first (Left/Up) button is pressed, NormalizeMax
;         if the second (Right/Down) button is pressed).
;------------------------------------------------------------------------------
GetControlData:
  PUSH BX,DI,SI,BP              ;Save used registers
  MOV  BL,[SI].XlatJIndex       ;BL = Joystick Index to use
  CMP  BL,-1                    ;Valid Index?
  JE  >C70                      ;If not, error
  CALL DIIndex2OffsetBL         ;If OK, [DI] = Device Info Pointer
  TEST [DI].DIFlags,DIFlagInUse ;Is a Joystick installed here?
  JZ  >C70                      ;If not, error
  MOV  BX,W [SI].XlatCType      ;BL = Control Type, BH = Control Index
  CMP  BL,-1                    ;Valid Control Type?
  JE  >C70                      ;If not, error
  CMP  BH,-1                    ;Valid Control Index?
  JE  >C70                      ;If not, error
  MOV  BP,SI                    ;[BP] = Translation Table entry
  LEA  SI,[DI].DIRawData        ;[SI] = Raw Data
  ADD  DI,OFFSET DIControlsData ;[DI] = Translation Data
  CMP  BL,CTDPad                ;Is it a DPad or a Button?
  JA  >C40                      ;If not, handle a regular Axis
C10:                            ;It's a button or a DPad
  CALL GetDIDataWord            ;Get the "Raw" Data (AX)
  CALL HandleSpecialCtl         ;Convert it as appropriate
  JNC >C80                      ;If it worked, we're done
  JMP >C70                      ;If not, error
C40:                            ;It's a "regular" Axis
  CALL GetDINormalized          ;Get the Normalized Data (AX)
  JNC >C80                      ;If it worked, we're done
C70:                            ;Error
  XOR  AX,AX                    ;AX = 0 (also sets ZF)
  STC                           ;Set CF return flag
  JMP >C90                      ;Done
C80:                            ;OK
  OR   AX,AX                    ;Set ZF return flag
  CLC                           ;Set CF return flag
C90:                            ;Done
  POP  BP,SI,DI,BX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE THE SPECIAL CONTROL PROCESSING NEEDED FOR A DPAD OR BUTTON
;Inputs:   DS  = ES = TSR Data Area
;         [BP] = Translation Table entry we're processing
;         [SI] = Raw Data
;         [DI] = Controls Data
;          BL  = Control Type (CTDPad or CTButton)
;          AX  = "Raw" Data (if DPad = 0-7, If Button = first button)
;Outputs: CF = Clear if OK
;              AX = Appropriate Data
;            = Set if Error
;              AX = 0
;Changes:
;------------------------------------------------------------------------------
HandleSpecialCtl:
  PUSH BX,DX           ;Save used registers
  CMP  BL,CTButton     ;Is it a Button?
  JNE >S40             ;If not, handle a DPAd
  MOV  BH,DS:[BP].XlatCIndex2 ;BH = Second Index
  CMP  BH,-1           ;Is this a Button Pair simulating an Axis?
  JE  >S80             ;If not, it's OK
S10:                   ;Is two buttons simulating an Axis
  MOV  DL,AL           ;If so, DL = first button status
  CALL GetDIDataWord   ;Get the second button Data (AX)
  JC  >S70             ;If error, quit
  MOV  DH,AL           ;If OK, DH = Second Button Status
  MOV  AX,NormalizeMax ;Assume maximized
  OR   DL,DL           ;Left/Up button pressed?
  JNZ >S30             ;If so, jump to handle it
S20:                   ;Left/Up button NOT pressed
  OR   DH,DH           ;Right/Down button pressed?
  JNZ >S80             ;If so, it's maximized
  SHR  AX,1            ;If not, it's centered
  JMP >S80             ;Done
S30:                   ;Left/up button pressed
  SHR  AX,1            ;Assume centered (both buttons pressed)
  OR   DH,DH           ;Is the right/down button also pressed?
  JNZ >S80             ;If so, we're done
  MOV  AX,MinCoord     ;If not, it's Left/Up
  JMP >S80             ;Done
S40:                   ;Is a DPad
  CALL AdjustDPadData  ;Adjust the DPad data into a "normal" format
  MOV  DX,CookDPadX    ;Assume X direction
  CMP  DS:[BP].XlatCIndex2,XlatDPadX ;Is it X direction?
  JE  >S50             ;If so, continue
  MOV  DX,CookDPadY    ;If not, it's Y Direction
S50:                   ;[DX] = Code to Call
  CALL DX              ;Do the conversion
  JMP >S80             ;Done
S70:                   ;Error
  XOR  AX,AX           ;AX = 0 (also sets ZF)
  STC                  ;Set CF return flag
  JMP >S90             ;Done
S80:                   ;OK
  OR   AX,AX           ;Set ZF return flag
  CLC                  ;Set CF return flag
S90:                   ;Done
  POP  DX,BX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NORMALIZED COORDINATE FOR A SPECIFIC CONTROL
;Inputs:  DS = ES  = TSR Data Area
;         [DI] = Controls Table Base Offset
;         [SI] = Raw Data
;         BL   = ControlType (Button, etc)
;         BH   = Index Number (0-based)
;Outputs: CF = Clear if OK
;              AX = "Normalized" Value for the Control
;            = Set if error (invalid Type or Index)
;              AX = 0
;Changes:
;------------------------------------------------------------------------------
GetDINormalized:
  PUSH BX,CX,DX        ;Save used registers
  CALL GetDIDataWord   ;Get the Word (AX)
  JC  >Z70             ;If Error, Quit (AX = 0)
  CALL GetDIMinMax     ;Get the Min (CX) and Max (DX)
  SUB  AX,CX           ;Compensate Value for Minimum
  SUB  DX,CX           ;Compensate Maximum for Minimum
  MOV  BX,DX           ;Move Maximum into BX
  MUL  NormalizeMax    ;Normalize the
  DIV  BX              ;  current Value
  SHR  BX,1            ;Do we need to
  CMP  DX,BX           ;  round it up?
  JB  >Z30             ;If not, continue
  INC  AX              ;If so, round it up
Z30:                   ;Value is rounded up
  CMP  AX,MinCoord     ;Is it too small?
  JAE >Z40             ;If not, continue
  MOV  AX,MinCoord     ;If so, make it the minimum
  JMP >Z80             ;Done
Z40:                   ;Test for too big (shouldn't be, but we'll test anyway)
  CMP  AX,NormalizeMax ;Is it more than it should be?
  JBE >Z80             ;If not, it's OK
  MOV  AX,NormalizeMax ;If so, just Maximize it
  JMP >Z80             ;Done
Z70:                   ;Error
  STC                  ;Set Error Flag
  JMP >Z90             ;Done
Z80:                   ;OK
  CLC                  ;Set OK flag
Z90:                   ;Done
  POP  DX,CX,BX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET A SPECIFIC WORD VALUE FROM A CONTROL
;Inputs:   ES  = TSR Data Area
;         [DI] = Controls Table Base Offset
;         [SI] = Raw Data
;          BL  = ControlType (Button, etc)
;          BH  = Index Number (0-based)
;Outputs: CF = Clear if OK
;              AX = Appropriate Data Word
;            = Set if error (invalid Type or Index)
;              AX = 0
;Changes
;NOTES: This will find any Index matching the Control Type given, even if the
;         Controls are not contiguous (if there are other types of Controls
;         separating the Controls we are looking for)
;------------------------------------------------------------------------------
GetDIDataWord:
  PUSH BX,CX,DX,DI,SI,DS      ;Save used registers
  MOV  DS,ES                  ;Point DS at TSR Data
  MOV  DL,MaxControls         ;Table Size Counter
W10:                          ;Loop to here to find the correct entry
  CMP  BL,[DI].ControlType    ;Is it the correct Type?
  JNE >W50                    ;If not, try the next one
  MOV  CH,[DI].ControlQty     ;Get Quantity of Controls Here
  MOV  CL,[DI].ControlOffset  ;Get Offset of First Control
W15:                          ;Loop here to find correct Index
  SUB  BH,1                   ;Decrement the Control Index
  JC  >W30                    ;If it's the Index we're looking for, we're done
  DEC  CH                     ;Decrement our local Counter
  JZ  >W50                    ;If no more here, look for another Control Set
  ADD  CL,[DI].ControlNumBits ;Update the Offset
  JMP  W15                    ;Keep looking in this Control Set
W30:                          ;Pointed at correct Entry
  CMP  CL,8                   ;Is the Offset more than a Byte?
  JB  >W40                    ;If not, we're close!
  INC  SI                     ;If so, go to
  SUB  CL,8                   ;  the next byte
  JMP  W30                    ;Keep skipping, if we need to
W40:                          ;Pointed at Correct Byte Offset
  MOV  BX,[SI+2]              ;Copy High Word into BX
  MOV  AX,[SI]                ;Copy Low Word into AX
  OR   CL,CL                  ;Do we need to Shift at all?
  JZ  >W47                    ;If not, we're done with this part
W43:                          ;Loop to here to shift bits to low part of AX
  SHR  AX,1                   ;Move the low bits over
  SHR  BX,1                   ;Move the high bits over
  JNC >W45                    ;If low bit was not set, continue
  OR   AH,80h                 ;If it was set, set the high bit of AX
W45:                          ;Done shifting this bit
  DEC  CL                     ;Decrement our Loop Counter
  JNZ  W43                    ;If not 0 yet, keep shifting
W47:                          ;Done putting bits into low part of AX
  MOV  CL,16                  ;Calculate
  SUB  CL,[DI].ControlNumBits ;  Shift Factor
  SHL  AX,CL                  ;Get rid of
  SHR  AX,CL                  ;  unwanted bits
  JMP >W80                    ;Done
W50:                          ;Go to the next one
  ADD  DI,ControlStrucSize    ;Point at next entry
  DEC  DL                     ;End of Table?
  JNZ  W10                    ;If not, keep looking
W70:                          ;Error
  XOR  AX,AX                  ;Set
  STC                         ;  Error Codes
  JMP >W90                    ;Done
W80:                          ;OK
  CLC                         ;Clear Carry Flag for the return
W90:                          ;Done
  POP  DS,SI,DI,DX,CX,BX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE MIN/MAX VALUES FOR A SPECIFIC CONTROL
;Inputs:   ES  = TSR Data Area
;          BL  = ControlType (Button, etc)
;          BH  = Index Number (0-based)
;         [DI] = Controls Table Base Offset
;         [SI] = Raw Data
;Outputs: CX = Min Value for Coordinate
;         DX = Max Value for Coordinate
;Changes:
;NOTES: This assumes that the Control actually exists (it was tested some
;         other way).
;------------------------------------------------------------------------------
GetDIMinMax:
  PUSH AX,BX,DI,DS         ;Save used registers
  MOV  DS,ES               ;Point DS at TSR Data
  MOV  AL,MaxControls      ;Table Size Counter
X10:                       ;Loop to here for each Table Entry
  CMP  BL,[DI].ControlType ;Is it the correct Type?
  JNE >X50                 ;If not, try the next one
  MOV  AH,[DI].ControlQty  ;Get Quantity of Controls Here
X20:                       ;Loop here to find correct Index
  SUB  BH,1                ;Decrement the Control Index
  JC  >X90                 ;If it's the Inded we're looking for, we're done
  DEC  AH                  ;Decrement our local Counter
  JNZ  X20                 ;If nto zero yet, keep looking
X50:                       ;Go to the next one
  ADD  DI,ControlStrucSize ;Point at next entry
  DEC  AL                  ;End of Table?
  JNZ  X10                 ;If not, keep looking
X90:                       ;Done ([DI] Points at Correct Control)
  MOV  CX,[DI].ControlMin  ;Put Min in CX
  MOV  DX,[DI].ControlMax  ;Put Min in DX
  POP  DS,DI,BX,AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT "STANTARD" COORDINATES INTO THRUSTMASTER PROTOCOL
;Inputs:  DS = ES = TSR Data Area
;         CXValue, DXValue, GXValue
;         MovePercent, NormalizeMax
;Outputs: CXValue, DXValue
;Changes:
;------------------------------------------------------------------------------
CookThrust:
  PUSH AX,BX,CX,DX       ;Save used registers
  PUSH DI,SI             ;Save used registers
  MOV  AX,NormalizeMax   ;Get the
  SHR  AX,1              ;  Center
  MOV  DI,AX             ;  Value
  MOV  SI,MovePercent    ;Multiply
  MUL  SI                ;  by
  MOV  SI,100            ;  Movement
  DIV  SI                ;  Percent
  MOV  SI,DI             ;SI =
  ADD  SI,AX             ;  Maximum value
  SUB  DI,AX             ;DI = Minimum value
  MOV  CX,CXValue        ;CX = X Direction
  MOV  DX,DXValue        ;DX = Y Direction
T10:                     ;Have values calculated
  XOR  BL,BL             ;Default = Centered (Valid, not Left/Right/Up/Down)
  OR   CX,CX             ;Valid X?
  JNZ >T11               ;If so, continue
  OR   BL,XInvalid       ;If not, mark it as invalid
T11:                     ;X validity handled
  OR   DX,DX             ;Valid Y?
  JNZ >T12               ;If so, continue
  OR   BL,YInvalid       ;If not, mark it as invalid
T12:                     ;Y validity handled
  OR   CX,CX             ;Invalid?
  JZ  >T14               ;If so, it can't be Left or Right
  CMP  CX,DI             ;X Left?
  JA  >T13               ;If not, continue
  OR   BL,XLeft          ;If so, mark it as Left
T13:                     ;X Left handled
  CMP  CX,SI             ;X Right?
  JB  >T14               ;If not, continue
  OR   BL,XRight         ;If so, mark it as Right
T14:                     ;X handled
  OR   DX,DX             ;Invalid?
  JZ  >T20               ;If so, it can't be Up or Down
  CMP  DX,DI             ;Y Up?
  JA  >T15               ;If not, continue
  OR   BL,YUp            ;If so, mark it as Up
T15:                     ;Y Up handled
  CMP  DX,SI             ;Y Down?
  JB  >T20               ;If not, continue
  OR   BL,YDown          ;If so, mark it as Down
T20:                     ;All bit-masks set
  XOR  AX,AX             ;Assume invalid
  CMP  BL,(XInvalid+YInvalid) ;Both coordinates invalid?
  JE  >T90               ;If so, we're done
T22:                     ;At least 1 coordinate is valid
  MOV  AX,NormalizeMax   ;Assume centered
  OR   BL,BL             ;Everything centered?
  JZ  >T90               ;If so, we're done
  CMP  BL,XInvalid       ;X Invalid & Y Centered?
  JE  >T90               ;If so, we're done
  CMP  BL,YInvalid       ;Y Invalid & X Centered?
  JE  >T90               ;If so, we're done
T30:                     ;Not Invalid or Centered
  TEST BL,(YUp+YDown)    ;Y Uncentered?
  JNZ >T40               ;If so, handle it
T32:                     ;Y Zero or Centered
  TEST BL,XLeft          ;Is it Left?
  JZ  >T36               ;If not, it's Right
T34:                     ;Left
  SHR  AX,1              ;Set
  MOV  BX,AX             ;  Left
  SHR  AX,1              ;  Return
  ADD  AX,BX             ;  Value
  JMP >T90               ;Done
T36:                     ;Right
  SHR  AX,1              ;Set
  SHR  AX,1              ;  Return Value
  JMP >T90               ;Done
T40:                     ;Y Uncentered
  TEST BL,(XLeft+XRight) ;X Uncentered?
  JNZ >T90               ;If so, we're done
T42:                     ;Y Uncentered, X Zero or Centered
  TEST BL,YUp            ;Is it Up?
  JZ  >T46               ;If not, it's Down
T44:                     ;Up
  MOV  AX,MinCoord       ;Set Up Value
  JMP >T90               ;Done
T46:                     ;Down
  SHR  AX,1              ;Set Down Value
T90:                     ;Done (AX = THrustMaster coordinate to store)
  MOV  DXValue,AX        ;Store the ThrustMaster value
  MOV  AX,GXValue        ;Store the
  MOV  CXValue,AX        ;  Gas Pedal value
  POP  SI,DI             ;Restore used registers
  POP  DX,CX,BX,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ADJUST DPAD DATA INTO A "NORMAL" FORMAT
;Inputs:  DS = ES = TSR Data Area
;          AX  = Raw DPad Coordinate from Joystick
;          BL  = ControlType (DPad)
;          BH  = Index Number (0-based)
;         [DI] = Controls Table Base Offset
;         [SI] = Raw Data
;Outputs: AX = Value converted to a "normalized" format
;                0 = Center, 1 = Up, 2 = Up & Right, ..., 8 = Up & Left
;Changes:
;NOTE: This will adjust for either a 4-way or 8-way Hat Switch
;------------------------------------------------------------------------------
AdjustDPadData:
  PUSH CX,DX       ;Save used registers
  OR   AH,AH       ;Is it more than 255?
  JNZ >D70         ;If so, mark it as Centered
  CALL GetDIMinMax ;Get the Minimum (CX) and Maximum (DX), uses BX
  CMP  AL,DL       ;Is the current value more than the Maximum?
  JA  >D70         ;If so, mark it as Centered
  CMP  CL,1        ;Is the Minimum 0 or 1?
  JA  >D70         ;If neither, Error
  JE  >D40         ;If 1, jump to handle it
D10:               ;Minimum is 0
  CMP  DL,3        ;Is it 4-way?
  JNE >D30         ;If not, jump to handle it
D20:               ;Minimum 0, 4-way
  SHL  AX,1        ;Multiply by 2
  INC  AX          ;Add 1
  JMP >D90         ;Done
D30:               ;Minimum 0, not 4-way
  CMP  DL,7        ;Is it 8-way?
  JNE >D70         ;If not, Error
D35:               ;Minimum 0, 8-way
  INC  AX          ;Add 1
  JMP >D90         ;Done
D40:               ;Minimum is 1
  OR   AX,AX       ;Is it 0 (Centered)?
  JZ  >D90         ;If so, it's OK like it is
  CMP  DL,4        ;Is it 4-way?
  JNE >D60         ;If not, jump to handle it
D50:               ;Minimum 1, 4-Way
  DEC  AX          ;Subtract 1
  SHL  AX,1        ;Multiply by 2
  INC  AX          ;Add 1
  JMP >D90         ;Done
D60:               ;Minimum 1, not 4-way
  CMP  DL,8        ;Is it 8-way?
  JE  >D90         ;If so, it's OK like it is
D70:               ;Return 0 (Centered)
  XOR  AX,AX       ;Just return 0
D90:               ;Done
  POP  DX,CX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT (COOK) A D-PAD COORDINATE INTO A JOYSTICK COORDINATE
;Inputs:  DS = ES  = TSR Data Area
;         AX = Raw DPad Coordinate (8-Way) from Joystick
;         NormalizeMax
;Outputs: AX = Value converted to "Cooked" Coordinate
;Changes:
;------------------------------------------------------------------------------
CookDPadX:
  PUSH DX              ;Save used registers
  XOR  DL,DL           ;Mark as getting X Axis
  JMP >P00             ;Do it
CookDPadY:
  PUSH DX              ;Save used registers
  MOV  DL,1            ;Mark as getting Y Axis
;  JMP >P00             ;Do it
P00:
  OR   AL,AL           ;Is it Centered?
  JZ  >P40             ;If so, it's Centered
  OR   DL,DL           ;Getting X or Y?
  JNZ >P30             ;If Y, jump to handle it
P10:                   ;Getting X
  CMP  AL,1            ;Is it just Up?
  JE  >P40             ;If so, it's Centered
  CMP  AL,5            ;Is it just Down?
  JE  >P40             ;If so, it's Centered
  CMP  AL,4            ;Is it Right?
  JBE >P50             ;If so, it's Right
  JMP >P60             ;If not, it's Left
P30:                   ;Getting Y
  CMP  AL,3            ;Is it just Right?
  JE  >P40             ;If so, it's Centered
  CMP  AL,7            ;Is it just Left?
  JE  >P40             ;If so, it's Centered
  CMP  AL,2            ;Is it Up?
  JBE >P60             ;If so, it's Up
  CMP  AL,8            ;Is it Up?
  JE  >P60             ;If so, it's Up
  JMP >P50             ;If not, it's Down
P40:                   ;Centered
  MOV  AX,NormalizeMax ;Set
  SHR  AX,1            ;  Centered
  JMP >P90             ;Done
P50:                   ;Right/Down
  MOV  AX,NormalizeMax ;Set Right/Down
  JMP >P90             ;Done
P60:                   ;Left/Up
  MOV  AX,MinCoord     ;Set Left/Up
P90:                   ;Done
  POP  DX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESET THE AXValue -> GXValue VARIABLES
;Inputs:  DS = ES  = TSR Data Area
;Outputs:
;Changes:
;------------------------------------------------------------------------------
ResetXxValues:
  PUSH AX,CX,DI          ;Save used registers
  MOV  DI,OFFSET AXValue ;Point at beginning of Table
  XOR  AX,AX             ;Fill with Zeroes
  MOV  CX,5              ;Number of entries to changes
  REP  STOSW             ;Fill teh Table
  POP  DI,CX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY AXValue -> DXValue to REGISTERS AX -> DX
;Inputs:  DS = ES  = TSR Data Area
;Outputs:
;Changes:
;------------------------------------------------------------------------------
CopyXxValues:
  PUSH DI        ;Save used registers
  MOV  DI,OFFSET AXValue ;[DI] = First Coordinate Value
  MOV  AX,[DI+0] ;Copy
  MOV  BX,[DI+2] ;  return
  MOV  CX,[DI+4] ;  register
  MOV  DX,[DI+6] ;  values
  POP  DI        ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO HANDLE I/O VIRTUALIZATION
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;ISSUE A VIRTUALIZE I/O REQUEST TO THE MEMORY MANAGER
;Inputs:  CS = DS = TSR Data Area
;         VirtIOHandle (-1 if not Virtualized yet)
;Outputs: ZF = Clear if OK (I/O is Virtualized)
;                VirtIOHandle is stored
;            = Set if error
;                VirtIOHandle = unchanged (-1)
;Changes:
;NOTES: This must be called with CS = DS, and DS must be our TSR Data Area
;         (the segment that will be used when the I/O Virtualization Code
;         is called).  Therefore, this code is in the TSR portion of our
;         program.  Normally, something like this would be in the
;         initialization code instead of the TSR, but there are special
;         circumstances here.
;------------------------------------------------------------------------------
InstallVIO:
  PUSH AX,BX,CX         ;Save used registers
  PUSH EDX,DI,SI        ;Save used registers
  CMP  VirtIOHandle,-1  ;Already virtualized?
  JNE >V90              ;If so, we're done
  MOV  AX,VirtIOFuncAX  ;Function 4A15h (Manipulate Virtual I/O Handler)
  MOV  BX,VirtIOBXInst  ;BX = Function  = Install
  MOV  CX,VirtIOTblSize ;CX = Number if Ports to Trap (# Entries in Table)
  MOV  SI,VirtIOTable   ;DS:[SI] = I/O Dispatch Table
  MOV  DX,[SI]          ;EDX =
  SHL  EDX,16           ;  Port Number
  MOV  DX,[SI]          ;  Range
  MOV  DI,LastTSRByte   ;DI = Number of Bytes needed in Segments
  STC                   ;Preset Error Flag
  INT  2Fh              ;Do it (returns CF)
  JC  >V90              ;If error, quit
  MOV  VirtIOHandle,AX  ;If OK, store the Handle Number
V90:                    ;Done
  CMP  VirtIOHandle,-1  ;Set return flag
  POP  SI,DI,EDX        ;Restore used registers
  POP  CX,BX,AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;UNINSTALL THE I/O VIRTUALIZATION
;Inputs:  CS = DS = TSR Data Area
;         VirtIOHandle
;Outputs: CF = Clear if OK (I/O virtualization is uninstalled)
;            = Set if error
;Changes:
;NOTES: This code does not actually need to be called from the TSR, unlike
;         the initialization code directly above.  We are putting it here
;         simply for convenience, and because it's small.  If it was a large,
;         complicated piece of code, we wouldn't put it in the TSR section
;         even for the sake of convenience.
;------------------------------------------------------------------------------
UninstallVIO:
  PUSH AX,BX,SI          ;Save used registers
  MOV  SI,VirtIOHandle   ;SI = Handle
  CMP  SI,-1             ;Did virtualization work?
  JNE >U20               ;If so, continue
  CLC                    ;If not, set Uninstalled return code
  JMP >U90               ;Done
U20:                     ;We are virtualized
  MOV  AX,VirtIOFuncAX   ;Function 4A15h (Manipulate Virtual I/O Handler)
  MOV  BX,VirtIOBXUninst ;BX = Function = Uninstall
                         ;SI = Handle
  STC                    ;Preset Error Flag
  INT  2Fh               ;Do it (returns CF)
  JC  >U90               ;If it didn't work, we're done
  MOV  VirtIOHandle,-1   ;If it did, mark Handle as unused
U90:                     ;Done (CF set as needed)
  POP  SI,BX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PERFORM THE PM CALL FOR I/O PORT 201h ACCESS
;Inputs:  CS = PM CS Selector
;         DS = PM DS Selector (Alias for CS)
;         AL/AX/EAX = Input Value (if OUT DX,Ax)
;         CL = Direction
;              Bit 2 (04h) Clear if IN Ax,DX
;                          Set if OUT DX,Ax
;         DX = Port Number Accessed
;Outputs: CF = Clear if I/O has been virtualized
;                AL = Value to return if call was IN Ax,DX
;                     EMM386 does CBW to AL if call was IN AX,DX
;                     EMM386 hangs on IN EAX,DX
;              Set if I/O is not virtualized
;                PM Virtualization hander gets data from actual I/O Port
;Changes:
;NOTE: This procedure is called from 16-bit protected mode, NOT from
;        V86 or real mode.  For the most part, that doesn't really matter,
;        except for one thing -- the segment registers.  The segment
;        registers are actually indexes to entries in a not-easy-to-access
;        table, NOT segment addresses as is usually the case.  The practical
;        implication of that is that we cannot issue any FAR procedure calls
;        (including INT requests?).  We can freely access code and data
;        within our own segment (the TSR), but can't access any memory outside
;        our segment.
;      This must end in a RETF, not a RET!
;------------------------------------------------------------------------------
PMCode201:
  TEST TSRFlags,DisableSts ;Are we Disabled?
  JNZ >W70                 ;If so, quit
  CALL GotAJoystick        ;Are there any Enabled Joysticks?
  JC  >W70                 ;If not, quit
W10:                       ;Need to process things
  TEST CL,PMCallCXOut      ;Is it an OUT DX,AL?
  JNZ >W50                 ;If so, handle it
W20:                       ;Is IN AL,DX
  PUSH BP,ES               ;Save used registers
  MOV  ES,DS               ;ES = TSR Data Area
  CALL CalcElapsedTimeBP   ;Calculate elapsed Time since OUT instruction (BP)
  MOV  AL,BtnMask          ;High Nibble of AL = Button Status
  CALL CalcAxesBits        ;Calculate Axis Data (AL Low Nibble)
  POP  ES,BP               ;Restore used registers
  JMP >W80                 ;Done
W50:                       ;Is OUT DX,AL
  PUSH AX                  ;Save used registers
  CALL GetTimer            ;Get the current Timer value
  MOV  StartTimer,AX       ;Store it
  OR   TSRFlags2,SameTick  ;Mark the Timer Tick Flag
  POP  AX                  ;Restore used registers
  JMP >W80                 ;Done
W70:                       ;Not virtualized
  STC                      ;Set return flag
  JMP >W90                 ;Done
W80:                       ;Virtualized
  CLC                      ;Set return flag
W90:                       ;Done
  RETF

;------------------------------------------------------------------------------
;CALCULATE THE ELAPSED TIME SINCE THE ORIGINAL OUT DX,AL
;Inputs:  DS = ES = TSR Data Selector
;         StartTimer
;         TSRFlags2.SameTick
;Outputs: ZF = Set if error (Elapsed Time unknown)
;                BP = 1-
;            = Clear if OK
;                BP = Elapsed Time
;Changes:
;------------------------------------------------------------------------------
CalcElapsedTimeBP:
  PUSH AX                 ;Save used registers
  MOV  BP,-1              ;Assume Unknown Time
  TEST TSRFlags2,SameTick ;Has the Timer Tick "rolled"?
  JZ  >P90                ;If so, we're done
  MOV  BP,StartTimer      ;BP = Starting Timer
  CALL GetTimer           ;AX = Current Timer
  SUB  BP,AX              ;BP = Elapsed Time
P90:                      ;Done
  CMP  BP,-1              ;Set return flag
  POP  AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE THE APPROPRIATE BIT MASK FOR THE VIRTUALIZED IN AL,DX FUNCTION
;Inputs:  DS = ES = TSR Data Selector
;         BP = Elapsed Time since OUT DX,AL was issued
;         CLD already issued
;Outputs: AL (Low Nibble)  = Appropriate Bit Mask
;         AL (High Nibble) = Unchanged
;Changes:
;------------------------------------------------------------------------------
CalcAxesBits:
  PUSH BX,CX,DX,DI,SI    ;Save used registers
  MOV  BX,AX             ;Save original AX
  AND  BL,0F0h           ;Mask out low nibble of BL
  MOV  SI,OFFSET AXValue ;[SI] = First coordinate to get
  MOV  CX,4              ;CX = Number of coordinates to get
  MOV  DL,1              ;DL = Bit mask to use
X10:                     ;Loop to here for each Axis
  LODSW                  ;Get the next Axis word
  CALL CalcAxisBit       ;Mask in the coordinaet bit as appropriate
  LOOP X10               ;Keep going until we're done
  MOV  AX,BX             ;Put return value in AX
  POP  SI,DI,DX,CX,BX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE AND SET AN APPROPRIATE BIT FOR AN AXIS CONTROL
;Inputs:  DS = ES = TSR Data Selector
;         BP = Elapsed Time since OUT DX,AL was issued (-1 if expired)
;         AX = Coordinate, as appropriate for BIOS (INT 15h) call
;         DL = Bit Mask to use
;         BL = Current Bit mask (in low nibble)
;              Bit we're working with preset to 0 (we set it to 1 if approp)
;Outputs: BL = Bit masked in if appropriate
;         DL = DL SHL 1
;         AX = AX SHL 4
;Changes: AX
;------------------------------------------------------------------------------
CalcAxisBit:
  SHL  AX,4  ;Multiply by 16 (appropriate for direct I/O calls)
  JZ  >B80   ;If invalid coordinate, bit is always 1
  CMP  BP,-1 ;Expired Timer after last OUT?
  JE  >B90   ;If so, bit is 0
  CMP  BP,AX ;Are we still within the timeout parameters?
  JAE >B90   ;If not, the bit is 0
B80:         ;Need to set the bit
  OR   BL,DL ;Mask in the bit
B90:         ;Done
  SHL  DL,1  ;Shift the bit mask over for next time
  RET

;------------------------------------------------------------------------------
;GET CURRENT TIMER COUNTER FROM PORT 40h (THE CLOCK INTERRUPT)
;Inputs:
;Outputs: AX = Current timer counter word
;Changes:
;------------------------------------------------------------------------------
GetTimer:
  PUSHF       ;Save flags
  CLI         ;Disable interrupts
  MOV  AL,06h ;Bits 7:6 =  00 = Timer 0
              ;Bits 5:4 =  00 = Latch Counter
              ;Bits 3:1 = 011 = Mode 3 (Square Wave)
              ;Bit    0 =   0 = Binary Counter (16 bits)
  OUT  43h,AL ;Tell the PIT what we're going to do
  IN   AL,40h ;Get LSB of timer counter
  MOV  AH,AL  ;Save it
  IN   AL,40h ;Get MSB of timer counter
  XCHG AH,AL  ;Put things in the right order
  POPF        ;Restore flags
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;MISCELLANEOUS SUPPORT FUNCTIONS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;SEARCH A BYTE-TO-WORD LOOKUP TABLE FOR A MATCH
;Inputs:  ES:[BX] = Table to search
;         AL      = Byte to search for
;Outputs: CF = Clear if a byte match as found in table
;              BX = Word from Table
;            = Set if no match found in table
;              BX = unchanged
;Changes: Last Entry in Table (end-of-table marker) = DB -1, DW -1
;------------------------------------------------------------------------------
SearchByteToWordTbl:
  PUSH AX,SI,DS  ;Save used registers
  CLD            ;Go forward with string functions
  MOV  DS,ES     ;Point DS at correct data area
  MOV  SI,BX     ;Put pointer in SI
  MOV  AH,AL     ;Put byte to check in AH
B10:             ;Loop to here for each table entry
  LODSB          ;Get the next table entry
  CMP  AL,-1     ;Is it possibly the end of the table?
  JNE >B20       ;If not, continue
  CMP  W [SI],-1 ;Is it actually the end of the table?
  JE  >B70       ;If so, there's no match
B20:             ;Not end of table
  CMP  AL,AH     ;Is it a match?
  JE  >B80       ;If so, handle it!
  INC  SI,2      ;If not, skip over this table entry
  JMP  B10       ;And keep looking
B70:             ;No entry found
  STC            ;Set the not found flag
  JMP >B90       ;Quit
B80:             ;Entry found [SI]
  MOV  BX,[SI]   ;Point BX at the table entry
  CLC            ;Set the found flag
B90:             ;Done
  POP  DS,SI,AX  ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;STACK-RELATED FUNCTIONS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;CREATE/DELETE OUR OWN STACK
;Inputs:  CS = TSR Data Area
;         StackInUse
;Outputs: Interrupts Enabled
;Changes: SS,SP to/from our stack
;------------------------------------------------------------------------------
MakeStack:
  CLI                   ;Disable interrupts
  PUSHF                 ;Save
  POP  CS:TempFlags     ;  Flags
  INC  CS:StackInUse    ;Increment number-of-calls counter
  CMP  CS:StackInUse,1  ;Is this the first call?
  JNE >M90              ;If not, the stack is already in use
  MOV  CS:TempBX,BX     ;  BX in memory
  POP  BX               ;Save MakeStack's return address in BX
  MOV  CS:OldSS,SS      ;Save original SS
  MOV  CS:OldSP,SP      ;Save original SP
  MOV  SP,CS            ;Put our CS
  MOV  SS,SP            ;  into SS
  MOV  SP,LastTSRByte-2 ;SP = Top of Stack
  PUSH BX               ;Restore MakeStack's return address to stack
  MOV  BX,CS:TempBX     ;Restore BX from memory
M90:                    ;Done
  PUSH CS:TempFlags     ;Restore
  POPF                  ;  Flags
  STI                   ;Enable interrupts
  RET

ReturnStack:
  CLI                ;Disable interrupts
  PUSHF              ;Save
  POP  CS:TempFlags  ;  Flags
  DEC  CS:StackInUse ;Decrement number-of-calls counter
  JNZ >R90           ;If sack is still in use, quit
  MOV  CS:TempBX,BX  ;Save BX in memory
  POP  BX            ;Save MakeStack's return address in BX
  MOV  SS,CS:OldSS   ;Restore original SS
  MOV  SP,CS:OldSP   ;Restore original SP
  PUSH BX            ;Restore MakeStack's return address to stack
  MOV  BX,CS:TempBX  ;Restore BX from memory
R90:                 ;Done
  PUSH CS:TempFlags  ;Restore
  POPF               ;  Flags
  STI                ;Enable interrupts
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SPACE FOR OUR TSR STACK
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

  EVEN 16
  DB (StackSize/8) DUP ('TSRStack')


LastTSRByte:


;圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹
;圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹圹


;哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
;DATA NEEDED TO INSTALL/UPDATE/SHOW STATUS (NOT NEEDED DIRECTLY IN TSR)
;苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘

;==============================================================================
;TSR Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Table of Interrupt Numbers that the TSR Intercepts
  ;First byte of each entry is the Interrupt Number
  ;Second word is the Pointer to our Interrupt Handler Code Header
  ;----------------------------------------------------------------------------
  IntNumTable:
    DB 2Fh       ;TSR Multiplex
     DW Int2FHdr
    DB 15h       ;BIOS Services
     DW Int15Hdr
    DB 08h       ;Timer Tick
     DW Int08Hdr
    DB 0         ;End of Table

  ;----------------------------------------------------------------------------
  ;Data needed to install as a TSR, Hooking into the Multiplex Int
  ;----------------------------------------------------------------------------
  FirstHandle DB 0 ;First available user handle for Int 2Fh


;==============================================================================
;Miscellaneous
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Miscellaneous Yes/No flags (individual bits)
  ;----------------------------------------------------------------------------
  ProgFlags        DB 00h ;General Program Yes/No Flags #1
    Help          EQU 01h ;User wants help?
    Uninstall     EQU 02h ;Uninstall resident program?
    InMemory      EQU 04h ;Already installed in memory?
    UseLowMemory  EQU 08h ;User does not want us in High Memory
    MemoryMoved   EQU 10h ;TSR Memory has been moved by our Auto-Code
    FoundSuptFile EQU 20h ;Found Support File in the Path

  ProgFlags2   DB 00h ;General Program Yes/No Flags #2
    DoEnvir   EQU 01h ;Parsing/Testing Environment variable
    DoStdIn   EQU 02h ;Parsing/Testing StdIn buffer
    Write2Err EQU 04h ;Write to ERR instead of Pause/CON

  StatusFlags    DB 00h ;General Program Yes/No Flags #3
    Report      EQU 01h ;Show HID Report Data
    Status      EQU 02h ;Show Status
    Descriptors EQU 04h ;Show Device/Config Descriptors Data
    BadDevices  EQU 08h ;Print Bad Device Table
    Alias       EQU 10h ;Write the Alias List to the Screen
    ErrLvl      EQU 20h ;Show ErrorLevel Table

  ;----------------------------------------------------------------------------
  ;Miscellaneous Strings we need to write to the screen
  ;----------------------------------------------------------------------------
  HexString: DB '0000',0
  CrLfMsg:   DB CR,LF,0
  SingleMsg: DB ' '
  NoMsg:     DB 0
  Space3Msg: DB ' '
  Space2Msg: DB ' '
  SpaceMsg:  DB ' ',0


;==============================================================================
;Pause Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings and Data needed to Pause the screen
  ;----------------------------------------------------------------------------
  PauseMsg:
    DB CR,LF
    DB ' MORE   (Esc stops Pausing, Ctrl-C stops Completely, any other Continues)',CR,0
  PauseNMsg:
    DB ' MORE                                                                    ',CR,LF,0
  PauseDMsg:
    DB '                                                                               ',CR,0
  PauseRows       DW ? ;Number of rows per screen
  PauseHeaderPtr  DW 1 ;Pointer to Header Message (start with a fake one <> 0)
  PauseHeaderSize DW 2 ;Number of lines in the Header Message
  PauseRowCount   DW 2 ;Current Row Counter


;==============================================================================
;DOS Environment Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Variables to search for our Environment Variable
  ;----------------------------------------------------------------------------
  EnvVarString:  DB 'USBJSTIK='    ;Environment Variable String
  EnvVarSize    EQU $-EnvVarString ;Length of String
  EnvVarPointer  DW -1             ;Pointer to Program Environment Variable

  ;----------------------------------------------------------------------------
  ;Variables to search for an extended Command Line String
  ;This should only be valid/needed if the Command Line Size (PSP:[80h]) is 7Eh
  ;----------------------------------------------------------------------------
  CmdLineVarString:  DB 'CMDLINE='         ;Environment Variable String
  CmdLineVarSize    EQU $-CmdLineVarString ;Length of String
  CmdLineVarPointer  DW -1                 ;Pointer to Program Environment Variable


;==============================================================================
;EXEC Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Size of our program stack (in bytes)
  ;----------------------------------------------------------------------------
  ProgStackSize EQU 512

  ;----------------------------------------------------------------------------
  ;Variables to Locate and Run another Program (EXEC Child Program)
  ;----------------------------------------------------------------------------
  PathToUse:       DB MaxPathSize DUP (0) ;Path to Use
  OurPathPointer   DW -1                  ;Pointer to our Path String
  CurrentPath:     DB  0                  ;Current Path (Zero-Length ASCIIZ)
  SupportFile:     DB 'USBSUPT1.COM',0    ;Support File Name to Look for

  PathVarString1:  DB 'PATH='             ;PATH Environment String Header (DOS)
  PathVarString2:  DB 'Path='             ;PATH Environment String Header (NT?)
  PathVarSize     EQU $-PathVarString2    ;Length of String
  PathVarPointer   DW -1                  ;Pointer to PATH Environment Variable

  ;----------------------------------------------------------------------------
  ;Variables needed to do EXEC Call
  ;----------------------------------------------------------------------------
  CmdTailSize DB CmdTailMax-2           ;Length of command tail (Maximize it)
  CmdTail:    DB CmdTailMax DUP (CR)    ;Command Tail for EXEC Call
  ParamBlock: DB ParamBlockSize DUP (?) ;Parameter Block for EXEC Call
  FCB1:       DB FCBSize DUP (?)        ;File Control Block #1
  FCB2:       DB FCBSize DUP (?)        ;File Control Block #2
  OldSPExec   DW ?                      ;SP may be destroyed by EXEC Call

  ;----------------------------------------------------------------------------
  ;Strings needed to write Descriptor Header Information to the screen
  ;----------------------------------------------------------------------------
  HostIndexMsg:  DB 'USB HOST INDEX: ',0
  DvcAddressMsg: DB 'DEVICE ADDRESS: ',0

  ;----------------------------------------------------------------------------
  ;Strings needed to set up Command Tail for Exec Call
  ;----------------------------------------------------------------------------
  ReportDescrMsg:     DB 'ReportDescriptor '
  ReportDescrMsgAddr: DB 'xxxx:xxxx '
  ReportDescrCallBk:  DB 'xxxx:xxxx'
                      DB  0

  DescriptorMsg:     DB 'Descriptor '
  DescriptorMsgAddr: DB 'xxxx:xxxx '
  DescriptorMsgHost: DB 'xxh '
  DescriptorMsgDvc:  DB 'xxh '
  DescriptorCallBk:  DB 'xxxx:xxxx'
                     DB  0

  TDStatusMsg:    DB 'TDStatusCode '
  TDStatusCode:   DB 'xxxxh '
  TDStatusCallBk: DB 'xxxx:xxxx'
                  DB 0

  Int14ErrMsg:    DB 'Int14ErrorCode '
  Int14ErrCode:   DB 'xxxxh '
  Int14ErrCallBk: DB 'xxxx:xxxx'
                  DB 0

  ;----------------------------------------------------------------------------
  ;Error Message for when the Support File is not found
  ;----------------------------------------------------------------------------
  NoSuptFileMsg:
    DB 'Error! Need Support File ',0


;==============================================================================
;Help Screen Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Data needed if the User has changed the name of the program from USBJSTIK
  ;----------------------------------------------------------------------------
  RealName:         ;Real name of our program (MUST end in 0)
    DB 'USBJSTIK',0 ;  & MUST have enough space for 8-char name
  RealNameSz DW 8   ;Size of RealName (not incl 0)

  ;----------------------------------------------------------------------------
  ;Syntax Help String (if user asks for Help)
  ;----------------------------------------------------------------------------
  SyntaxMsgHdr:
    DB 'SYNTAX: ',0
  SyntaxMsg:
    DB '[Options]',CR,LF
    DB LF
    DB '  ?       Show this HELP screen',CR,LF
    DB '  A       Show all ALIASES for these command line Options',CR,LF
    DB '  ErrLvl  Show all ErrorLevels (DOS Return Codes)',CR,LF
    DB '  U       UNINSTALL from memory',CR,LF
    DB '  L:Y/N   Force installation into LOW memory (Default = No)',CR,LF
    DB LF
    DB '  S    Display current STATUS of program',CR,LF
    DB '  C    Diplay CONFIGURATION (& other) descriptors of last joystick found',CR,LF
    DB '  R    Show HID REPORT descriptor of last joystick found',CR,LF
    DB '  J:#  Display current status of JOYSTICK Index # (0-7)',CR,LF
    DB LF
    DB '  B         Show list of BAD (incompatible) joysticks found',CR,LF
    DB '  Beep:Y/N  BEEP speaker when bad joystick is found (Default = Yes)',CR,LF
    DB LF
    DB '  Ctr:#  Use # as BIOS Center coordinate (10-200, Default = 50)',CR,LF
    DB '  I:#    Virtualize I/O Port # (200h-20Fh, Default = 201h)',CR,LF
    DB '             (requires appropriate memory manager, like EMM386 or 386MAX)',CR,LF
    DB LF
    DB '  F:Y/N  Use FIFTEEN-button protocol (Default = No (4-buttons))',CR,LF
    DB '  T:Y/N  Use THRUSTMASTER protocol for 4-way DPad/HatSwitch (Default = No)',CR,LF
    DB '  P:#    Be # PERCENT from Center to detect as movement (20-80, Default = 50)',CR,LF
    DB LF
    DB '  M:Entity FROM J#,CType,C#[,C2#]  MAP Analog Entity FROM USB Control',CR,LF
    DB '    Entity = Analog Joystick Axis (AX, AY, BX, BY, AZ) or Button (A01-A15)',CR,LF
    DB '    J#     = USB Joystick/GamePad Index (0-7)',CR,LF
    DB '    CType  = Control Type (Axis, Throttle, Slider, DPad, Button)',CR,LF
    DB '    C#     = Control Index (0-99)',CR,LF
    DB '    C2#    = Second Control Index (0-99) (if required)'
    DB 0


;==============================================================================
;Program Status Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings needed to print the Program Status to the screen
  ;----------------------------------------------------------------------------
  StatusMsg1:
    DB '  COMBINED USB JOYSTICK STATUS            INDIVIDUAL JOYSTICKS',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯   屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 'Number of Buttons (4,15):    ',0
  StatusMsg2:
    DB                                 '   Idx Axs Thr Sld Hat Btn Hst Adr Itf',CR,LF
    DB 'ThrustMaster Protocol:       ',0
  StatusMsg3:
    DB                                 '   哪 哪 哪 哪 哪 哪 哪 哪 哪',CR,LF
    DB 'Virtualize I/O Port:       ',0
  StatusMsg4:
    DB 'BIOS Center Coord:         ',0
  StatusMsg5:
    DB 'Movement Percent:          ',0
  Status1EndMsg:
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',0
  Status2EndMsg:
    DB '                                   屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB LF
    DB LF
    DB '       USB JOYSTICK CONTROL => ANALOG JOYSTICK ENTITY MAPPING',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯淹屯屯屯屯屯屯屯屯屯屯屯淹屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 'Alg Joy Cntrl Control  Alg Joy Cntrl Control  Alg Joy Cntrl Control',CR,LF
    DB 'Ety Idx Type   Index   Ety Idx Type   Index   Ety Idx Type   Index',CR,LF
    DB '哪 哪 哪哪 哪哪哪  哪 哪 哪哪 哪哪哪  哪 哪 哪哪 哪哪哪',CR,LF
    DB 0

  MapEndMsg:
    DB CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯贤屯屯屯屯屯屯屯屯屯屯屯贤屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  CMsgSep:       DB '  ',0

  CMsgButton:    DB 'Buttn',0
  CMsgHat:       DB 'HatSw',0
  CMsgSlider:    DB 'Slide',0
  CMsgThrottle:  DB 'Throt',0
  CMsgAxis:      DB 'Axis ',0
  CMsgSize      EQU $-CMsgAxis

  ;----------------------------------------------------------------------------
  ;Note: This is the order we actually write the Translation Table entries in.
  ;      The Translation table is stored Axes first (AX ... AZ) followed by
  ;        the Buttons (A01 .. A15).
  ;      When we write them we do it in three columns, with the first column
  ;        being the Axes, the second column being Buttons 1-8, and the third
  ;        column being Buttons 9-15.  This list is sorted by the order we
  ;        need to write them in.
  ;----------------------------------------------------------------------------
  XlatMsgList:
    DB 'AX ',0
    DB 'A01',0
    DB 'A09',0
    DB 'AY ',0
    DB 'A02',0
    DB 'A10',0
    DB 'BX ',0
    DB 'A03',0
    DB 'A11',0
    DB 'BY ',0
    DB 'A04',0
    DB 'A12',0
    DB 'AZ ',0
    DB 'A05',0
    DB 'A13',0
    DB 'A06',0
    DB 'A14',0
    DB 'A07',0
    DB 'A15',0
    DB 'A08',0

  YesMsg:  DB 'Yes',0
  NoMsgS:  DB ' No',0

  DuplicatesMsg:
    DB LF
    DB 'A USB Joystick Control is mapped to more than one Analog Joystick Entity.',CR,LF
    DB 'USBJSTIK will continue, but this may cause you some problems!',CR,LF
    DB 0


;==============================================================================
;Individual Joystick Status Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Joystick Index to Get the status of
  ;----------------------------------------------------------------------------
  JoyStatusIndex DB -1 ;-1 = N/A

  ;----------------------------------------------------------------------------
  ;Strings needed to print the Status to the screen
  ;----------------------------------------------------------------------------
  AxisMsg:     DB ' AXIS ',0
  ThrottleMsg: DB 'THROT ',0
  SliderMsg:   DB 'SLIDE ',0
  DPadMsg:     DB 'HAT ',0
  ButtonMsg:   DB 'B ',0
  LineMsg5:    DB '=='
  LineMsg3:    DB '=='
  LineMsg1:    DB '= ',0
  CrMsg:       DB CR,0

  NoStatusMsg:
    DB CR
    DB 'Could not find a valid USB Joystick or GamePad at Index ',0


;==============================================================================
;Bad Device Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings needed to print Bad Device Table to the screen
  ;----------------------------------------------------------------------------
  BadDeviceHdr:
    DB ' BAD (INCOMPATIBLE) JOYSTICKS',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB  LF
    DB 'HOST  DVC   INTF  STAGE  ERROR',CR,LF
    DB 'INDX  ADDR  NUM    NUM   CODE',CR,LF
    DB '哪哪  哪哪  哪哪  哪哪  哪哪',CR,LF
    DB  0

  NoneFoundMsg:
    DB '#哪#  #哪#  None  #哪# #哪#',CR,LF,0
  BadBeepMsg:
    DB 'Beep for Bad Joysticks: ',0


;==============================================================================
;ErrorLevel Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;ErrorLevels, and Table for ErrorLevel Description Strings
  ;----------------------------------------------------------------------------
  ErLvlOption   EQU  1 ;Bad Option on the command line
  ErLvlMemory   EQU  2 ;Memory problem with the computer
  ErLvlUninst   EQU  3 ;Not in memory yet, can't perform Request
  ErLvlVersion  EQU  4 ;Another version of the program already installed
  ErLvlDOSVer   EQU  5 ;DOS Version too old
  ErLvlStdIn    EQU  6 ;StdIn file too big
  ErLvlNoHost   EQU  7 ;No USB Host Driver installed
  ErLvlHostInit EQU  8 ;Error while trying to initialize host request
  ErLvlNoData   EQU  9 ;No Report/Descriptor Data to Print
  ErLvlSuptFile EQU 10 ;Could not Find Support File
  ErLvlNoDevice EQU 11 ;No Devices installed, can't show Raw Data

  ErrLvlHdr:
    DB  CR,LF
    DB '                      ERRORLEVELS (DOS RETURN CODES)',CR,LF
    DB '  屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB '   0 No Error.',CR,LF
    DB 0

  ErrLvlTbl:
    DB ErLvlOption
       DB "Bad Command-line Option.",0
    DB ErLvlMemory
       DB "Memory problem with the computer.",0
    DB ErLvlUninst
       DB "USBJSTIK is not installed in memory yet -- can't perform Request.",0
    DB ErLvlVersion
       DB "A different version of USBJSTIK is already installed in memory.",0
    DB ErLvlDOSVer
       DB "DOS must be at least version 3.00.",0
    DB ErLvlStdIn
       StdInTooBigMsg:
       DB 'The Redirected Input Option File ("USBJSTIK < {FileName}") is too big.',0
    DB ErLvlNoHost
       DB "Could not find a compatible USB Host Driver in memory.",0
    DB ErLvlHostInit
       DB "Could not properly initialize the USB Host Driver.",0
    DB ErLvlNoData
       DB "There is no Report or Configuration Descriptor data to write.",0
    DB ErLvlSuptFile
       DB "Could not find the required USBSUPT1.COM support file.",0
    DB ErLvlNoDevice
       DB "No Joystick is available to show the Raw Data for.",0
    DB -1,-1 ;End of Table


;==============================================================================
;Alias Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings needed to print Command Line Aliases to the screen
  ;----------------------------------------------------------------------------
  AliasHdrMsg:
    DB '                       ALIASES FOR COMMAND-LINE OPTIONS',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  YesNoHdrMsg:
    DB '       ALIASES FOR YES & NO',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  JStikHdrMsg:
    DB '         ALIASES FOR JOYSTICK INDEXES',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  DIHdrMsg:
    DB 'ALIASES FOR JOYSTICK NAMES',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  EntityHdrMsg:
    DB 'ALIASES FOR ANALOG JOYSTICK ENTITIES',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  SubEntityHdrMsg:
    DB 'ALIASES FOR ANALOG JOYSTICK SUBENTITIES',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  CtrlTypeHdrMsg:
    DB '           ALIASES FOR CONTROL TYPES',CR,LF
    DB '屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯',CR,LF
    DB 0

  ;----------------------------------------------------------------------------
  ;Table needed to temporarily store Aliases.
  ;They are originally sorted in reverse alphabetical order.
  ;To write them in forward alphabetical order, we temporarily store
  ;  the String Pointers, in the original reverse order, in this table.
  ;  We then write the strings, starting with the last entry in this table
  ;  and moving backwards.
  ;The number of entries in this table must be AT LEAST as big as the largest
  ;  number of Aliases for a single command Option.  Most Options don't have
  ;  more than a dozen or so Aliases.
  ;----------------------------------------------------------------------------
  AliasSortTbl: DW 50 DUP (?) ;Table to store Alias Strings (to reverse order)


;==============================================================================
;Command-line (Parsing) Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Extra Memory Segment (used to store compressed contents of STDIN)
  ;----------------------------------------------------------------------------
  StdInSegment DW ?

  ;----------------------------------------------------------------------------
  ;Data needed to Parse the command line for Options
  ;----------------------------------------------------------------------------
  ParseTest  DB Yes ;Testing the command for errors?
  ParseError DB  No ;Error while Parsing Command Line?

  ;----------------------------------------------------------------------------
  ;Various Parsing-related messages we may need to print to the screen
  ;----------------------------------------------------------------------------
  ParseCmdMsg:
    DB 'Error in the command-line Options for USBJSTIK:',CR,LF,0
  ParseEnvMsg:
    DB 'Error in the Environment variable "USBJSTIK":',CR,LF,0
  ParseStdInMsg:
    DB 'Error in the redirected input file ("USBJSTIK < {FileName}"):',CR,LF,0
  ParseErrMsg:
    DB '^ Error',CR,LF,LF,0
  ParseErrEnv:
    DB 'Reset the Environment variable (type "SET USBJSTIK=") and then',CR,LF
  ParseErrMsg2:
    DB 'Type "USBJSTIK ?" for Help.',CR,LF,0

  Copyright:
    DB "USBJSTIK 0.11, (C) 2007-2009, Bret E. Johnson.",CR,LF
    DB "DOS Driver for up to 8 USB Joysticks or GamePads.",CR,LF
    DB  LF,0
  EnvMsg:
    DB  'Environment: ',0
  InstallMsg1:
    DB 'USBJSTIK has been ',0
  InstallMsgAuto:
    DB 'automatically ',0
  InstallMsgHigh:
    DB 'installed into Upper Memory.',0
  InstallMsgHole:
    DB 'installed into a Low Memory "Hole".',0
  InstallMsgNorm:
    DB 'installed into Low Memory.',0
  InstallMsgLast:
    DB CR,LF
    DB 'Type "USBJSTIK U" to Uninstall USBJSTIK from memory.',CR,LF,0
  DOSVerMsg:
    DB 'You must be using DOS version 3.00 or later to use this program.',0
  NoHostMsg:
    DB 'There must be a compatible USB Host Driver installed in memory',CR,LF
    DB '  (something like USBUHCI.COM) before this program can be installed.',0
  HostInitErrMsg:
    DB 'There was an error while trying to initialize the USB Host.',0
  NoEnvironMsg:
    DB 'There is no DOS Environment.',CR,LF
    DB 'There is something WEIRD going on here!',0
  ExtraMemoryMsg:
    DB 'There is not enough available memory to run this program!',0
  WrongVerMsg:
    DB 'Another version of USBJSTIK was found in memory.',CR,LF
    DB 'Please UNINSTALL the other version before continuing.',0
  NoDescrDataMsg:
    DB 'There is no Descriptor Data to Write!',0
  UninstallMsg:
    DB 'USBJSTIK has been Uninstalled from memory.',CR,LF,0
  NoUninstallMsg:
    DB 'Unable to Uninstall USBJSTIK from memory, but it has been Disabled.',CR,LF
    DB "Remove any TSR's installed after USBJSTIK and try again.",CR,LF
    DB LF
    DB 'To Re-Enable USBJSTIK, simply type "USBJSTIK" at the command-line.',0
  ReEnabledMsg:
    DB 'USBJSTIK has been Re-Enabled.',CR,LF
    DB LF,0
  UninstallErrMsg:
    DB "Can't find USBJSTIK in memory.",CR,LF
    DB 'You must install it before it can be Uninstalled.',0
  UpdateMsg:
    DB 'Resident USBJSTIK has been updated with new information.',0

  BadDvcErrMsg:
    DB 'Bad Device',0
  JoyStatusErrMsg:
    DB 'Joystick Status',0
  ReportErrMsg:
    DB 'Report '
  DescrErrMsg:
    DB 'Descriptor',0
  StatusErrMsg:
    DB 'Program Status',0
  DataErrMsg:
    DB ' Data is not available until after',CR,LF
    DB '  USBJSTIK has been installed into memory.',0

  ;----------------------------------------------------------------------------
  ;Strings needed to write the Command-line error message
  ;----------------------------------------------------------------------------
  MoreLeftMsg:   DB '<<-More}  ',0
  MoreRightMsg:  DB '  {More->>',0
  MoreMsgSize   EQU $-MoreRightMsg

  ;----------------------------------------------------------------------------
  ;Command Line Options with no single-character equivalents
  ;These must be in the same numerical order that we want the
  ;  Alias Option to Sort them in!
  ;----------------------------------------------------------------------------
  OptionBeep   EQU 1 ;Beep the speaker
  OptionCenter EQU 2 ;Center coordinate for BIOS
  OptionErrLvl EQU 3 ;Write ErrorLevel Table

  ;----------------------------------------------------------------------------
  ;Command Line Options
  ;----------------------------------------------------------------------------
  SwitchList:
    DB 'Help',0
    DB  0, '?'
      ; ^              Beginning-of-List Marker
      ;    ^   ^        Code(s) to use

    DB 'VirtualizePort',0
    DB 'VirtualizeIOPort',0
    DB 'VirtualizeIOAddress',0
    DB 'VirtualizeIOAddr',0
    DB 'VirtualizeIO',0
    DB 'VirtualizeAddress',0
    DB 'VirtualizeAddr',0
    DB  0, 'I'

    DB 'UseLowMemory',0
    DB 'UseLowMem',0
    DB 'UseLowerMemory',0
    DB 'UseLowerMem',0
    DB 'UseLow',0
    DB  0, 'L'

    DB 'Uninstall',0
    DB  0, 'U'

    DB 'TstJoystick',0
    DB 'TstJoy',0
    DB 'TstGamePad',0
    DB  0, 'J'

    DB 'ThrustMstrProtocol',0
    DB 'ThrustMstr',0
    DB 'ThrustMasterProtocol',0
    DB 'ThrustMaster',0
    DB 'Thrust',0
    DB  0, 'T'

    DB 'TestJoystick',0
    DB 'TestJoy',0
    DB 'TestGamePad',0
    DB  0, 'J'

    DB 'StatusTbl',0
    DB 'StatusTable',0
    DB  0, 'S'

    DB 'StatusofJoysticks',0
    DB 'StatusofJoystick',0
    DB 'StatusofGamePads',0
    DB 'StatusofGamePad',0
    DB  0, 'S'

    DB 'Status',0
    DB  0, 'S'

    DB 'Sounds',0
    DB 'Sound',0
    DB  0, OptionBeep

    DB 'ShowStatusTbl',0
    DB 'ShowStatusTable',0
    DB 'ShowStatus',0
    DB  0, 'S'

    DB 'RptDescriptor',0
    DB 'RptDescr',0
    DB 'Rpt',0
    DB  0, 'R'

    DB 'ReportDescriptor',0
    DB 'ReportDescr',0
    DB 'Report',0
    DB  0, 'R'

    DB 'Remove',0
    DB  0, 'U'

    DB 'RawData',0
    DB 'Raw',0
    DB  0, 'J'

    DB 'Port',0
    DB  0, 'I'

    DB 'PercentFromCtr',0
    DB 'PercentFromCenter',0
    DB 'Percent',0
    DB  0, 'P'

    DB 'PctFromCtr',0
    DB 'PctFromCenter',0
    DB 'Pct',0
    DB  0, 'P'

    DB 'Out',0
    DB  0, 'U'

    DB 'OptionAliases',0
    DB 'OptionAlias',0
    DB  0, 'A'

    DB 'Midpoint',0
    DB 'Mid',0
    DB  0, OptionCenter

    DB 'MovePercent',0
    DB 'MovePct',0
    DB 'MovementPercent',0
    DB 'MovementPct',0
    DB  0, 'P'

    DB 'Map',0
    DB  0, 'M'

    DB 'LowMemory',0
    DB 'LowMem',0
    DB 'LowerMemory',0
    DB 'LowerMem',0
    DB 'Low',0
    DB  0, 'L'

    DB 'JoyTst',0
    DB 'JoyTest',0
    DB 'JoystickTst',0
    DB 'JoystickTest',0
    DB  0, 'J'

    DB 'JoystickStatusTbl',0
    DB 'JoystickStatusTable',0
    DB 'JoystickStatus',0
    DB  0, 'S'

    DB 'IOPort',0
    DB 'IOAddress',0
    DB 'IOAddr',0
    DB 'IO',0
    DB  0, 'I'

    DB 'IntfDescriptor',0
    DB 'IntfDescr',0
    DB 'Intf',0
    DB  0, 'C'

    DB 'InterfaceDescriptor',0
    DB 'InterfaceDescr',0
    DB 'Interface',0
    DB  0, 'C'

    DB 'IncompatibleJoystickList',0
    DB 'IncompatibleJystickList',0
    DB 'IncompatibleGamepadList',0
    DB 'IncompatibleGamepad',0
    DB 'Incompatible',0
    DB  0, 'B'

    DB 'Hlp',0
    DB  0, '?',

    DB 'HIDRptDescriptor',0
    DB 'HIDRptDescr',0
    DB 'HIDRpt',0
    DB  0, 'R'

    DB 'HIDReportDescriptor',0
    DB 'HIDReportDescr',0
    DB 'HIDReport',0
    DB  0, 'R'

    DB 'H',0
    DB  0, '?',

    DB 'GamePadTst',0
    DB 'GamePadTest',0
    DB  0, 'J'

    DB 'GamepadStatusTbl',0
    DB 'GamepadStatusTable',0
    DB 'GamepadStatus',0
    DB  0, 'S'

    DB 'ForceLowMemory',0
    DB 'ForceLowMem',0
    DB 'ForceLowerMemory',0
    DB 'ForceLowerMem',0
    DB 'ForceLow',0
    DB  0, 'L'

    DB 'FifteenButtonsProtocol',0
    DB 'FifteenButtons',0
    DB 'FifteenButtonProtocol',0
    DB 'FifteenButton',0
    DB 'FifteenBtnsProtocol',0
    DB 'FifteenBtns',0
    DB 'FifteenBtnProtocol',0
    DB 'FifteenBtn',0
    DB 'Fifteen',0
    DB  0, 'F'

    DB 'ErrorLvlTbl',0
    DB 'ErrorLvlTable',0
    DB 'ErrorLvls',0
    DB 'ErrorLvl',0
    DB 'ErrorLevelTbl',0
    DB 'ErrorLevelTable',0
    DB 'ErrorLevels',0
    DB 'ErrorLevel',0
    DB  0, OptionErrLvl

    DB 'ErrLvlTbl',0
    DB 'ErrLvlTable',0
    DB 'ErrLvls',0
    DB 'ErrLvl',0
    DB 'ErrLevelTbl',0
    DB 'ErrLevelTable',0
    DB 'ErrLevels',0
    DB 'ErrLevel',0
    DB  0, OptionErrLvl

    DB 'ErLvlTbl',0
    DB 'ErLvlTable',0
    DB 'ErLvls',0
    DB 'ErLvl',0
    DB 'ErLevelTbl',0
    DB 'ErLevelTable',0
    DB 'ErLevels',0
    DB 'ErLevel',0
    DB  0, OptionErrLvl

    DB 'EndPointDescriptor',0
    DB 'EndPointDescr',0
    DB 'EndPoint',0
    DB  0, 'C'

    DB 'EndPtDescriptor',0
    DB 'EndPtDescr',0
    DB 'EndPt',0
    DB  0, 'C'

    DB 'DvcDescriptor',0
    DB 'DvcDescr',0
    DB 'Dvc',0
    DB  0, 'C'

    DB 'DeviceDescriptor',0
    DB 'DeviceDescr',0
    DB 'Device',0
    DB  0, 'C'

    DB 'Descriptors',0
    DB 'Descriptor',0
    DB 'Descrs',0
    DB 'Descr',0
    DB  0, 'C'

    DB 'Ctr',0
    DB  0, OptionCenter

    DB 'ConfigurationDescriptor',0
    DB 'ConfigurationDescr',0
    DB 'Configuration',0
    DB  0, 'C'

    DB 'ConfigDescriptor',0
    DB 'ConfigDescr',0
    DB 'Config',0
    DB  0, 'C'

    DB 'CommandLineAliases',0
    DB 'CommandLineAlias',0
    DB  0, 'A'

    DB 'Cntr',0
    DB  0, OptionCenter

    DB 'CmdLineAliases',0
    DB 'CmdLineAlias',0
    DB  0, 'A'

    DB 'Center',0
    DB  0, OptionCenter

    DB 'Beeps',0
    DB 'Beep',0
    DB  0, OptionBeep

    DB 'BadJoystickList',0
    DB 'BadJoystick',0
    DB 'BadGamepadList',0
    DB 'BadGamepad',0
    DB 'Bad',0
    DB  0, 'B'

    DB 'Aliases',0
    DB 'Alias',0
    DB  0, 'A'

    DB '15ButtonsProtocol',0
    DB '15Buttons',0
    DB '15ButtonProtocol',0
    DB '15Button',0
    DB '15BtnsProtocol',0
    DB '15Btns',0
    DB '15BtnProtocol',0
    DB '15Btn',0
    DB '15',0
    DB  0, 'F'

    DB 0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Enable/Disable Switches
  ;----------------------------------------------------------------------------
  SwitchListJStik:
    DB 'Global',0
    DB  0, -1,

    DB 'AllJoysticks',0
    DB 'AllJoys',0
    DB  0, -1,

    DB 'AllGamePads',0
    DB  0, -1,

    DB 'All',0
    DB  0, -1,

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Yes/No Options
  ;----------------------------------------------------------------------------
  SwitchListYesNo:
    DB 'Yes',0
    DB  0, 'Y'

    DB 'True',0
    DB 'T',0
    DB  0, 'Y'

    DB 'On',0
    DB  0, 'Y'

    DB 'Off',0
    DB  0, 'N'

    DB 'No',0
    DB  0, 'N'

    DB 'False',0
    DB 'F',0
    DB  0, 'N'

    DB 'Enabled',0
    DB 'Enable',0
    DB  0, 'Y'

    DB 'Disabled',0
    DB 'Disable',0
    DB  0, 'N'

    DB '1',0
    DB  0, 'Y'

    DB '0',0
    DB  0, 'N'

    DB 0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Ignore Control, Joystick Indices
  ;----------------------------------------------------------------------------
  SwitchListDIIndex:
    DB  'JoyStick',0
    DB  'Joy',0
    DB   0, 'J'

    DB  'GamePad',0
    DB   0, 'G'

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Control Types
  ;----------------------------------------------------------------------------
  SwitchListCtrlType:
    DB  'Throttles',0
    DB  'Throttle',0
    DB  'T',0
    DB   0, CTThrottle

    DB  'Sliders',0
    DB  'Slider',0
    DB  'S',0
    DB   0, CTSlider

    DB  'HatSwitch',0
    DB  'Hat',0
    DB  'H',0
    DB   0, CTDPad

    DB  'GasPedals',0
    DB  'GasPedal',0
    DB  'Gas',0
    DB  'G',0
    DB   0, CTThrottle

    DB  'DPads',0
    DB  'DPad',0
    DB  'D',0
    DB   0, CTDPad

    DB  'Buttons',0
    DB  'Button',0
    DB  'Btns',0
    DB  'Btn',0
    DB  'B',0
    DB   0, CTButton

    DB  'Axis',0
    DB  'Axes',0
    DB  'A',0
    DB   0, CTAxis

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Analog Joystick Entity Types
  ;----------------------------------------------------------------------------
  SwitchListEntity:
    DB  'Direction',0
    DB  'Dir',0
    DB   0, 'A'

    DB  'Coordinate',0
    DB  'Coord',0
    DB   0, 'A'

    DB  'Button',0
    DB  'Btn',0
    DB   0, 'B'

    DB  'Axis',0
    DB   0, 'A'

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Analog Joystick Entity SubTypes
  ;----------------------------------------------------------------------------
  SwitchListSubEntity:
    DB  'Vertical',0
    DB  'Vert',0
    DB  'Ver',0
    DB   0, 'Y'

    DB  'UpDown',0
    DB  'UD',0
    DB   0, 'Y'

    DB  'Throttle',0
    DB   0, 'Z'

    DB  'Rudder',0
    DB   0, 'Z'

    DB  'LR',0
    DB  'LeftRight',0
    DB   0, 'X'

    DB  'Horizontal',0
    DB  'Hor',0
    DB   0, 'X'

    DB  'GasPedal',0
    DB  'Gas',0
    DB   0, 'Z'

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Mapping Switch Separators
  ;----------------------------------------------------------------------------
  SwitchListFrom:
    DB 'From',0
    DB '=',0

    DB 'As',0
    DB '=',0

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;TABLE OF SUBROUTINE OFFSETS TO PROCESS THE VARIOUS COMMAND LINE OPTIONS
  ;----------------------------------------------------------------------------
  SwitchTbl:
    DB  '?'
       DW DoSwitchH      ;Help
    DB  'A'
       DW DoSwitchA      ;Command Line Aliases
    DB  'B'
       DW DoSwitchB      ;Bad Device Table
    DB  'C'
       DW DoSwitchC      ;Device/Config/Interface/HID/EndPoint Descriptors
    DB  'F'
       DW DoSwitchF      ;Fifteen-button Protocol
    DB  'I'
       DW DoSwitchI      ;Virtualize I/O Port Address
    DB  'J'
       DW DoSwitchJ      ;Joystick Test
    DB  'L'
       DW DoSwitchL      ;Low Memory
    DB  'M'
       DW DoSwitchM      ;Map Controls to analog Joystick Entities
    DB  'P'
       DW DoSwitchP      ;Percent from Center
    DB  'R'
       DW DoSwitchR      ;Report Descriptor
    DB  'S'
       DW DoSwitchS      ;Status
    DB  'T'
       DW DoSwitchT      ;ThrustMaster Protocol
    DB  'U'
       DW DoSwitchU      ;Uninstall

    DB  OptionBeep
       DW DoSwitchBeep   ;Beep the Speaker
    DB  OptionCenter
       DW DoSwitchCenter ;BIOS Center Coordinate Value
    DB  OptionErrLvl
       DW DoSwitchErrLvl ;ErrorLevel Table

    DB  0                ;End of Table


;哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌哌
;PROGRAM CODE (NOT NEEDED IN TSR)
;Needed to initialize/update/show status of TSR, but not actually needed
;  in memory while TSR is running
;苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘苘

;------------------------------------------------------------------------------
;INITIALIZE/UPDATE THE PROGRAM
;Inputs:
;Outputs: Lots of Stuff
;Changes: Everything
;------------------------------------------------------------------------------
Main:
  CLD                         ;Go forward with string functions
  MOV  SP,LastProgByte        ;Reset Stack Pointer
  CALL ResizeMemory           ;Resize our Memory Allocation
  CALL CopyNameToMCB          ;Copy our Program name to the MCB
  CALL SetupPauseNoHdr        ;Set up the Pause
  MOV  DX,Copyright           ;Write the
  CALL WriteZPause            ;  Copyright message
  CALL SetupExtraMem          ;Set up the Extra Memory Segment (Quits if Err)
  CALL SetXlatDefaults1       ;Setup Translation Table with Defaults
  CALL TestMemory             ;See if we're already installed in memory
                              ;QUIT if another version is installed
  CALL GetRealName            ;Get the Real Program Name
  CALL SetupExec              ;Setup Exec Parameters
  CALL GetOurPath             ;Get the Pointer to Our Path
  CALL GetEnvVars             ;Get the Environment Variable Pointers
  CALL ParseIt                ;Parse the command line
  CALL ReleaseExtraMem        ;Release the Extra Memory Segment
  CALL ReEnable               ;Re-Enable the Program, if required
  CALL DoHelp                 ;Print Help and Quit, if necessary
  CALL DoAliases              ;Print Aliases and Quit, if necessary
  CALL DoErrLvls              ;Print ErrorLevel Table and Quit, if necessary
  CALL DoUninstall            ;Uninstall and quit, if necessary
  CALL DoBadDevices           ;Print Bad Devices and Quit if requested
  CALL DoJoyStatus            ;Print Joystick Status, if necessary
  CALL DoDescriptors          ;Print USB Descriptors and Quit if requested
  CALL DoReport               ;Print Report Data and Quit if Requested
  CALL DoStatus               ;Print Status, if necessary
  CALL TestCompatibility      ;Test for compatibility (quit if not)
  TEST ProgFlags,InMemory     ;Are we already Installed in memory?
  JZ  >M50                    ;If not, Install as a TSR
M40:                          ;Already Installed
  MOV  DX,UpdateMsg           ;If so, point at Update message
  XOR  AL,AL                  ;Errorlevel = 0
  JMP  Exit                   ;QUIT

M50:                          ;First-time installation (install as TSR)
  CALL DelEnvironment         ;Delete environment space (don't need it now)
  CALL MoveProgramToUMB       ;Move Program to Upper Memory, if possible
   ;All Subroutines after this MUST assume ES is TSR Segment, NOT CS!!
  MOV  DS,ES                  ;Point DS at TSR Data Area
M60:                          ;Ready to install TSR
  CALL InitTables             ;Initialize all Tables
  CALL ChangeInts             ;Change the interrupts to point at us
  CALL InitInts               ;Initialize periodic "Search for New Devices"
                              ;  (quits if there's a problem)
  CALL TestCPUCategory        ;Test CPU category
  CALL TestWindowsNT          ;Test for Windows NT
  CALL StartVIO               ;Initialize I/O Virtualization, if applicable
  MOV  DS,CS                  ;Point DS at Local Data Area
  XOR  AL,AL                  ;ErrorLevel = 0
  MOV  BX,DS                  ;Point BX at the Local Segment
  MOV  CX,ES                  ;Point CX at the TSR Segment
  MOV  DX,InstallMsg1         ;Write the first part of the
  CALL WriteZPause            ;  Install Message
  TEST ProgFlags,MemoryMoved  ;Did we move the TSR Memory?
  JZ  >M90                    ;If not, use TSR Exit
  MOV  DX,InstallMsgAuto      ;Write
  CALL WriteZPause            ;  "automatically"
  MOV  DX,InstallMsgHole      ;Assume we installed in a Low Memory Hole
  CMP  BX,CX                  ;Did we install in a UMB or a Low Memory Hole?
  JA  >M80                    ;If a Low Memory Hole, leave the message as is
  MOV  DX,InstallMsgHigh      ;If a UMB, point at Installed High message
M80:                          ;[DX] = appropriate Memory Message
  CALL WriteZPause            ;Write the Memory Message
  MOV  DX,InstallMsgLast      ;Point at the last part of the Installed Message
  JMP  Exit                   ;Done

M90:                          ;Exit Program as a TSR
  MOV  DX,InstallMsgNorm      ;Assume we're in Low Memory
  CMP  CX,0A000h              ;Are we in Low Memory?
  JB  >M95                    ;If so, continue
  MOV  DX,InstallMsgHigh      ;If not, point at the High Memory message
M95:                          ;[DX] = appropriate Memory Message to write
  CALL WriteZPause            ;Write the Memory Message
  MOV  DX,InstallMsgLast      ;Write the last part of the
  CALL WriteZPause            ;  Install Message
  MOV  DX,(LastTSRByte SHR 4) ;DX = number of TSR paragraphs
  MOV  AH,31h                 ;Function 31h (TSR), ErrorLevel in AL (0)
  INT  21h                    ;Do it

;------------------------------------------------------------------------------
;MAKE SURE THE MCB (MEMORY CONTROL BLOCK) CONTAINS OUR PROGRAM NAME
;Inputs:  DS = Local Data Area
;         DS:RealName = ASCIIZ Real Name of our program
;         CLD Already Issued
;Outputs:
;Changes: Data in MCB
;NOTES:   This makes sure that the MCB (Memory Control Block) for our
;           program contains our program name.  DOS 4+ does this
;           automatically, but DOS versions 1-3 do not.  This code
;           does not verify whihc DOS version is in use, but simply
;           fills in the MCB no matter what.
;         Our program name is needed inthe MCB to make sure other programs
;           can identify that we are installed in memory.  This code should
;           be added to all TSR's, so that memory-mapping programs
;           (like MAPMEM or ISLOADED) can tell which TSR's are installed
;           in memory.  It should also be added to all programs that
;           EXEC other programs, so that the EXEC'd program can figure out
;           who called it.
;         Programs that are not TSR's and do not EXEC other programs
;           do not need this code.
;------------------------------------------------------------------------------
CopyNameToMCB:
  PUSH CX,DI,SI,ES              ;Save used registers
  MOV  CX,DS                    ;Point ES:[DI]
  DEC  CX                       ;  at the Owners Name
  MOV  ES,CX                    ;  in the MCB
  MOV  DI,(OFFSET MCBOwnerName) ;  of our program
  MOV  SI,RealName              ;Point DS:[SI] at our Program Name
  MOV  CX,4                     ;Copy 4 words (8 bytes)
  REP  MOVSW                    ;Copy our program name to the MCB
  POP  ES,SI,DI,CX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SETUP/RELEASE AN EXTRA 64K MEMORY SEGMENT FOR OUR PROGRAM
;Inputs:  DS = Local Data Area
;Outputs: StdInSegment = Segment of New Segment
;         Quits Program with Error Message if Memory Problem
;Changes:
;------------------------------------------------------------------------------
SetupExtraMem:
  PUSH AX,BX              ;Save used registers
  MOV  BX,4096            ;Number of Paragraphs in 64k Program
  MOV  AH,4Ah             ;Service 4Ah (Modify memory allocation)
  INT  21h                ;Do it (uses ES = Segment, BX = # Paragraphs)
  JC  >X70                ;If Error, Quit
  MOV  AH,48h             ;Service 48h (Allocate Memory Block)
  INT  21h                ;Do it (uses BX = # paragraphs, rtns AX = Sgmt)
  JC  >X70                ;If Error, Quit
  MOV  CS:StdInSegment,AX ;If OK, store the segment
  JMP >X90                ;Done
X70:                      ;Error
  MOV  DX,ExtraMemoryMsg  ;Point at Error Message
  MOV  AL,ErLvlMemory     ;ErrorLevel = Memory Problem
  JMP  Exit               ;Quit
X90:                      ;Done
  POP  BX,AX              ;Restore used registers
  RET

ReleaseExtraMem:
  PUSH AX,BX,ES ;Save used registers
  MOV  BX,OFFSET StdInSegment ;Point [BX] at the Extra Memory Segment
  CMP  W [BX],0 ;Did we already Release it?
  JE  >X90      ;If so, quit
  MOV  ES,[BX]  ;If not, ES = Segment to Remove
  MOV  AH,49h   ;Service 49h (Free Memory)
  INT  21h      ;Do it
  MOV  W [BX],0 ;Mark it as released
X90:            ;Done
  POP  ES,BX,AX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RE-ENABLE THE PROGRAM IF IT WAS DISABLED DUE TO A FAILED UNINSTALL
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         DS:ProgFlags.InMemory
;         DS:ProgFlags.Uninstall
;         ES:Int08Counter (= 0 if TSR is Disabled)
;Outputs: If Installed as TSR, currently Disabled, and not Uninstalling:
;           ES:Int08Counter = 5
;         If any other status,
;           does nothing
;Changes:
;------------------------------------------------------------------------------
ReEnable:
  PUSH DX                  ;Save used registers
  TEST ProgFlags,InMemory  ;Already installed as TSR?
  JZ  >B90                 ;If not, quit
  TEST ProgFlags,Uninstall ;User wants us to Uninstall?
  JNZ >B90                 ;If so, quit
  CMP  ES:Int08Counter,0   ;Disabled?
  JNE >B90                 ;If not, quit
  MOV  ES:Int08Counter,5   ;Re-enable
  MOV  DX,ReEnabledMsg     ;Notify the user
  CALL WriteZPause         ;  about what we did
B90:                       ;Done
  POP  DX                  ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE NEEDED BECAUSE WE ARE A TSR.  NEEDED TO FIND EXISTING PROGRAM
;  IN MEMORY, UNINSTALL FROM MEMORY, ETC.
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;CHECK MEMORY FOR COPY OF CODE TO SEE IF PROGRAM IS ALREADY INSTALLED
;Inputs:  DS = ES = Local Data Area
;         ProgFlags.InMemory = No
;Outputs: ES = TSR program segment, whether a TSR yet or not
;         InMemory  = Yes if program is already installed as TSR
;                   = No if not
;         HandleNum = Multiplex interrupt handle number for Interrupt 2F
;Changes:
;------------------------------------------------------------------------------
TestMemory:
  PUSH AX,BX,CX,DX        ;Save all registers that might change
  PUSH DI,SI,BP,DS        ;  (includes everything except SS & SP)
  MOV  AH,0C0h            ;Function C0, (first available user handle)
M10:                      ;Loop to here for each handle number
  CLD                     ;Go forward with string functions
  XOR  BX,BX              ;Make sure
  XOR  CX,CX              ;  all registers
  XOR  DX,DX              ;  are zero
  MOV  HandleNum,AH       ;Store the handle number
  MOV  AL,MuxInstallChk   ;Install Check Function
  INT  2Fh                ;Do it
  CMP  AL,-1              ;Is this function installed?
  JNE >M40                ;If not, our code can't be installed yet
M12:                      ;Test Program Name
  MOV  AH,HandleNum       ;Get the handle number back again
  MOV  AL,MuxGetProgName  ;Get Program Name
  XOR  DI,DI              ;  Function
  INT  2Fh                ;Do it
  MOV  SI,ProgName        ;Point at our name
  MOV  CX,ProgNameLen     ;Get the string length
  REPE CMPSB              ;Compare the two strings
  JNE >M50                ;If not the same, it's not us
M14:                      ;Test Author
  MOV  AH,HandleNum       ;Get the handle number back again
  MOV  AL,MuxGetAuthor    ;Get Author Function
  INT  2Fh                ;Do it (returns ES:[DI])
  MOV  SI,Author          ;Point at our name
  MOV  CX,AuthorLen       ;Get the string length
  REPE CMPSB              ;Compare the two strings
  JNE >M50                ;If no match, it's somebody elses TSR, not ours
M16:                      ;Test Program Version
  MOV  AH,HandleNum       ;Get the handle number back again
  MOV  AL,MuxGetProgVer   ;Get Program Version Function
  INT  2Fh                ;Do it
  CMP  AX,ProgVer         ;Is it the same version?
  JE  >M80                ;If so, we've found it
M30:                      ;A different version is installed in memory
  MOV  AL,ErLvlVersion    ;If not, ErrorLevel = Wrong version
  MOV  DX,WrongVerMsg     ;Point to error message
  JMP  Exit               ;Quit
M40:                      ;No function installed at this handle at all
  CMP  FirstHandle,0      ;Have we already located an available handle?
  JNE >M50                ;If so, just keep searching
  MOV  AH,HandleNum       ;If not, store this one
  MOV  FirstHandle,AH     ;  as the first available
M50:                      ;Increment the Handle number and try again
  MOV  DS,CS              ;Make sure another
  MOV  ES,CS              ;  Int 2F installation check routine
  MOV  AH,HandleNum       ;  hasn't changed things
  ADD  AH,1               ;Look at the next handle number
  JNC  M10                ;If still less than FFh, keep looking
  MOV  AH,FirstHandle     ;If we've searched them all, we're not in memory
  MOV  HandleNum,AH       ;  so go ahead and store our handle number
  JMP >M90                ;And we're done
M80:                      ;We've already been installed (ES contains segment)
  OR   ProgFlags,InMemory ;Mark as already installed
M90:                      ;We're done
  POP  DS,BP,SI,DI        ;Restore
  POP  DX,CX,BX,AX        ;  all registers
  RET

;------------------------------------------------------------------------------
;PRINT MESSAGE IN DX AND QUIT WITH ERRORLEVEL IN AL
;Inputs:  AL = Errorlevel to exit program with
;         CS:[DX] = Message to Print (to ERR)
;Outputs:
;Changes: Quits program
;------------------------------------------------------------------------------
Exit:
  MOV  DS,CS           ;Point DS at Segment where Error Message is Located
  CALL ReleaseExtraMem ;Release the Extra memory Segment
  OR   AL,AL           ;Was there an Error?
  JZ  >X80             ;If not, jump to handle a normal termination
  CALL BeepErr         ;If error, Beep Once
  CALL WriteZErr       ;Write message to ERR
  CALL WriteCrLfErr    ;Move down
  JMP >X90             ;Jump to quit
X80:                   ;No Error - Write to CON
  CALL WriteZPause     ;Write the message to CON
  CALL WriteCrLfPause  ;Write a New Line
X90:                   ;We're done!!
  MOV  AH,4Ch          ;Service 4Ch (terminate program)
  INT  21h             ;Do it

;------------------------------------------------------------------------------
;FINISH UP A "CAN'T PERFORM REQUEST BECAUSE WE'RE NOT INSTALLED YET" ERROR
;Inputs:  DS:[DX] = First message to Print
;Outputs: Writes Message pointed at by DS:[DX]
;         Sets Exit ErrorLevel (ErLvlUninst)
;         Writes Second Half of Error Message
;Changes: Terminates Program
;------------------------------------------------------------------------------
FinishDataErr:
  CALL WriteZErr      ;Write first part of Error Message
  MOV  AL,ErLvluninst ;ErrorLevel = not installed yet
  MOV  DX,DataErrMsg  ;Point at second part of Error Message
  JUMP Exit           ;Terminate program

;------------------------------------------------------------------------------
;UNINSTALL RESIDENT PROGRAM
;Inputs:  ES = TSR Code Segment
;Outputs:
;Changes: Attempts to Uninstall Program from memory
;         If it can't be Uninstalled, it is Disabled
;------------------------------------------------------------------------------
DoUninstall:
  TEST ProgFlags,Uninstall ;Are we supposed to Uninstall?
  JZ  >U90                 ;If not, just quit
  TEST ProgFlags,InMemory  ;If so, are we even installed yet?
  JZ  >U60                 ;If not, there's an error
  MOV  ES:Int08Counter,0   ;Disable the TSR
  CALL UnconfigureDevices  ;Disable all of the Devices that are configured
  MOV  SI,IntNumTable      ;Point at the Interrupt Address Table
  CALL CompareInts         ;Can we Uninstall the Interrupt Vectors?
  JC  >U70                 ;If not, we can't Uninstall
  MOV  AH,49h              ;If not, service 49h (Release Memory, uses ES)
  INT  21h                 ;Do it
  JC  >U70                 ;If unable to do it, quit with error
U20:                       ;OK to Restore Interrupts
  CALL StopVIO             ;Uninstall I/O Virtualization
  CALL UnRegDvc0Owner      ;Unregister as a Device 0 Owner
  CALL RestoreInts         ;Restore Int's back to normal
  MOV  AX,50               ;Wait a little while
  CALL DelayMSAX           ;  for things to take effect
  MOV  CX,((LastTSRByte-StackSize)/2) ;Number of Words in TSR
  XOR  DI,DI               ;Start of TSR Data Area
  XOR  AX,AX               ;Fill with Zeroes
  REP  STOSW               ;Do it
  MOV  DX,UninstallMsg     ;Point to successful uninstall message
  XOR  AL,AL               ;Errorlevel = 0
  JUMP Exit                ;QUIT
U60:                       ;Not in memory yet - can't Uninstall
  MOV  DX,UninstallErrMsg  ;Point at error message
  MOV  AL,ErLvlUninst      ;Errorlevel = Not Installed Yet
  JUMP Exit                ;QUIT
U70:                       ;Another TSR is in the way - can't Uninstall
  MOV  DX,NoUninstallMsg   ;Point at error message
  MOV  AL,ErLvlMemory      ;Errorlevel = Memory Problem
  JUMP Exit                ;QUIT
U90:                       ;No need to Uninstall
  RET

;------------------------------------------------------------------------------
;CLOSE ALL OPEN HANDLES & UNCONFIGURE DEVICES THAT WE'RE CONTROLLING
;Inputs:  ES = TSR Data Segment
;Outputs:
;Changes: Closes Open Handles, Unconfigures Devices
;------------------------------------------------------------------------------
UnconfigureDevices:
  PUSH CX,DI,SI,DS              ;Save used registers
  MOV  DS,ES                    ;Point DS at TSR Data
  MOV  SI,Int14Request          ;Point at Request Structure
  MOV  DI,DeviceInfoTable       ;Point at Table
  MOV  CX,MaxDevices            ;Number of Table Entries
G10:                            ;Loop to here for each Entry
  TEST [DI].DIFlags,DIFlagInUse ;Valid Entry?
  JZ  >G30                      ;If not, skip it
  CALL RlsDvcDoResetDI          ;Release & Reset the Device
G30:                            ;Done with this entry
  ADD  DI,DeviceInfoStrucSize   ;Point at next entry
  LOOP G10                      ;Keep going until we're done
  POP  DS,SI,DI,CX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;UNREGISTER AS A DEVICE 0 (NEW DEVICE) OWNER
;Inputs:  DS = Local Data Area
;         ES = TSR Data Segment
;Outputs:
;Changes: Unregisters as Device 0 Owner
;NOTES: We do this as a separate item from UnconfigureDevices, and perform it
;         after we've verified that we can actually Uninstall the program.
;       If we are unable to Uninstall and simply Disable ourselves, we don't
;         want to go through the bother of re-Registering as a Device 0 Owner
;         again.  We leave our Device 0 Registration in effect, but effectively
;         just ignore it until we re-Enable ourselves.
;------------------------------------------------------------------------------
UnRegDvc0Owner:
  PUSH AX,SI,DS                      ;Save used registers
  MOV  DS,ES                         ;Point DS at TSR Data
  MOV  SI,Int14Request               ;Point at Request Structure
  MOV  W [SI].I14RCallBackAddr[0],-1 ;Set CallBackAddr to nothing
  MOV  [SI].I14RRequestType,I14RRTUnRegIntfOwner ;Unregister as owner
  MOV  [SI].I14RHostIndex,-1         ;All Hosts
  MOV  [SI].I14RDeviceAddress,0      ;Address 0 (New Connects)
  MOV  AX,Reg0Handle                 ;Handle Number
  MOV  [SI].I14RRequestHandle,AX     ;  returned by Host
  CALL DoInt14CallSaveAllSI          ;Do it
  POP  DS,SI,AX                      ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO COPY DATA FROM THE REDIRECTED INPUT FILE, "COMPRESSING" IT AND
;  STORING IT IN OUR LOCAL MEMORY BUFFER.
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;READ THE DATA FROM STDIN (REDIRECTED INPUT) AND STORE IT IN OUR BUFFER
;Inputs:  StdInSegment
;Outputs:
;Changes: Fills up StdInBuffer
;         As data is read, multiple spaces are converted to single spaces,
;           and EOL's (CR/LF) are also changed to spaces
;         Last character of buffer is a 0
;         Quits with error message if StdIn is too big for buffer
;------------------------------------------------------------------------------
FillStdInBuffer:
  PUSH AX,DI,ES             ;Save used registers
  CALL FlushKbdBuff         ;Don't let keyboard buffer mess with File Input
  MOV  ES,StdInSegment      ;Point ES:DI at
  XOR  DI,DI                ;  our Buffer
  MOV  AL,' '               ;Start the buffer
  STOSB                     ;  with a space
B10:                        ;Loop to here for each character
  CALL ReadStdInChar        ;Get the next character
  JZ  >B80                  ;If end of input, we're done
B15:                        ;AL contains the next character
  CMP  AL,EOF               ;Is it End-of-File?
  JE  >B80                  ;If so, it's the end of input
B20:                        ;Test for Comment
  CMP  AL,';'               ;Is it the start of a comment?
  JNE >B30                  ;If not, try the next possibility
  CALL SkipOverStdInComment ;If so, skip over the comment
  JC  >B80                  ;If EOF, we're done
  JMP  B10                  ;If not EOF, continue
B30:                        ;Test for String
  CMP  AL,'"'               ;Double Quote?
  JE  >B35                  ;If so, handle a string
  CMP  AL,"'"               ;Single Quote?
  JE  >B35                  ;If so, handle a string
  CMP  AL,"`"               ;Left Quote?
  JNE >B40                  ;If not, try the next possibility
B35:                        ;It's a String
  CALL GetStdInString       ;Get the rest of the String
  JC  >B80                  ;If error, just quit (Error handled later)
  JMP  B10                  ;If OK, continue
B40:                        ;Test for EOL
  CMP  AL,CR                ;Carriage Return?
  JE  >B45                  ;If so, handle it
  CMP  AL,LF                ;Carriage Return?
  JNE >B50                  ;If not, it's a regular character
B45:                        ;End of Line
  CALL AddSpace2StdInBuff   ;Add a Space to the Buffer, if needed
  JMP  B10                  ;Continue
B50:                        ;Is a regular character
  CALL AddChar2StdInBuff    ;Add the character to the buffer
  JMP  B10                  ;Continue
B80:                        ;Done, no error
  XOR  AL,AL                ;Mark the end
  STOSB                     ;  of the buffer
B90:                        ;Done
  POP  ES,DI,AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER A COMMENT IN THE REDIRECTED INPUT FILE
;Inputs:  DS = Local Data Area
;         ES:[DI] = StdInBuffer
;         AL = Semicolon (the start of the Comment)
;Outputs: CF = Clear if OK (not EOF)
;            = Set if we've reached EOF
;         Quits Program with error message if Buffer gets full
;Changes:
;NOTES: This looks for either an EOF or LF character to indicate the end of
;         the comment.  If we see a CR, we simply skip it and wait for the LF
;         that should immediately follow the CR.
;       If a line ends in a CR or CR/LF combination, this will not work
;         this will not work correctly.  However, no file we work with
;         should ever be in that format.
;------------------------------------------------------------------------------
SkipOverStdInComment:
  PUSH AX                 ;Save used registers
C10:                      ;Loop to here for each character on the line
  CALL ReadStdInChar      ;Get the next character (AL)
  JZ  >C70                ;If no characters available, EOF
  CMP  AL,EOF             ;End-of-File character?
  JE  >C70                ;If so, we're done
  CMP  AL,CR              ;Carriage Return?
  JE  >C20                ;If so, End of Comment
  CMP  AL,LF              ;Line Feed?
  JNE  C10                ;If not, keep looking
C20:                      ;End of Line
  CALL AddSpace2StdInBuff ;Add a Space to the Buffer if we need it
  JMP >C80                ;Done
C70:                      ;EOF
  STC                     ;Set EOF Flag
  JMP >C90                ;Done
C80:                      ;Not EOF
  CLC                     ;Set not EOF Flag
C90:                      ;Done
  POP  AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A STRING FROM STDIN TO THE STDINBUFFER
;Inputs:  DS = Local Data Area
;         ES:[DI] = StdInBuffer
;         AL = Quote Characer (the start of the String)
;Outputs: CF = Clear if OK (not EOF)
;            = Set if we've reached EOF (Bad String)
;         Quits Program with error message if Buffer gets full
;Changes:
;NOTES: This looks for the matching Quote Character to indicate the end of
;         the String.
;       If we find an EOF, CR, or LF in the string, we return a CF to indicate
;         a bad String.
;------------------------------------------------------------------------------
GetStdInString:
  PUSH AX                 ;Save used registers
  MOV  AH,AL              ;Save quote character
  CALL AddSChar2StdInBuff ;Add the Quote Character to the Buffer
S10:                      ;Loop to here for each character of string
  CALL ReadStdInChar      ;Get the next character (AL)
  JZ  >S70                ;If no characters available, Error
  CMP  AL,EOF             ;End-of-File character?
  JE  >S70                ;If so, Error
  CMP  AL,CR              ;Carriage Return?
  JE  >S70                ;If so, Error
  CMP  AL,LF              ;Line Feed?
  JE  >S70                ;If so, Error
  CALL AddSChar2StdInBuff ;Add the Character to the Buffer
  CMP  AL,AH              ;Is it the end-of-string character?
  JNE  S10                ;If not, keep looking
  JMP >S80                ;Done
S70:                      ;EOF (Bad String)
  STC                     ;Set EOF Flag
  JMP >S90                ;Done
S80:                      ;String is OK
  CLC                     ;Set OK flag
S90:                      ;Done
  POP  AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;READ A CHARACTER FROM STDIN (REDIRECTED INPUT)
;Inputs:
;Outputs: AL = Next character from StdIn
;              ZF Clear
;            = 0 if no character to get
;              ZF Set
;Changes:
;------------------------------------------------------------------------------
ReadStdInChar:
  PUSH DX      ;Save used registers
  MOV  DH,AH   ;Save original AH
  MOV  AH,06h  ;Function 06h
  MOV  DL,0FFh ;  (Direct Console Input)
  INT  21h     ;Do it (returns AL and ZF)
  MOV  AH,DH   ;Restore original AH
  POP  DX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ADD A CHARACTER OR STRING CHARACTER TO THE END OF THE STDIN BUFFER
;Inputs:  DS = Local Data Area
;         ES:[DI] = StdInBuffer
;         AL = Character to add to end of Buffer
;         CLD already issued
;Outputs: Returns to calling program if Character was added OK
;         Quits Program with Error if Buffer is already full
;Changes: DI (maybe)
;------------------------------------------------------------------------------
AddSpace2StdInBuff:
  PUSH AX                ;Save used registers
  MOV  AL,' '            ;Add a Space to the Buffer
  CALL AddChar2StdInBuff ;  if we need it
  POP  AX                ;Restore used registers
  RET

AddChar2StdInBuff:
  CMP  AL,' '            ;Are we adding a space?
  JNE >B00               ;If not, just add it
  CMP  B ES:[DI-1],AL    ;If so, is there already a space?
  JNE >B00               ;If not, just add it
  JMP >B90               ;If so, we're done
AddSChar2StdInBuff:
B00:                     ;Add the character to the Buffer
  CMP  DI,65534          ;Is the Buffer full?
  JAE >B70               ;If so, Error
  STOSB                  ;If not, Store the Character
  JMP >B90               ;Done
B70:                     ;Buffer already Full
  MOV  DX,StdInTooBigMsg ;Point at error message
  MOV  AL,ErLvlStdIn     ;ErrorLevel = problem with StdIn
  JMP  Exit              ;Quit
B90:                     ;Done
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO INITIALIZE VARIOUS TABLES AND STRUCTURES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;RESET/INITIALIZE TRANSLATION TABLE TO DEFAULT VALUES FOR 1 JOYSTICK
;Inputs:   ES = TSR Data Area
;         CLD already issued
;Outputs: Sets all values in XXXlat table to default values appropriate
;           for one joystick
;Changes:
;------------------------------------------------------------------------------
SetXlatDefaults1:
  PUSH AX,BX,CX,DI,DS    ;Save used registers
  MOV  DS,ES             ;Point DS at TSR Data Area
  CALL SetXlatDefaults2  ;Start with defaults for two joysticks
  MOV  AX,(CTAxis SHL 8) ;AL = Joystick Index (0), AH = Control Type (Axis)
  MOV  BX,0FF02h         ;BL = Control Index (2), BH = Control Index 2 (-1)
  MOV  DI,BXXlat         ;[DI] = Translation Table entry
  MOV  CX,2              ;Do 2 Axes (BX, BY)
  CALL StoreXlates       ;Store them
  MOV  AH,CTButton       ;Control Type = Button
  MOV  DI,A03Xlat        ;[DI] = Translation Table entry
  CALL StoreXlates       ;Store them
  POP  DS,DI,CX,BX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESET/INITIALIZE TRANSLATION TABLE TO DEFAULT VALUES FOR 2 JOYSTICKS
;Inputs:   ES = TSR Data Area
;         CLD already issued
;Outputs: Sets all values in XXXlat table to default values appropriate
;           for two joysticks
;Changes:
;------------------------------------------------------------------------------
SetXlatDefaults2:
  PUSH AX,BX,CX,DI,DS    ;Save used registers
  MOV  DS,ES             ;Point DS at TSR Data Area
  MOV  DI,AXXlat         ;[DI] = Translation Table entry
  MOV  AX,(CTAxis SHL 8) ;AL = Joystick Index (0), AH = Control Type (Axis)
  MOV  BX,0FF00h         ;BL = Control Index (0), BH = Control Index 2 (-1)
  MOV  CX,2              ;Do 2 Axes (AX, AY)
  CALL StoreXlates       ;Store them
  INC  AL                ;Joystick Index = 1
  CALL StoreXlates       ;Store BX, BY Axes
  DEC  AL                ;Joystick Index 0
  MOV  BL,4              ;Control Index 4
  DEC  CX                ;Do 1 Axis (Gas Pedal)
  CALL StoreXlates       ;Store it
  MOV  AH,CTButton       ;Control Type = Button
  XOR  BL,BL             ;Control Index 0
  INC  CX                ;Do 2 Buttons (A01, A02)
  CALL StoreXlates       ;Store them
  INC  AL                ;Joystick Index = 1
  CALL StoreXlates       ;Store B01, B02 Buttons
  DEC  AL                ;Joystick Index 0
  MOV  BL,4              ;Control Index 4
  MOV  CX,11             ;Do 11 buttons (B05-B15)
  CALL StoreXlates       ;Store them
  POP  DS,DI,CX,BX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FILL IN A SINGLE ENTRY IN THE TRANSLATION TABLE
;Inputs:   DS = ES = TSR Data Area
;          CX = Number of Translate Table entries to store
;          AL = Value to store in Joystick Index
;          AH = Value to store in Control Type
;          BL = Value to store in Control Index
;          BH = Value to store in Control Index 2
;         [DI] = Translation table entry to fill in
;Outputs: [DI] filled in
;          DI updated (points at next table entry)
;          BL (Control Index) incremented by 1 for each table Entry
;Changes:
;------------------------------------------------------------------------------
StoreXlates:
  PUSH BX,CX ;Save used registers
S10:         ;Loop to here for each entry
  STOSW      ;Store Joystick Index & Control Type
  XCHG AX,BX ;Store
  STOSW      ;  Control
  XCHG AX,BX ;  Indices
  INC  BL    ;Update Control Index
  LOOP S10   ;Keep going until we're done
  POP  CX,BX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE TABLES AND STRUCTURES TO THEIR APPROPRIATE VALUES
;Inputs:  ES = TSR Data Area
;         DS = Area where Int14Requests are stored that we want to configure
;Outputs:
;Changes: Various things
;------------------------------------------------------------------------------
InitTables:
  PUSH DI               ;Save used registers
  MOV  DI,Int14Request  ;Point at first Int 14 Request
  CALL InitInt14Req     ;Fill it up
  MOV  DI,Int14Request2 ;Point at second Int 14 Request
  CALL InitInt14Req     ;Fill it up
  POP  DI               ;Restore used registers
  RET

InitInt14Req:
  MOV  [DI].I14RDvcClass,DvcClassNone         ;Device Class = None
  MOV  [DI].I14RDvcSubClass,DvcSubClassNone   ;Device SubClass = None
  MOV  [DI].I14RDvcProtocol,DvcProtocolNone   ;Device Protocol = None
  MOV  [DI].I14RIntfClass,IntfClassHID        ;Interface Class = HID
  MOV  [DI].I14RIntfSubClass,IntfSubClassNone ;Interface SubClass = None
  MOV  [DI].I14RIntfProtocol,IntfProtocolNone ;Interface Protocol = None
  MOV  [DI].I14RVendorID,-1                   ;Vendor ID = Don't Care
  MOV  [DI].I14RProductID,-1                  ;Product ID = Don't Care
  MOV  W [DI].I14RDataAddress[2],ES           ;Set Segment
  MOV  W [DI].I14RCallBackAddr[2],ES          ;  Registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE NECESSARY INTERRUPTS TO CHECK FOR A NEW DEVICE
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes:
;------------------------------------------------------------------------------
InitInts:
  PUSH CX,DI                    ;Save used registers
I10:                            ;Register Interface 0 Owner
  MOV  DI,Int14Request          ;Point at Request Structure
  MOV  [DI].I14RRequestType,I14RRTRegIntfOwner ;Register as Owner
  MOV  [DI].I14RHostIndex,-1    ;Any/All Hosts
  MOV  [DI].I14RDeviceAddress,0 ;New Device
  MOV  W [DI].I14RCallBackAddr[0],DvcOwnerFarCall ;Code to call
  MOV  [DI].I14RUserPktID,0     ;Packet ID 0
  CALL DoInt14CallCXDI          ;Do it
  JC  >I70                      ;If error, quit
  MOV  Reg0Handle,CX            ;Save Registry Handle
  JMP >I90                      ;Done
I70:                            ;Error
  MOV  DX,HostInitErrMsg        ;If not, point at Error Message
  MOV  AL,ErLvlHostInit         ;Error Initializing Host
  JMP  Exit                     ;Done
I90:                            ;It worked
  POP  DI,CX                    ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO SAVE/INITIALIZE/RESTORE INTERRUPT VECTORS
;This code is needed in every program that intercepts Interrupt Vectors.
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;INITIALIZE ALL APPROPRIATE INTERRUPT VECTORS TO OUR TSR CODE
;Inputs:  DS = ES = Data area to store Interrupt Vectors in (Presumably a UMB)
;         CS = Data Area where Interrupt Number Table is stored
;         CLD Already Issued!
;Outputs:
;Changes: Various Int vectors
;------------------------------------------------------------------------------
ChangeInts:
  PUSH AX,DI,SI       ;Save used registers
  MOV  SI,IntNumTable ;Point at our Interrupt Nubmer Table
C10:                  ;Loop to here for each table entry
  MOV  AL,CS:[SI]     ;Get Interrupt Number
  OR   AL,AL          ;Is it the end of the table?
  JZ  >C90            ;If so, we're done
  MOV  DI,CS:[SI+1]   ;If valid, get our Code Header Offset
  CALL ChangeInt      ;Point the Interrupt at our code
  ADD  SI,3           ;Point at the next table entry
  JMP  C10            ;Keep going until we're done
C90:                  ;Done with the Interrupt Table
  POP  SI,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CHANGE INTERRUPT VECTOR TO THIS PROGRAMS INTERRUPT ROUTINE, AND SAVE THE OLD
;Inputs:  DS:[DI] = Pointer to our new interrupt header
;         AL = Interrupt number to change
;Outputs:
;Changes: Interrupt vector
;------------------------------------------------------------------------------
ChangeInt:
  PUSH AX,BX,DX,ES            ;Save used registers
  CALL InitIntHdr             ;Initialize our Interrupt Header
  MOV  AH,35h                 ;Service 35h (Get interrupt vector)
  INT  21h                    ;Do it (returns ES:BX)
  MOV  W [DI].OldVector,BX    ;Save it
  MOV  W [DI].OldVector[2],ES ;  in [DI]
  LEA  DX,[DI].CodeJmp        ;Point DS:DX at our code address
  MOV  AH,25h                 ;Service 25h (Set interrupt vector)
  INT  21h                    ;Do it
  POP  ES,DX,BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE THE DATA IN A REMOVABLE INTERRUPT VECTOR HEADER
;Inputs:  DS:[DI] = Pointer to Our Interrupt Header Structure
;Outputs:
;Changes: The Interrupt Header Data
;------------------------------------------------------------------------------
InitIntHdr:
  MOV  [DI].HdwreRst,OpCodeRETF                    ;RETF
  MOV  [DI].CodeJmp,(OpCodeJMPShort+(10h SHL 8))   ;JMP ENDS
  MOV  [DI].Signature,424Bh                        ;Signature = 424Bh
  MOV  [DI].EOIFlag,0                              ;We will not issue EOI's
  MOV  [DI].HdwreJmp,(OpCodeJMPShort+(0F4h SHL 8)) ;JMP HdwreRst
  RET

;------------------------------------------------------------------------------
;CHECK THE INTERRUPT VECTORS TO SEE IF THEY'VE BEEN REHOOKED BY ANOTHER PROGRAM
;Inputs:  ES = TSR Data area
;         CLD Already Issued!
;Outputs: CF = Clear if Vectors are OK (We can safely Uninstall our Program)
;            = Set if at least one Vector is unchangeably rehooked
;Changes:
;------------------------------------------------------------------------------
CompareInts:
  PUSH AX,DX,SI          ;Save used registers
  MOV  SI,IntNumTable    ;Point at our interrupt number table
C10:                     ;Loop to here for each table entry
  LODSB                  ;Get the Interrupt Number
  OR   AL,AL             ;Is it the end of the table?
  JZ  >C80               ;If so, we're done
  MOV  DX,[SI]           ;If valid, get our Code Header Offset
  ADD  DX,OFFSET CodeJmp ;Add the Offset to the actual Vector Address
  CALL CompareInt        ;Has it been unchangeably rehooked?
  JC  >C70               ;If so, we can't Uninstall our Program
  INC  SI,2              ;Point at the next table entry
  JMP  C10               ;Keep going until we're done
C70:                     ;At lest one Interupt has been rehooked
  STC                    ;Set the Rehooked flag
  JMP >C90               ;We're done
C80:                     ;Vectors can be Uninstalled
  CLC                    ;Set the OK flag
C90:                     ;We're done
  POP  SI,DX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COMPARE OUR INTERRUPT ADDRESS TO MEMORY SEE IF IT'S BEEN REHOOKED
;Inputs:  ES:[DX] = Vector address to test (our TSR's address)
;         AL = Interrupt number to test
;Outputs: CF = Clear if OK to remove
;         CF = Set if rehooked and can't be removed
;Changes:
;------------------------------------------------------------------------------
CompareInt:
  PUSH BX,CX,ES ;Save used registers
  MOV  CX,ES    ;Point CX:DX at our vector
  CALL FindInt  ;Look for it (returns ES:BX and CF
  POP  ES,CX,BX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESTORE ALL INTERRUPT VECTORS BACK TO THEIR ORIGINAL STATE
;Inputs:  ES = Data Area where original Vector information is stored
;         CLD already issued!
;Outputs:
;Changes: Various Int vectors
;------------------------------------------------------------------------------
RestoreInts:
  PUSH AX,DI,SI       ;Save used registers
  MOV  SI,IntNumTable ;Point at our Interrupt Nubmer Table
R10:                  ;Loop to here for each table entry
  LODSB               ;Get the Interrupt Number
  OR   AL,AL          ;Is it the end of the table?
  JZ  >R90            ;If so, we're done
  MOV  DI,[SI]        ;If valid, get our Code Header Offset
  CALL RestoreInt     ;Restore Interrupt Vector back to original
  INC  SI,2           ;Point at the next table entry
  JMP  R10            ;Keep going until we're done
R90:                  ;We're done
  POP  SI,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESTORE INTERRUPT VECTOR TO ITS ORIGINAL PLACE
;Inputs:  ES:[DI] = Pointer to our Interrupt Header
;         AL = Interrupt number to restore
;Outputs:
;Changes: Interrupt vector back to where it belongs
;Notes:   If we got to here, nothing should have intercepted
;           an interrupt that we haven't checked for.
;         We shouldn't have to worry about running into a road-block!
;------------------------------------------------------------------------------
RestoreInt:
  PUSHF                          ;Save flags
  PUSH BX,CX,DX,SI,BP,ES         ;Save used registers
  MOV  SI,W ES:[DI].OldVector    ;Put the address we'll need to store
  MOV  BP,W ES:[DI].OldVector[2] ;  in BP:SI
  MOV  CX,ES                     ;Point CX:DX
  LEA  DX,[DI].CodeJmp           ;  at our address
  CALL FindInt                   ;Search for it (returns ES:BX)
  CLI                            ;Disable interrupts
  MOV  ES:[BX],SI                ;Store the
  MOV  ES:[BX+2],BP              ;  address
  POP  ES,BP,SI,DX,CX,BX         ;Restore used registers
  POPF                           ;Restore flags
  RET

;------------------------------------------------------------------------------
;SEARCH FOR AN INTERRUPT VECTOR ADDRESS (TO SEE IF OUR CODE CAN BE REMOVED)
;Inputs:  CX:DX = Interrupt Vector address to find (address of our TSR code)
;         AL = Interrupt number to find
;Outputs: CF = Clear if OK to remove
;           ES:BX = address where it is stored
;         CF = Set if rehooked and can't be removed
;           ES:BX = Undefined
;Changes:
;------------------------------------------------------------------------------
FindInt:
  PUSH AX                                ;Save used registers
  XOR  AH,AH                             ;Point ES:BX
  ADD  AX,AX                             ;  at the
  ADD  AX,AX                             ;  main
  MOV  BX,AX                             ;  interupt
  XOR  AX,AX                             ;  vector
  MOV  ES,AX                             ;  table
  CMP  ES:[BX],DX                        ;Is the offset the same as ours?
  JNE >F20                               ;If not, it can't be ours
  CMP  ES:[BX+2],CX                      ;Is the segment the same as ours?
  JE  >F80                               ;If so, it's ours - we're done
F20:                                     ;The main interrupt vector has changed
  LES  BX,ES:[BX]                        ;Get the current vector address
F30:                                     ;Loop for each removable interrupt
  CALL TestIntHdr                        ;Is this a removable interrupt?
  JC  >F70                               ;If not, we can't be removed
  CMP  W ES:[BX-IntOfst].OldVector,DX    ;If so, is the old offset ours?
  JNE >F50                               ;If not, keep looking
  CMP  W ES:[BX-IntOfst].OldVector[2],CX ;If so, is old segment ours?
  JNE >F50                               ;If not, keep looking
  ADD  BX,(OFFSET OldVector) - IntOfst   ;If so, ES:BX = old vector pointer
  JMP >F80                               ;And we're done
F50:                                     ;Removable, but doesn't point at us
  LES  BX,ES:[BX-IntOfst].OldVector      ;Get its old vector address
  JMP  F30                               ;And keep looking for our vector
F70:                                     ;Been rehooked and can't be removed
  STC                                    ;Set the "rehooked" flag
  JMP >F90                               ;We're done
F80:                                     ;It's not rehooked, or it's removable
  CLC                                    ;Set the "OK to remove" flag
F90:                                     ;We're done
  POP  AX                                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AN INTERRUPT POINTER TO SEE IF IT'S REMOVABLE
;Inputs:  ES:[BX] = Interrupt Vector to test
;Outputs: CF = Set if it's not a removable vector
;            = Clear if it is removable
;Changes:
;Notes:   The references to [BX-IntOfst] instead of [BX] are because of
;           our "unusual" Interrupt Header Structure design
;         We have included the (unneeded) Hardware Reset Routine code
;           (a simple RETF) in our Header Structure for efficiency
;------------------------------------------------------------------------------
TestIntHdr:
  PUSH AX,DS                         ;Save used registers
  MOV  DS,ES                         ;Point DS at the interrupt segment
  CMP  B [BX-IntOfst].CodeJmp,0EBh   ;Is the first instruction a Short JMP?
  JNE >H70                           ;If not, it's not removable
  CMP  [BX-IntOfst].Signature,0424Bh ;Is there a Removeable Interrupt Signature?
  JNE >H70                           ;If not, it's not removable
  MOV  AL,[BX-IntOfst].EOIFlag       ;Get the EOI flag
  OR   AL,AL                         ;Is it zero?
  JZ  >H10                           ;If so, continue checking
  CMP  AL,80h                        ;Is it 80h?
  JNE >H70                           ;If not, it's not removable
H10:                                 ;EOI flag is OK
  CMP  B [BX-IntOfst].HdwreJmp,0EBh  ;Is the HdwreJmp instruction a Short JMP?
  JE  >H80                           ;If so, it's removable!
H70:                                 ;Vector is not removable
  STC                                ;Set the "not removable" flag
  JMP >H90                           ;We're done
H80:                                 ;Vector is removable
  CLC                                ;Set the "removable" flag
H90:                                 ;We're done
  POP  DS,AX                         ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO START AND STOP I/O VIRTUALIZATION
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;START I/O VIRTUALIZATION
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes: Initializes I/O Virtualization, if appropriate
;------------------------------------------------------------------------------
StartVIO:
  PUSH AX               ;Save used registers
  MOV  AH,HandleNum     ;AH = TSR Multiplex Handle
  MOV  AL,MuxInstallVIO ;AL = Function = Install VIO
  INT  2Fh              ;Do it
  POP  AX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;STOP I/O VIRTUALIZATION
;Inputs:  ES = TSR Data Area
;Outputs:
;Changes: Uninstalls I/O Virtualization, if it was installed
;------------------------------------------------------------------------------
StopVIO:
  PUSH AX                 ;Save used registers
  MOV  AH,ES:HandleNum    ;AH = TSR Multiplex Handle
  MOV  AL,MuxUninstallVIO ;AL = Function = Uninstall VIO
  INT  2Fh                ;Do it
  POP  AX                 ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE NEEDED TO SET UP AND DO SCREEN PAUSES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;SETUP PARAMETERS FOR DOING A SCREEN PAUSE (FOR PRINTING A LARGE TABLE)
;Inputs:  DS = Local Data Area
;         DS:[DX] = ASCIIZ Header to Print at top of each screen
;                   Last character of the Header MUST be an LF
;                   If SetupPauseNoHdr, we "inject" NoMsg as the Header
;Outputs: Pauses the screen with a MORE message every time it gets full
;NOTES:   If a Screen Header is really large (almost taking up the whole
;           screen itself, with little room for data), this procedure
;           may not work correctly.  This procedure does not validate
;           the size of the Header -- it simply assumes that it is a
;           "reasonable" size compared to the size of the screen.  Most
;           anything that needs a Screen Header (a long data table) will
;           only have a few lines of Header, so this should be OK as a
;           general purpose procedure.
;         After this Setup is complete (which should be near the beginning
;           of MAIN, before anything is sent to the screen), WriteZPause
;           must be called for ALL screen writing to CON:, not WriteZCon!
;         The Pausing occurs in "real time", meaning we can send things
;           to the Pause routine a byte, line, or several screens at a
;           time for processing.  Because we do it in "real time",
;           we can end up with extra MORE's and screen Headers appearing
;           on screen than are actually needed.  During a screen
;           transition (or a transition to a new Header) whether we
;           actually need to write anything or not depends on if there
;           is actually any real data coming afterward (in time), which
;           of course we don't know (since it hasn't happened yet).
;           So, we must assume there is more data to come and process the
;           MORE's and Screen Headers accordingly.  This only causes problems
;           (in the form of extraneous MORE's and Screen Headers)
;           when things happen near the bottom of the screen.
;           DOS's MORE utiltiy (and other programs of similar ilk) do not
;           process in "real time", but rather store things in a Temporary
;           File and then process the file.
;------------------------------------------------------------------------------
SetupPauseNoHdr:
  PUSH DX         ;Save used registers
  MOV  DX,NoMsg   ;Point at 0-length Message
  CALL SetupPause ;Setup the Pause
  POP  DX         ;Restore used registers
  RET

SetupPause:
  PUSH AX,BX,CX,DI,SI     ;Save used registers
  MOV  SI,OFFSET PauseHeaderPtr ;Point [SI] at PauseHeaderPtr
  CMP  W [SI],-1          ;Has the user already cancelled us?
  JE  >S90                ;If so, just quit
  CMP  W [SI],0           ;Has the user stopped pausing already?
  JE  >S85                ;If so, just write the header
  CALL TestRedir          ;Is our output redirected?
  JNC >S10                ;If not, setup our Headers
  MOV  W [SI],0           ;Mark as Redirected or Pausing Stopped/Disabled
  JMP >S85                ;Jump to write the Header and Quit
S10:                      ;Output not redirected
  CALL GetScreenRows      ;Store the number of rows
  MOV  PauseRows,AX       ;  on the screen
  CALL CountZLinesCX      ;Calculate the number of lines
  INC  CX,2               ;  in the new Header
  MOV  DI,PauseRowCount   ;Get the Current Row count
  ADD  DI,CX              ;Add the size of the new header
  SUB  DI,3               ;Adjust for Headers
  DEC  AX                 ;Will writing the new Header
  CMP  DI,AX              ;  cause a Pause event?
  JB  >S50                ;If not, handle it
S20:                      ;New Header is too big to fit on screen
  MOV  W [SI],NoMsg       ;Point Header for second page at nothing
  MOV  PauseHeaderSize,2  ;Set Pause Header Size
  PUSH CX                 ;Save New Header Size
  SUB  AX,PauseRowCount   ;Calculate how many rows to fill up the screen
  MOV  CX,AX              ;Put number of rows in CX
  INC  CX                 ;Adjust it
S30:                      ;Loop to here to fill up the screen
  CALL WriteCrLfPause     ;Write a new line
  LOOP S30                ;Keep going until we're done
  POP  CX                 ;Restore New Header Size
  CMP  W [SI],-1          ;Did the user cancel during the header transition?
  JE  >S90                ;If so, we're done
  CMP  W [SI],0           ;Did the user stop Pausing during the transition?
  JE  >S85                ;If so, just write the Header
S50:                      ;Need to adjust current Row Count
  SUB  PauseRowCount,2    ;Adjust things for the previous Header
S80:                      ;Store new Header Info
  MOV  [SI],DX            ;Store the Header Address
  MOV  PauseHeaderSize,CX ;Store the Header Size
  ADD  PauseRowCount,CX   ;Add the Header Size to the Row Count
  MOV  AX,PauseRows       ;Compensate if
  CMP  AX,PauseRowCount   ;  the new header
  JAE >S85                ;  put us past
  DEC  AX                 ;  the end
  MOV  PauseRowCount,AX   ;  of hte screen
S85:                      ;Write Header
  CALL WriteZCon          ;Write the Header
S90:                      ;We're done
  POP  SI,DI,CX,BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CHECK TO SEE IF STDOUT HAS BEEN REDIRECTED
;Inputs:
;Outputs: CF = Clear if STDOUT is normal (usually the screen)
;            = Set if STDOUT been redirected
;Changes:
;------------------------------------------------------------------------------
TestRedir:
  PUSH AX,BX,DX        ;Save used registers
  MOV  AX,4400h        ;Function 4400h (Get Device Info)
  MOV  BX,StdOutHandle ;Device = STDOUT
  INT  21h             ;Do it (returns DX, changes AX)
  JC  >R80             ;If error, assume not Redirected
  AND  DL,(DvcInfoIsDevice+DvcInfoIsStdOut) ;Mask out unwanted bits
  CMP  DL,(DvcInfoIsDevice+DvcInfoIsStdOut) ;Is it still the STDOUT Device?
  JE  >R80             ;If so, it's not Redirected
R70:                   ;STDOUT has been Redirected
  STC                  ;Set the Redirected flag
  JMP >R90             ;Done
R80:                   ;STDOUT is not Redirected
  CLC                  ;Set the Not-Redirected flag
R90:                   ;Done
  POP  DX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NUMBER OF VIDEO ROWS ON THE SCREEN
;Inputs:
;Outputs: AX = Number of text rows on the screen
;                (usually 25, 43, or 50)
;Changes:
;------------------------------------------------------------------------------
GetScreenRows:
  PUSH BX,CX,DS  ;Save used registers
  MOV  AX,40h    ;Point ES to
  MOV  DS,AX     ;  BIOS data area
  MOV  AH,12h    ;Function 12h (EGA Alternate Select)
  MOV  BX,0FF10h ;Subfunction (BL) 10h (Return EGA information)
                 ;  Preset unrealistic return value (BH)
  INT  10h       ;Do it (returns AX, BX, CX)
  CMP  BL,10h    ;Is the function supported (EGA or better)?
  JE  >R50       ;If not, jump to handle MDA/CGA
  CMP  BH,0FFh   ;Is the return value valid?
  JE  >R50       ;If not, jump to handle MDA/CGA
  TEST B [87h],8 ;If so, is bit 3 of the EGA information byte set?
  JNZ >R50       ;If so, EGA is not the active display
  XOR  AX,AX     ;Get # of
  MOV  AL,[84h]  ;  rows - 1
  JMP >R90       ;We're done
R50:             ;Video card is less than EGA (MDA or CGA)
  MOV  AX,24     ;Number of rows must be 25
R90:             ;We're done
  INC  AX        ;Bump the number of rows by 1
  POP  DS,CX,BX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE ASCIIZ STRING(s) TO THE SCREEN, PAUSING WHEN IF NECESSARY
;Inputs:  DS:[DX] = String to write
;         PauseRows, PauseHeaderPtr, PauseHeaderSize, PauseRowCount
;         CS = Local Data Area
;Outputs: PauseRowCount
;Changes: Pauses the screen and waits for user input, if necessary
;NOTES: This should be called instead of WriteZCon every time something
;         is written during the Pausing.
;       A string can be written by WriteZCon during the pausing, as long
;         as the string does not contain a Line Feed.  Any line containing
;         a Line Feed must come through here or the Line Count will get
;         screwed up!
;------------------------------------------------------------------------------
WriteZPauseFar:
  CALL WriteZPause
  RETF

WriteZPause:
  PUSH AX,BX,CX,DX          ;Save used registers
  TEST CS:ProgFlags2,Write2Err ;Should we write to ERR instead of Pause?
  JZ  >P10                  ;If not, continue
  CALL WriteZErr            ;If so, write to ERR
  JMP >P90                  ;Done
P10:                        ;Write to Pause
  MOV  AX,CS:PauseHeaderPtr ;Put Header Pointer in AX
  CMP  AX,-1                ;Has the Output been cancelled by the user?
  JE  >P90                  ;If so, don't write anything at all
  OR   AX,AX                ;Is STDOUT Redirected?
  JZ  >P80                  ;If so, just write it with no Pausing
  CALL CountZLinesCX        ;Count how many Lines are in the string
  MOV  AX,CS:PauseRowCount  ;Add the new lines
  ADD  AX,CX                ;  to the current lines
  CMP  AX,CS:PauseRows      ;Will we be at or past the end of the screen?
  JB  >P70                  ;If not, just write it
  JE  >P20                  ;If at the end of the screen, handle it
  SUB  AX,CS:PauseRows      ;If not, find
  SUB  CX,AX                ;  the end of the String
  CALL FindEndOfLines       ;  we need to write ([BX])
  MOV  AL,[BX]              ;Save the character that's there now
  MOV  B [BX],0             ;Replace it with a 0
  CALL WriteZPause          ;Write it (will Pause correctly)
  MOV  [BX],AL              ;Restore original character
  MOV  DX,BX                ;Use the new string pointer
  CALL WriteZPause          ;Write it (Pausing if necessary)
  JMP >P90                  ;Done
P20:                        ;At end of screen
  CALL WriteZCon            ;Write the String
  CALL HandlePause          ;Handle the Pause (may change Pointers/Counters)
  JMP >P90                  ;We're done for now
P70:                        ;Store new Row Count
  MOV  CS:PauseRowCount,AX  ;Save the new Row Count
P80:                        ;Write the String
  CALL WriteZCon            ;Write the string
P90:                        ;Done
  POP  DX,CX,BX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE PAUSE LINE, AND WAIT FOR A KEYSTROKE
;Inputs:  CS = Local Data Area
;         PauseRows, PauseHeaderPtr, PauseHeaderSize, PauseRowCount
;Outputs: PauseRowCount, PauseHeaderPtr
;Changes: Pauses the screen and waits for user input, if necessary
;------------------------------------------------------------------------------
HandlePause:
  PUSH AX,BX,DX,DS        ;Save String Segment
  MOV  DS,CS              ;Point DS at our Data Segment
  MOV  BX,OFFSET PauseHeaderPtr ;Point [BX] at the Pause Header
  MOV  DX,PauseMsg        ;Write the
  CALL WriteZCon          ;  Enter a Keystroke message
  CALL FlushKbdBuff       ;Clear the keyboard buffer
L10:                      ;Loop to here to wait for a keystroke
  CALL GetKey             ;Is there a key in the keystroke buffer?
  JZ   L10                ;If not, keep waiting until there is
  MOV  DX,PauseDMsg       ;Delete the
  CALL WriteZCon          ;  MORE Message
  CMP  AL,CtrlC           ;Was the keystroke a Control-C?
  JE  >L40                ;If so, jump to handle it
  CMP  AL,Escape          ;Was the keystroke an Escape?
  JNE >L20                ;If not, just keep going
  MOV  W [BX],0           ;If so, mark us as redirected (stop Pausing)
  JMP >L90                ;And quit
L20:                      ;Continue with the Pause
  MOV  DX,PauseNMsg       ;Write the
  CALL WriteZCon          ;  MORE Replacement Line
  MOV  DX,[BX]            ;Point DX at the Header
  CALL WriteZCon          ;Write it
  MOV  AX,PauseHeaderSize ;Reset the Row Count
  MOV  PauseRowCount,AX   ;Store the new Row Count
  JMP >L90                ;We're done for now
L40:                      ;Ctrl-C (stop altogether)
  MOV  W [BX],-1          ;Mark as stopped
L90:                      ;Done
  POP  DS,DX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE THE NUMBER OF LINES IN AN ASCIIZ STRING (COUNT CR/LF COMBINATIONS)
;Inputs:  DS:[DX] = Pointer to String to measure
;         CLD already issued
;Outputs: CX = Number of LF's in the String (may be 0)
;         ZF = Set if CX = 0
;            = Clear if CX > 0
;Changes:
;------------------------------------------------------------------------------
CountZLinesCX:
  PUSH AX,SI ;Save used registers
  MOV  SI,DX ;Put the pointer in DI
  XOR  CX,CX ;Initialize Lines Counter
Z10:         ;Loop to here for each character
  LODSB      ;Get the next character
  OR   AL,AL ;Is it the end of the string?
  JZ  >Z90   ;If so, we're done
  CMP  AL,LF ;Is it a Line Feed?
  JNE  Z10   ;If not, it's not a new line
  INC  CX    ;If so, Increment the Line Counter
  JMP  Z10   ;Keep looking
Z90:         ;We're done
  OR   CX,CX ;Set return flag
  POP  SI,AX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND THE POINTER AFTER THE FIRST SEVERAL LINES OF A STRING
;Inputs:  DS:[DX] = String to use
;         CX = Number of lines to "skip"
;         CLD already issued
;Outputs: DS:[BX] = "New" String Pointer (after the last line written)
;Changes:
;NOTES: This assumes that the string actually has MORE than the number of
;         lines in AX (it does not look for the end of an ASCIIZ string).
;------------------------------------------------------------------------------
FindEndOfLines:
  PUSH AX,CX  ;Save used registers
  PUSH DI,ES  ;Save used registers
  MOV  BX,CX  ;BX = Line Counter
  MOV  ES,DS  ;Point ES:[DI] at
  MOV  DI,DX  ;  the string
  MOV  AL,LF  ;Look for Line Feeds
  MOV  CX,-1  ;Maximize counter
L10:          ;Loop to here for each new line
  REPNE SCASB ;Look for the end-of-line character
  DEC  BX     ;Decrement the Line Counter
  JNZ  L10    ;If not 0 yet, keep looking
  MOV  BX,DI  ;Store the new string beginning
L90:          ;Done
  POP  ES,DI  ;Restore used registers
  POP  CX,AX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FLUSH KEYBOARD BUFFER
;Inputs:
;Outputs:
;Changes: Flushes the Keyboard Buffer
;------------------------------------------------------------------------------
FlushKbdBuff:
  PUSH AX     ;Save used registers
F10:          ;Loop to here for each key
  CALL GetKey ;Get a key from the keyboard buffer
  JNZ  F10    ;If there was one, get another
  POP  AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;READ CHARACTER FROM THE KEYBOARD BUFFER
;Inputs:
;Outputs: AH = Keyboard scan code
;         AL = ASCII value of keypress (0 if extended ASCII)
;         AX = 0 if no key is waiting
;Changes: ZF = Set if no key in buffer (AX = 0)
;            = Clear if a key was found (AX = key)
;------------------------------------------------------------------------------
GetKey:
  MOV  AH,1  ;Service 1 (Keystroke waiting?)
  INT  16h   ;Do It
  JZ  >K10   ;If no key waiting, we're done
  XOR  AH,AH ;If a key is waiting, service 0 (Get keystroke)
  INT  16h   ;Do It
  JMP >K90   ;We're done
K10:         ;No keystroke waiting
  XOR  AX,AX ;Make sure AX=0
K90:         ;We're done
  OR   AX,AX ;Set the found/not found flag
  RET

;------------------------------------------------------------------------------
;WRITE A CR/LF TO THE CONSOLE WITH A PAUSE
;Inputs:
;Outputs: To Screen, Updates Pause Counters
;Changes:
;------------------------------------------------------------------------------
WriteCrLfPause:
  PUSH DX          ;Save used registers
  MOV  DX,CrLfMsg  ;Write a Cr/Lf to
  CALL WriteZPause ;  the Pause Device
  POP  DX          ;Restore used registers
  RET

WriteCrLfPause2:
  CALL WriteCrLfPause ;Move down
  CALL WriteCrLfPause ;  2 lines
  RET

WriteCrLfErr:
  PUSH DX         ;Save used registers
  MOV  DX,CrLfMsg ;Write a Cr/Lf to
  CALL WriteZErr  ;  the ERR Device
  POP  DX         ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SOME GENERIC CODE TO WRITE STRINGS, ETC.
;This code is needed in almost every program we write.
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;GETS RID OF ENVIRONMENT INFORMATION - NOT NEEDED IN TSR
;Inputs:
;Outputs:
;Changes: Quits if problem releasing memory
;------------------------------------------------------------------------------
DelEnvironment:
  PUSH AX,ES            ;Save used registers
  MOV  ES,[PSPEnvirSeg] ;Point ES at the Environment Segment
  MOV  AH,49h           ;Service 49h (free up memory)
  INT  21h              ;Do it
  JNC >E90              ;If no error occurred, Quit
  MOV  DX,NoEnvironMsg  ;If error, point to message
  MOV  AL,ErLvlMemory   ;Errorlevel = Memory Problem
  JMP  Exit             ;QUIT
E90:                    ;We're done
  POP  ES,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;BEEP THE SPEAKER
;Inputs:
;Outputs: Beeps the speaker
;Changes:
;------------------------------------------------------------------------------
BeepErr:
  PUSH AX,BX    ;Save used registers
  XOR  BX,BX    ;BH = Video page = 0
  MOV  AX,0E07h ;AH = 0Eh = Function = Write TTY
                ;AL = 07h = Character to write = BEL
  INT  10h      ;Do it
  POP  BX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CAPITALIZE A CHARACTER (Byte)
;Inputs:  AL = character to be capitalized
;Outputs: AL = capitalized character
;Changes:
;------------------------------------------------------------------------------
Capitalize:
  CMP  AL,'a'     ;Is it less than 'a'?
  JB  >Z90        ;If so, quit
  CMP  AL,'z'     ;Is it more than 'z'?
  JA  >Z90        ;If so, quit
  SUB  AL,'a'-'A' ;If between 'a' and 'z', capitalize it
Z90:
  RET

;------------------------------------------------------------------------------
;WRITE A SPECIFIC CHARACTER OR SET OF CHARACTERS TO THE SCREEN
;Inputs:
;Outputs: To screen
;Changes:
;------------------------------------------------------------------------------
WriteAL:
  PUSH DX              ;Save used registers
  MOV  DL,AL           ;Write what's in AL
  JMP >C00             ;Do it
WriteBlock:
  PUSH DX              ;Save used registers
  MOV  DL,''          ;Write a Block Character
  JMP >C00             ;Do it
WriteCSep:
  PUSH DX              ;Save used registers
  MOV  DX,CMsgSep      ;Point at Message
  JMP >C10             ;Do it
WriteDot:
  PUSH DX              ;Save used registers
  MOV  DL,'.'          ;Write a Dot
  JMP >C00             ;Do it
WriteHexEnd:
  PUSH DX              ;Save used registers
  MOV  DL,'h'          ;Write an "h"
  JMP >C00             ;Do it
WriteSpace:
  PUSH DX              ;Save used registers
  MOV  DX,SpaceMsg     ;Point at Message
  JMP >C10             ;Do it
Write2Spaces:
  PUSH DX              ;Save used registers
  MOV  DX,Space2Msg    ;Point at Message
  JMP >C10             ;Do it
Write3Spaces:
  PUSH DX              ;Save used registers
  MOV  DX,Space3Msg    ;Point at Message
  JMP >C10             ;Do it
C00:
  MOV  SingleMsg[0],DL ;Put the character in the Single Character String
  MOV  DX,SingleMsg    ;Point at Single Character String
C10:                   ;DX is pointed at String
  CALL WriteZPause     ;Write the String
  POP  DX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE SOME SPACES TO THE SCREEN
;Inputs:  CL = Number of spaces to write
;         SpaceMsg
;Outputs: To the screen
;Changes:
;------------------------------------------------------------------------------
;WriteSpacesErr:
;  PUSH AX          ;Save used registers
;  MOV  AL,-1       ;Mark as ERR
;  JMP >S00         ;Do it
WriteSpaces:
  PUSH AX          ;Save used registers
  XOR  AL,AL       ;Mark as CON
;  JMP >S00         ;Do it
S00:
  PUSH CX,DX       ;Save used registers
  OR   CL,CL       ;Should we write 0 spaces?
  JZ  >S90         ;If so, we're done
  XOR  CH,CH       ;If OK, change it to a word
  MOV  DX,SpaceMsg ;Point at the message
  OR   AL,AL       ;Write to CON?
  JNZ >S30         ;If not, Handle ERR
S10:               ;Loop to here for each space
  CALL WriteZPause ;Write the space
  LOOP S10         ;Keep going until we're done
  JMP >S90         ;Done
S30:               ;Write to Err
  CALL WriteZErr   ;Write the space
  LOOP S10         ;Keep going until we're done
S90:               ;We're done
  POP  DX,CX       ;Restore used registers
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT NIBBLE, BYTE, OR WORD TO HEX CHARACTER STRING, AND WRITE IT
;Inputs:  AL/AX = Nibble/Byte/Word to be written
;         CLD must be set already!
;Outputs: Writes to the Screen
;Changes: HexString
;------------------------------------------------------------------------------
WriteNibbleHex:
  PUSH BX             ;Save used registers
  MOV  BX,3           ;Need to start of offset 3 in string
  JMP >H00            ;Jump to do it
;WriteByteHex:
;  PUSH BX             ;Save used registers
;  MOV  BX,2           ;Need to start of offset 2 in string
;  JMP >H00            ;Jump to do it
WriteWordHex:
  PUSH BX             ;Save used registers
  XOR  BX,BX          ;Need to start of offset 0 in string
;  JMP >H00            ;Jump to do it
H00:                  ;Start converting the strings
  PUSH DX,DI          ;Save used registers
  MOV  DI,HexString   ;Point DS:[DI] at HexString
  MOV  DX,DI          ;Point DS:[DX] at HexString
  CALL Word2HexString ;Convert the entire Word to a String
  ADD  DX,BX          ;Point DX at the beginning of where we write
  CALL WriteZPause    ;Write it
  POP  DI,DX          ;Restore used registers
  POP  BX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE LEAST SIGNIFICANT NUMERAL OF A NUMBER TO THE SCREEN
;Inputs:  AL = Number to write (0-255)
;Outputs: Write LSN (Least Significant Numeral) of Number to ERR
;Changes:
;------------------------------------------------------------------------------
WriteNumeralAL:
  PUSH AX      ;Save used registers
R10:           ;Loop to here to calculate numeral
  CMP  AL,9    ;Is it between 0 and 9 yet?
  JBE >R20     ;If so, continue
  SUB  AL,10   ;If not, subtract 10
  JMP  R10     ;Keep going
R20:           ;AL contains numeral
  ADD  AL,'0'  ;Convert to a Numeral
  CALL WriteAL ;Write it
  POP  AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT A BYTE OR WORD TO A HEXADECIMAL STRING (2 OR 4 CHARACTERS)
;Inputs:  AX = Word to Convert to a Hex String
;         DS = Local Data Area
;         CLD already issued
;Outputs: DS:[DI]
;Changes: DI
;------------------------------------------------------------------------------
Byte2HexString:
  PUSH AX,ES            ;Save used registers
  MOV  ES,DS            ;Point ES:[DI] at the String
  CALL Word2HexString10 ;Convert it
  POP  ES,AX            ;Restore used registers
  RET

Word2HexString:
  PUSH AX,BX,ES         ;Save used registers
  MOV  BX,AX            ;Save original Word
  MOV  ES,DS            ;Point ES:[DI] at
  CALL Word2HexString00 ;Convert it
  POP  ES,BX,AX         ;Restore used registers
  RET

Word2HexString00:
  MOV  AL,BH            ;Convert High Byte
  CALL Word2HexString10 ;  to ASCII
  MOV  AL,BL            ;Work with Low Byte
Word2HexString10:       ;Convert Byte to ASCII
  AAM  10h              ;Separate Low & High Nibbles
  XCHG AH,AL            ;Put High Nibble in AL
  CALL Word2HexString20 ;Convert High Nibble to ASCII
  XCHG AH,AL            ;Work with Low Nibble
Word2HexString20:       ;Convert Nibble to ASCII
  ADD  AL,90h           ;Convert BCD
  DAA                   ;  to Decimal
  ADC  AL,40h           ;Convert Decimal
  DAA                   ;  to ASCII
  STOSB                 ;Store the Nibble
  RET

;------------------------------------------------------------------------------
;WRITE A BYTE OR WORD TO THE SCREEN AS A DECIMAL
;Inputs:  AL/AX = Byte/Word to Write
;Outputs:
;Changes:
;------------------------------------------------------------------------------
;WriteByteMin:
;  PUSH AX,BX         ;Save used registers
;  XOR  AH,AH         ;Make sure high byte is 0
;  MOV  BH,1          ;Mark as doing minimum
;  JMP >W00           ;Do it
;WriteWordMin:
;  PUSH AX,BX         ;Save used registers
;  MOV  BH,1          ;Mark as doing minimum
;  JMP >W00           ;Do it
ByteToString:
  PUSH AX,BX         ;Save the original word & BP
  XOR  AH,AH         ;Make sure high byte is 0
  XOR  BH,BH         ;BH = No Minimum, Byte
  JMP >W00           ;Jump to start the loop
WordToString:
  PUSH AX,BX         ;Save the original word
  MOV  BH,2          ;BP = No Minimum, Word
W00:                 ;Do a word or a byte
  PUSH CX,DX,SI      ;Save used registers
  MOV  SI,SingleMsg  ;Point at the single character message
  XOR  BL,BL         ;Test for first non-zero character is false
  MOV  CX,10000      ;First divisor is 10,000
W20:                 ;Loop to here for each character
  XOR  DX,DX         ;High word of Dividend = 0
  DIV  CX            ;Divide DX:AX by current divisor
  OR   AX,AX         ;Is this a non-zero character?
  JNZ >W30           ;If so, we need to write it
  OR   BL,BL         ;Has there already been a non-zero character?
  JNZ >W30           ;If so, we need to write it
  CMP  CX,1          ;Is this the last character?
  JE  >W30           ;If so, we need to write it
  TEST BH,1          ;Are we doing a minimum?
  JNZ >W40           ;If not, don't write anything
  MOV  AL,' '        ;Assume a space
  TEST BH,2          ;Doing a word?
  JNZ >W35           ;If so, write the space
  CMP  CX,100        ;If not, doing the hundreds character?
  JBE >W35           ;If so, write the space
  JMP >W40           ;If not, don't write anything
W30:                 ;We need to write this character
  INC  BL            ;Test for first non-zero character is true
  ADD  AL,'0'        ;Convert the Number to ASCII
W35:                 ;Write the character
  MOV  [SI],AL       ;Write the
  CALL WriteZPauseSI ;  character
W40:                 ;Skip this character - just make it a space
  CMP  CX,1          ;Was this the last division to do?
  JE  >W90           ;If so, we're done
  MOV  AX,DX         ;If not, start next loop with remainder of division
  PUSH AX            ;Save register for a second
  XOR  DX,DX         ;So dividend won't be messed up
  MOV  AX,CX         ;Want to divide divisor in CX
  MOV  CX,10         ;Want to divide by 10
  DIV  CX            ;Divide it
  MOV  CX,AX         ;Store it back in CX
  POP  AX            ;Restore register
  JMP  W20           ;And do it again
W90:                 ;We're done
  POP  SI,DX,CX      ;Restore used registers
  POP  BX,AX         ;Restore original word & BP
  RET

;------------------------------------------------------------------------------
;SEARCH A BYTE-TO-STRING LOOKUP TABLE FOR A MATCHING ENTRY
;Inputs:  DS:[BX] = Table to search
;         AL      = Byte to search for
;         CLD already issued
;Outputs: CF = Clear if a byte match as found in table
;              BX = Pointer to the String
;            = Set if no match found in table
;              BX = unchanged
;Changes:
;NOTES: Last Entry in Table (end-of-table marker) = DW -1
;------------------------------------------------------------------------------
SearchByte2StringTbl:
  PUSH AX,SI            ;Save used registers
  CLD                   ;Go forward with string functions
  MOV  SI,BX            ;Put pointer in SI
  MOV  AH,AL            ;Put byte to check in AH
S10:                    ;Loop to here for each table entry
  LODSB                 ;Get the next table entry
  CMP  AL,-1            ;Is it possibly the end of the table?
  JNE >S20              ;If not, continue
  CMP  B [SI],-1        ;Is it actually the end of the table?
  JE  >S70              ;If so, there's no match
S20:                    ;Not end of table
  CMP  AL,AH            ;Is it a match?
  JE  >S80              ;If so, handle it!
  CALL SkipOverStringSI ;If not, skip over the string
  JMP  S10              ;And keep looking
S70:                    ;No entry found
  STC                   ;Set the not found flag
  JMP >S90              ;Quit
S80:                    ;Entry found [SI]
  MOV  BX,SI            ;Point BX at the String
  CLC                   ;Set the found flag
S90:                    ;Done
  POP  SI,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER AN ASCIIZ STRING
;Inputs:  DS:[SI] = ASCIIZ String to Skip Over
;Outputs:
;Changes: [SI] points at character after the 0
;------------------------------------------------------------------------------
SkipOverStringSI:
  PUSH CX            ;Save used registers
  CALL CalcStrSizeSI ;Calculate the size of the string
  ADD  SI,CX         ;Add it to the pointer
  INC  SI            ;Skip over the 0
  POP  CX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE THE LENGTH OF AN ASCIIZ STRING
;Inputs:  DS:[SI] = ASCIIZ String to wrok with
;Outputs: CX = Length of String
;         ZF = Set if string is 0 length
;            = Clear if not
;Changes:
;------------------------------------------------------------------------------
CalcStrSizeSI:
  PUSH DX            ;Save used registers
  MOV  DX,SI         ;Calculate the
  CALL CalcStrSizeDX ;  length of the string
  POP  DX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE AN ASCIIZ STRING, POINTED AT BY SI, TO PAUSE
;Inputs:  DS:[SI] = ASCIIZ String to Write
;Outputs: To Screen (CON)
;Changes:
;------------------------------------------------------------------------------
WriteZPauseSI:
  PUSH DX          ;Save used registers
  MOV  DX,SI       ;Write the
  CALL WriteZPause ;  string
  POP  DX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE ASCIIZ STRING TO CON OR TO ERR
;Inputs:  DS:[DX] = Pointer to string
;         CS = Local Data Area
;Outputs: Writes the string to CON or ERR
;Changes:
;------------------------------------------------------------------------------
WriteZCon:                  ;Write to CON
  CMP  CS:PauseHeaderPtr,-1 ;Should we write anything?
  JE  >Z90                  ;If not, just quit
  PUSH BX                   ;Save used register
  MOV  BX,StdOutHandle      ;Write to STDOUT
  JMP >Z00                  ;Jump to do it
WriteZErr:                  ;Write to ERR
  PUSH BX                   ;Save used register
  MOV  BX,StdErrHandle      ;Write to STDERR
Z00:
  PUSH AX,CX                ;Save used registers
  CALL CalcStrSizeDX        ;Calculate the size of the string (returns CX)
  JZ  >Z80                  ;If nothing to write, just quit
  MOV  AH,40h               ;Function 40h (Write to Device)
  INT  21h                  ;Do it
Z80:                        ;We're done
  POP  CX,AX                ;Restore used registers
  POP  BX                   ;Restore used register
Z90:                        ;We're done
  RET

;------------------------------------------------------------------------------
;CALCULATE THE LENGTH OF AN ASCIIZ STRING
;Inputs:  DS:[DX] = Pointer to the string
;Outputs: CX = Length of the string
;         ZF = Set if string is 0 length
;            = Clear if not
;Changes:
;------------------------------------------------------------------------------
CalcStrSizeDX:
  PUSH AX,DI  ;Save used registers
  PUSH ES     ;Save used registers
  MOV  ES,DS  ;Point ES:[DI]
  MOV  DI,DX  ; at the string
  XOR  AL,AL  ;Look for a 0
  MOV  CX,-1  ;Start with max count for REPNE
  REPNE SCASB ;Find the end of the string
  NOT  CX     ;Calculate the size
  DEC  CX     ;  of the string (sets ZF)
  POP  ES     ;Restore used registers
  POP  DI,AX  ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO PARSE THE COMMAND LINE FOR SWITCHES, AND PRINT ERRORS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;PARSE THE COMMAND LINE FOR SWITCHES
;Inputs:
;Outputs: All kinds of stuff!
;Changes:
;------------------------------------------------------------------------------
ParseIt:
  CALL FixCmdLineCR    ;Replace CR at end of Command Line with a 0
  CALL FillStdInBuffer ;Copy redirected input to our buffer, if needed
                       ;  Quits if error
  MOV  ParseTest,Yes   ;Test input strings for errors
  CALL ParseThings     ;Do all input strings
  MOV  ParseTest,No    ;Parse string for real
  CALL ParseThings     ;Do all input strings
P90:
  RET

ParseThings:
  PUSH SI                       ;Save used registers
  MOV  SI,EnvVarPointer         ;Point at our Environment Variable


  CMP  SI,-1                    ;Is there an Environment Variable to parse?
  JE  >P30                      ;If no, skip it
  OR   ProgFlags2,DoEnvir       ;Mark as doing the Environment
  CMP  ParseTest,Yes            ;Just doing the Test Run?
  JE  >P20                      ;If so, don't write the variable contents yet
  MOV  DX,EnvMsg                ;Write the first part of the
  CALL WriteZPause              ;  environment message to the screen
  CALL WriteEnvCon              ;Write the environment variable contents
  CALL WriteCrLfPause2          ;Move down
P20:                            ;Do the Environment Variable
  CALL ParseCmdLine             ;Parse the Environment (quits if Error)
P30:                            ;Do StdIn
  AND  ProgFlags2,(NOT DoEnvir) ;Mark as doing Command Line
  OR   ProgFlags2,DoStdIn       ;Parse the
  XOR  SI,SI                    ;  StdIn buffer
  CALL ParseCmdLine             ;  Quit if error
  AND  ProgFlags2,(NOT DoStdIn) ;  is found
  MOV  SI,81h                   ;Assume "real" Command Line
  CMP  CmdLineVarPointer,-1     ;Have a CMDLINE Environment variable?
  JE  >P50                      ;If not, just do regular command line
  MOV  SI,CmdLineVarPointer     ;If so, point at the variable
P50:                            ;Pointed at Command Line Tail
  CALL ParseCmdLine             ;Parse Command Line
  POP  SI                       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PARSE THE COMMAND LINE FOR SWITCHES
;Inputs:  ParseTest
;         [SI] = Option String to Parse
;Outputs:
;Changes: SI
;------------------------------------------------------------------------------
ParseCmdLine:
  PUSH AX             ;Save used registers
P10:                  ;Look for the next switch
  CALL IsSpaceOrEOX   ;Look for a valid character
  JZ  >P90            ;If EOL, we're done
  CMP  AL,'/'         ;Is it a slash?
  JE  >P20            ;If so, jump to handle it
  DEC  SI             ;If not, point back at the first character
  JMP >P30            ;Continue
P20:                  ;It's a slash
  CALL IsSpaceOrEOL   ;Look for a valid switch character
  JZ  >P70            ;If no character, there's an error
P30:                  ;Look for the switch
  CALL GetSwitch      ;See if it matches one of our switches
  JNZ  P10            ;If a match, look for the next switch
P70:                  ;Error - tell the user where it was
  CALL WriteCmdErr    ;Display the error for the user
  MOV  AL,ErLvlOption ;ErrorLevel = Bad Option on command line
  MOV  DX,NoMsg       ;Point at an empty message
  JMP  Exit           ;Print error message and quit
P90:                  ;We're done
  POP  AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE SWITCH CHARACTERS, AND DO WHATEVER IT IS THEY'RE ASKING
;Inputs:  [SI] = Command line pointer
;          ES  = TSR Data Area
;          DS  = Local Data Area (same as CS)
;Outputs: Lots of stuff!!!
;         ParseError
;         ZF = Set if Error
;            = Clear if OK
;Changes:
;------------------------------------------------------------------------------
GetSwitch:
  PUSH AX,BX,DI,BP         ;Save used registers
  MOV  BX,SwitchList       ;Point at the list of switch code words
  CALL GetCode             ;See if its a match
  JNC >S10                 ;If so, jump to test it
  CALL GetNextChar         ;If not, it's a single character
  CALL Capitalize          ;Capitalize it
S10:                       ;We have a Switch to test!
  MOV  BX,SwitchTbl        ;Point at the Table of Offsets
S20:                       ;Loop to here for each table entry
  MOV  AH,[BX]             ;Get the switch from our table?
  OR   AH,AH               ;Is it the end of the table?
  JZ  >S70                 ;If so, there's an error
  CMP  AL,AH               ;If a valid entry, is it a match?
  JE  >S30                 ;If so, jump to handle it
  ADD  BX,3                ;If not, point at the next table entry
  JMP  S20                 ;And keep looking
S30:                       ;It's a match from the table
  MOV  DI,OFFSET ProgFlags ;Point [DI] at ProgFlags
  MOV  BP,OFFSET TSRFlags  ;Point [BP] at TSRFlags
  MOV  AH,ParseTest        ;Pass the "Testing" flag
  OR   AH,AH               ;Set the ZF for Testing
  CALL W [BX+1]            ;Do the Switch stuff
  JMP >S90                 ;And quit
S70:                       ;There's an error
  MOV  ParseError,Yes      ;Mark the error flag
S90:                       ;We're done
  CMP  ParseError,Yes      ;Set/Clear the error flag
  POP  BP,DI,BX,AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Display Help (Syntax) Message
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchH:
  OR   B [DI],Help ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Display ErrorLevel Table
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchErrLvl:
  OR   B [DI+2],ErrLvl ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Display list of Command-line (Option) Aliases
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchA:
  OR   B [DI+2],Alias ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Uninstall Program from Memory
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchU:
  OR   B [DI],Uninstall ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Install Program in Lower Memory, even if Upper Memory is available
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchL:
  PUSH AX                   ;Save used registers
  MOV  AL,UseLowMemory      ;Bit mask to use
  CALL DoSwitchWithYNPrgFwd ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Display Program Status
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchS:
  OR   B [DI+2],Status ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Display HID Report Descriptor of Last Device found
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchR:
  OR   B [DI+2],Report ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Display list of Bad Devices
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchB:
  OR   B [DI+2],BadDevices ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Display USB Descriptors of Last Device found
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchC:
  OR   B [DI+2],Descriptors ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Beep Speaker when "Bad" Devices are discovered
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchBeep:
  PUSH AX                   ;Save used registers
  MOV  AL,NoBeep            ;Bit mask to use
  CALL DoSwitchWithYNTsrRvs ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Use 15-button Protocol
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchF:
  PUSH AX                   ;Save used registers
  MOV  AL,Buttons15         ;Bit mask to use
  CALL DoSwitchWithYNTsrFwd ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Use ThrustMaster Protocol
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchT:
  PUSH AX                   ;Save used registers
  MOV  AL,ThrustMaster      ;Bit mask to use
  CALL DoSwitchWithYNTsrFwd ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Display Single Joystick Status
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchJ:
  PUSH AX                ;Save used registers
  CALL SkipColonEquals   ;Skip over the Colon or Equals, if there
  CALL GetNumber         ;Get the Joystick Index
  JC  >J70               ;If none, error
  CMP  AX,MaxDevices     ;Is it too big?
  JAE >J70               ;If so, Error
  MOV  JoyStatusIndex,AL ;If not, store it
  JMP >J90               ;Done
J70:                     ;Error
  MOV  ParseError,Yes    ;Mark as an Error
J90:                     ;Done
  POP  AX                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Set I/O Port for Virtualization
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchI:
  PUSH AX                   ;Save used registers
  CALL SkipColonEquals      ;Skip over the Colon or Equals, if there
  CALL GetNumber            ;Get the Port Number
  JC  >I70                  ;If none, Error
  CMP  AX,MinPort           ;Is it a valid Port number?
  JB  >I70                  ;If not, Error
  CMP  AX,MaxPort           ;Is it a valid Port number?
  JA  >I70                  ;If not, Error
  CMP  ParseTest,Yes        ;Just the test run?
  JE  >I90                  ;If so, we're done
  CMP  AX,ES:VirtIOTable[0] ;If not, is the Port Number changing?
  JE  >I90                  ;If not, we're done
  CALL StopVIO              ;If so, Uninstall original VIO
  MOV  ES:VirtIOTable[0],AX ;Store the New Port Number
  CALL StartVIO             ;Reinstall VIO
  JMP >I90                  ;Done
I70:                        ;Error
  MOV  ParseError,Yes       ;Mark as an Error
I90:                        ;Done
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Set BIOS Center Coordinate
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchCenter:
  PUSH AX                 ;Save used registers
  CALL SkipColonEquals    ;Skip over the Colon or Equals, if there
  CALL GetNumber          ;Get the Center Value
  JC  >C70                ;If none, error
  CMP  AX,MinCenter       ;Is it a valid number?
  JB  >C70                ;If not, Error
  CMP  AX,MaxCenter       ;Is it a valid number?
  JA  >C70                ;If not, Error
  CMP  ParseTest,Yes      ;Just the test run?
  JE  >C90                ;If so, we're done
  SHL  AX,1               ;Multiply by 2 (convert from Center to maximum)
  MOV  ES:NormalizeMax,AX ;Store it
  JMP >C90                ;Done
C70:                      ;Error
  MOV  ParseError,Yes     ;Mark as an Error
C90:                      ;Done
  POP  AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Set Movement Percent
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchP:
  PUSH AX                ;Save used registers
  CALL SkipColonEquals   ;Skip over the Colon or Equals, if there
  CALL GetNumber         ;Get the Center Value
  JC  >P70               ;If none, error
  CMP  AX,MinPercent     ;Is it a valid number?
  JB  >P70               ;If not, Error
  CMP  AX,MaxPercent     ;Is it a valid number?
  JA  >P70               ;If not, Error
  CMP  ParseTest,Yes     ;Just the test run?
  JE  >P90               ;If so, we're done
  MOV  ES:MovePercent,AX ;Store it
  JMP >P90               ;Done
P70:                     ;Error
  MOV  ParseError,Yes    ;Mark as an Error
P90:                     ;Done
  POP  AX                ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SWITCH AND SUPPORT ROUTINES FOR HANDLING MAPPING FUNCTIONS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;Process Mapping Functions
;Inputs:   DS    = Local Data Area
;          ES    = TSR Data Area
;         [DI]   = ProgFlags
;         [DI+1] = ProgFlags2
;         [DI+2] = StatusFlags
;         [SI]   = Command Line Pointer
;         [BP]   = TSRFlags
;          AH    = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchM:
  PUSH AX,BX,CX,DX,DI          ;Save used registers
  CALL SkipColonEquals         ;Skip Colon/Equals (if there)
  MOV  BX,SwitchListDIIndex    ;Look for a
  CALL GetCode                 ;  Joystick Descriptor
  JNC >M10                     ;If there, continue
  CALL GetNextChar             ;If not, get the next character
  CALL Capitalize              ;Capitalize it
  CMP  AL,'J'                  ;Is it J?
  JE  >M10                     ;If so, continue
  CMP  AL,'G'                  ;Is it G?
  JE  >M10                     ;If so, continue
  DEC  SI                      ;If not, point back at the character
M10:                           ;Handled the Joystick Descriptor
  CALL IsSpaceOrEOX            ;Get the next character
  JZ  >M70                     ;If EOL, error
  CALL Capitalize              ;Capitalize it
  CMP  AL,'A'                  ;Joystick A?
  JE  >M20                     ;If so, continue
  CMP  AL,'B'                  ;Joystick B?
  JNE >M70                     ;If not, error
M20:                           ;DL = Joystick ID ('A' or 'B')
  CALL GetXlatEntity           ;Get the Translation Entity & SubEntity Type (DX)
  JC  >M70                     ;If error, quit
  CALL GetXlatPtr              ;If OK, get the Translation table pointer ([DI])
  CALL SkipColonEquals         ;Skip Colon/Equals (if there)
  MOV  BX,SwitchListFrom       ;Look for a
  CALL GetCode                 ;  Separator Code (don't actually need it)
  CALL GetControlInfo          ;Get the Map From Control Info (CX, DX)
  JC  >M70                     ;If error, quit
  CMP  ParseTest,Yes           ;Is this just the Test Run?
  JE  >M90                     ;If so, we're done
  MOV  W ES:[DI].XlatJIndex,DX ;If real, store Joystick Index & Control Type
  MOV  W ES:[DI].XlatCIndex,CX ;Store the Control Indices
  JMP >M90                     ;Done
M70:                           ;Error
  MOV  ParseError,Yes          ;Mark as an Error
M90:                           ;Done
  POP  DI,DX,CX,BX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE ENTITY TYPE WE SHOULD CHANGE FROM THE COMMAND-LINE
;Inputs:   AL = 'A' or 'B' (Analog Joystick ID)
;         [SI] = Command-line Pointer
;Outputs: CF = Clear if OK
;              DL = Entity Type ('A' = Axis, 'B' = Button)
;              DH = SubEntity Type ('X'-'Z' for Axes, 1-15 for Button)
;            = Set if Error
;              DX = Undefined
;Changes: SI
;------------------------------------------------------------------------------
GetXlatEntity:
  PUSH AX,BX,CX               ;Save used registers
  MOV  CL,AL                  ;Save Joystick ID ('A' or 'B')
  XOR  DX,DX                  ;Default Entity & SubEntity Descriptor = Nada
  CALL SkipComma              ;Skip over the Comma, if there
  CALL IsSpaceOrEOL           ;Skip spaces
  JZ  >Y701                   ;If EOL, Error
  MOV  BX,SwitchListEntity    ;Look for an
  CALL GetCode                ;  Entity Descriptor
  JNC >Y10                    ;If found, continue
  CALL GetNextChar            ;If not found, get the character
  CALL Capitalize             ;Capitalize it
  CMP  AL,'A'                 ;Axis?
  JE  >Y10                    ;If so, continue
  CMP  AL,'B'                 ;Button?
  JE  >Y10                    ;If so, continue
  DEC  SI                     ;If not, point back at the character
  JMP >Y15                    ;Look for the SubEntity
Y10:                          ;Have Entity Code
  MOV  DL,AL                  ;Store Entity Descriptor ({A}xis or {B}utton)
Y15:                          ;Entity Descriptor stored
  CALL IsSpaceOrEOL           ;Skip spaces
  JZ  >Y70                    ;If EOL, Error
  MOV  BX,SwitchListSubEntity ;Look for a
  CALL GetCode                ;  SubEntity Descriptor
  JNC >Y30                    ;If we got one,
  CALL GetNextChar            ;If not found, get the character
  CALL Capitalize             ;Capitalize it
  CMP  AL,'X'                 ;Axis?
  JB  >Y20                    ;If not, handle it
  CMP  AL,'Z'                 ;Button?
  JBE >Y25                    ;If so, continue
Y20:                          ;Not 'X'-'Z'
  DEC  SI                     ;Point back at the character
  CALL GetDecimal             ;Look for a Number
  JC  >Y70                    ;If none, Error
  OR   AX,AX                  ;Is it too small?
  JZ  >Y70                    ;If so, Error
  CMP  AX,15                  ;Is it too big?
  JA  >Y70                    ;If so, Error
Y25:                          ;Have SubEntity Code
  MOV  DH,AL                  ;Store SubEntity Code ('X'-'Z', 1-15)
  OR   DL,DL                  ;Did we get an Entity Descriptor?
  JNZ >Y30                    ;If so, continue
  MOV  DL,'A'                 ;Assume an Axis
  CMP  DH,'X'                 ;Is it an Axis?
  JAE >Y30                    ;If so, continue
  INC  DL                     ;If not, it's a Button
Y30:                          ;Entity Descriptor filled in
  CMP  DL,'A'                 ;Entity = Axis?
  JNE >Y35                    ;If not, handle Buttons
  CMP  DH,'X'                 ;If so, is it an Axis SubEntity?
  JB  >Y70                    ;If not, Error
  CMP  CL,'A'                 ;Joystick A?
  JE  >Y80                    ;If so, we're done
  CMP  DH,'Z'                 ;If not, is it SubEntity Z?
  JNE >Y80                    ;If not, we're done
Y701:                         ;To avoid JMP > 128
  JMP >Y70                    ;If so, Error
Y35:                          ;Entity = Button
  CMP  DH,15                  ;Is it a Button SubEntity?
  JA  >Y70                    ;If not, Error
  CMP  CL,'A'                 ;Joystick A?
  JE  >Y80                    ;If so, we're done
  CMP  DH,2                   ;If so, is it Button 1 or 2?
  JBE >Y80                    ;If so, it's OK
Y70:                          ;Error
  STC                         ;Set return flag
  JMP >Y90                    ;Done
Y80:                          ;OK
  CLC                         ;Set return flag
Y90:                          ;Done
  POP  CX,BX,AX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE POINTER TO THE TRANSLATION TABLE ENTRY WE SHOULD CHANGE
;Inputs:   AL = 'A' or 'B' (Analog Joystick ID)
;          DL = Entity Type, {A}xis or {B}utton
;          DH = SubEntity Type, 'X'-'Z' if Axis, 1-15 if Button
;          All inputs already tested for validity
;Outputs: [DI] = Translation Table Entry associated with Inputs
;Changes:
;------------------------------------------------------------------------------
GetXlatPtr:
  PUSH DX               ;Save used registers
  CMP  AL,'A'           ;Joystick A?
  JNE >P30              ;If not, handle Joystick B
P10:                    ;Joystick A
  CMP  DL,'A'           ;Is it an Axis?
  JNE >P20              ;If not, handle Buttons
  MOV  DI,AXXlat        ;Assume Axis AX
  CMP  DH,'X'           ;Is it Direction X?
  JE  >P90              ;If so, we're done
  CMP  DH,'Y'           ;Is it Direction Y?
  JE  >P80              ;If so, we're done
  MOV  DI,AZXlat        ;If not, it's Axiz AZ
  JMP >P90              ;Done
P20:                    ;Joystick A, Buttons
  MOV  DI,A01Xlat       ;Point at the first button
P25:                    ;Loop to here to find the correct Table entry
  DEC  DH               ;Decrement the Loop Counter
  JZ  >P90              ;If 0, we're done
  ADD  DI,XlatStrucSize ;If not, point at the next Table entry
  JMP  P25              ;Keep going until we're done
P30:                    ;Joystick B
  CMP  DL,'A'           ;Is it an Axis?
  JNE >P40              ;If not, handle Buttons
  MOV  DI,BXXlat        ;Assume Axis BX
  CMP  DH,'X'           ;Is it Direction X?
  JE  >P90              ;If so, we're done
  JMP >P80              ;If not, it's Axis BY
P40:                    ;Joystick B, Buttons
  MOV  DI,A03Xlat       ;Assume Button B01 (A03)
  CMP  DH,1             ;Is it Button 1?
  JE  >P90              ;If so, we're done
P80:                    ;Point at the next Table Entry
  ADD  DI,XlatStrucSize ;Point at the next Translation table entry
P90:                    ;Done
  POP  DX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;OBTAIN A CONTROL INDEX FROM THE COMMAND LINE
;Inputs:   [SI] = Command Line Pointer
;          [DI] = Translation table Entry we will be Stroing things in
;Outputs:  CF = Clear if OK
;               DL = Joystick Index
;               DH = Control Type
;               CL = Control Index 1
;               CH = Control Index 2 (-1 if N/A)
;             = Set if Error
;               CX, DX = Undefined
;Changes:
;NOTES: This will let the user enter Control Indices in either forward or
;         reverse order (if he user gives a range)
;------------------------------------------------------------------------------
GetControlInfo:
  PUSH AX,BX                  ;Save used registers
  MOV  CX,-1                  ;Start all Indices
  MOV  DX,CX                  ;  at -1
  CALL SkipColonEquals        ;Skip Colon/Equals (if there)
  MOV  BX,SwitchListDIIndex   ;Point at list of code words
  CALL GetCode                ;See if it's a match
  JNC >L05                    ;If so, continue
  CALL GetNextChar            ;If not, get the next character
  CALL Capitalize             ;Capitalize it
  CMP  AL,'G'                 ;Is it G?
  JE  >L05                    ;If so, continue
  CMP  AL,'J'                 ;Is it J?
  JE  >L05                    ;If so, continue
  DEC  SI                     ;If not, point back at the character
L05:                          ;Joystick Descriptor handled
  CALL GetDecimal             ;Get the DI Index
  JC  >L701                   ;If no number, Error
  CMP  AX,(MaxDevices-1)      ;Is it valid?
  JA  >L701                   ;If not, Error
  MOV  DL,AL                  ;If OK, store DI Index
  CALL SkipComma              ;Skip over the Comma if it's there
  MOV  BX,SwitchListCtrlType  ;Point at list of Code Words
  CALL GetCode                ;See if it's a match
  JC  >L701                   ;If no match, Error
  MOV  DH,AL                  ;Store Control Type
  CMP  DI,A01Xlat             ;Are we getting a Button?
  JB  >L10                    ;If not, any type of Control is OK
  CMP  DH,CTButton            ;If so, is it a Button Control?
  JNE >L701                   ;If not, Error
L10:                          ;Control Type is valid for Entity
  CALL SkipComma              ;Skip over the comma if it's there
  CALL GetDecimal             ;Get Control Index 1
  JC  >L70                    ;If no number, Error
  CMP  AX,MaxXlatIndex        ;Is it too big?
  JA  >L70                    ;If so, Error
  MOV  CL,AL                  ;If OK, store it
  CMP  DI,A01Xlat             ;Are we getting a Button?
  JAE >L80                    ;If so, we're done
  CMP  DH,CTDPad              ;If not, is Control Type a Slider/Throttle/Axis?
  JA  >L80                    ;If so, we're done
  JE  >L30                    ;If a DPad, jump to handle it
L20:                          ;Mapping Button to Axis
  CALL SkipComma              ;Skip over the Comma if it's there
  CALL GetDecimal             ;Get Button Index 2
  JC  >L70                    ;If none, Error
  CMP  AX,MaxXlatIndex        ;Is it too big?
  JA  >L70                    ;If so, Error
  MOV  CH,AL                  ;If OK, store it
  JMP >L80                    ;Done
L701:                         ;To avoid JMP > 128
  JMP >L70                    ;Error
L30:                          ;Mapping DPad to Axis
  CALL SkipComma              ;Skip over the comma if it's there
  MOV  BX,SwitchListSubEntity ;Get a DPad
  CALL GetCode                ;  Axis Direction
  JNC >L40                    ;If found, continue
  CALL GetNextChar            ;If not found, get a single character
  CALL Capitalize             ;Capitaize it
L40:                          ;AL = DPad Axis Code
  XOR  CH,CH                  ;Assume Axis X
  CMP  AL,'X'                 ;Direction X?
  JE  >L80                    ;If so, we're done
  INC  CH                     ;Assume Axis Y
  CMP  AL,'Y'                 ;Direction Y?
  JE  >L80                    ;If so, we're done
L70:                          ;Error
  STC                         ;Set Error Flag
  JMP >L90                    ;Done
L80:                          ;OK, Go back one space (EOL)
  CLC                         ;Set OK Flag
L90:                          ;Done
  POP  BX,AX                  ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SUPPORT ROUTINES FOR PROCESSING SWITCHES
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;GET A YES/NO OPTION FOR A COMMAND-LINE SWITCH
;Inputs:  [SI] = Command Line Pointer
;Outputs: CF = Clear if OK
;              ZF = Set if No
;                   AL = 0 (No)
;                 = Clear if Yes
;                   AL = -1 (Yes)
;            = Set if Error
;              AL = 0
;Changes:
;------------------------------------------------------------------------------
GetYesNo4Switch:
  PUSH BX,CX              ;Save used registers
  MOV  CH,AH              ;Save original AH
  CALL SkipColonEquals    ;Skip Colon/Equals (if there)
  MOV  BX,SwitchListYesNo ;Point at the list of code words
  CALL GetCode            ;See if its a match
  JNC >Y20                ;If so, jump to test it
  CALL GetNextChar        ;If not, get a single character
  JZ  >Y70                ;If end-of-line, error
  CALL Capitalize         ;Capitalize it
Y20:                      ;Got a character
  CMP  AL,'Y'             ;Is it Yes?
  JE  >Y40                ;If so, handle it
  CMP  AL,'N'             ;Is it No?
  JNE >Y70                ;If not, error
Y30:                      ;No
  XOR  AL,AL              ;If so, make it No
  JMP >Y80                ;Done
Y40:                      ;Yes
  MOV  AL,Yes             ;Mark it as a Yes
  JMP >Y80                ;Done
Y70:                      ;Error
  XOR  AL,AL              ;Set AL to 0
  STC                     ;Set Error Flag
  JMP >Y90                ;Done
Y80:                      ;OK
  OR   AL,AL              ;Set Zero Flag for Return
  CLC                     ;Set OK Flag
Y90:                      ;Done
  MOV  AH,CH              ;Restore original AH
  POP  CX,BX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE A SWITCH WITH A YES/NO OPTION
;Inputs:  [SI] = Command Line Pointer
;         ES = TSR Data Area
;         DS = Local Data Area
;         AL = Bit mask to Set/Unset
;         AH = ParseTest status
;Outputs: Sets/Unsets bit in ES:TSRFlags or DS:ProgFlags as appropriate
;Changes:
;NOTES: If we are changing the resident (TSR) part of the program
;         (ES:TSRFlags), we do not actually change anything unless
;         ParseTest is set to No.  If we are changing the transient
;         part of the program (DS:ProgFlags), we change it no matter
;         what ParseTest is set to.
;       Forward and Reverse refer to Forward and Reverse Logic.  With
;         Forward Logic, if the user says Yes, we set the particular bit
;         to 1.  With Reverse Logic, when the user says Yes, we set
;         to bit to 0.
;------------------------------------------------------------------------------
DoSwitchWithYNTsrRvs:
  PUSH CX                  ;Save used registers
  MOV  CL,-1               ;Mark as Reverse Logic
  JMP >Y000                ;Jump to do ES:TSRFlags
DoSwitchWithYNTsrFwd:
  PUSH CX                  ;Save used registers
  XOR  CL,CL               ;Mark as Forward Logic
  JMP >Y000                ;Jump to do ES:TSRFlags
;DoSwitchWithYNPrgRvs:
;  PUSH CX                  ;Save used registers
;  MOV  CL,-1               ;Mark as Reverse Logic
;  JMP >Y001                ;Jump to do DS:ProgFlags
DoSwitchWithYNPrgFwd:
  PUSH CX                  ;Save used registers
  XOR  CL,CL               ;Mark as Forward Logic
  JMP >Y001                ;Jump to do DS:ProgFlags
Y000:                      ;ES:TSRFlags
  PUSH BX                  ;Save used registers
  MOV  BX,OFFSET TSRFlags  ;Point at TSR Flags
  JMP >Y00                 ;Do it
Y001:                      ;DS:ProgFlags
  PUSH BX                  ;Save used registers
  MOV  BX,OFFSET ProgFlags ;Point at TSR Flags
;  JMP >Y00                 ;Do it
Y00:
  PUSH AX                  ;Save used registers
  MOV  CH,AL               ;Save Bit Mask to set
  CALL GetYesNo4Switch     ;Get the Yes/No parameter (AL)
  JC  >Y70                 ;If error, handle it
  OR   AL,AL               ;Is it Yes?
  JNZ >Y20                 ;If so, jump to handle it
Y10:                       ;No
  OR   CL,CL               ;Is it Forward Logic?
  JZ  >Y40                 ;If so, jump to handle it
  JMP >Y50                 ;If not, jump to handle Reverse Logic
Y20:                       ;Yes
  OR   CL,CL               ;Is it Forward Logic?
  JZ  >Y50                 ;If so, jump to handle it
Y40:                       ;Unset the Bit
  NOT  CH                  ;Need to Unset
  CMP  BX,LastTSRByte      ;Is it for ProgFlags?
  JAE >Y45                 ;If so, jump to handle it
Y42:                       ;Unset ES:TSRFlags
  OR   AH,AH               ;Just the Test Run?
  JNZ >Y90                 ;If so, quit
  AND  ES:[BX],CH          ;Unset the appropriate flag
  JMP >Y90                 ;Done
Y45:                       ;Unset DS:ProgFlags
  AND  [BX],CH             ;Unset the appropriate flag
  JMP >Y90                 ;Done
Y50:                       ;Set the Bit
  CMP  BX,LastTSRByte      ;Is it for ProgFlags?
  JAE >Y55                 ;If so, jump to handle it
Y52:                       ;Set ES:TSRFlags
  OR   AH,AH               ;Just the Test Run?
  JNZ >Y90                 ;If so, quit
  OR   ES:[BX],CH          ;Set the appropriate flag
  JMP >Y90                 ;Done
Y55:                       ;Set DS:ProgFlags
  OR   [BX],CH             ;Set the appropriate flag
  JMP >Y90                 ;Done
Y70:                       ;Error
  MOV  ParseError,Yes      ;Mark as an Error
Y90:                       ;Done
  POP  AX                  ;Restore used registers
  POP  BX                  ;Restore used registers
  POP  CX                  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;REPLACE THE CARRIAGE RETURN AT THE END OF THE CMD LINE TAIL WITH A 0
;Inputs:  CS = DS = PSP Segment
;Outputs:
;Changes: Last Byte of Command Tail
;------------------------------------------------------------------------------
FixCmdLineCR:
  PUSH AX,CX,DI,ES   ;Save used registers
  MOV  AL,CR         ;Look for a CR
  MOV  CX,7Fh        ;Maximize Byte Counter
  MOV  ES,DS         ;Point ES:[DI]
  MOV  DI,81h        ;  at the Command Line Tail
  REPNE SCASB        ;Look for the CR
  MOV  B ES:[DI-1],0 ;Replace the CR with a 0
  POP  ES,DI,CX,AX   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NEXT CHARACTER FROM THE ENVIRONMENT OR COMMAND LINE
;Inputs:  [SI] = Pointer (Command Line or Environment)
;Outputs: AL = The next character
;         ZF = Set if End-Of-Line (Character = ASCII 0)
;            = Clear if "real" character
;Changes: SI
;------------------------------------------------------------------------------
GetNextChar:
  PUSH DS                   ;Save used registers
  TEST ProgFlags2,DoStdIn   ;Are we doing StdIn?
  JNZ >N20                  ;If so, jumpto handleit
  TEST ProgFlags2,DoEnvir   ;Are we doing the Program Environment Variable?
  JNZ >N10                  ;If so, handle it
  CMP  CmdLineVarPointer,-1 ;Need to use the CMDLINE Environment variable?
  JE  >N90                  ;If not, just do the "real" command line
N10:                        ;Get next character from the Environment
  MOV  DS,[PSPEnvirSeg]     ;Point at the Environment Segment
  JMP >N90                  ;Continue
N20:                        ;StdIn
  MOV  DS,StdInSegment      ;Point DS at the correct Segment
N90:                        ;DS:[SI] points at the next character
  LODSB                     ;Get the character from the "real" command line
  OR   AL,AL                ;Set ZF if EOL
  POP  DS                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER SPACES TO FIND THE NEXT LEGITIMATE CHARACTER, TESTING FOR EOL
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: ZF = Set if EOL
;               AL  = Unchanged
;              [SI] = End-of-Line Character
;            = Clear if not EOL
;               AL  = First non-space character
;              [SI] = Character AFTER the next non-space character
;Changes:
;------------------------------------------------------------------------------
IsSpaceOrEOX:
  CALL IsSpaceOrEOL ;Skip over the spaces
  JZ  >X90          ;If end-of-line, we're done
  CALL GetNextChar  ;If not, get the next character
  OR   AL,AL        ;Set return flag
X90:                ;Done
  RET

;------------------------------------------------------------------------------
;SKIP OVER SPACES TO FIND LEGITIMATE CHARACTERS, AND TEST FOR EOL
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: ZF = Set if EOL
;            = Clear if legitimate character
;              [SI] = The Next non-Space Character
;Changes:
;------------------------------------------------------------------------------
IsSpaceOrEOL:
  PUSH AX          ;Save used registers
L10:               ;Loop to here to skip over spaces
  CALL GetNextChar ;Get the next character
  CMP  AL,' '      ;Is it a space?
  JE   L10         ;If so, get the next character
  CMP  AL,Tab      ;Is it a Tab?
  JE   L10         ;If so, get the next character
  DEC  SI          ;If not, point back at the character
  OR   AL,AL       ;Set EOL flag
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER A COLON OR EQUALS, IF THERE
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: CF = Clear if Colon or Equals was found (and skipped)
;              [SI] = First non-Space Character after the Colon/Equals
;            = Set if No Colon or Equals was found
;              [SI] = First non-Space Character after where we were looking
;Changes:
;------------------------------------------------------------------------------
SkipColonEquals:
  PUSH AX           ;Save used registers
  CALL IsSpaceOrEOX ;Skip over the Spaces
  JZ  >Q75          ;If End-of-Line, nothing to get
  CMP  AL,':'       ;Is it a Colon?
  JE  >Q80          ;If so, we're done
  CMP  AL,'='       ;Is it an equals?
  JE  >Q80          ;If so, we're done
Q70:                ;No Colon/Equals Found
  DEC  SI           ;Point back at the character
Q75:                ;[SI] correct
  STC               ;Set return flag
  JMP >Q90          ;Done
Q80:                ;Colon/Equals Found
  CALL IsSpaceOrEOL ;Go to the next character
  CLC               ;Set return flag
Q90:                ;Done
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER A COMMA, IF THERE
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: SI = Pointer to spot after the Comma, if there
;Changes:
;------------------------------------------------------------------------------
SkipComma:
  PUSH AX           ;Save used registers
  CALL IsSpaceOrEOX ;Skip over the Spaces
  JZ  >C90          ;If EOL, we're done
  CMP  AL,','       ;Is it a Comma?
  JE  >C40          ;If so, we're done
  DEC  SI           ;Point back at previous character
  JMP >C90          ;Done
C40:                ;Skip over spaces after the colon/equals
  CALL IsSpaceOrEOL ;Skip over the Spaces
C90:                ;Done
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT A CODE WORD FROM THE COMMAND LINE TO A BYTE FROM THE LOOKUP TABLE
;Inputs:  [SI]    = Command Line Pointer
;         DS:[BX] = List to Test
;Outputs: CF = Clear if a match was found
;              AL = Entry from Table
;              [SI] = Next character after the Code Match
;         CF = Set if no match found (error)
;              AL = Unchanged
;              SI = Unchanged
;Changes: SI if valid match found
;------------------------------------------------------------------------------
GetCode:
  PUSH BX,DX         ;Save used registers
  MOV  DX,AX         ;Save AX in case there's no match
C10:                 ;Loop to here for each list of words
  CMP  B [BX],0      ;Are we at the end of our table?
  JE  >C80           ;If so, we couldn't find a match
  XOR  AH,AH         ;Assume it's a match
  PUSH SI            ;Save our command line pointer for a second
C20:                 ;Loop to here for each character in a word
  CALL GetNextChar   ;Get the next character
  CALL Capitalize    ;Capitalize it
  CALL CmpALBXNoCase ;Is it a match?
  JZ  >C30           ;If so, handle it
  INC  AH            ;If not, mark us as no match
C30:                 ;The character is tested
  INC  BX            ;Point at the next character in our test word
  CMP  B [BX],0      ;Are we at the end of the word?
  JNE  C20           ;If not, keep going until we're done
  INC  BX            ;Point at the next word or code list
  OR   AH,AH         ;Was it a match?
  JZ  >C50           ;If so, jump to handle it
  POP  SI            ;If not, restore our command line pointer
  CMP  B [BX],0      ;Is this the end of our word list?
  JNE  C10           ;If not, just test the next word
  INC  BX,2          ;If so, skip over the codes
  JMP  C10           ;Keep going until we're done with our list
C50:                 ;We've found a match!
  POP  AX            ;Lose our original command line pointer
  DEC  BX            ;Point back at the end-of-word marker
C60:                 ;Loop to here to find the end-of-list
  MOV  AX,[BX]       ;Get the next two characters in our list
  INC  BX            ;Point at the next character for next time
  OR   AX,AX         ;Is this the start of our code list?
  JNE  C60           ;If not, keep looking
  INC  BX            ;If so, point at the character we're looking for
  MOV  DL,[BX]       ;Get the character from the table
  CLC                ;Mark our OK flag
  JMP >C90           ;And we're done
C80:                 ;No match found
  STC                ;Mark us as no match
C90:                 ;We're done
  MOV  AX,DX         ;Set AX to the correct value
  POP  DX,BX         ;Restore used registers
  RET

CmpALBXNoCase:
  PUSH AX         ;Save used registers
  CALL Capitalize ;Capitalize AL
  MOV  AH,AL      ;Store it in AH
  MOV  AL,[BX]    ;Get [BX]
  CALL Capitalize ;Capitalize it
  CMP  AL,AH      ;Compare AL & [BX]
  POP  AX         ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO GET A NUMBER FROM A STRING (HEX OR DECIMAL)
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;GET A NUMBER FROM A STRING (CAN BE DECIMAL OR HEX)
;Inputs:  [SI] = String Pointer
;Outputs: CF = Clear if we got a valid number
;              AX = Number
;            = Set if Error (no number found)
;              AX = Unchanged
;Changes: [SI] = next byte after the number
;NOTES: A Hex String must start with '0x' and/or end in 'H'!
;       This assumes we MUST return a number, so if no number is found,
;         [SI] = first character that should have been a numeral.
;------------------------------------------------------------------------------
GetNumber:
  PUSH BX           ;Save used registers
  MOV  BX,AX        ;Save original AX
  CALL IsSpaceOrEOL ;Skip past spaces
  JZ  >N70          ;If End-of-Line, Error
  CALL TestForHex   ;Is it a Hex Number?
  JC  >N20          ;If not, try a decimal
  CALL Test0xPrefix ;Skip over the 0x (C-style) prefix, if there
  CALL GetHex       ;If so, get it
  JNC >N80          ;If OK, we're done
  JMP >N70          ;If not, Error
N20:                ;Not Hex
  CALL GetDecimal   ;Try a Decimal
  JNC >N80          ;If OK, we're done
N70:                ;Error
  MOV  AX,BX        ;Restore original AX
  STC               ;Set Error Flag
  JMP >N90          ;Done
N80:                ;OK
  CLC               ;Set OK Flag
N90:                ;Done
  POP  BX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AN ASCII STRING TO SEE IF IT IS A HEX STRING (ENDS IN H)
;Inputs:  [SI] = String to test
;Outputs: CF = Clear if it is a Hex String
;            = Set if Error (not a Hex String)
;Changes:
;NOTES: A valid Hex String will contain numerals 0-9, A-F, and Underscores,
;         and will end in an H.  It cannot contain spaces.
;------------------------------------------------------------------------------
TestForHex:
  PUSH AX,SI        ;Save used registers
  CALL Test0xPrefix ;Test for the 0x (C-style) prefix (changes SI)
  JNC >H80          ;If so, it's Hex
  CALL GetNextChar  ;Get the
  DEC  SI           ;  first character
  CALL IsHexNumber  ;A valid Hex Number?
  JC  >H70          ;If not, Error
H40:                ;Loop to here for each character
  CALL GetNextChar  ;Get the next character
  CALL IsHexNumber  ;A valid Hex Number?
  JNC  H40          ;If so, keep looking
  CMP  AL,'_'       ;An Underscore?
  JE   H40          ;If so, keep looking
  CALL IsHexEnd     ;End of the Hex Number?
  JZ  >H80          ;If so, we're done
H70:                ;Not Hex
  STC               ;Set error Flag
  JMP >H90          ;Done
H80:                ;Is Hex
  CLC               ;Set OK Flag
H90:                ;Done
  POP  SI,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST FOR A 0x (C-STYLE) HEX PREFIX
;Inputs:  [SI] = String to test
;Outputs: CF = Set if not a 0x Prefix
;              SI unchanged
;            = Clear if not a 0x Prefix
;              [SI] = Character after the 0x
;Changes:
;------------------------------------------------------------------------------
Test0xPrefix:
  PUSH AX,BX       ;Save used registers
  MOV  BX,SI       ;Save original Pointer
  CALL GetNextChar ;Get the next character
  CMP  AL,'0'      ;Is it a 0?
  JNE >X70         ;If not, it's not a Prefix
X20:               ;Loop to here to skip over all zeroes
  CALL GetNextChar ;Get the next charcter
  CMP  AL,'0'      ;Is it another 0?
  JE   X20         ;If so, keep skipping the zeroes
  CALL Capitalize  ;If not, capitalize it
  CMP  AL,'X'      ;Is it an X?
  JNE >X70         ;If not, it's not a 0x Prefix
  CALL GetNextChar ;Get the next charcter
  CALL IsHexNumber ;Is it a Hex Number?
  JNC >X80         ;If so, it's a 0x Prefix
X70:               ;Not a 0x Prefix
  MOV  SI,BX       ;Restore original pointer
  STC              ;Set return flag
  JMP >X90         ;Done
X80:               ;Is a 0x Prefix
  DEC  SI          ;Point back at the first hex number
  CLC              ;Set return flag
X90:               ;Done
  POP  BX,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NEXT SERIES OF HEX CHARACTERS, AND CONVERT THEM TO A NUMBER
;Inputs:  [SI] = Command Line Pointer
;Outputs: CF = Clear if OK
;              AX = Character string converted to a number, ?? if not
;            = Set if Error
;              AX = Unchanged
;Changes:
;NOTES: The string can either have an H at the end or not, and this code
;         will work.
;------------------------------------------------------------------------------
GetHex:
  PUSH BX,CX,DX    ;Save used registers
  MOV  DX,AX       ;Save original AX
  XOR  BX,BX       ;Start with zero
  MOV  CX,4        ;CL = bits to shift by, CH = Character Counter
  XOR  AH,AH       ;Set High byte to 0
D10:               ;Loop to here for each character
  CALL GetNextChar ;Get the next character
  CALL IsHexNumber ;A valid Hex Number?
  JNC >D20         ;If so, continue
  CMP  AL,'_'      ;If not, is it an Underscore?
  JE  >D45         ;If so, keep looking
  JMP >D50         ;If not, we're done
D20:               ;Valid Hex Number
  INC  CH          ;Increment the character counter
  SHL  BX,CL       ;Make room for the New Nibble
  JNC >D40         ;If we're not too big yet, keep going
  DEC  SI          ;If too big, Point back at bad character
  JMP >D70         ;Error
D40:               ;OK to add in new Nibble
  ADD  BX,AX       ;Add in the new Nibble
D45:               ;Done with this character
  JMP  D10         ;Jump to get next character
D50:               ;Done getting string
  DEC  SI          ;Point back at the bad character
  OR   CH,CH       ;Any valid characters at all?
  JZ  >D70         ;If not, error
  CALL GetNextChar ;Is the next character
  CALL IsHexEnd    ;  an H?
  JZ  >D80         ;If so, we're done
  DEC  SI          ;If not, point back at the non-H
  JMP >D80         ;Done
D70:               ;There was an error
  MOV  AX,DX       ;Restore original AX
  STC              ;Mark the error flag
  JMP >D90         ;And quit
D80:               ;We have a good number
  MOV  AX,BX       ;Put the value where it belongs
  CLC              ;Mark the OK flag
D90:               ;We're done
  POP  DX,CX,BX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NEXT SERIES OF NUMERIC CHARACTERS, AND CONVERT THEM TO A NUMBER
;Inputs:  [SI] = Command Line Pointer
;Outputs: AX = decimal character string converted to a number, ?? if not
;         CF = Clear if AX is OK
;            = Set if no decimal found
;Changes: SI points at character after the number
;------------------------------------------------------------------------------
GetDecimal:
  PUSH BX,CX,DX     ;Save used registers
  XOR  AX,AX        ;Start with zero
  XOR  BX,BX        ;  "    "    "   - accumulator
  MOV  CX,10        ;CX = multiplier & digit limit comparer
  CALL IsSpaceOrEOX ;Skip over the spaces
  JZ  >D70          ;If EOL, Error
  SUB  AL,'0'       ;If not, convert it to a number
  CMP  AL,9         ;Is it really a number?
  JA  >D70          ;If not, there's an error
  DEC  SI           ;If so, step back to the number
D10:                ;Loop to here for each character
  CALL GetNextChar  ;Get the next character
  SUB  AL,'0'       ;Change it to 0-9 range
  XOR  AH,AH        ;Extend byte to word
  CMP  AX,CX        ;Is it a digit?
  JAE >D80          ;If not, we're done
  CMP  BX,6553      ;Is our number getting too big?
  JB  >D20          ;If not, jump to store it
  CMP  AL,5         ;If so, will this number make it too big?
  JA  >D70          ;If so, there's an error
D20:                ;Add the digit to our number
  XCHG AX,BX        ;Swap accumulation into AX, new digit into BX
  MUL  CX           ;Multiply the previous accumulation by 10
  ADD  BX,AX        ;Add in the new digit
  JMP  D10          ;Jump to get next character
D70:                ;There was an error
  STC               ;Mark the error flag
  JMP >D90          ;And quit
D80:                ;We have a good number
  DEC  SI           ;Point back at the non-number
  MOV  AX,BX        ;Put the value where it belongs
  CLC               ;Mark the OK flag
D90:                ;We're done
  POP  DX,CX,BX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AN ASCII CHARACTER TO SEE IF IT IS A HEX NUMERAL (BETWEEN 0 AND F)
;Inputs:  AL = ASCII Character to Test
;Outputs: CF = Clear if it is a Number
;              AL = ASCII Code converted to a real Number
;            = Set if Error (not a Number)
;              AL = Unchanged
;Changes:
;------------------------------------------------------------------------------
IsHexNumber:
  PUSH BX          ;Save used registers
  MOV  BL,AL       ;Save the character in case we need to restore it
  CALL IsNumber    ;A Decimal Number?
  JNC >H85         ;If so, we're done
  CALL Capitalize  ;If not, Capitalize it
  CMP  AL,'A'      ;A Hex Number?
  JB  >H70         ;If not, Error
  CMP  AL,'F'      ;A Hex Number?
  JBE >H80         ;If so, handle it
H70:               ;Not a Number
  MOV  AL,BL       ;Restore original character
  STC              ;Set Error Flag
  JMP >H90         ;Done
H80:               ;A Number > 9
  SUB  AL,('A'-10) ;Convert it to a real number
  JMP >H90         ;Done
H85:               ;OK (is a Number)
  CLC              ;Set OK Flag
H90:               ;Done
  POP  BX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AN ASCII CHARACTER TO SEE IF IT IS A NUMERAL (BETWEEN 0 AND 9)
;Inputs:  AL = ASCII Character to Test
;Outputs: CF = Clear if it is a Number
;              AL = ASCII Code converted to a real Number
;            = Set if Error (not a Number)
;              AL = Unchanged
;Changes:
;------------------------------------------------------------------------------
IsNumber:
  CMP  AL,'0' ;A Number?
  JB  >N70    ;If not, Error
  CMP  AL,'9' ;A Number?
  JBE >N80    ;If so, handle it
N70:          ;Not a Number
  STC         ;Set Error Flag
  JMP >N90    ;Done
N80:          ;A number
  SUB  AL,'0' ;Convert it to a real number
  CLC         ;Set OK Flag
N90:          ;Done
  RET

;------------------------------------------------------------------------------
;TEST AN ASCII CHARACTER TO SEE IF IT IS A PARTICULAR CHARACTER
;Inputs:  AL = ASCII Character to Test
;Outputs: ZF = Set if it is the appropriate character
;            = Clear if not
;Changes:
;------------------------------------------------------------------------------
IsHexEnd:
  PUSH AX         ;Save used registers
  CALL Capitalize ;Capitalize the Character
  CMP  AL,'H'     ;See if it matches
  POP  AX         ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE THE TELL THE USER WHERE THE COMMAND LINE ERROR OCCURRED
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;WRITE THE COMMAND LINE OR ENVIRONMENT ERROR INFORMATION TO THE SCREEN
;Inputs:  [SI] = Command line Pointer (if Command Line)
;              = StdIn Buffer Pointer (if StdIn String)
;              = Environment Pointer (if Environment Variable)
;         ProgFlags2.DoEnvir, DoStdIn
;Outputs:
;Changes: Writes information to screen
;------------------------------------------------------------------------------
WriteCmdErr:
  PUSH AX,BX,CX,DX        ;Save used registers
  PUSH DI,SI,BP           ;Save used registers
  CALL FindBadChar        ;Find the actual bad character in the string (SI)
  CALL SetupCmdErrStrings ;Set up the String Pointers and Sizes
                          ;DI:[AX] = Command Line String
                          ;DS:[BX] = Program name at beginning of String
                          ;    CX  = Size of BX String
                          ;DS:[DX] = Header String
                          ;DS:[BP] = Footer String
  CALL WriteZErr          ;Write the Header
  CALL WriteCrLfErr       ;Move
  CALL WriteCrLfErr       ;  down
  SUB  SI,AX              ;Calculate the number
  ADD  CX,SI              ;  of spaces
  DEC  CX                 ;  to write
  CALL WriteCmdString     ;Write the Command-line String (may modify CX)
  CALL WriteCrLfErr       ;Move down
  MOV  B SingleMsg[0],' ' ;Want to write spaces
  MOV  DX,SingleMsg       ;Point at the string
E60:                      ;Loop to here to write each space (number in CX)
  CALL WriteZErr          ;Write the space
  LOOP E60                ;Keep writing them until we're done
  MOV  DX,ParseErrMsg     ;Write the Pointer Part
  CALL WriteZErr          ;  of the Error Message
  MOV  DX,BP              ;Write the last part
  CALL WriteZErr          ;  of the message
  POP  BP,SI,DI           ;Restore used registers
  POP  DX,CX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GO BACKWARDS IN AN ERRORED OPTION STRING TO INDICATE TO USER WHERE ERROR WAS
;Inputs:  SI = Option String Pointer (Environment, StdIn, or Command Line)
;                where error was found
;Outputs: SI = Options String Pointer, adjusted to point at a "Real" character
;Changes:
;------------------------------------------------------------------------------
FindBadChar:
  PUSH AX          ;Save used registers
  DEC  SI          ;Go back 1 character to start
B10:               ;Loop to here to find a legitimate character
  CALL GetNextChar ;Get the character
  JZ  >B40         ;If end-of-line, keep going back
  CMP  AL,' '      ;Is it a space?
  JNE >B90         ;If not, we're done
B40:               ;Need to keep going back
  DEC  SI,2        ;Go back to the previous character
  JMP  B10         ;Keep looking
B90:               ;Done
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SET UP THE STRING POINTERS AND SIZES TO BE ABLE TO WRITE THE COMMAND ERROR
;Inputs:  DS = Local Data Area
;Outputs: DI:[AX] = Command-line String to write
;         DS:[DX] = First Error Message (Header) to write
;         DS:[BX] = Second message to write (first part of Command string)
;             CX  = Number of Bytes to add to String Size
;         DS:[BP] = Third message to write (Footer)
;Changes:
;------------------------------------------------------------------------------
SetupCmdErrStrings:
  TEST ProgFlags2,DoEnvir   ;Working with the Environment?
  JNZ >S40                  ;If so, handle it
  TEST ProgFlags2,DoStdIn   ;Working with StdIn?
  JNZ >S50                  ;If so, handle it
S10:                        ;Command Line
  MOV  AX,81h               ;DI:[AX] =
  MOV  DI,DS                ;  Command-line Tail
  MOV  BX,RealName          ;DS:[BX] = Program Name
  MOV  CX,RealNameSz        ;CX = Size of [BX] String
  CMP  CmdLineVarPointer,-1 ;Real Command Line?
  JE  >S30                  ;If so, continue
S20:                        ;CMDLINE Environment Variable
  MOV  DI,[PSPEnvirSeg]     ;DI:[AX] =
  MOV  AX,CmdLineVarPointer ;  CMDLINE
  SUB  AX,CmdLineVarSize    ;  Environment Variable
S30:                        ;Have Command-line parameters set up
  MOV  DX,ParseCmdMsg       ;DS:[DX] = Header String
  MOV  BP,ParseErrMsg2      ;DS:[BP] = Footer String
  JMP >S90                  ;Done
S40:                        ;Environment
  MOV  DI,[PSPEnvirSeg]     ;DI:[AX] =
  MOV  AX,EnvVarPointer     ;  USBJSTIK
  SUB  AX,EnvVarSize        ;  Environment Variable
  MOV  BX,NoMsg             ;DS:[BX] = Second message to write
  XOR  CX,CX                ;CX = Size of [BX] String
  MOV  DX,ParseEnvMsg       ;DS:[DX] = Header String
  MOV  BP,ParseErrEnv       ;DS:[BP] = Footer String
  JMP >S90                  ;Done
S50:                        ;StdIn
  MOV  DI,StdInSegment      ;DI:[AX] =
  XOR  AX,AX                ;  StdIn Buffer
  MOV  BX,NoMsg             ;DS:[BX] = Second message to write
  XOR  CX,CX                ;CX = Size of [BX] String
  MOV  DX,ParseStdInMsg     ;DS:[DX] = Header String
  MOV  BP,ParseErrMsg2      ;DS:[BP] = Footer String
S90:                        ;Done
  RET

;------------------------------------------------------------------------------
;WRITE THE CONTENTS OF THE COMMAND STRING, WITH MORE MESSAGES AS NEEDED
;Inputs:  CS = DS = Local Data Area
;         DS:[BX] = First String to write (may not be needed!)
;         DI:[AX] = Second String (main part) to write
;             CX  = Number of Spaces to write at beginning (if normal)
;Outputs: CX = Adjusted number of spaces to write
;Changes:
;------------------------------------------------------------------------------
WriteCmdString:
  PUSH AX,BX,DX,DI,SI      ;Save used registers
  PUSH BP,DS,ES            ;Save used registers
  MOV  BP,CX               ;Save the original Space Counter in BP
  MOV  ES,DI               ;Save the second string Pointer
  MOV  SI,AX               ;  in ES:[SI]
  MOV  DX,BX               ;Save the length
  CALL CalcStrSizeDX       ;  of the first string
  MOV  DI,CX               ;  in DI
  MOV  DS,ES               ;Save the length
  CALL CalcStrSizeSI       ;  of the second string
  MOV  AX,CX               ;  in AX
  CMP  BP,70               ;Are there too many spaces to fit on one line?
  JBE >G20                 ;If not, continue
G10:                       ;Need to lop off the beginning
  SUB  BP,DI               ;We will not be
  XOR  DI,DI               ;  writing the
  MOV  BX,NoMsg            ;  first string
  CMP  BP,70               ;Are there still too many spaces to fit?
  JBE >G20                 ;If not, continue
  MOV  BX,MoreLeftMsg      ;Point at the More Message
  ADD  BP,MoreMsgSize      ;Compensate number of spaces for More Message
  MOV  DI,60               ;We want the number of spaces to be 60
  SUB  DI,BP               ;Calculate how far into the Second string
  NEG  DI                  ;  we need to start to accomplish that
  MOV  BP,59               ;BP = New Number of Spaces
  ADD  SI,DI               ;  where we need to start writing
  SUB  AX,DI               ;  of second string
  MOV  DI,MoreMsgSize      ;DI = Size of new first string (More Message)
G20:                       ;Beginning of line is accounted for
  MOV  DS,CS               ;Write the
  MOV  DX,BX               ;  first
  CALL WriteZErr           ;  string
  MOV  DS,ES               ;Point DS:[DX] at the
  MOV  DX,SI               ;  second string
  ADD  DI,AX               ;Calculate total length of both strings
  CMP  DI,79               ;Will it fit on one line?
  JBE >G40                 ;If so, continue
  MOV  BX,(80-MoreMsgSize) ;Calculate how many characters
  SUB  BX,DI               ;  of the second string
  ADD  BX,AX               ;  we need to write
  MOV  AL,[SI+BX]          ;Save the old character in the string
  MOV  B [SI+BX],0         ;Convert it to an end-of-string character
  CALL WriteZErr           ;Write the truncated string
  MOV  [SI+BX],AL          ;Restore the original character of the string
  MOV  DS,CS               ;Write the
  MOV  DX,MoreRightMsg     ;  More Message
G40:                       ;Write last message
  CALL WriteZErr           ;Write it
G90:                       ;Done
  MOV  CX,BP               ;Return CX = number of spaces to write
  POP  ES,DS,BP            ;Restore used registers
  POP  SI,DI,DX,BX,AX      ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;MISCELLANEOUS COMMAND-LINE RELATED STUFF
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;GET OUR REAL PROGRAM NAME
;Inputs:
;Outputs: RealName, RealNameSz
;Changes:
;------------------------------------------------------------------------------
GetRealName:
  PUSH AX,CX,DI,SI      ;Save used registers
  PUSH DS,ES            ;Save used registers
  MOV  DS,[PSPEnvirSeg] ;Point DS and ES
  MOV  ES,DS            ;  at the environment segment
  XOR  DI,DI            ;Point at the beginning of Environment variable list
  MOV  CX,-1            ;Max out the count limiter
  XOR  AX,AX            ;Need to look for a double 0
N10:                    ;Loop to here to skip over environment variables
  REPNE SCASB           ;Look for the end of this environment variable
  CMP  B [DI],0         ;Is it the end of the variable list (double zero)?
  JNE  N10              ;If not, keep skipping over environment variables
  CMP  W [DI+1],0       ;If so, is there at least one string after the env?
  JE  >N90              ;If not, we're done
  LEA  SI,[DI+3]        ;If so, point DS:[SI] at the True Name
  MOV  ES,CS            ;Point ES (for ES:[DI]) at our local data
N20:                    ;Loop to here to skip over drive/path info
  MOV  DI,RealName      ;Point at the beginning of our data area
  XOR  CX,CX            ;Start the length counter at 0
N30:                    ;Loop to here for each character in the name
  LODSB                 ;Get the next character of the name
  CMP  AL,'.'           ;Is it a dot?
  JE  >N40              ;If so, we may have the name
  CMP  AL,BackSlash     ;Is it a path separator?
  JE   N20              ;If so, start looking for the name again
  CALL Capitalize       ;If part of a name, capitalize the character
  STOSB                 ;Store it in our data area
  INC  CX               ;Increment the size counter
  JMP  N30              ;And keep looking for more characters
N40:                    ;We may have the name
  LODSB                 ;Get the next character of the name
  OR   AL,AL            ;End of the name?
  JZ  >N50              ;If so, it really was our program name
  CMP  AL,BackSlash     ;Was it a Directory with a dotted name?
  JE   N20              ;If so, keep looking for a file name
  JMP  N40              ;If not, keep looking for the end of the name
N50:                    ;We have the name
  XOR  AL,AL            ;Store
  STOSB                 ;  a 0
  MOV  CS:RealNameSz,CX ;Store the Size
N90:                    ;We're done
  POP  ES,DS            ;Restore used registers
  POP  SI,DI,CX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE POINTERS TO THE ENVIRONMENT VARIABLES WE NEED
;Inputs:  CS = DS = Local Data Area (also PSP Data Area)
;Outputs: Various Environment Pointers
;Changes:
;------------------------------------------------------------------------------
GetEnvVars:
  PUSH BX,CX,SI                    ;Save used registers
  MOV  SI,PathVarString1           ;Get
  MOV  BX,OFFSET PathVarPointer    ;  the
  MOV  CX,PathVarSize              ;  PATH
  CALL GetEnvVarPointer            ;  Pointer
  JNZ >V20                         ;If OK, continue
  MOV  SI,PathVarString2           ;If no, try
  CALL GetEnvVarPointer            ;  "Path" instead of "PATH"
V20:                               ;Get Program Variable
  MOV  SI,EnvVarString             ;Get
  MOV  BX,OFFSET EnvVarPointer     ;  our
  MOV  CX,EnvVarSize               ;  Program Variable
  CALL GetEnvVarPointer            ;  Pointer
V50:                               ;Get CMDLINE string
  CMP  B [80h],7Eh                 ;Is command line size maxed out?
  JB  >V90                         ;If not, don't worry about CMDLINE Env Var
  MOV  SI,CmdLineVarString         ;Get
  MOV  BX,OFFSET CmdLineVarPointer ;  the
  MOV  CX,CmdLineVarSize           ;  CMDLINE
  CALL GetEnvVarPointer            ;  Pointer
  CALL AdjustCmdLine               ;Compensate CMDLINE Pointer for Program Name
V90:                               ;Done
  POP  SI,CX,BX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE POINTER TO AN ENVIRONMENT VARIABLE
;Inputs:   CS = DS = Local Data Area (also contains PSP)
;         [BX] = Where to store the Pointer at
;          CX  = Length of String
;         [SI] = Environment Variable String to Look for (ends in =)
;Outputs: ZF = Set if Error (Variable not found)
;               [BX] = -1
;             = Clear if OK (variable was found)
;               [BX] = Pointer to String (in Environment Segment, NOT PSP!!)
;Changes:
;------------------------------------------------------------------------------
GetEnvVarPointer:
  PUSH AX,CX,SI,BP      ;Save used registers
  PUSH DS,ES            ;Save used registers
  MOV  W [BX],-1        ;Default = -1 (does not exist)
  MOV  ES,[PSPEnvirSeg] ;ES:[DI] =
  XOR  DI,DI            ;  Environment Variable List
  MOV  DX,CX            ;Save the Size
  MOV  BP,SI            ;Save the original Pointer
  XOR  AL,AL            ;Need to look for 0's
V10:                    ;Loop to here for each Environment variable
  MOV  CX,DX            ;CX = Number of bytes to test
  MOV  SI,BP            ;DS:[SI] = String to Test for
  REPE CMPSB            ;Is it a match?
  JZ   >V30             ;If so, continue
  MOV  CX,-1            ;Max out the count limiter
  REPNE SCASB           ;Look for the end of this environment variable
  CMP  B ES:[DI],AL     ;End of Environment?
  JNE  V10              ;If not, keep looking
  JMP >V90              ;If so, no variable
V30:                    ;Found it!
  MOV  [BX],DI          ;Store the Pointer
V90:                    ;Done
  CMP  W [BX],-1        ;Set return flag
  POP  ES,DS            ;Restore used registers
  POP  BP,SI,CX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ADJUST THE CMDLINE ENVIRONMENT VARIABLE POINT TO POINT AT THE COMMAND TAIL
;Inputs:  DS = Local Data Area (and PSP Segment)
;         CmdLineVarPtr (-1 if does not exist)
;           Unadjusted for Program Path/Name
;Outputs: CmdLineVarPtr
;           Adjusted (Program Path/Name skipped over to leave Command Tail)
;Changes:
;NOTES: The CMDLINE environment variable, which is only set by SOME
;         versions of DOS, contains the entire contents of the command
;         line.  The "entire contents" includes our executable file name
;         (including the path, if it was provided), but does not include
;         the redirection and/or piping parameters.
;       This routine skips over the executable file name part and simply
;         points at the command tail (the "Options" part of the command line).
;       The CMDLINE variable is used by some versions of DOS (such as 4DOS/NDOS
;         and DOS 95) to allow the command-line to be longer than the 126
;         characters normally allowed by DOS.
;------------------------------------------------------------------------------
AdjustCmdLine:
  PUSH AX,DX,SI,DS             ;Save used registers
  MOV  SI,CmdLineVarPointer    ;[SI] = CMDLINE environment variable contents
  CMP  SI,-1                   ;Anything there?
  JE  >J90                     ;If not, no need for changes
  MOV  DL,"/"                  ;Assume Get SwitchChar function is unsupported
  MOV  AX,3700h                ;Function 3700h (Get DOS SwitchChar)
  INT  21h                     ;Do it (returns DL)
  MOV  DS,[PSPEnvirSeg]        ;DS:[SI] = CMDLINE contents
J10:                           ;Loop to here to skip initial spaces & tabs
  LODSB                        ;Get the next character
  CMP  AL,' '                  ;Space?
  JE   J10                     ;If so, keep skipping
  CMP  AL,Tab                  ;Tab?
  JE   J10                     ;If so, keep skipping
J20:                           ;Skipped initial Tabs & Spaces
  DEC  SI                      ;Point back at the first character
  XOR  AH,AH                   ;Assume no quotes
  CMP  AL,Quote2               ;Double Quote (part of LFN Path & File Name)?
  JNE >J30                     ;If not, continue
  MOV  AH,AL                   ;If so, mark as a quote
  INC  SI                      ;Update the Pointer
J30:                           ;Loop to here for each character
  LODSB                        ;Get next character
  OR   AH,AH                   ;Are we looking for a Quoted Path/File Name?
  JZ  >J40                     ;If not, continue
J35:                           ;Looking for the Closing Quote
  CMP  AL,AH                   ;Is it the Closing Quote?
  JNE >J50                     ;If not, keep looking
  JMP >J85                     ;If so, we're done
J40:                           ;Not a Quoted Path/File Name
  OR   AL,AL                   ;End of variable?
  JE  >J80                     ;If so, handle it
  CMP  AL,' '                  ;Space?
  JE  >J80                     ;If so, we're done
  CMP  AL,Tab                  ;Tab?
  JE  >J80                     ;If so, we're done
  CMP  AL,'/'                  ;Slash?
  JE  >J80                     ;If so, we're done
  CMP  AL,DL                   ;SwitchChar?
  JE  >J80                     ;If so, we're done
J50:                           ;Not end-of-name
  JMP  J30                     ;Keep looking
J80:                           ;[SI-1] = CMDLINE Pointer
  DEC  SI                      ;Point at beginning of string
J85:                           ;[SI] = New CMDLINE Pointer
  MOV  CS:CmdLineVarPointer,SI ;Store it
J90:                           ;Done
  POP  DS,SI,DX,AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE ENVIRONMENT VARIABLE CONTENTS TO THE SCREEN
;Inputs:  EnvVarPointer (-1 if no Environment variable exists)
;Outputs:
;Changes: Writes to CON or ERR
;------------------------------------------------------------------------------
WriteEnvCon:
  CMP  EnvVarPointer,-1 ;Is there even an Environment variable to write?
  JE  >W90              ;If not, we're done
  PUSH DX,DS            ;Save used registers
  MOV  DX,EnvVarPointer ;Point at the
  SUB  DX,EnvVarSize    ;  beginning of the Variable
  MOV  DS,[PSPEnvirSeg] ;Point at the Environment Segment
  CALL WriteZPause      ;Write it
  POP  DS,DX            ;Restore used registers
W90:                    ;We're done
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO WRITE SPECIAL MESSAGES TO THE SCREEN (HELP, STATUS, ETC.)
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;Write Program Help Message
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;PRINT THE SYNTAX HELP MESSAGE AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;Outputs: Writes Help Message to the screen
;Changes: Quits program
;------------------------------------------------------------------------------
DoHelp:
  TEST ProgFlags,Help  ;Does the user want help?
  JZ  >H90             ;If not, we're done
  MOV  DX,SyntaxMsgHdr ;Write the first part
  CALL WriteZPause     ;  of the Help Message
  MOV  DX,RealName     ;Write the Real
  CALL WriteZPause     ;  Program Name
  CALL WriteSpace      ;Write a space
  MOV  DX,SyntaxMsg    ;Point at the last part of the Help Message
  XOR  AL,AL           ;ErrorLevel = 0
  JMP  Exit            ;Quit
H90:                   ;No need to do help
  RET


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE BAD DEVICE LIST
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;PRINT THE BAD DEVICE TABLE AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;         ES = TSR Data Area
;Outputs: Writes contents of Bad Device Table to the screen
;Changes: Quits program
;         Lots of Registers
;------------------------------------------------------------------------------
DoBadDevices:
  TEST StatusFlags,BadDevices ;Does the user want the Bad Device List?
  JZ  >B000                   ;If not, we're done
  TEST ProgFlags,InMemory     ;Are we installed in memory yet?
  JNZ >B00                    ;If so, continue
  MOV  DX,BadDvcErrMsg        ;If not, point at the first part of Error Message
  JMP  FinishDataErr          ;Terminate program
B000:
  RET

B00:                               ;Write it
  MOV  DX,BadBeepMsg               ;Write the
  CALL WriteZPause                 ;  Beep Header
  MOV  DX,YesMsg                   ;Assume Yes
  TEST ES:TSRFlags,NoBeep          ;Is it Yes?
  JZ  >B05                         ;If so, continue
  MOV  DX,NoMsgS                   ;If not, point at No Message
B05:                               ;Have Beep Status Message
  CALL WriteZPause                 ;Write the Beep Status
  CALL WriteCrLfPause2             ;Move down
  MOV  DX,BadDeviceHdr             ;Write the
  CALL WriteZPause                 ;  Header
  CALL TestSupportFile             ;Look for the Support File in the Path
  XOR  BL,BL                       ;Start Bad Counter at 0
  MOV  SI,BadDeviceTable           ;Point at Bad Table
  MOV  CX,MaxBadDevices            ;Number of Table Entries
B10:                               ;Loop to here for each table entry
  TEST ES:[SI].BDFlags,BDFlagInUse ;Valid Entry?
  JZ  >B40                         ;If not, skip it
  INC  BL                          ;Increment Bad Counter
  CALL WriteSpace                  ;Write
  MOV  AL,ES:[SI].BDHostIndex      ;  the
  CALL ByteToString                ;  Host Index
  CALL Write3Spaces                ;Write
  MOV  AL,ES:[SI].BDDvcAddress     ;  the
  CALL ByteToString                ;  Device Address
  CALL Write3Spaces                ;Write
  MOV  AL,ES:[SI].BDInterfaceNum   ;  the
  CALL ByteToString                ;  Interface Number
  CALL WriteBadStage               ;Write the Stage details
B40:                               ;Go to next entry
  ADD  SI,BadDeviceStrucSize       ;Point at next Entry
  LOOP B10                         ;Keep going until we're done
  OR   BL,BL                       ;Were there any Bad Devices?
  JNZ >B70                         ;If so, we're done
  MOV  DX,NoneFoundMsg             ;If not, write
  CALL WriteZPause                 ;  "None Found" Message
B70:                               ;Done
  MOV  DX,NoMsg                    ;Write nothing when done
  XOR  AL,AL                       ;ErrorLevel=0
  JMP  Exit                        ;And QUIT

;------------------------------------------------------------------------------
;WRITE THE BAD STAGE INFORMATION TO THE SCREEN
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [SI] = Bad Device Table Entry
;                Already checked for Validity (BDFlagInUse = True)
;                Host Index, Device Address, & Interface Number alread written
;Outputs: Write Try, Stage, and Error Code Numbers to the Table
;Changes:
;------------------------------------------------------------------------------
WriteBadStage:
  PUSH AX,CX                       ;Save used registers
  CALL Write2Spaces                ;Move over
  XOR  AH,AH                       ;Write the
  MOV  AL,ES:[SI].BDNewDeviceStage ;  Stage
  CALL WordToString                ;  Number
  CALL Write2Spaces                ;Move over
  MOV  CX,ES:[SI].BDNewDeviceError ;Write the
  CALL WriteBadError               ;  Error Code
  CALL WriteCrLfPause              ;Move down
  POP  CX,AX                       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE ERROR CODE ND APPROPRIATE DESCRIPTION TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         AL = Stage Number
;         CX = Error Code to write
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
WriteBadError:
  PUSH AX,DX            ;Save used registers
  XCHG AX,CX            ;AX = Error Code, CL = Stage
  CALL WriteWordHex     ;Write the Error Code
  CALL WriteHexEnd      ;Write an h
  CMP  CL,200           ;Is the stage more than 200?
  JAE >E90              ;If so, the Error Code is meaningless
  CALL WriteSpace       ;Move over
  TEST ProgFlags,FoundSuptFile ;Is there a Support File in the Path?
  JNZ >E20              ;If so, continue
E10:                    ;No Support File
  MOV  DX,NoSuptFileMsg ;If not, write first part
  CALL WriteZPause      ;  of Error Message
  MOV  DX,SupportFile   ;Write the Support
  CALL WriteZPause      ;  File Name
  JMP >E90              ;Done
E20:                    ;OK to call support File
  TEST CL,1             ;Is it even (TD Status) or odd (Int 14h Error)?
  JZ  >E50              ;If Even, jump to handle it
E40:                    ;Int 14h Error Code (Stage is Odd)
  CALL WriteInt14Err    ;Write the Int 14h Error Code
  JMP >E90              ;Done
E50:                    ;TD Status Code (Stage is Even)
  CALL WriteTDStsErr    ;Write the TD Status Error Code
E90:                    ;Done
  POP  DX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE TD STATS OR INT 14H ERROR DESCRIPTION TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         AX = TD Status Code to write
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
WriteTDStsErr:
  PUSH BX,CX,DX           ;Save used registers
  MOV  BX,TDStatusCode    ;[BX] = Where to write Code Number
  MOV  CX,TDStatusCallBk  ;[CX] = Where to write Screen-writing Address
  MOV  DX,TDStatusMsg     ;[DX] = Start of command-tail string
  JMP >R00                ;Do it
WriteInt14Err:
  PUSH BX,CX,DX           ;Save used registers
  MOV  BX,Int14ErrCode    ;[BX] = Where to write Code Number
  MOV  CX,Int14ErrCallBk  ;[CX] = Where to write Screen-writing Address
  MOV  DX,Int14ErrMsg     ;[DX] = Start of command-tail string
;  JMP >R00               ;Do it
R00:
  PUSH AX                 ;Save used registers
  CALL WriteWordToString  ;Write the Error Code to our string
  MOV  BX,CX              ;Write our
  MOV  AX,CS              ;  screen-writing
  MOV  CX,WriteZPauseFar  ;  code address
  CALL WriteSegmentOffset ;  to the string
  MOV  BX,DX              ;Copy our String
  CALL CopyToCmdTail      ;  to the Command Tail String
  CALL DoExec             ;Write the Description
  POP  AX                 ;Restore used registers
  POP  DX,CX,BX           ;Restore used registers
  RET


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE REAL-TIME JOYSTICK STATUS
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;WRITE THE CURRENT JOYSTICK STATUS TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         JoyStatusIndex (Joystick Index to test)
;Outputs: Writes the real-time status of a single Joystick to the Screen
;Changes:
;------------------------------------------------------------------------------
DoJoyStatus:
  CMP  JoyStatusIndex,-1  ;Anything to do?
  JE  >S000               ;If not, we're done
  TEST ProgFlags,InMemory ;If so, are we installed in memory yet?
  JNZ >S00                ;If so, show the data
  MOV  DX,JoyStatusErrMsg ;Point at first part of Error Message
  JMP  FinishDataErr      ;Terminate Program
S000:
  RET

S00:                            ;Do the Status thing!
  PUSH AX,BX,CX,DX,DI,SI        ;Save used registers
  OR   ES:TSRFlags,DisableSts   ;Mark as doing the Status
  OR   ProgFlags2,Write2Err     ;Write to ERR instead of Pause
  MOV  CL,JoyStatusIndex        ;Point at
  CALL DIIndex2Offset           ;  Joystick Data Structure (DI)
  LEA  SI,[DI].DIRawData        ;Point SI at the Raw Data
  ADD  DI,OFFSET DIControlsData ;Point DI at Translation Data
  CALL WriteJoyStatusHdr        ;Calculate and Write the Header
  JNZ >S10                      ;If OK, continue
  MOV  DX,NoStatusMsg           ;If not, write
  CALL WriteZErr                ;  "Nothing to Show" message
  MOV  AL,CL                    ;Write the Joystick
  CALL WriteNibbleHex           ;  Index Number
  CALL WriteDot                 ;Write a Period
  JMP >S80                      ;Done
S10:                            ;Loop to here until we stop
  CALL Write2Spaces             ;Move over
  MOV  BL,CTAxis                ;Start with axes
  XOR  BH,BH                    ;Start at Index 0
S20:                            ;Loop to here for each Control of this type
  CALL GetDIDataWord            ;Get Value
  JC  >S50                      ;If none there, we're done with this Control Type
  CMP  BL,CTButton              ;Is it a Button?
  JNE >S25                      ;If not, try next possibility
  MOV  AH,'.'                   ;Assume it's not pressed
  OR   AL,AL                    ;Is it pressed?
  JZ  >S23                      ;If not, continue
  MOV  AH,BH                    ;If so, get the Index
S21:                            ;Loop to here to get last numeral of Index
  CMP  AH,9                     ;Is it greater than 9?
  JBE >S22                      ;If not, it's OK
  SUB  AH,10                    ;If so, subtract 10
  JMP  S21                      ;Keep looking for the last numeral
S22:                            ;AH contains last numeral of Index
  ADD  AH,'0'                   ;Convert to an ASCII numeral
S23:                            ;AH contains charcater to write
  MOV  AL,AH                    ;Write the
  CALL WriteAL                  ;  character
  JMP >S30                      ;Continue
S25:                            ;Not a Bit
  CMP  BL,CTDPad                ;Need to write a Byte?
  JNE >S27                      ;If not, it's a Word
  CALL ByteToString             ;Write it
  JMP >S30                      ;Continue
S27:                            ;Write a word
  CALL WordToString             ;Write it
S30:                            ;Done writing this value
  CALL WriteSpace               ;Move over
  INC  BH                       ;Go to next Index
  JMP  S20                      ;Keep writing
S50:                            ;Done with this type of control
  CALL WriteSpace               ;Move over
  DEC  BL                       ;Go to next control type
  JZ  >S60                      ;If none left, we're done
  XOR  BH,BH                    ;Start at Index 0 again
  JMP  S20                      ;Keep going
S60:                            ;Done writing data
  MOV  DX,CrMsg                 ;Write a
  CALL WriteZErr                ;  Carriage Return
  CALL GetKey                   ;Key waiting?
  JNZ >S80                      ;If so, we're done
  MOV  AX,50                    ;If not, wait
  CALL DelayMSAX                 ;  a little bit
  JMP  S10                      ;Do it again
S80:                            ;Done
  CALL WriteCrLfErr             ;Move
  CALL WriteCrLfErr             ;  down
S90:                            ;Done
  AND  ProgFlags2,(NOT Write2Err) ;Write to Pause Again
  AND  ES:TSRFlags,(NOT DisableSts) ;Mark as done getting the Status
  POP  SI,DI,DX,CX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE STATUS HEADER MESSAGE TO THE SCREEN
;Inputs:  DS = Local data Area
;         ES = TSR Data Area
;         [SI] = Raw Data
;         [DI] = Controls Table Base Offset
;Outputs: ZF = Set if no Controls exist (no Joystick found)
;            = Clear if Header is OK
;              Writes appropriate Status Header Information to the screen
;Changes:
;------------------------------------------------------------------------------
WriteJoyStatusHdr:
  PUSH AX,BX,CX,DX    ;Save used registers
  PUSH DI,SI,BP       ;Save used registers
  XOR  BP,BP          ;Start Control Count at 0
  CALL WriteCrLfErr   ;Start a new line
  MOV  CH,3           ;Start with first line of Header
S10:                  ;Loop to here until we're done
  CALL Write2Spaces   ;Move over
  MOV  BL,CTAxis      ;Start with Axes
  XOR  BH,BH          ;Start at Index 0
S20:                  ;Loop to here for each Control of this type
  CALL GetDIDataWord  ;Get Value
  JC  >S50            ;If none there, we're done with this Control Type
  INC  BP             ;Increment Control Counter
  CMP  CH,3           ;Doing first line?
  JNE >S22            ;If not, handle second line
  MOV  DX,AxisMsg     ;Assume Axis
  CMP  BL,CTAxis      ;Is it?
  JE  >S40            ;If so, write it
  MOV  DX,ThrottleMsg ;Assume Throttle
  CMP  BL,CTThrottle  ;Is it?
  JE  >S40            ;If so, write it
  MOV  DX,SliderMsg   ;Assume Slider
  CMP  BL,CTSlider    ;Is it?
  JE  >S40            ;If so, write it
  MOV  DX,DPadMsg     ;Assume DPad/HatSwitch
  CMP  BL,CTDPad      ;Is it?
  JE  >S40            ;If so, write it
  MOV  DX,ButtonMsg   ;If not, it's a Button
  JMP >S40            ;Write it
S22:                  ;Doing second line
  CMP  CH,2           ;Doing second line?
  JNE >S30            ;If not, handle third line
  MOV  AL,BH          ;Set AL to Index Number
  CMP  BL,CTSlider    ;Write a Word?
  JB  >S23            ;If not, continue
  CALL Write2Spaces   ;If so, write 2 spaces
S23:                  ;Handled
  CMP  BL,CTDPad      ;Write a Byte?
  JB  >S24            ;If not, continue
  CALL Write2Spaces   ;If so, write 2 spaces
S24:                  ;Done writing spaces
  CALL WriteNumeralAL ;Write the Index
  CALL WriteSpace     ;Move over
  JMP >S45            ;Continue
S101:
  JMP S10             ;To avoid JMP > 128
S30:                  ;Doing third line
  MOV  DX,LineMsg5    ;Assume it's a Word
  CMP  BL,CTSlider    ;Is it?
  JAE >S40            ;If so, write it
  MOV  DX,LineMsg3    ;Assume it's a Byte
  CMP  BL,CTDPad      ;Is it?
  JE  >S40            ;If so, write it
  MOV  DX,LineMsg1    ;If not, it's a Bit
S40:                  ;Have correct Control Header String
  CALL WriteZErr      ;Write it
S45:                  ;Done writing Header Word
  INC  BH             ;Go to next Index
  JMP  S20            ;Keep looking
S50:                  ;Done with this type of control
  CALL WriteSpace     ;Move over
  DEC  BL             ;Go to next control type
  JZ  >S60            ;If none left, we're done
  XOR  BH,BH          ;Start at Index 0 again
  JMP  S20            ;Keep going
S60:                  ;Done writing data
  OR   BP,BP          ;Did we write anything?
  JZ  >S90            ;If not, quit
  CALL WriteCrLfErr   ;Write a new line
  DEC  CH             ;Go to next line of Header
  JNZ  S101           ;Do it again
S90:                  ;Done
  OR   BP,BP          ;Any valid controls?
  POP  BP,SI,DI       ;Restore used registers
  POP  DX,CX,BX,AX    ;Restore used registers
  RET


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE COMMAND-LINE ALIASES
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;PRINT THE COMMAND LINE ALIASES AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;Outputs: Writes Aliases to the screen
;Changes: Quits program
;------------------------------------------------------------------------------
DoAliases:
  TEST StatusFlags,Alias      ;Does user want to see the Aliases?
  JNZ >A00                    ;If so, continue
  RET

A00:
  PUSH BX,DX,SI               ;Save used registers

  CALL WriteCrLfPause         ;Move down
  MOV  DX,AliasHdrMsg         ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchList          ;Point at Table of Aliases
  MOV  BX,'?'+('Z' SHL 8)     ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases
  MOV  BX,1+(31 SHL 8)        ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases
  CALL WriteCrLfPause2        ;Move down

  MOV  DX,JStikHdrMsg         ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchListJStik     ;Point at Table of Aliases
  MOV  BX,255+(255 SHL 8)     ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNum         ;Write the Aliases
  CALL WriteCrLfPause2        ;Move down

  MOV  DX,DIHdrMsg            ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchListDIIndex   ;Point at Table of Aliases
  MOV  BX,'A'+('Z' SHL 8)     ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases
  CALL WriteCrLfPause2        ;Move down

  MOV  DX,EntityHdrMsg        ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchListEntity    ;Point at Table of Aliases
  MOV  BX,'A'+('Z' SHL 8)     ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases
  CALL WriteCrLfPause2        ;Move down

  MOV  DX,SubEntityHdrMsg     ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchListSubEntity ;Point at Table of Aliases
  MOV  BX,'A'+('Z' SHL 8)     ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases
  CALL WriteCrLfPause2        ;Move down

  MOV  DX,CtrlTypeHdrMsg      ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchListCtrlType  ;Point at Table of Aliases
  MOV  BX,1+(31 SHL 8)        ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases
  CALL WriteCrLfPause2        ;Move down

  MOV  DX,YesNoHdrMsg         ;Set up
  CALL SetupPause             ;  the Header
  MOV  SI,SwitchListYesNo     ;Point at Table of Aliases
  MOV  BX,'N'+('Y' SHL 8)     ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum       ;Write the Aliases

A90:                          ;Done
  POP  SI,DX,BX               ;Restore used registers
  MOV  DX,NoMsg               ;Don't write anything
  XOR  AL,AL                  ;ErrorLevel = 0
  JMP  Exit                   ;Quit

;------------------------------------------------------------------------------
;WRITE THE LIST OF SWITCH CHARACTERS TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         BL = Minimum Value to look for in the Table
;         DH = Maximum Value to look for in the Table
;         SI = Switch List ("Code Word") Table
;Outputs: Writes List of Switch Characters to the Screen
;Changes:
;NOTES:   Sets CH =  0 if we write Blocks for Switches < " "
;                 = -1 if we write numbers for the all Switches (0-255)
;                 = -2 if we subtract 128 from the numbers (128-255)
;------------------------------------------------------------------------------
;DoAliasesGoNum80h:
;  PUSH CX               ;Save used registers
;  MOV  CH,2             ;Mark as writing Numbers, needing to subtract 128
;  JMP >G00              ;Continue
DoAliasesGoNum:
  PUSH CX               ;Save used registers
  MOV  CH,1             ;Mark as writing Numbers, not needing to modify
  JMP >G00              ;Continue
DoAliasesGoNoNum:
  PUSH CX               ;Save used registers
  XOR  CH,CH            ;Mark as not writing Numbers
;  JMP >G00              ;Continue
G00:
  PUSH AX,DI            ;Save used registers
  MOV  DI,AliasSortTbl  ;Point at the Alias Sort Table
  MOV  AL,BL            ;Start with the Lower Limit
G10:                    ;Loop to here for each Alias
  CALL GetAliasPointers ;Get the Alias Pointers
  JZ  >G60              ;If none for this entry, go to the next one
G20:                    ;Have at least one valid Alias
  CALL WriteCrLfPause   ;If valid, move down
  OR   CH,CH            ;Writing Single characters?
  JNZ >G40              ;If not, handle full numbers
  MOV  CL,78            ;Write this many characters per line
  CMP  AL,' '           ;Is there a valid single-character equivalent?
  JB  >G30              ;If not, don't write it
  CALL WriteAL          ;If so, write it
  JMP >G50              ;Continue
G30:                    ;Not a writable character
  CALL WriteBlock       ;Write a Block Character
  JMP >G50              ;Continue
G40:                    ;Need to write Numbers
  MOV  CL,76            ;Write this many characters per line
  PUSH AX               ;Save the number for a second
  CMP  CH,2             ;Do we need to modify the Number?
  JNE >G45              ;If not, continue
  AND  AL,(NOT 80h)     ;Subtract 128 from it
G45:                    ;Write the Number
  CALL ByteToString     ;Write the Switch as a Number
  POP  AX               ;Restore the Number
G50:                    ;Done writing single character
  CALL WriteAliases     ;Write the Alias List
G60:                    ;Done with this Code
  CMP  AL,BH            ;Have we done the whole list yet?
  JE  >G90              ;If so, we're done
  INC  AL               ;If not, go to the next possibility
  JMP  G10              ;Keep going until we're done
G90:                    ;Done
  POP  DI,AX            ;Restore used registers
  POP  CX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET STRING POINTERS FOR ALL ALIASES FOR A SINGLE CHARACTER OPTION
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;          AL  = Switch Table entry to look for (not 0)
;         [SI] = Switch Table to look through, sorted backwards
;         [DI] = Alias Sort Table
;Outputs:  ZF = Clear if OK
;               [DI] = Filled with String Pointers, sorted backwards
;                      Last Entry is always a 0
;             = Set if no matches found
;                      First Entry is 0
;Changes:
;------------------------------------------------------------------------------
GetAliasPointers:
  PUSH AX,BX,CX,DX      ;Save used registers
  PUSH DI,SI            ;Save used registers
  MOV  CX,DI            ;Sve original pointer
P10:                    ;Loop to here for each Table Entry
  MOV  DX,SI            ;Save beginning Alias Pointer
P20:                    ;Loop to here to find the end of an Alias List Entry
  CMP  B [SI],0         ;End of Alias List?
  JE  >P30              ;If so, handle it
  CALL SkipOverStringSI ;If not, skip over the string
  JMP  P20              ;Keep looking
P30:                    ;End of Alias List Entry
  MOV  BX,SI            ;Save End-of-Entry Pointer
  CMP  AL,[SI+1]        ;Is this entry a match?
  JNE >P50              ;If not, go to the next one
  MOV  SI,DX            ;Point at Alias String
P40:                    ;Loop to here for each Alias String
  CMP  B [SI],0         ;End of the entry?
  JE  >P50              ;If so, we're done
  MOV  [DI],SI          ;If not, store it in the sort table
  INC  DI,2             ;Update the Table Pointer
  CALL SkipOverStringSI ;Skip over the string
  JMP  P40              ;Keep going until we have all of the strings
P50:                    ;Done with this entry
  MOV  SI,BX            ;Get End-of-Entry Pointer
  INC  SI,2             ;Skip over End-of-Entry stuff
  CMP  B [SI],0         ;End of Table?
  JNE  P10              ;If not, keep looking
P90:                    ;Done
  MOV  W [DI],0         ;Make sure last entry is a 0
  CMP  CX,DI            ;Set return flag
  POP  SI,DI            ;Restore used registers
  POP  DX,CX,BX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE ALL ALIAS STRINGS FOR A SINGLE CHARACTER OPTION
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = Alias String Pointer Table (Sorted backwards)
;                  Last Entry is a 0
;          CL  = Number of characters left available to write on first line
;                 (76 if Writing Numbers, 78 if Writing Characters)
;         CLD already issued
;Outputs:
;Changes:
;------------------------------------------------------------------------------
WriteAliases:
  PUSH AX,BX,CX,DX    ;Save used registers
  PUSH DI,SI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at the Local Data Area
  MOV  BL,CL          ;BL = Character Countdown Measurer
  MOV  BH,CL          ;BH = Original Counter
  MOV  DX,DI          ;Save start-of-table Pointer
  MOV  CX,-1          ;Maximize counter
  XOR  AX,AX          ;Find the last
  REPNE SCASW         ;  entry in
  SUB  DI,4           ;  the Table
W10:                  ;Loop to here for each Alias String
  CMP  DI,DX          ;Are we done writing?
  JB  >W90            ;If so, quit
  MOV  SI,[DI]        ;Get the next string
  DEC  DI,2           ;Update the table pointer
  CALL CalcStrSizeSI  ;See how long the string is
  OR   BL,BL          ;Is the character counter down to 0?
  JZ  >W20            ;If so, we need to reset it
  CMP  CL,BL          ;Is this string too long to fit on this line?
  JB  >W40            ;If not, go ahead and write it
W20:                  ;Need to start a new line
  CALL WriteCrLfPause ;Start a new line
  PUSH CX             ;Save String Size for a second
  MOV  CL,79          ;Calculate how many
  SUB  CL,BH          ;  spaces to write
  CALL WriteSpaces    ;Write the spaces
  POP  CX             ;Restore String Size
  MOV  BL,BH          ;Reset the character counter
W40:                  ;Write the Alias String
  SUB  BL,CL          ;Update the
  DEC  BL             ;  character counter
  CALL WriteSpace     ;Write a Space
  CALL WriteZPauseSI  ;Write the string
  JMP  W10            ;Keep writing
W90:                  ;Done
  CALL WriteCrLfPause ;Move down
  POP  ES,SI,DI       ;Restore used registers
  POP  DX,CX,BX,AX    ;Restore used registers
  RET


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE THE ERRORLEVEL TABLE
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;WRITE ERRORLEVEL TABLE TO THE SCREEN AND QUIT
;Inputs:  CS = DS = Local Data Area
;Outputs: Writes ErrorLevel Table to the Screen
;Changes: Quits Program
;         Several registers
;------------------------------------------------------------------------------
DoErrLvls:
  TEST StatusFlags,ErrLvl   ;Does the user want to see the ErrorLevels?
  JZ  >V95                  ;If not quit
  MOV  DX,ErrLvlHdr         ;Write the
  CALL SetupPause           ;  Table Header
  MOV  AL,1                 ;Start with ErrorLevel 1
V10:                        ;Loop to here for each table entry
  MOV  BX,ErrLvlTbl         ;Table to search
  CALL SearchByte2StringTbl ;Valid ErrorLevel?
  JC  >V50                  ;If not, keep looking
  CALL WriteSpace           ;Move Over
  CALL ByteToString         ;If valid, write the Number
  CALL WriteSpace           ;Write a space
  MOV  DX,BX                ;Write the
  CALL WriteZPause          ;  Description String
  CALL WriteCrLfPause       ;Write a New Line
V50:                        ;Done with this entry
  INC  AL                   ;Increment ErrorLevel
  JNZ  V10                  ;If not rolled over yet, keep looking
V90:                        ;Done
  MOV  DX,NoMsg             ;Don't write anything
  XOR  AL,AL                ;ErrorLevel = 0
  JMP  Exit                 ;Quit
V95:                        ;No need to write Aliases
  RET


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE PROGRAM STATUS
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;WRITE THE CURRENT PROGRAM STATUS TO THE SCREEN
;Inputs:  ES = TSR Data Area
;         Status (Flag)
;Outputs: Writes a bunch of stuff to the screen if Status Flag is True
;Changes:
;------------------------------------------------------------------------------
DoStatus:
  TEST StatusFlags,Status ;Need to to anything?
  JZ  >S000               ;If not, quit
  TEST ProgFlags,InMemory ;Are we installed in memory yet?
  JNZ >S00                ;If so, go ahead and do it
  MOV  DX,StatusErrMsg    ;If not, point at the first part of Error Message
  JMP  FinishDataErr      ;Terminate program
S000:
  RET

S00:
  PUSH AX,BX,CX,DX,SI,BP       ;Save used registers

  MOV  DX,StatusMsg1           ;Write Number of Buttons
  CALL WriteZPause
  MOV  AL,4
  TEST ES:TSRFlags,Buttons15
  JZ  >S10
  MOV  AL,15
S10:
  CALL ByteToString

  MOV  DX,StatusMsg2           ;Write ThrustMaster Status
  CALL WriteZPause
  MOV  DX,YesMsg
  TEST ES:TSRFlags,ThrustMaster
  JNZ >S20
  MOV  DX,NoMsgS
S20:
  CALL WriteZPause

  MOV  DX,StatusMsg3           ;Write I/O Virtualization Status
  CALL WriteZPause
  CMP  ES:VirtIOHandle,-1
  JE  >S30
  MOV  AX,ES:VirtIOTable[0]
  CALL WriteWordHex
  CALL WriteHexEnd
  JMP >S35
S30:
  CALL Write2Spaces
  MOV  DX,NoMsgS
  CALL WriteZPause
S35:

  MOV  BX,0300h                ;BL = Index (0), BH = indent size
  CALL DoIndividualStatus      ;Do Individual Status for Index 0

  MOV  DX,StatusMsg4           ;Write BIOS Center Coordinate
  CALL WriteZPause
  MOV  AX,ES:NormalizeMax
  SHR  AX,1
  CALL WordToString
  CALL DoIndividualStatus      ;Do Individual Status for Index 1

  MOV  DX,StatusMsg5           ;Write Movement Percent
  CALL WriteZPause
  MOV  AX,ES:MovePercent
  CALL WordToString
  CALL DoIndividualStatus      ;Do Individual Status for Index 2

  MOV  DX,Status1EndMsg        ;Write last line
  CALL WriteZPause             ;  of Combined Table
  CALL DoIndividualStatus      ;Do Individual Status for Index 3

  MOV  BH,35                   ;Increase Indent
  CALL DoIndividualStatus      ;Do Individual Status for Index 4
  CALL DoIndividualStatus      ;Do Individual Status for Index 5
  CALL DoIndividualStatus      ;Do Individual Status for Index 6
  CALL DoIndividualStatus      ;Do Individual Status for Index 7
  MOV  DX,Status2EndMsg        ;Write the last line of the Individual Table,
  CALL WriteZPause             ;  first line of the Ignore Table
  CALL WriteXlatTable          ;Write the Translation Table Contents
S80:                           ;Done with User Table
  CALL WriteCrLfPause          ;Move down
S90:                           ;Done
  POP  BP,SI,DX,CX,BX,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE STATUS OF AN INDIVIDUAL DEVICE TO THE SCREEN
;Inputs:  BL = Device Index to write
;         BH = Number of spaces to write at beginning of line (Indent)
;         ES = TSR Data Area
;Outputs: To Screen
;Changes: BL is incremented
;------------------------------------------------------------------------------
DoIndividualStatus:
  PUSH AX,CX,DX,DI                 ;Save used registers
  MOV  CL,BH                       ;Do the
  CALL WriteSpaces                 ;  Indent
  MOV  AL,BL                       ;Write the Index Number
  CALL WriteSpace
  CALL WriteNibbleHex
  CALL WriteSpace
  CALL DIIndex2OffsetBL            ;Convert Index to Offset (DI)
  TEST ES:[DI].DIFlags,DIFlagInUse ;Valid Table Entry?
  JZ  >I90                         ;If not, we're done with this one
  CALL WriteSpace                  ;Write Number of Controls
  CALL WriteNumControls
  MOV  AL,ES:[DI].DIHostIndex      ;Write Host Index
  CALL ByteToString
  CALL WriteSpace                  ;Write Device Address
  MOV  AL,ES:[DI].DIDvcAddress
  CALL ByteToString
  CALL WriteSpace                  ;Write Interface Number
  MOV  AL,ES:[DI].DIInterfaceNum
  CALL ByteToString
I90:                               ;Done
  CALL WriteCrLfPause              ;Start a new line
  INC  BL                          ;Increment Index
  POP  DI,DX,CX,AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE QUANTITIES OF CONTROLS TO THRE SCREEN
;Inputs:   ES  = TSR Data Area
;         [DI] = Device Info Table Entry
;Outputs: Writes all Control Quantities to the Screen
;Changes:
;------------------------------------------------------------------------------
WriteNumControls:
  PUSH AX,BX,DI,SI              ;Save used registers
  LEA  SI,[DI].DIRawData        ;Point SI at the Raw Data
  ADD  DI,OFFSET DIControlsData ;Point DI at Translation Data
  MOV  BL,CTAxis                ;Start with Axes
C10:                            ;Loop to here for each Control Type
  XOR  BH,BH                    ;Start with Index 0
C20:                            ;Loop to here for each Control Index
  CALL GetDIDataWord            ;Try to get the Data
  JC  >C40                      ;If no Control here, we're done
  INC  BH                       ;Increment Control Index
  JMP  C20                      ;Keep going
C40:                            ;Done Counting controls
  MOV  AL,BH                    ;Write the Number
  CALL ByteToString             ;  of Controls
  CALL WriteSpace               ;Move over
  DEC  BL                       ;Go to next Control Type
  JNZ  C10                      ;If valid, keep writing
  POP  SI,DI,BX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE CONTENTS OF THE TRANSLATION TABLE TO THE SCREEN
;Inputs:  ES = TSR Data Area
;Outputs: Writes Translation Table Contents to the Screen
;Changes:
;------------------------------------------------------------------------------
WriteXlatTable:
  PUSH BX,CX,DX,DI         ;Save used registers
  MOV  BX,AXXlat           ;[BX] = Axis AX Translation
  MOV  CH,1                ;CH = Line Number
  MOV  CL,21               ;CL = Number of Spacing characters
  MOV  DX,XlatMsgList      ;[BP] = Analog Joystick Description
X10:                       ;Loop to here for each line
  CMP  CH,5                ;Should we write anything in Column 1?
  JA  >X15                 ;If not, skip it
  CALL WriteZPause         ;Write the Analog Description
  ADD  DX,4                ;Update the Description pointer
  CALL WriteSpace          ;Move over
  XOR  DI,DI               ;Translation Table Offset = 0
  CALL WriteXlatEntry      ;Write the Translation Table Entry
  JMP >X20                 ;Done with column 1
X15:                       ;Don't write Column 1
  CALL WriteSpaces         ;Skip this column
X20:                       ;Done with Column 1
  CALL WriteCSep           ;Write the Separator Line
  CALL WriteZPause         ;Write the Analog Description
  ADD  DX,4                ;Update the Description pointer
  CALL WriteSpace          ;Move over
  MOV  DI,(A01Xlat-AXXlat) ;DI = Translation Table Offset
  CALL WriteXlatEntry      ;Write the Translation Table Entry
X30:                       ;Done with Column 2
  CALL WriteCSep           ;Write the Separator Line
  CMP  CH,8                ;Is this the last Line?
  JE  >X90                 ;If so, we're done
  CALL WriteZPause         ;Write the Analog Description
  ADD  DX,4                ;Update the Description pointer
  CALL WriteSpace          ;Move over
  MOV  DI,(A09Xlat-AXXlat) ;DI = Translation Table Offset
  CALL WriteXlatEntry      ;Write the Translation Table Entry
  CALL WriteCrLfPause      ;Move down
  INC  CH                  ;Increment the Line Number
  ADD  BX,XlatStrucSize    ;Point at the next Table entry
  JMP  X10                 ;Keep going until we're done
X90:                       ;Done writing Table contents
  MOV  DX,MapEndMsg        ;Write the
  CALL WriteZPause         ;  Table Footer
  CALL TestXlatDups        ;Test for Duplicate Translation Table entries
  JNC >X95                 ;If none, quit
  MOV  DX,DuplicatesMsg    ;If so, warn
  CALL WriteZPause         ;  the user
X95:                       ;Done
  POP  DI,DX,CX,BX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE CONTENTS OF A DESCRIPTION TABLE ENTRY
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         ES:[BX+DI] = Translation Table Entry to write
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
WriteXlatEntry:
  PUSH AX                          ;Save used registers
  MOV  AX,W ES:[BX+DI].XlatJIndex  ;Write the
  CALL ByteToString                ;  Joystick Index
  CALL WriteCtlType                ;Write the Control Type Description
  MOV  AX,W ES:[BX+DI].XlatCIndex  ;Get Control Indices
  CALL ByteToString                ;Write Index 1
  CALL WriteSpace                  ;Move over
  CMP  AH,-1                       ;Valid Control Index 2?
  JE  >E50                         ;If not, write Spaces
  CMP  ES:[BX+DI].XlatCType,CTDPad ;Are we doing a DPad?
  JE  >E20                         ;If so, handle it
E10:                               ;Writing Button Control Index 2
  MOV  AL,AH                       ;Write Button
  CALL ByteToString                ;  Index 2
  JMP >E90                         ;Done
E20:                               ;Writing DPad Control Index 2 (Axis)
  CALL Write2Spaces                ;Move over
  MOV  AL,'X'                      ;Assume Axis X
  OR   AH,AH                       ;Is it Axis X?
  JZ  >E25                         ;If so, continue
  INC  AL                          ;If not, it's Axis Y
E25:                               ;AL = Axis Description
  CALL WriteAL                     ;Write the Axis Description
  JMP >E90                         ;Done
E50:                               ;Invalid Second Index
  CALL Write3Spaces                ;Move over
E90:                               ;Done
  POP  AX                          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE CONTROL TYPE DESCRIPTION
;Inputs:  DS = Local Data Area
;         AH = Control Type (CTbutton, etc)
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
WriteCtlType:
  PUSH AX,DX         ;Save used registers
  CALL WriteSpace    ;Move over
  SUB  AH,CTButton   ;Convert
  MOV  AL,CMsgSize   ;  Control Type
  MUL  AH            ;  to a String Offset
  MOV  DX,CMsgButton ;Point [DX]
  ADD  DX,AX         ;  at the String
  CALL WriteZPause   ;Write it
  CALL WriteSpace    ;Move over
  POP  DX,AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST FOR DUPLICATIONS (POSSIBLE CONFLICTS) IN THE TRANSLATION TABLE
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         Translation table (AXXlat, etc.)
;Outputs: CF = Clear if no Duplicates
;            = Set if there is at least one Duplicate
;Changes:
;------------------------------------------------------------------------------
TestXlatDups:
  PUSH AX,BX,CX,DX,DI,SI,DS  ;Save used registers
  MOV  DS,ES                 ;DS = TSR Data ARea
  MOV  CX,(NumXlats-1)       ;CX = Number of Tests to perform
  MOV  SI,AXXlat             ;[SI] = Translation Table
D10:                         ;Loop to here for each Test
  MOV  AX,W [SI].XlatJIndex  ;AL = Joystick Index, AH = Control Type
  MOV  BX,W [SI].XlatCIndex  ;BL = Control Index 1, BH = Control Index 2
  LEA  DI,[SI].XlatStrucSize ;[DI] = Next Translation entry
  MOV  DX,CX                 ;Save outer Loop Counter
;  DEC  CX                    ;Inner Loop Counter = Outer Loop Counter - 1
D20:                         ;Loop to here for each SubTest
  CMP  AX,W [DI].XlatJIndex  ;Are the Joystick Index & Control Type the Same?
  JNE >D40                   ;If not, it can't be a Duplicate
  CMP  AH,CTButton           ;Is it a Button?
  JE  >D30                   ;If so, jump to handle it
  CMP  BX,W [DI].XlatCIndex  ;If not, do the Control Indices match?
  JNE >D40                   ;If not, it's not a Duplicate
  JMP >D70                   ;If so, it is a Duplicate
D30:                         ;It's a Button
  CMP  BL,[DI].XlatCIndex    ;Does the first Index match?
  JE  >D70                   ;If so, it's a Duplicate
  CMP  BL,[DI].XlatCIndex2   ;if not, does the second Index match?
  JE  >D70                   ;If so, it's a Duplicate
  CMP  BH,-1                 ;Is the second Index valid?
  JE  >D40                   ;If not, it can't be a Duplicate
  CMP  BH,[DI].XlatCIndex    ;Does the first Index match?
  JE  >D70                   ;If so, it's a Duplicate
  CMP  [DI].XlatCIndex2,-1   ;Is the second Index valid?
  JE  >D40                   ;If not, it can't be a Duplicate
  CMP  BH,[DI].XlatCIndex2   ;Does the second Index match?
  JE  >D70                   ;If so, it's a Duplicate
D40:                         ;Done with this Inner Loop entry
  ADD  DI,XlatStrucSize      ;Point at the next Table Entry
  LOOP D20                   ;Keep going until we're done
D50:                         ;Done with this Outer Loop entry
  MOV  CX,DX                 ;Restore Outer Loop Counter
  ADD  SI,XlatStrucSize      ;Point at the next Table Entry
  LOOP D10                   ;Keep going until we're done
  JMP >D80                   ;No Duplicates!
D70:                         ;Error (at least one duplicate)
  STC                        ;Set return flag
  JMP >D90                   ;Done
D80:                         ;OK (no duplicates)
  CLC                        ;Set return flag
D90:                         ;Done
  POP  DS,SI,DI,DX,CX,BX,AX  ;Restore used registers
  RET


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE DEVICE/CONFIG DESCRIPTORS
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;WRITE THE DESCRIPTORS OF THE LAST DEVICE TO THE SCREEN, IF NECESSARY
;Inputs:  ES:DescriptorData
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
DoDescriptors:
  TEST StatusFlags,Descriptors ;Does the user want to see the Descriptors?
  JZ  >D000                    ;If not, quit
  TEST ProgFlags,InMemory      ;Are we installed in memory yet?
  JNZ >D00                     ;If so, go ahead and do it
  MOV  DX,DescrErrMsg          ;If not, point at first part of Error Message
  JMP  FinishDataErr           ;Terminate program
D000:
  RET

D00:
  CALL TestLastDevice          ;Anything to do (quits if not)?
  MOV  DI,ES:LastDeviceFound   ;Get Info Table Pointer
  MOV  AX,ES                   ;Write the address
  MOV  CX,DvcDescrData         ;  of the Descriptor Data
  MOV  BX,DescriptorMsgAddr    ;  to the
  CALL WriteSegmentOffset      ;  Command Line Parameters
  MOV  BX,DescriptorMsgHost    ;Write
  MOV  AL,ES:[DI].DIHostIndex  ;  the
  CALL WriteByteToString       ;  Host Index
  INC  BX                      ;  and the
  MOV  AL,ES:[DI].DIDvcAddress ;  Device Address
  CALL WriteByteToString       ;  to the string
  MOV  AX,CS                   ;Write our
  MOV  CX,WriteZPauseFar       ;  Call-Back Address
  MOV  BX,DescriptorCallBk     ;  to the
  CALL WriteSegmentOffset      ;  Command Tail String
  MOV  BX,DescriptorMsg        ;Copy our String
  CALL CopyToCmdTail           ;  to the Command Tail String
  CALL DoExec                  ;Write the Descriptor
  MOV  DX,NoMsg                ;Write nothing
  XOR  AL,AL                   ;ErrorLevel = 0
  JMP  Exit                    ;Quit


;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯
;WRITE REPORT DESCRIPTOR
;屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯屯

;------------------------------------------------------------------------------
;PRINT OUT THE REPORT DESCRIPTOR DATA, IF NECESSARY
;Inputs:  ES:ReportData
;Outputs:
;Changes:
;------------------------------------------------------------------------------
DoReport:
  TEST StatusFlags,Report ;Does the user want to see the Descriptors?
  JZ  >R000               ;If not, quit
  TEST ProgFlags,InMemory ;Are we installed in memory yet?
  JNZ >R00                ;If so, go ahead and do it
  MOV  DX,ReportErrMsg    ;If not, point at the first part of Error Message
  JMP  FinishDataErr      ;Terminate program
R000:
  RET

R00:
  CALL TestLastDevice        ;Anything to do (quits if not)?
  MOV  DI,ES:LastDeviceFound ;Get Info Table Pointer
  MOV  AX,ES                 ;Write the address
  MOV  CX,ReportData         ;  of the Report Data
  MOV  BX,ReportDescrMsgAddr ;  to the
  CALL WriteSegmentOffset    ;  Command Tail String
  MOV  AX,CS                 ;Write our
  MOV  CX,WriteZPauseFar     ;  Call-Back Address
  MOV  BX,ReportDescrCallBk  ;  to the
  CALL WriteSegmentOffset    ;  Command Tail String
  MOV  BX,ReportDescrMsg     ;Copy our String
  CALL CopyToCmdTail         ;  to the Command Tail String
  CALL DoExec                ;Call the Support Program
  MOV  DX,NoMsg                ;Write nothing
  XOR  AL,AL                   ;ErrorLevel = 0
  JMP  Exit                    ;Quit


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SUPPORT CODE NEEDED TO WRITE USB DESCRIPTORS
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;TEST AND SEE IF WE CAN PRINT A DESCRIPTOR (LASTDEVICEFOUND & SUPPORTFILE)
;Inputs:  ES = TSR Data Area
;         LastDeviceFound
;Outputs: Quits program if error
;         Initializes PathToUse, Prints Header if OK
;Changes:
;------------------------------------------------------------------------------
TestLastDevice:
  PUSH AX,DX,SI                    ;Save used registers
  MOV  SI,ES:LastDeviceFound       ;Get the Device Pointer that this is for
  TEST ES:[SI].DIFlags,DIFlagInUse ;Device still there?
  JNZ >L10                         ;If so, continue
  MOV  DX,NoDescrDataMsg           ;Point at Error Message
  MOV  AL,ErLvlNoData              ;ErrorLevel = No Data
  JMP  Exit                        ;Quit
L10:                               ;Have some Data
  CALL TestSupportFile             ;Look for the Support File in the Path
  JNZ >L20                         ;If found, continue
  MOV  DX,NoSuptFileMsg            ;If not, write first part
  CALL WriteZErr                   ;  of Error Message
  MOV  DX,SupportFile              ;Write the
  CALL WriteZErr                   ;  File Name
  JMP  Exit                        ;Quit

L20:                               ;Everything is OK
  MOV  DX,HostIndexMsg             ;Write
  CALL WriteZPause                 ;  the
  MOV  AL,ES:[SI].DIHostIndex      ;  Host
  CALL ByteToString                ;  Index
  CALL WriteCrLfPause              ;Write a new line
  MOV  DX,DvcAddressMsg            ;Write
  CALL WriteZPause                 ;  the
  MOV  AL,ES:[SI].DIDvcAddress     ;  Device
  CALL ByteToString                ;  Address
  CALL WriteCrLfPause2             ;Write 2 new lines
L90:                               ;Done
  POP  SI,DX,AX                    ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO DELAY FOR A LITTLE WHILE
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;DELAY FOR A GIVEN NUMBER OF MILLISECONDS
;Inputs:  AX = number of milliseconds to wait (0-65,535)
;Outputs:
;Changes:
;------------------------------------------------------------------------------
DelayMSAX:
  PUSH AX,BX,DX  ;Save used registers
  OR   AX,AX     ;Is there anything to do?
  JZ  >D90       ;If not, just quit
  MOV  DX,1000   ;Convert
  MUL  DX        ;  to
  MOV  BX,AX     ;  microseconds (DX:BX)
D10:             ;Loop to here for each 65535 uS
  OR   DX,DX     ;Counted down high word yet?
  JZ  >D30       ;If so, handle it
  DEC  DX        ;Decrement high word
  MOV  AX,-1     ;Maximum microseconds
  JMP >D50       ;Do it
D30:             ;Counted down high word
  OR   BX,BX     ;Anything to do?
  JZ  >D90       ;If not, we're done
  MOV  AX,BX     ;Get Low Word
  XOR  BX,BX     ;Make sure it's zero next time
D50:             ;AX = Microseconds to wait
  CALL DelayUSAX ;Delay the number of microseconds specified
  JMP  D10       ;See if we need to wait some more
D90:             ;We're done
  POP  DX,BX,AX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DELAY FOR A GIVEN NUMBER OF MICROSECONDS
;Inputs:  AX = number of microseconds to wait (0-65535)
;Outputs:
;Changes:
;   NOTE: Delays will be slightly longer than what's asked for, never shorter.
;         Accuracy decreases with small delays or slow computers!
;------------------------------------------------------------------------------
DelayUSAX:
  PUSH AX,BX           ;Save used registers
  OR   AX,AX           ;Is there anything to do?
  JZ  >D90             ;If not, just quit
  MOV  BX,AX           ;Put it in BX
  MOV  AX,(0FFFFh/3)+1 ;Maximum time for each sub-delay
D10:                   ;Loop to here for each sub-delay
  CMP  BX,AX           ;Is it more than the maximum?
  JA  >D40             ;If so, just do the sub-delay
  MOV  AX,BX           ;If not, just do the remainder that's left
D40:                   ;Do the delay
  CALL DoDelayUS       ;Do the US Delay
  SUB  BX,AX           ;SUbtract out how long we just waited
  JNZ  D10             ;If not 0 yet, do the sub-delay again
D90:                   ;We're done
  POP  BX,AX           ;Restore used registers
  RET

DoDelayUS:
  PUSH AX,BX,CX,DX ;Save used registers
  PUSHF            ;Save Flags
  SHL  AX,1        ;Multiply by 2
  MOV  BX,11932    ;Compensate for the fact
  MUL  BX          ;  that the clock frequency
  MOV  BX,10000    ;  is actually
  DIV  BX          ;  1.19318 MHz
  MOV  DX,AX       ;Save it
  CLI              ;Disable interrupts
  CALL GetTimer    ;Get the starting timer tick counter
  MOV  CX,AX       ;Save it
D20:               ;Keep looping to here until we've waited long enough
  MOV  BX,CX       ;Get the starting timer tick counter
  CALL GetTimer    ;Get the current timer tick counter
  SUB  BX,AX       ;Calculate the elapsed time
  CMP  BX,DX       ;Has it been long enough?
  JB   D20         ;If not, keep waiting
D90:               ;We're done
  POPF             ;Restore Flags
  POP  DX,CX,BX,AX ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO TEST AND SEE IF THE COMPUTER IS COMPATIBLE OR NOT.
;Tests DOS Version, Hardware installed, etc. as appropriate to make sure
;  we don't do something that could crash the computer.
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;TEST THE COMPUTER TO SEE IF IT'S COMPATIBLE (CPU, DOS VERSION, WHATEVER)
;Inputs:  InMemory
;         ES = TSR Data Area
;Outputs: Quits the program if Computer is Incompatible
;         Returns if Computer is OK
;         Stores all kinds of values in memory variables
;Changes:
;------------------------------------------------------------------------------
TestCompatibility:
  PUSH AX,DX              ;Save used registers
  TEST ProgFlags,InMemory ;Are we already installed?
  JNZ >C90                ;If so, no need to test again
  MOV  AL,ErLvlDOSVer     ;Assume incorrect
  MOV  DX,DOSVerMsg       ;  DOS Version
  CALL TestDOSVer         ;  new enough?
  JC  >C70                ;If not, quit
  MOV  AL,ErLvlNoHost     ;Assume Host Driver
  MOV  DX,NoHostMsg       ;  is not installed
  CALL TestUSBHost        ;Is there a USB Host Driver installed?
  JNC >C90                ;If not, we're OK
C70:                      ;The computer is incompatible for some reason
  JMP  Exit               ;Quit!
C90:                      ;The computer is compatible!
  POP  DX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST THE CURRENT DOS VERSION
;Inputs:  MinDOSVer (EQUate)
;Outputs: CF = Clear if DOS version >= MinDOSVer
;         CF = Set if DOS version < MinDOSVer
;Changes:
;------------------------------------------------------------------------------
TestDOSVer:
  PUSH AX,BX,CX     ;Save used registers
  MOV  AX,3000h     ;Service 30h (get DOS version number)
                    ;  DOS 5.0 requires AL = 00h, may change BX and CX
  INT  21h          ;Do it (Returns AL= Major, AH = Minor)
  XCHG AL,AH        ;Swap Major and Minor versions for testing
  CMP  AX,MinDOSVer ;Is the version new enough?
  JAE >D80          ;If so, it's OK
D70:                ;DOS is older than the minimum
  STC               ;Set the "too old" flag
  JMP >D90          ;We're done
D80:                ;DOS is >= the minimum
  CLC               ;Set the OK flag
D90:                ;We're done
  POP  CX,BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF A COMPATIBLE USB HOST DRIVER IS INSTALLED
;Inputs:
;Outputs: CF = Set if error (No Host Driver Installed)
;            = Clear if OK
;Changes:
;------------------------------------------------------------------------------
TestUSBHost:
  PUSH AX,BX,CX ;Save used registers
  MOV  AX,5000h ;Function 5000h (Install Check)
  MOV  BX,'US'  ;Set
  MOV  CX,'B!'  ;  registers
  STC           ;Preset Error Flag
  INT  14h      ;Do it
  JC  >U70      ;If Error, USB Host can't be installed
  OR   AX,AX    ;Is AX Correct?
  JNZ >U70      ;If not, Error
  CMP  BX,'B!'  ;Is BX correct?
  JNE >U70      ;If not, Error
  CMP  CX,'US'  ;Is CX correct?
  JE  >U80      ;If so, it's installed
U70:            ;Not installed
  STC           ;Set Error Flag
  JMP >U90      ;Done
U80:            ;Installed
  CLC           ;Set OK flag
U90:            ;Done
  POP  CX,BX,AX ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO TEST THE CPU CATEGORY
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;TEST THE CPU CATEGORY
;Inputs:  DES = TSR Data Area
;         TSRFlags2.Is80386 = 0
;Outputs: TSRFlags2.Is80386 = True if CPU is 80386+
;Changes:
;------------------------------------------------------------------------------
TestCPUCategory:
  CALL Test80386         ;Is the CPU at least an 80386?
  JC  >C90               ;If not, quit
  OR   TSRFlags2,Is80386 ;If so, mark the flag
C90:                     ;Done
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF THE CPU IS AN 80386+ (CAN HANDLE 32-BIT INSTRUCTIONS)
;Inputs:
;Outputs: CF = Clear if the CPU is an 80386+
;            = Set if CPU is not an 80386+
;Changes:
;------------------------------------------------------------------------------
Test80386:
  PUSH AX,BX     ;Save used registers
  PUSHF          ;Save flags
  CALL Test8086  ;Is it an 8086?
  JNC >E70       ;If so, it can't be an 80386
  MOV  BX,0F000h ;Flags mask for testing (these bits always clear on 80286)
  PUSHF          ;Put the current flags
  POP  AX        ;  in AX
  OR   AX,BX     ;Try to set the
  PUSH AX        ;  four high bits
  POPF           ;  of the flags
  PUSHF          ;Put the current flags
  POP  AX        ;  in AX
  TEST AX,BX     ;Are any of the four high bits set?
  JNZ >E80       ;If so, it's not an 80286 - it must be an 80386+
E70:             ;It's not an 80386
  POPF           ;Restore flags
  STC            ;Set the not 80386 flag
  JMP >E90       ;We're done
E80:             ;It is an 80386
  POPF           ;Restore flags
  CLC            ;Set the 80386 flag
E90:             ;We're done
  POP  BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF THE CPU IS AN 8086/8088
;Inputs:
;Outputs: CF = Clear if the CPU is an 8086/8088
;            = Set if not at 8086/8088
;Changes:
;NOTE: Unlike the other CPU tests, this is not a "greater than or equal to".
;      The CPU must be EXACTLY an 8086/8088, or this returns with CF set.
;------------------------------------------------------------------------------
Test8086:
  PUSH AX,BX     ;Save used registers
  PUSHF          ;Save flags
  MOV  BX,0F000h ;Flags mask for testing (these bits always set on 8086/8088)
  PUSHF          ;Put the current flags
  POP  AX        ;  in AX
  AND  AX,0FFFh  ;Try to clear the
  PUSH AX        ;  four high bits
  POPF           ;  of the flags
  PUSHF          ;Put the current flags
  POP  AX        ;  in AX
  AND  AX,BX     ;Are the four high bits
  CMP  AX,BX     ;  of the flags set?
  JE  >E80       ;If so, it's an 8086/8088
  POPF           ;Restore flags
E70:             ;It is not an 8086/8088
  STC            ;Set the not 8086/8088 flag
  JMP >E90       ;We're done
E80:             ;It is an 8086/8088
  POPF           ;Restore flags
  CLC            ;Set the 8086/8088 flag
E90:             ;We're done
  POP  BX,AX     ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO TEST IF WE'RE RUNNING UNDERNEATH WINDOWS NT
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;TEST AND SEE IF WINDOWS NT IS LOADED
;Inputs:  TSRFlags2.IsWindowsNT = False
;Outputs: TSRFlags2.IsWindowsNT = True if running underneath NT
;Changes:
;NOTES: This performs a "GET TRUE DOS VERSION" call to the OS.  DOS boxes in
;         all versions of NT, at least up through XP, all return the same
;         value.  This test will probably not be valid unless using the
;         COMMAND or CMD shell that comes with NT.
;------------------------------------------------------------------------------
TestWindowsNT:
  PUSH AX,BX,DX ;Save used registers
  MOV  AX,3306h ;Function 3306h (Get True DOS version)
  XOR  BX,BX    ;Preset Error code
  INT  21h      ;Do it (returns AX, BX, DX)
  CMP  BX,3205h ;Is it version 5.50 (NT DOS Box)?
  JNE >N90      ;If not, quit
  OR   TSRFlags2,IsWindowsNT ;If so, set the flag
N90:            ;Done
  POP  DX,BX,AX ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO SET UP COMMAND TAIL WITH PROPER PARAMETERS FOR EXEC CALL
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;WRITE A SEGMENT:OFFSET COMBINATION TO A STRING VARIABLE
;Inputs:   AX  = Segment to Write
;          CX  = Offset to Write
;         [BX] = String to Write to
;Outputs: Converts AX:CX to a Hex String and stores at [BX]
;Changes:
;------------------------------------------------------------------------------
WriteSegmentOffset:
  PUSH AX,DI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at Local Data Area
  MOV  DI,BX          ;Point DI at String
  CALL Word2HexString ;Store the Segment
  MOV  AL,':'         ;Store the
  STOSB               ;  Colon
  MOV  AX,CX          ;Store
  CALL Word2HexString ;  the Offset
  POP  ES,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE A WORD AS HEX TO A STRING
;Inputs:   AX  = Word to Write
;         [BX] = String to write to
;Outputs:
;Changes: BX
;------------------------------------------------------------------------------
WriteWordToString:
  PUSH AX,DI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at Local Data Area
  MOV  DI,BX          ;Point DI at String
  CALL Word2HexString ;Store the Word
  MOV  AL,'h'         ;Write the
  STOSB               ;  'h'
  ADD  BX,5           ;Update Pointer
  POP  ES,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE A BYTE AS HEX TO A STRING
;Inputs:  AL   = Byte to Write
;         [BX] = String to write to
;Outputs:
;Changes: BX
;------------------------------------------------------------------------------
WriteByteToString:
  PUSH AX,DI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at Local Data Area
  MOV  DI,BX          ;Point DI at String
  CALL Byte2HexString ;Store the Byte
  MOV  AL,'h'         ;Write the
  STOSB               ;  'h'
  ADD  BX,3           ;Update Pointer
  POP  ES,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A STRING INTO THE COMMAND TAIL
;Inputs:  [BX] = ASCIIZ String to Copy
;Outputs: To CmdTail
;Changes:
;------------------------------------------------------------------------------
CopyToCmdTail:
  PUSH AX,CX,DX,DI,SI,ES ;Save used registers
  MOV  ES,DS             ;Point ES at Local Data Area
  MOV  CX,(CmdTailMax/2) ;Number of Words to Copy
  MOV  AX,CR+(CR SHL 8)  ;Fill with CR's
  MOV  DI,CmdTail        ;Point at Command Tail
  PUSH DI                ;Save Pointer
  REP  STOSW             ;Fill the Command Tail
  POP  DI                ;Restore Pointer
  MOV  DX,BX             ;Calculate number of bytes
  CALL CalcStrSizeDX     ;  to copy
  MOV  CmdTailSize,CL    ;Store the Command Tail Size
  MOV  SI,BX             ;Copy the string
  REP  MOVSB             ;  to the Command Tail
  POP  ES,SI,DI,DX,CX,AX ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO EXEC ANOTHER PROGRAM
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;SETUP PARAMETERS TO BE ABLE TO PERFORM THE EXEC FUNCTION
;Inputs:
;Outputs:
;Changes: Lots of stuff
;------------------------------------------------------------------------------
SetupExec:
  PUSH AX,SI                       ;Save used registers
  MOV  SI,ParamBlock               ;Point at Parameter Block
  MOV  AX,CS                       ;Use our data segment
  MOV  [SI].PBEnvirSegment,0       ;Use Copy of our Environment
  MOV  W [SI].PBCmdTailPtr[0],OFFSET CmdTailSize
  MOV  W [SI].PBCmdTailPtr[2],AX   ;Command Tail Pointer
  MOV  W [SI].PBFCB1Offset[0],FCB1
  MOV  W [SI].PBFCB1Offset[2],AX   ;FCB #1 Pointer
  MOV  W [SI].PBFCB2Offset[0],FCB2
  MOV  W [SI].PBFCB2Offset[2],AX   ;FCB #2 Pointer
  POP  SI,AX                       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PERFORM THE EXEC FUNCTION
;Inputs:  ParamBlock set up with appropriate data
;         CmdTail/CmdTailSize set up with appropriate Info
;         PathToUse contains Child Program to Call
;         DS = Our data Area
;Outputs: CF = Clear if Program Returned ErrorLevel 0
;Changes: Performs EXEC Function
;------------------------------------------------------------------------------
DoExec:
  PUSH ES            ;Save used register
  MOV  ES,DS         ;Point ES at our Data Area
  PUSH AX,BX,CX,DX   ;Save all registers
  PUSH DI,SI,BP      ;Save all registers
  MOV  OldSPExec,SP  ;Save the Stack Pointer (may be destroyed by Call)
  MOV  DX,PathToUse  ;DS:[DX] = Child Program to Run
  MOV  BX,ParamBlock ;ES:[BX] = Parameter Block
  MOV  AX,4B00h      ;Function 4B00h (Load and Execute child program)
  INT  21h           ;Do it (May Destroy BX & DX)
  JC  >X70           ;If Error, quit
X00:                 ;When the Child Program Exits, here we are!
  CLD                ;Go forward with string functions
  CLI                ;Disable interrupts
  MOV  DS,CS         ;Restore
  MOV  ES,DS         ;  all
  MOV  SS,CS         ;  Segments
  MOV  SP,OldSPExec  ;Restore Stack Pointer
  STI                ;Enable interrupts
  MOV  AH,4Dh        ;Function 4Dh (Get ErrorLevel)
  INT  21h           ;Do it (Returns AH = Term Type, AL = ErrorLevel)
  OR   AL,AL         ;ErrorLevel 0?
  JZ  >X80           ;If so, it was OK
X70:                 ;Error
  STC                ;Set Error Flag
  JMP >X90           ;Done
X80:                 ;OK
  CLC                ;Set OK Flag
X90:                 ;Done
  POP  BP,SI,DI      ;Restore all registers
  POP  DX,CX,BX,AX   ;Restore all registers
  POP  ES            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;LOOK FOR THE SUPPORT FILE SOMEHWERE IN THE PATH
;Inputs:  DS = Local Data Area
;         SupportFile
;         ProgFlags.FoundSuptFile = False
;Outputs: ZF = Clear if Support File Found
;              ProgFlags.FoundSuptFile = True
;Outputs: ZF = Set if Support File Not Found
;              ProgFlags.FoundSuptFile = False
;Changes:
;------------------------------------------------------------------------------
TestSupportFile:
  PUSH DI,SI                ;Save used registers
  MOV  SI,OFFSET ProgFlags  ;Point [SI] at ProgFlags
  MOV  DI,SupportFile       ;Look for the
  CALL FindSupportFile      ;  Support File
  JC  >S90                  ;If not found, quit
  OR   B [SI],FoundSuptFile ;If found, mark it as found
S90:                        ;Done
  TEST B [SI],FoundSuptFile ;Set return flag
  POP  SI,DI                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND THE LOCATION OF THE SUPPORT FILE
;Inputs:  [DI] = FIleName to Look for
;Outputs: CF = Clear if OK (File Found)
;              PathToUse = Path & FileName Concatenated Together
;            = Set if Error (File not Found)
;              PathToUse = Undefined
;Changes: Various Path pointer Registers (OurPathPointer, PathVarPointer, etc.)
;------------------------------------------------------------------------------
FindSupportFile:
  PUSH AX,SI,ES          ;Save used registers
F10:                     ;Do Our Path
  MOV  SI,OurPathPointer ;Point [SI] at our Program's Path
  CMP  SI,-1             ;Valid?
  JE  >F20               ;If not, skip it
  MOV  ES,[PSPEnvirSeg]  ;Point ES at the Environment Segment
  CALL CopyPathName      ;Copy it
  CALL FindFile          ;File exists?
  JNC >F80               ;If so, we're done
F20:                     ;Do Current Path
  MOV  ES,CS             ;Point ES at us
  MOV  SI,CurrentPath    ;If not, try the Current Path
  CALL CopyPathName      ;Copy it
  CALL FindFile          ;File exists?
  JNC >F80               ;If so, we're done
F30:                     ;Do Environment PATH
  MOV  SI,PathVarPointer ;SI = Environment PATH Pointer
  CMP  SI,-1             ;Is it valid?
  JE  >F70               ;If not, we're done
  MOV  ES,[PSPEnvirSeg]  ;Point ES at the PATH (Environment) Segment
F35:                     ;Loop to here for each element in PATH
  CALL CopyPathName      ;Copy the PATH element
  JC  >F70               ;If End of Path, we're done
  CALL FindFile          ;File exists?
  JNC >F80               ;If so, we're done
F40:                     ;Loop to here to skip over PATH Element
  MOV  AL,ES:[SI]        ;Get next character
  INC  SI                ;Increment Pointer
  OR   AL,AL             ;End of PATH?
  JZ  >F70               ;If so, we're done
  CMP  AL,';'            ;End of PATH Element?
  JNE  F40               ;If not, Keep looking
  JMP  F35               ;Try the next Element
F70:                     ;Error
  STC                    ;Set Error Flag
  JMP >F90               ;Done
F80:                     ;Found our File
  CLC                    ;Set OK flag
F90:                     ;Done
  POP  ES,SI,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SEE IF THE SUPPORT FILE EXISTS
;Inputs:  PathToUse
;Outputs: CF = Clear if OK (File Found)
;            = Set if Error (File not Found)
;Changes:
;------------------------------------------------------------------------------
FindFile:
  PUSH AX,BX,CX,DX   ;Save used registers
  PUSH DS,ES         ;Save used registers
  MOV  AH,2Fh        ;Function 2Fh (Get DTA Address)
  INT  21h           ;Do it (returns ES:BX)
  MOV  AH,1Ah        ;Function 1Ah (Set DTA Address)
  MOV  DX,FCB1       ;DS:DX = New DTA Address
  INT  21h           ;Do it
  MOV  AX,4E00h      ;Function 4Eh (Find First Matching File)
  MOV  CX,FAttrToUse ;Search for any file, including Hidden
  MOV  DX,PathToUse  ;DS:DX = ASCIIZ FileName to search for
  INT  21h           ;Do it (returns CF)
  PUSHF              ;Save return flag
  MOV  DS,ES         ;Point DS:DX
  MOV  DX,BX         ;  at the old DTA
  MOV  AH,1Ah        ;Function 1Ah (Set DTA Address)
  INT  21h           ;Do it
  POPF               ;Restore return flag
  POP  ES,DS         ;Restore used registers
  POP  DX,CX,BX,AX   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CREATE A COMPLETE PATH NAME GIVEN A PATH AND A FILE NAME
;Inputs:  ES:[SI] = Path name to Copy
;         DS:[DI] = FileName to Append to the Path
;Outputs: PathToUse = Path & File Concatenated Together
;NOTES: The Path String can either end with a 0 (ASCIIZ String) or a SemiColon
;         (the way strings are stored in the PATH Environment variable).
;       This code will properly handle an empty Path String, which will
;         simply set PathToUse to the FileName.
;       The FileName must be an ASCIIZ String.
;------------------------------------------------------------------------------
CopyPathName:
  PUSH AX,BX,CX,DI,SI,DS,ES  ;Save used registers
  SwapDSES                   ;Point ES at our Data, DS at Data Path
  MOV  BX,DI                 ;Save File Name Pointer
  XOR  AX,AX                 ;Fill with Zeroes
  MOV  CX,(MaxPathSize/2)    ;Number of Words to Write
  MOV  DI,PathToUse          ;Where to write them
  PUSH DI                    ;Save Pointer
  REP  STOSW                 ;Reset the Buffer
  POP  DI                    ;Restore Pointer
  XOR  CL,CL                 ;Start String Length Counter at 0
N10:                         ;Loop to here for each character in Path
  LODSB                      ;Get next character
  OR   AL,AL                 ;End of Path?
  JZ  >N20                   ;If so, do Filename
  CMP  AL,';'                ;End of Path (Environment PATH)?
  JE  >N20                   ;If so, do FileName
  INC  CL                    ;Increment String Length Counter
  STOSB                      ;If not, store the character
  JMP  N10                   ;Keep going
N20:                         ;Done with Path
  OR   CL,CL                 ;Any characters at all in the Path?
  JZ  >N30                   ;If so, is Current Path, and don't want backslash!
  CMP  B ES:[DI-1],Backslash ;Path ends in a Backslash?
  JE  >N30                   ;If so, it's OK
  MOV  AL,Backslash          ;If not, store
  STOSB                      ;  a Backslash
N30:                         ;Path is OK
  MOV  DS,ES                 ;Point DS:[SI]
  MOV  SI,BX                 ;  at the File Name
N40:                         ;Loop to here for each character in File name
  LODSB                      ;Get next character
  OR   AL,AL                 ;End of File Name?
  JZ  >N90                   ;If so, we're done
  STOSB                      ;If not, store it
  JMP  N40                   ;Keepgoing
N90:                         ;Done
  POP  ES,DS,SI,DI,CX,BX,AX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE PATH THAT OUR PROGRAM IS IN
;Inputs:
;Outputs: OurPathPointer = Pointer to Our Path String (in Environment Segment)
;                        = -1 if we couldn't find our own Path
;Changes:
;------------------------------------------------------------------------------
GetOurPath:
  PUSH AX,BX,CX,DI,SI   ;Save used registers
  PUSH DS,ES            ;Save used registers
  MOV  BX,OFFSET OurPathPointer ;Point at where we're storing
  MOV  DS,[PSPEnvirSeg] ;Point DS and ES
  MOV  ES,DS            ;  at the Environment segment
  XOR  DI,DI            ;Point at the beginning of Environment list
  MOV  CX,-1            ;Max out the count limiter
  XOR  AX,AX            ;Need to look for a double 0
N10:                    ;Loop to here to skip over Environment vars
  REPNE SCASB           ;Look for the end of this environment variable
  CMP  [DI],AL          ;Is it the end of the variable list (double 0)?
  JNE  N10              ;If not, keep skipping over Environment vars
  CMP  [DI+1],AX        ;If so, is there a string after the Envir?
  JE  >N70              ;If not, we're done
  ADD  DI,3             ;Point DI and SI at the
  MOV  SI,DI            ;  True Name
  MOV  CS:[BX],DI       ;Store the Pointer
N20:                    ;Loop to here to find the end of the PATH
  LODSB                 ;Get the next character of the name
  CMP  AL,':'           ;Is it a Colon?
  JE  >N30              ;If so, possible end-of-path
  CMP  AL,Backslash     ;Is it a backslash?
  JE  >N30              ;If so, possible end-of-path
  OR   AL,AL            ;End of File Name?
  JZ  >N50              ;If so, we're done
  JMP >N40              ;If not, keep looking
N30:                    ;Update end-of-path Pointer
  MOV  DI,SI            ;Save possible end-of-path pointer
N40:                    ;Not end-of-path yet
  JMP  N20              ;Keep looking
N50:                    ;At end of Path
  CMP  DI,CS:[BX]       ;Was there a real path?
  JE  >N70              ;If not, error
  MOV  B [DI],0         ;If OK, put 0 at end of Path
  JMP >N90              ;Done
N70:                    ;No Path
  MOV  W CS:[BX],-1     ;Set Pointer as Invalid
N90:                    ;Done
  POP  ES,DS            ;Restore used registers
  POP  SI,DI,CX,BX,AX   ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;CODE TO AUTOMATICALLY MOVE THE PROGRAM INTO UPPER MEMORY
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;TRY TO ALLOCATE A UMB, AND COPY THE PROGRAM TO THE UMB SEGMENT
;Inputs:  DS = ES = Current Data Segment
;Outputs: ES = New Data Segment (in UMB or Low Memory Hole)
;              If no UMB or Low Memory Hole is available, ES remains unchanged
;         Entire TSR Portion of Program is copied to new Segment
;Changes:
;------------------------------------------------------------------------------
MoveProgramToUMB:
  PUSH AX,BX,CX,DX,DI,SI      ;Save used registers
  MOV  AX,DS                  ;Save original Data Segment
  CMP  AX,0A000h              ;Already in Upper Memory?
  JAE >U90                    ;If so, just quit
  TEST ProgFlags,UseLowMemory ;Does user want us in Low Memory?
  JNZ >U90                    ;If so, just quit
  MOV  BX,(LastTSRByte SHR 4) ;Number of Paragraphs to Allocate
  CALL AllocateUMBDOS         ;Try to get a UMB from DOS (Returns ES)
  JZ  >U90                    ;If it didn't work, quit
  MOV  DX,ES                  ;Save new Segment
  PUSH DS                     ;Save data Segment
  MOV  AX,DS                  ;Point DS
  DEC  AX                     ;  at current
  MOV  DS,AX                  ;  MCB
  MOV  AX,ES                  ;Point ES
  DEC  AX                     ;  at new
  MOV  ES,AX                  ;  MCB
  MOV  SI,OFFSET MCBOwnerName ;Copy
  MOV  DI,SI                  ;  the
  MOV  CX,4                   ;  Owner
  REP  MOVSW                  ;  Name
  POP  DS                     ;Restore Data Segment
  MOV  ES:[MCBOwnerID],DX     ;Make itself the Owner
  MOV  AH,26h                 ;Function 26h (Create new PSP, DX = Segment)
  INT  21h                    ;Do it
  MOV  ES,DX                  ;Point ES at new Segment again
  ADD  DX,BX                  ;Calculate where next Segment will be
  MOV  ES:[PSPNextSegment],DX ;Store it in the PSP
  MOV  CL,3                   ;Calculate
  SHL  BX,CL                  ;  number
  MOV  CX,BX                  ;  of words
  SUB  CX,(PSPStrucSize/2)    ;  to Copy
  MOV  SI,PSPStrucSize        ;SI = Old Data
  MOV  DI,SI                  ;DI = New Data
  REP  MOVSW                  ;Copy Program to new Segment
  OR   ProgFlags,MemoryMoved  ;Mark memory as having been moved
U90:                          ;Done
  POP  SI,DI,DX,CX,BX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TRY TO ALLOCATE A UMB THROUGH DOS
;Inputs:  BX = Number of Paragraphs to Allocate
;Outputs: ZF = Clear if OK
;              ES = Segment of Allocated UMB
;            = Set if Error
;              ES = Unchanged
;Changes:
;NOTES: Allocating USB's through DOS normally requires a
;         DOS=HIGH,UMB line in CONFIG.SYS
;------------------------------------------------------------------------------
AllocateUMBDOS:
  PUSH AX,BX,CX,DX   ;Save used registers
  PUSH DI,SI         ;Save used registers
  MOV  CX,ES         ;Save original ES
  MOV  DI,BX         ;Save Number of Paragraphs to Allocate
  MOV  AX,5800h      ;Function 5800h (Get Memory Allocation Strategy)
  INT  21h           ;Do it (returns AX)
  JC  >D90           ;If error, quit
  MOV  SI,AX         ;If OK, save it
  MOV  AX,5802h      ;Function 5802h (Get UMB Link State - DOS 5+ Only)
  INT  21h           ;Do it (Returns AL)
  JC  >D90           ;If error, quit
  MOV  DL,AL         ;If OK, Save it
  MOV  BX,UMBLinkYes ;Enable USB's in DOS Memory Chain
  MOV  AX,5803h      ;Function 5803h (Set UMB Link State)
  INT  21h           ;Do it
  JC  >D90           ;If error, quit
  MOV  BX,StrategyHighLowBest ;Strategy = Try High, then Low, Best Fit
  MOV  AX,5801h      ;Do
  INT  21h           ;  it
  JC  >D80           ;If error, quit
D20:                 ;Strategy is set
  MOV  BX,DI         ;BX = # of Paragraphs to Allocate
  MOV  AH,48h        ;Function 48h (Allocate Memory)
  INT  21h           ;Do it (returns AX = Segment)
  JC  >D80           ;If error, quit
  MOV  ES,AX         ;If OK, put Segment in ES
  CMP  AX,0A000h     ;Is it really a UMB?
  JAE >D80           ;If so, continue
  MOV  DI,CS         ;If not, is it a memory "hole" lower in memory
  CMP  AX,DI         ;  than we are (from a previously removed TSR)?
  JB  >D80           ;If so, continue
  MOV  AH,49h        ;Function 49h (Free Memory, Segment in ES)
  INT  21h           ;Do it
  MOV  ES,CX         ;Restore original ES
D80:                 ;Restore original UMB Link State & Strategy
  XOR  BH,BH         ;Restore
  MOV  BL,DL         ;  original
  MOV  AX,5803h      ;  USB
  INT  21h           ;  Link State
  MOV  BX,SI         ;Restore original
  MOV  AX,5801h      ;  Memory Allocation
  INT  21h           ;  Strategy
D90:                 ;Done
  MOV  AX,ES         ;Set
  CMP  CX,AX         ;  Return Flag
  POP  SI,DI         ;Restore used registers
  POP  DX,CX,BX,AX   ;Restore used registers
  RET


;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北
;SPACE AND CODE TO PROCESS OUR PROGRAM STACK
;  This code and must be physically at the END of the Program,
;    but must be one of the FIRST procedures called by the Program.
;  In addition, the Stack Pointer (SP) must be set properly
;    (to LastProgByte - 2) BEFORE this code is called.
;北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北北

;------------------------------------------------------------------------------
;RESIZE OUR MEMORY ALLOCATION TO THE MINIMUM SIZE NEEDED
;Inputs:  SP = LastProgByte - 2
;         ES = Local Segment
;Outputs: Rellocates Memory to the minimum size required to run the program
;Changes:
;------------------------------------------------------------------------------
ResizeMemory:
  PUSH AX,BX  ;Save used registers
  MOV  AH,4Ah ;Function 4Ah (Resize Memory Block pointed at by ES)
  MOV  BX,(LastProgByte SHR 4) ;BX = Number of Paragraphs to Resize to
  INT  21h    ;Do it
  POP  BX,AX  ;Restore used registers
  RET

  EVEN 16
  DB (ProgStackSize/8) DUP ('PrgStack')

LastProgByte: