  JMP Main ;Skip over TSR Code to Main Program


;==============================================================================
;NOTE: This program is written for and compiled with Eric Isaacson's A86
;      assembly language compiler, using Options +P2 -F (80286 CPU
;      with no FPU).
;==============================================================================





;Change INT 70h code in all programs that use it (SCANCODE, JOYKEYS,
;  MOUSKEYS, SLOWDOWN, etc.)

;Add external function to BIOS (INT 15h, AX = C230h-C23xh, BX/ES:[BX] as
;  params, return AX/BX/CX/DX, to send fake mouse data.  Need button press,
;  button release, button press & release, button double-click, move mouse
;  (# pixels in graphics mode, # characters in text mode), click wheel,
;  install check.

;Horizontal Wheel: Right = Increment by 2, Left = Decrement by 2.  Verify!

;Get/Set Mouse BIOS Data in XBDA!  (Copy from PS2MTest)

;Need separate scaling factors per mouse.  Wireless Mouse in much more
;  sensitive than wired mouse.  Allow slow-down of many times (up to 16 or so).

;Not reading Environment right away??  SET Resolution:8, run USBMOUSE, says
;  resolution is still 4.  Run it again, it says 8.  Happens because we
;  display status before we call ChangeRates if already in memory (things
;  have changed, but we're not displaying the updated status).

;IntelliMouse2 protocol = IntelliMouse Explorer Mouse

;HW Emulation doesn't work at all, even with CTMOUS19.  Fix it or remove it.
;Handle DisableAll
;Have "HorizontalWheelTrigger" button.  This will handle GE Mouse, and
;  also allow horizontal scrolling with a single wheel.
;Is our beeping/keyboard problem related to IRQ priorities?  If so, we
;  may not be able to do hardware emulation at all unless the USB IRQ
;  has a lower priority than IRQ 12??
;If HW emulation, show status because there MAY be a PS2 Mouse
;Resolution doesn't have any effect, at least in HW emulation
;Beeping not a problem if no USB Mice are attached.
;  Need to monitor MouseChangeCode closely to see what's happening.
;When doing HW Emulation, handle Resolution & Scaling Properly
;Take Over INT 9 when simulating Mouse Movements?  Disable Speaker at same time?
;When emulating HW, Keyboard slows down, beeps, repeat keys don't work.
;Need to Disable Keyboard when emulating w/ Method 2 or 3?
;  If not, since both Mouse and Keyboard use Port 60h, we could have problems.
;Sometimes SEVERELY slows down keyboard, at least in hardware emulation mode
;   Method 3 on laptop
;Doesn't work on Desktop with Method 2 or 3.  Find out why.  May need to
;  do a Trace.
;If emulating HW, disabling PS2 Mouse doesn't do anything.  Need to handle
;  appropriately in Status.
;Verify unused code with +x Option
;Add API similar to USBKEYB to move mouse/press buttons?
;Add support for Keyboard keys also, not just system keys.  Saw a reference
;  to a Mouse that does that.
;Make sure Wheels Option gets handled properly when changing from
;  BIOS to HW emulation (or vice versa), especially of BIOS Option
;  occurs after Wheel Option on command-line.
;May need special handling if USBUHCI uses IRQ 8 or 12.
;Test everything (including "adding" PS/2+USB Mice) since we've changed
;  how everything works
;Order of Axes on GE Mouse is X, Y, Wheel, Z (not X, Y, Z, Wheel).  Find out
;  exactly how it works and make it compatible.  Order is the same on
;  the HP wireless mouse, even though it doesn't have a second wheel and
;  the wheel doesn't move sideways.
;Convert Keyboard Codes (DisableMouse, DisableKeyboard, etc.) to EQUates
;If using Hardware emulation, we don't want to reset the PS2 Mouse when
;  installing.  We also may need to warn user if they use us to Reset
;  the PS2 Mouse the INT 33h driver will stop working.
;Only allow BIOS Option on initial install?  If not, we will need to test
;  if we actually changed it.  If we switched from BIOS to Hardware, we'll
;  need to transfer the INT 33h caller address to the real BIOS, and purge
;  our PS2 Mouse Entry.  If switching from Hardware to BIOS, we'll need
;  to reset the mouse like we used to do at the beginning of our
;  installation and fill in the PS2 Mouse entry.  There may be other
;  things we need to do as well.  It would be nice to figure out a way
;  to get the INT 33h call-back address from the existing BIOS!
;Verify CMOS (INT 70h) exists, and PS2 BIOS??
;If emulating HW, need to call Old Interrupt 15h but process and return our
;  own data.  Need to monitor how PS2 HW responds to data to try and match
;  it (at least as far as number of wheels is concerned).  If INT 33h Driver
;  uses BIS to set Handler Address, we can keep track of it.
;Warn user that they MAY need a real PS2 Mouse in some circumstances w/
;  Hardware Emulation, depending on how much it uses the BIOS and how much
;  it uses the HW.  All Drivers seem to mix and match BIOS & HW, so it's
;  hard to tell what might work.  Need to test at least one MS and one
;  Logitech Driver, along w/ CTMOUSE v2, to see what happens.
;GE mouse w/ two wheels acts like it only has one, but has 7 buttons.
;  "Pushes" Button 7 at same time as second wheel to for indication.
;  Need to talk in docs about how mouse lie about how many buttons and wheels
;   they actually have, and then give you special software to compensate
;   for it (the software only works with Windows, of course).
;  Use ErLvlRawData & next one (both related to Raw Data).
;Verify Mouse works with Doom & DOSShell after converting to HW Emulation
;For HW Emulation, it will be complicated.  Under Windows (or other
;  Protected Mode apps) we won't be able to combine button presses
;  together.
;  In Real/V86 mode, will need to intercept IRQ 12's from real PS/2
;    Mouse, modify button info, and issue a fake IRQ to the old Vector.
;    If the mouse program takes control of the vector after we do,
;    we're screwed.
;  May not be able to allow the real mouse and USB mice to work at the
;    same time.  If we do, we'll need to buffer one or the other until
;    all 3/4 bytes are sent.
;According to Windows Vista Docs, Horizontal Wheel is always designated
;  AC Pan (from Consumer Devices Usage Page)

;Mouse attached through USB Keyboard/Mouse "Splitter" sometimes keeps moving
;  horizontally after we have stopped actually moving the mouse.
;  Thought it might be related to remainders, but that didn't fix it.
;Jerry S says his mouse sometimes does it on his laptop.

;Trying to "Wrap Around" the Mouse Driver as it is loading is very
;  problematic, since the Mouse Driver is a TSR (or loaded via
;  CONFIG.SYS).

;If using any method other than BIOS, we won't be able to "add" PS2 buttons,
;  and moving more than one mouse at a time can cause byte interleaves
;  (potentially serious problems).  This doesn't happen if using the BIOS.



;DONE:





;In Docs:

;No way to "Disable" entire program, only individual devices.
;  Must Uninstall and then Reinstall/Reinitialize.
;Should only need Wheel Option if emulating HW and don't have a real PS2 Mouse.
;If emulating HW, can only OR in buttons 1-3 for PS2 Mouse.  Don't have access
;   to buttons 4 & 5.
;I've never actually had a USB Mouse with more than
;  one wheel, so second wheel may not work correctly.
;For Micro Innovations Mouse (with "second" wheel), moving wheel left
;  shows status of M & 4, while moving right shows status of M & 5.
;  It is not really a second (horizontal) wheel.
;If not installed yet, ReportDescr, Descr, & BadDevices Options are
;  ignored and program is just installed into memory.
;Some PS2 Mice don't seem to respond to Resolution, Scaling, or
;  Sample Rate changes.  It's possibly related to having Legacy Mouse Support
;  installed on the computer, but don't know that for sure.
;Therefore, we handle Resolution and Scaling Factor Requests in USBMOUSE
;  even for the PS2 Mouse, so that you can control the Mouse like you're
;  supposed to be able to do.
;Unfortunately, the one thing we can't easily control externally on the
;  PS2 Mouse is the Sampling Rate.  While it is possible to set up some        ling
;  sort of timing mechanism to limit the output of the PS2 calls to certain
;  time frames, it would not be very easy to do.
;Also, unlike the Scaling Factor and Resolution Options, it is very unlikely
;  that a User will actually need or want to change the Sampling Rate
;  from the Default value (except an an experiment just to see what
;  it looks like).  I doubt that any user would want to set the Sampling
;  Rate to anything other than 100 or 200 samples/second.
;Could make this require a 286, since PS/2 mice require at least a 286,
;  but we won't to be able to more easily make another 8088-compatible
;  mouse program.
;If using hardware emulation, can't control real PS2 Mouse.  Must make
;  sure it's set up in same IntelliMouse Mode as this program (number of
;  data bytes), won't be able to OR buttons together (buttons may start
;  being unpressed before you really want them to be).  May be best not
;  to have a PS2 mouse at all.
;When not using the BIOS, all mice must work the same way (number of
;  data bytes/buttons/wheels) or things will get all screwed up.  That's
;  what Wheels Option is for.  It may not be needed, depending on exactly
;  how the INT 33h Driver operates.
;Depending on how INT 33h Driver works, may need a Real PS2 mouse for
;  INT 33h Driver to work, or may need to load USBMOUSE before loading
;  INT 33h Driver.  CTMOUSE (v 1.9 & V2.0) may not be a problem??
;HW Emulation with Method 1 may allow use of USB Mice in older versions
;  of Windows.  Haven't test it, though.
;Disabling PS2 Mouse doesn't do anything if emulating Hardware
;If forcing into Low Memory, it will neither use Upper Meory nor will it
;  attempt to find a "Hole" in Low Memory.  It will use whatever memory
;  DOS provides it.
;Added INT 15h Function C20Ah, "Get Handler Address"


COMMENT 

  This program uses a USB Mouse to simulate a standard PS2 Port Mouse.  It does
    this by emulating the standard PS2 Port BIOS Functions
    (Int 15h, Functions C2xxh).  It does not virtualize the hardware IRQ 12
    and Keyboard Controller I/O Port functionality of a "real" PS2 mouse.
  This Driver should interface with any DOS (Int 33h) Mouse Driver that
    interfaces with the PS2 Mouse via the BIOS Interface.
    Unfortunately, many Int 33h Mouse Drivers (including those from Microsoft
    and Logitech) do not exclusively use the PS2 BIOS interface to access the
    PS2 mouse.  They instead access the mouse directly through the hardware
    (IRQ 12 and the Keyboard Controller I/O ports).
  There is a free Int 33h Mouse Driver, called CTMOUSE (Cute Mouse), that
    does use the PS2 BIOS to access the Mouse.  CTMOUSE and USBMOUSE work
    very well together.  CTMOUSE provides the same basic functionality as
    the Microsoft or Logitech drivers, but uses MUCH less memory (only about
    3.5k, vs 10~30k for Microsoft or Logitech).  I suggest you blow off
    Microsoft and Logitech and use CTMOUSE instead.
  Currently, there are two version paths of CTMOUSE: Version 1.9 and
    and version 2.0.  Version 2.0 allows the use of one Wheel, but (at least
    currently) uses direct hardware access, and therefore will not work with
    USBMOUSE.  Version 1.9 uses the BIOS functions provided by USBMOUSE,
    and works very well with this program.  Unfortunately, version 1.9 of
    CTMOUSE does not provide support for Wheel(s).  I have contacted Nagy
    Daniel, the author of CTMOUSE.  He has told me that would add a BIOS
    option to CTMOUSE version 2.0, but that has not happened yet (as of
    August 2004).  Nagy may decide to add native USB support to CTMOUSE
    version 2.0, instead of attempting to interface with the BIOS.
  I think it is better to use the BIOS interface, since it's hard to tell
    how many different types of mice there may be in the future (could be
    BlueTooth, FireWire, Bluetooth through USB, etc.).  Ten years from now
    there could even be some new kind of Bus architecture that doesn't
    even exist today that will be the "standard" way of attaching external
    devices to the computer.  Using the BIOS is a much more flexible way
    to do things, and will "future-proof" CTMOUSE to some degree.

END COMMENT ;

COMMENT 

  If the Mouse has Wheel(s) (most USB Mice have one Wheel), this driver
    emulates the Microsoft IntelliMouse protocols.  An IntelliMouse will
    return a Device ID of 0 after a Reset, just like a regular Mouse does.
    In this "standard" mode, the IntelliMouse always sends a packet of
    3 data bytes to the computer (just like a regular mouse does).
    The data does not include the Wheel, but does include pressing the
    Wheel as a third button (at least on some mice).

  By sending a special series of codes to the IntelliMouse, it will change
    and start using the IntelliMouse protocol.  In the IntelliMouse protocol,
    the Device ID is 3 (instead of 0), and the size of the data packet is
    4 bytes (instead of 3 bytes).  Of course, if the Mouse driver never asks
    for four bytes to be sent, then only three are actually sent even if the
    IntelliMouse protocol has been enabled.  The basic IntelliMouse protocol
    simply allows the addition of a Wheel, but doesn't allow for anything
    more complicated than that.

  There is also an IntelliMouse2 protocol (I don't know what the actual name
    for it is, so I will call it IntelliMouse2).  It is an additional extension
    to the IntelliMouse protocol, and allows a Mouse to have up to five buttons
    (instead of just three), and have two wheels (instead of just one).
    The Device ID returned by the Mouse is 4 (instead of 3 or 0) if the Mouse
    supports the IntelliMouse2 protocol, and the number of data bytes returned
    is 4, just as it is with the basic IntelliMouse protocol.  There is
    actually enough "space" in the IntelliMouse2 protocol to logically
    have up to seven buttons and seven wheels (as long as only one wheel is
    moved at a time), though "officialy" the protocol only supports 5 buttons
    and two wheels.

  This Driver supports both the IntelliMouse and the IntelliMouse2 protocols,
    though there is no DOS Mouse Driver around that can use either one of
    them, at least as provided by this program.  It's possible that CTMOUSE
    may add the appropriate support in the future, though it probably won't be
    the near future.

END COMMENT ;

COMMENT 

  This driver will handle mice with two to eight buttons, and with
    at most two Wheels.  Unfortunately, because of limitations in the DOS
    INT 33h protocol (as developed by Microsoft), only the first three buttons
    can actually be used for anything.  So, even if the Mouse has more than
    three buttons, the extra buttons will not be usable by any DOS programs
    until an appropriate Driver is written.

  CTMOUSE has developed an extension to the Microsoft standard that allows
    it to use a single Wheel.  The CTMOUSE extension still does not allow
    for more than three buttons, nor does it allow for a second Wheel.
    And, to be honest, mice with all those extra buttons and Wheels are
    not very common.  Hopefully, CTMOUSE will change this.

END COMMENT ;

COMMENT 

  In this version of the Driver, we do a VERY simple search of the USB
    Report Descriptor that comes back from the Mouse.  Basically, we
    assume the data is sent in the same order and same format as the
    data from a PS2 Mouse (1 byte of buttons, 1 byte of X Movement, 1 byte
    of Y Movement, and 1~2 bytes of Z (Wheel) Movement).  For a Mouse that is
    both USB and PS2 compatible (which many of them are nowadays), or mice
    that are compatible with the USB Mouse Boot Protocol (which they all
    should be), this will almost surely be the case.  It is definitely
    the case on the several different Wheel Mice that I used for testing.

  If there's a Mouse out there somewhere that sends the data with a
    different size or in a different order, this driver won't
    work correctly.  If that happens, we will either need to change this
    driver (making it "smarter" -- more general and more complicated),
    or come up with a special driver for that one particular mouse.

END COMMENT ;

COMMENT 

  As we are emulating the PS2 Mouse BIOS Function, there is an Option in the
    BIOS (actually, it's in the Mouse Hardware) to set the Mouse Sensitivity
    to 8, 4, 2, or 1 counts/mm (Mickeys).  In this Driver, we simply assume
    that the Raw data from the mouse is at 8 counts/mm, divide the Raw Data
    appropriately to compensate for the 8/4/2/1 counts/mm requested output.
    If the Mouse Hardware is not really 8 counts/mm (or at least pretty
    close to 8 counts/mm), the mouse will either be too sensitive or not
    sensitive enough without some additional adjustment.  On the several
    Wheel Mice I used to write and test this program, the resolution seems
    to be pretty close to 8 counts/mm.  They respond very similarly
    whether they are hooked up to the PS2 port or to the USB port, and also
    very similarly to a PS2-only (non-USB-compatible) Mouse.

  It is possible for the actual hardware resolution to be declared
    in the USB Report Descriptor.  It's also possible that a Mouse with
    a non-standard Hardware Resolution would somehow let you select or
    change the resolution so that it is standard.  On my mice,
    none of that is the case.

  This driver does not even look in the USB Report Descriptor for a declared
    resolution, or for a way to change the resolution.  So, even if it's there,
    it will be ignored.  In the future, this program may need to be modified
    to compensate for mice that do not have the "standard" 8 counts/mm
    resolution.  We may either need to do a more complete scan of the USB
    Report Descriptor, or we may need to add some additional command-line
    Option(s) to compensate for the hardware sensitivity, or both.
    The other possibility would be to build a special driver for the one
    particular "special" mouse.

END COMMENT ;

COMMENT 

  This program truly implements the plug-and-play opportunities allowed
    by the USB architecture.  Because of that, this program uses a LOT
    more memory than it would if it worked like most TSR's (including
    most of my other TSR's).  In order to preserve memory, a TSR normally
    will not install itself into memory unless the hardware it's trying to
    support, and any additional required support software, are already
    installed.  This program would be MUCH smaller if it took the
    "normal" TSR approach.  This program is VERY small compared to most
    TSR's (especially those written in a high-level language), but is still
    much bigger than it could be if the Plug-And-Play wasn't there.

  This program will continue to use the Plug-And-Play opportunites
    available to it, but somebody may someday want to implement a version
    that does not do that in order to preserve memory.  Such a TSR would
    require that a Mouse Actually be hooked up and Enabled on the USB Bus,
    and would not be able to automatically recover if the Mouse was
    disconnected while running.  Again, not as "nice" as this program,
    but would use a LOT less memory.

END COMMENT ;

COMMENT 

  It is possible to have more than one Mouse hooked up to the Computer at
    the same time.  This program supports that, and "ORs" all of the mice
    together so that they like like a single mouse at the BIOS Interface
    level (Int 15h, Functions C2xxh).

  If this program finds a PS2 Mouse already installed as it is being loaded
    into memory, it will OR itself to the PS2 Mouse that's already there.
    That way, you can use either the PS2 Mouse or any of the eight USB
    mice at any time.  You can also Disable (and, of course, Enable) any
    particular mouse from the command line if you want if there is one
    particular mouse that is annoying you for some reason.

  The way the mice are OR'd together is this.  Each time something on
    one of the mice changes, an IRQ is issued.  For the PS2 Mouse, it is
    always IRQ 12.  For the USB Mice, it is whatever IRQ(s) are associated
    with the USB Host(s) that the Mice are attached to.  This driver
    does NOT "stack up" several mouse-related IRQ issuances into a single,
    delayed "virtual IRQ", though.  Instead, this Driver lets each individual
    IRQ go through at the time it was originally issued.  What this Driver
    does, though, is to OR all of the buttons presses from all of the Mice
    together so that they act as a single set of buttons.  In essence, the
    mouse movements are handled and processed by each mouse individually,
    but the buttons are handled as a conglomerate.

END COMMENT ;

COMMENT 

  In Setting the Sampling Rate on the USB Mice, most of them have a Maximum
    Sampling Rate of 100Hz.  The BIOS (Int 15h) Driver allows for Sample
    Rates as high as 200Hz.  The Logitech Mouse I used to test this program
    does not work correctly with a Sampling Rate of 200Hz, so I suspect
    most, if not all, other Mice would have the same problem as well.

  In order to appear to be compatible with the PS2 interface, what we do
    is "pretend" to be Sampling at 200Hz when we are really only Sampling
    at 100Hz.  Basically, we lie about how fast we are really Sampling.

  Sampling at 100Hz is sufficiently fast anyway, and there's really no
    good, practical reason to sample any faster than that.  In fact, anything
    above about 60Hz doesn't provide any practical benefit that I can see.
    If somebody actually measures it, they will see 100Hz instead of 200Hz,
    but I bet they'll never be able to tell the difference in a practical way.

END COMMENT ;

COMMENT 

  In the Disable/Enable/eXchange Options, we do NOT verify whether a
    particular mouse (either USB or PS2) exists when we set the flags.
    We only check whether or not it is a valid Index.  This will let
    the user Disable/Enable/eXchange a Mouse that has not yet even been
    enumerated.  This, in turn, can let the user configure things
    from the command-line when this program is first installed in memory,
    and does not need to wait for the USB plug-and-play software to
    find and enumerate things.

  The reverse is also true.  The Disable/Enable/Exchange flags are preserved
    across all USB Mouse interactions (including Resets and Disconnects).
    This could potentially confuse the user, but hopefully they will
    be able to become un-confused if they look at the Program Status.

  Another confusing issue could be that the Disable/Enable/Exchange flags
    are stored by Mouse Index (a number), rather than by something more
    significant to the user (e.g., the second Logitech mouse).  Unfortunately,
    that's one drawback to plug-and-play with "generic" devices.

END COMMENT ;

COMMENT 

  One thing I have toyed around with is the idea of extending the
    INT 15h/C2xxh functions to allow for multiple mice.  The functions
    are currently designed assuming a single mouse.  It would be pretty
    easy to add a few functions that would allow for basic detecting,
    disabling/enabling, exchanging of buttons, etc., for multiple mice
    at the BIOS level.  Nothing like that is implemented now, but would
    be possible to do if there was enough interest.

END COMMENT ;


;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
;EQUATE/STRUCTURE/MACRO DEFINITIONS
;ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ

;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;EQUATES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;==============================================================================
;TSR Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Size of our TSR Stack
  ;----------------------------------------------------------------------------
  StackSize EQU 256

  ;----------------------------------------------------------------------------
  ;Special Interrupt 2Fh (TSR Multiplex) Function Numbers we monitor/use
  ;----------------------------------------------------------------------------
  MuxInstallChk  EQU 0 ;Handle # Install Check
  MuxGetProgName EQU 1 ;Get Program Name
  MuxGetProgVer  EQU 2 ;Get Program Version
  MuxGetAuthor   EQU 3 ;Get Program Author


;==============================================================================
;Miscellaneous
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Special Codes
  ;----------------------------------------------------------------------------
  No  EQU  0 ;Miscellaneous Yes/No tests
  Yes EQU -1 ;      "        "  "    "


;==============================================================================
;CPU Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Bit masks in the CPU Flags Register
  ;----------------------------------------------------------------------------
  CarryFlag  EQU 0001h ;Carry Flag
  ParityFlag EQU 0004h ;Parity Flag
  AuxFlag    EQU 0010h ;Auxiliary Flag
  ZeroFlag   EQU 0040h ;Zero Flag
  SignFlag   EQU 0080h ;Sign Flag
  TrapFlag   EQU 0100h ;Trap Flag
  IntFlag    EQU 0200h ;Interrupt Flag
  DirFlag    EQU 0400h ;Direction Flag
  OFFlag     EQU 0800h ;Overflow Flag

  ;----------------------------------------------------------------------------
  ;Special CPU OpCodes we need to concern ourselves with
  ;----------------------------------------------------------------------------
  OpCodeINTxx   EQU 0CDh ;CPU OpCode for INT XX
;  OpCodeLOOPNZ  EQU 0E0h ;CPU OpCode for LOOPNZ
;  OpCodeLOOPZ   EQU 0E1h ;CPU OpCode for LOOPZ
;  OpCodeLOOP    EQU 0E2h ;CPU OpCode for LOOP
  OpCodeINALxx  EQU 0E4h ;CPU OpCode for IN AL,xx
  OpCodeINAXxx  EQU 0E5h ;CPU OpCode for IN AX,xx
  OpCodeOUTxxAL EQU 0E6h ;CPU OpCode for OUT xx,AL
  OpCodeOUTxxAX EQU 0E7h ;CPU OpCode for OUT xx,AX
  OpCodeINALDX  EQU 0ECh ;CPU OpCode for IN AL,DX
  OpCodeINAXDX  EQU 0EDh ;CPU OpCode for IN AX,DX
  OpCodeOUTDXAL EQU 0EEh ;CPU OpCode for OUT DX,AL
  OpCodeOUTDXAX EQU 0EFh ;CPU OpCode for OUT DX,AL

  OpCodeInt1Min EQU OpCodeINTxx ;Minimum OpCode we care about in INT 1
;  OpCodeInt1Min EQU OpCodeINALxx ;Minimum OpCode we care about in INT 1


;==============================================================================
;CONsole/Screen Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Special ASCII Characters
  ;----------------------------------------------------------------------------
  Tab       EQU  9 ;Tab
  LF        EQU 10 ;Line Feed
  FormFeed  EQU 12 ;FormFeed
  CR        EQU 13 ;Carriage Return
  EOF       EQU 26 ;End-of-File
  Quote2    EQU 34 ;Double quote
  Quote1    EQU 39 ;Single quote
  BackSlash EQU 92 ;BackSlash (\)

  ;----------------------------------------------------------------------------
  ;Special Keys that can be pressed during Pause routine
  ;----------------------------------------------------------------------------
  CtrlC  EQU  3 ;Control-C Keystroke
  Escape EQU 27 ;Escape Keystroke


;==============================================================================
;DOS Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;;Minimum DOS Version required to use this program
  ;----------------------------------------------------------------------------
  MinDOSVer EQU 0300h

  ;----------------------------------------------------------------------------
  ;DOS Memory Allocation Strategies
  ;----------------------------------------------------------------------------
  StrategyLowFirst     EQU 00h ;First Fit, Low Memory Only
  StrategyLowBest      EQU 01h ;Best Fit, Low Memory Only
  StrategyLowLast      EQU 02h ;Last Fit, Low Memory Only
  ;For DOS 3&4, anything >2 is Last Fit Low Memory
  ;Below are for DOS 5+ Only
  StrategyHighFirst    EQU 40h ;First Fit, High Memory Only
  StrategyHighBest     EQU 41h ;Best Fit, High Memory Only
  StrategyHighLast     EQU 42h ;Last Fit, High Memory Only
  StrategyHighLowFirst EQU 80h ;First Fit, Try High Memory then Low Memory
  StrategyHighLowBest  EQU 81h ;Best Fit, Try High Memory then Low Memory
  StrategyHighLowLast  EQU 82h ;Last Fit, Try High Memory then Low Memory

  ;----------------------------------------------------------------------------
  ;UMB Link States
  ;----------------------------------------------------------------------------
  UMBLinkNo  EQU 00h ;UMB's are not part of DOS Memory Chain
  UMBLinkYes EQU 01h ;UMB's are in DOS Memory Chain (DOS=HIGH,UMB in CONFIG.SYS)

  ;----------------------------------------------------------------------------
  ;Sizes of various DOS data structures
  ;----------------------------------------------------------------------------
  FCBSize     EQU  44 ;Size of DOS File Control Block
  CmdTailMax  EQU 128 ;Maximum size of a DOS Command Tail
  MaxPathSize EQU  64 ;Maximum Size of a DOS Path/FileName String

  ;----------------------------------------------------------------------------
  ;Special "hard-coded" DOS Device Handles.
  ;These Handles are permanently hard-coded in DOS, and are always open.
  ;There is no need to issue Find, Open, or Close calls to these
  ;  devices - you can just use them at will.
  ;----------------------------------------------------------------------------
  StdInHandle  EQU 0 ;Standard Input Device Handle
  StdOutHandle EQU 1 ;Standard Output Device Handle
  StdErrHandle EQU 2 ;Standard Error Device Handle
  StdAuxHandle EQU 3 ;Standard Auxiliary Device Handle (COM1 by default)
  StdPrnHandle EQU 4 ;Standard Printer Device Handle (LPT1 by default)

  ;----------------------------------------------------------------------------
  ;Codes returned by INT 21h, Function 4400h (Get Device Information)
  ;----------------------------------------------------------------------------
  ;If bit 7 is Set (is a Device):
  DvcInfoIsStdIn    EQU 0001h ;STDIN
  DvcInfoIsStdOut   EQU 0002h ;STDOUT
  DvcInfoIsNUL      EQU 0004h ;NUL
  DvcInfoIsClock    EQU 0008h ;CLOCK$
  DvcInfoInt29h     EQU 0010h ;Uses Int 29h (Special Device)
  DvcInfoBinary     EQU 0020h ;Binary (Raw) Mode
  DvcInfoEOF        EQU 0040h ;EOF on Input
  DvcInfoIsDevice   EQU 0080h ;Is a Device (not a File)
  DvcInfoKEYB       EQU 0100h ;Unknown (set by DOS 6.2x KEYB program)
  DvcInfoOpenClose  EQU 0800h ;Supports Open/Close calls
  DvcInfoOutputBusy EQU 2000h ;Supports Output until Busy
  DvcInfoIOCTL      EQU 4000h ;Can process IOCTL requests (INT 21h, Func 4402h)

  ;If bit 7 is Clear (is a File):
  DvcInfoDriveMask    EQU 003Fh ;Drive number (0 = A:)
  DvcInfoNotWritten   EQU 0040h ;File has not been written (Dirty Buffer?)
  ;Bit 7 = Clear!
  DvcInfoInt24h       EQU 0100h ;Generate Int 24h on Errors (DOS 4 only)
  DvcInfoNotRemovable EQU 0800h ;Media Not Removable
  DvcInfoNoDateTime   EQU 4000h ;Don't set File Date/Time on Closing (DOS 3.0+)
  DvcInfoRemote       EQU 8000h ;File is Remote (DOS 3.0+)


;==============================================================================
;PIC-Related (for handling IRQ's)
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Programmable Interrupt Controller (PIC) I/O Addresses
  ;----------------------------------------------------------------------------
  PIC1Addr EQU  20h ;Address of PIC #1 (IRQ's 0-7)
  PIC2Addr EQU 0A0h ;Address of PIC #2 (IRQ's 8-15)

  ;----------------------------------------------------------------------------
  ;PIC Output Control Word 2
  ;----------------------------------------------------------------------------
  ;Bits 0-2 = IRQ Selection
  OCW2IRQNumMask     EQU 00000111xb ;IRQ Number that specific EOI applies to
  ;Bits 3-4 = OCW Selection
  OCW2SelectOCW2     EQU 00000000xb ;Select OCW2
  ;Bits 5-7 = Operation to Perform
  OCW2RotateClear    EQU 00000000xb ;Rotate in Auto-EOI mode (Clear)
  OCW2NonSpecificEOI EQU 00100000xb ;Non-specific EOI (20h)
  OCW2NoOp           EQU 01000000xb ;No Operation
  OCW2SpecificEOI    EQU 01100000xb ;Specific EOI
  OCW2RotateSet      EQU 10000000xb ;Rotate in Auto-EOI mode (Set)
  OCW2RotateNonSpec  EQU 10100000xb ;Rotate on Non-specific EOI
  OCW2SetPriority    EQU 11000000xb ;Set Priority
  OCW2RotateSpecific EQU 11100000xb ;Rotate on Specific EOI

  ;----------------------------------------------------------------------------
  ;PIC Output Control Word 3
  ;----------------------------------------------------------------------------
  ;Bits 0-1 = Interrupt Register Operations
  OCW3ReadNoOp0   EQU 00000000xb ;Read Register NoOp
  OCW3ReadNoOp1   EQU 00000001xb ;Read Register NoOp
  OCW3ReadIRR     EQU 00000010xb ;Read Interrupt Request Register
  OCW3ReadISR     EQU 00000011xb ;Read Interrupt In-service Register
  ;Bit 2 = Poll Command
  OCW3PollCommand EQU 00000100xb ;Poll Command
  ;Bits 3-4 = OCW Selection
  OCW3SelectOCW3  EQU 00001000xb ;Select OCW3
  ;Bits 5-6 = Special Mask Operations
  OCW3SpMaskNoOp0 EQU 00000000xb ;Special Mask NoOp
  OCW3SpMaskNoOp1 EQU 00100000xb ;Special Mask NoOp
  OCW3SpMaskReset EQU 01000000xb ;Reset Special Mask
  OCW3SpMaskSet   EQU 01100000xb ;Set Special Mask
  ;Bit 7 = Reserved (0)


;==============================================================================
;CMOS-Related (for handling IRQ 8 / INT 70h)
;==============================================================================
  ;NMIDisable    EQU  80h ;1 = Disable NMI (add to CMOSPortx when reading Port)
  CMOSPortA     EQU  0Ah ;CMOS RTC Status Byte A Address (Read-Write)
    ;Bit   7 = Time Update Cycle in Progress, Data Outputs Undefined (Read-Only)
    ;    6-4 = 22 Stage Divider
    ;          010 = Default = 32768 Hz Time Base
    ;    3-0 = Rate selection for Periodic Interrupt
    ;          0011 = 122 æs Period (8192 Hz Minimum)
    ;          0110 = 976.562 ms Period (1024 Hz Default)
    ;          1111 = 500 ms Period (2 Hz Maximum)
    Period0002  EQU  2Fh ;Byte to send for    2 Interrupts/Second
    Period0004  EQU  2Eh ;Byte to send for    4 Interrupts/Second
    Period0008  EQU  2Dh ;Byte to send for    8 Interrupts/Second
    Period0016  EQU  2Ch ;Byte to send for   16 Interrupts/Second
    Period0032  EQU  2Bh ;Byte to send for   32 Interrupts/Second
    Period0064  EQU  2Ah ;Byte to send for   64 Interrupts/Second
    Period0128  EQU  29h ;Byte to send for  128 Interrupts/Second
    Period0256  EQU  28h ;Byte to send for  256 Interrupts/Second
    Period0512  EQU  27h ;Byte to send for  512 Interrupts/Second
    Period1024  EQU  26h ;Byte to send for 1024 Interrupts/Second (Default)
    Period2048  EQU  25h ;Byte to send for 2048 Interrupts/Second
    Period4096  EQU  24h ;Byte to send for 4096 Interrupts/Second
    Period8192  EQU  23h ;Byte to send for 8192 Interrupts/Second
  CMOSPortB     EQU  0Bh ;CMOS RTC Status Byte A Address (Read-Write)
    ;Bit 7 = Enable Clock Setting by Freezing Updates
    ;    6 = Enable Periodic Interrupt
    ;    5 = Enable Alarm Interrupt
    ;    4 = Enable Update-Ended Interrupt
    ;    3 = Enable Square-Wave Output
    ;    2 = Data Mode: 0 = BCD, 1 = Binary
    ;    1 = Hour Mode: 0 = 12-Hour Mode, 1 = 24-Hour Mode
    ;    0 = Daylight Savings Enable
    PeriodicInt EQU  40h ;Bit Mask for RTC Periodic Interrupt
    ;IntMasks    EQU 0F1h ;Bit Mask for all Interrupt Enables
  CMOSPortC     EQU  0Ch ;CMOS RTC Status Byte C Address (Read-Only)
    ;Bit   7 = Interrupt Request Flag
    ;          1 when any or all of bits 6-4 are 1 and appropriate bits in
    ;            Port B are Enabled.  Generates IRQ 8 (Int 70h) when triggered.
    ;      6 = Periodic Interrupt Flag
    ;      5 = Alarm Interrupt Flag
    ;      4 = Update-Ended Interrupt Flag
    ;    3-0 = Unused
  CMOSPortD     EQU  0Dh ;CMOS RTC Status Byte D Address (Read-Only)
    ;Bit   7 = Battery Good (0 = Battery Dead or Disconnected)
    ;    6-0 = Unused


;==============================================================================
;PS2 Port BIOS Functions Related (Int 11h and Int 15h)
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Int 11h
  ;----------------------------------------------------------------------------
  Int11PS2Installed EQU 4 ;Bit mask for PS2 Port installed from Int 11

  ;----------------------------------------------------------------------------
  ;Int 15h Call Function (in AH)
  ;----------------------------------------------------------------------------
  Int15PS2PortCall EQU 0C2h ;PS2 Mouse Function

  ;----------------------------------------------------------------------------
  ;Int 15h Call SubFunctions (in AL)
  ;----------------------------------------------------------------------------
  I15ALEnableDisable EQU  0 ;Enable/Disable
  I15ALReset         EQU  1 ;Reset
  I15ALSetSampling   EQU  2 ;Set Sampling Rate
  I15ALSetResolution EQU  3 ;Set Resolution
  I15ALGetDeviceID   EQU  4 ;Get Device ID
  I15ALInitialize    EQU  5 ;Initialize
  I15ALStatusScaling EQU  6 ;Get Status/Set Scaling
  I15ALSetHandler    EQU  7 ;Set Handler Address
  I15ALWriteData     EQU  8 ;Write (1 byte) of Data to Pointing Device Port
  I15ALReadData      EQU  9 ;Read (3 bytes) of Data from Pointing Device Port
  I15ALGetHandler    EQU 10 ;Get Handler Address

  ;----------------------------------------------------------------------------
  ;Error Codes Returned by Int 15h PS2 Port Functions
  ;----------------------------------------------------------------------------
  Int15ErrNone            EQU 0 ;No Error
  Int15ErrInvalidFunction EQU 1 ;Invalid Function Number
  Int15ErrInvalidInput    EQU 2 ;Invalid Input Value
  Int15ErrInterfaceError  EQU 3 ;Interface Error
  Int15ErrNeedToResend    EQU 4 ;Resend Request
  Int15ErrNoDeviceHandler EQU 5 ;No Device Handler in Place

  ;----------------------------------------------------------------------------
  ;Device Types & IDs returned by Int 15h when a Mouse is attached
  ;----------------------------------------------------------------------------
  DvcTypeMouse   EQU 0AAh ;Device Type returned by a standard mouse
  DvcIDWheelNo   EQU   0  ;Device ID if Mouse does NOT have a Wheel
  DvcIDWheel1Yes EQU   3  ;Device ID if Mouse DOES have a Wheel
  DvcIDWheel2Yes EQU   4  ;Device ID if Mouse has 2 Wheels and/or 5 Buttons

  ;----------------------------------------------------------------------------
  ;Bit masks for Data returned by Int 15h Get PS2 Status Function
  ;----------------------------------------------------------------------------
  StatusRBtn       EQU 01h ;Right Button Pressed
  StatusCBtn       EQU 02h ;Center Button Pressed
  StatusLBtn       EQU 04h ;Left Button Pressed
   ;Bit 3 = Reserved (0)
  StatusScaling2   EQU 10h ;Scaling = 2:1 (as opposed to 1:1)
  StatusEnabled    EQU 20h ;Enabled
  StatusRemoteMode EQU 40h ;Remote Mode (as opposed to Stream Mode)
   ;Bit 7 = Reserved (0)

  ;----------------------------------------------------------------------------
  ;Bit masks for Int 15h Set Sample Rate Function
  ;----------------------------------------------------------------------------
  SampleCode010 EQU 0 ; 10 samples/second
  SampleCode020 EQU 1 ; 20 samples/second
  SampleCode040 EQU 2 ; 40 samples/second
  SampleCode060 EQU 3 ; 60 samples/second
  SampleCode080 EQU 4 ; 80 samples/second
  SampleCode100 EQU 5 ;100 samples/second (default)
  SampleCode200 EQU 6 ;200 samples/second

  DefaultPeriodicity EQU  10 ;Default Periodicity = 10
                             ;  (100 samples/sec on the 1000 Hz USB Int)

  ;----------------------------------------------------------------------------
  ;Bit masks for Int 15 Set Resolution Function
  ;----------------------------------------------------------------------------
  Resolution1 EQU 0 ;1 Count/mm
  Resolution2 EQU 1 ;2 Counts/mm
  Resolution4 EQU 2 ;4 Counts/mm (default)
  Resolution8 EQU 3 ;8 Counts/mm

  ;----------------------------------------------------------------------------
  ;Bit masks for buttons in "raw" data (first byte of data returned by Mouse)
  ;----------------------------------------------------------------------------
  RawLBtn     EQU 01h ;Left Button
  RawRBtn     EQU 02h ;Right Button
  RawCBtn     EQU 04h ;Center/Middle Button
  RawAll3Btns EQU RawLBtn+RawRBtn+RawCBtn ;All Buttons combined
  RawAll5Btns EQU 1Fh ;All 5 buttons (as returned by USB Device

  ;----------------------------------------------------------------------------
  ;Number of buttons allowed before we'll consider it legitimate
  ;----------------------------------------------------------------------------
  MinButtons EQU 1 ;Minimum number of buttons allowed
;  MaxButtons EQU 8 ;Maximum number of buttons allowed

  ;----------------------------------------------------------------------------
  ;Special Bit masks for Status Byte returned by User Far Call
  ;----------------------------------------------------------------------------
  HandlerXNeg EQU 10h ;X Data is Negative
  HandlerYNeg EQU 20h ;Y Data is Negative

  ;----------------------------------------------------------------------------
  ;Bit masks  (in third data byte) for mouse buttons 4, 5, and the Wheel(s).
  ;  These only apply if the IntelliMouse2 Protocol is enabled.
  ;----------------------------------------------------------------------------
  Btn4Mask    EQU 10h ;Button 4 is pressed
  Btn5Mask    EQU 20h ;Button 5 is pressed
  Wheel1Up    EQU 0Fh ;Wheel 1 (Vertical) has been moved up (-1)
  Wheel1Down  EQU 01h ;Wheel 1 (Vertical) has been moved down (+1)
  Wheel2Left  EQU 0Eh ;Wheel 2 (Horizontal) has been moved Left (-2)
  Wheel2Right EQU 02h ;Wheel 2 (Horizontal) has been moved Right (+2)
  WheelsMask  EQU 0Fh ;Mask needed if PS2 Mouse has single wheel, but
                      ;  IntelliMouse2 Protocol is enabled

  ;----------------------------------------------------------------------------
  ;Location on Stack, relative to Button data, where X/Y/Z Data is stored.
  ;We intercept the PS2 call coming from the BIOS (with the Mouse Data on
  ;  the stack) and "translate" it so that it matches what the
  ;  DOS Mouse Driver is expecting to see.  Our Driver suports the highest
  ;  level Driver that the DOS Mouse Driver supports, even if there is
  ;  no Mouse Hardware that matches the protocol.  This means we need to
  ;  modify the data on the stack as it comes from the BIOS and create
  ;  our own stack with the "transformed" data.
  ;----------------------------------------------------------------------------
  StackXNoWheel EQU -2 ;X Data Location if there is no Wheel
  StackYNoWheel EQU -4 ;Y Data Location if there is no Wheel
  StackXWheel   EQU  1 ;X Data Location if there is a Wheel
  StackYWheel   EQU -2 ;Y Data Location if there is a Wheel
  StackZWheel   EQU -4 ;Z Data Location (only applies if there is a Wheel)


;==============================================================================
;Mouse Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Number of Mouse Data Bytes we can store in our Buffer
  ;Should be a nmultiple of 12, since we store them in groups of
  ;  3 or 4 at a time.  It may need to store the data from several
  ;  mice at a time.
  ;----------------------------------------------------------------------------
  BufferSize EQU 60

  ;----------------------------------------------------------------------------
  ;Port 64h (Keyboard Controller) Bit Masks
  ;----------------------------------------------------------------------------
  P64OutBuffFull   EQU 01h ;Output Buffer Full (Port 60 has data for system)
  P64InBuffFull    EQU 02h ;Input Buffer Full (Port 60/64 has data for Kbd)
  P64SelfTestOK    EQU 04h ;Self-Test OK
  P64DataIsCommand EQU 08h ;Data is Command (Port 64h), not Data (Port 60h)
  P64KbdInhibit    EQU 10h ;Keyboard Locked/Inhibited
  P64TxTimeout     EQU 20h ;Transmit Timeout
  P64RxTimeout     EQU 40h ;Receive Timeout
  P64ParityError   EQU 80h ;Parity Error

  ;----------------------------------------------------------------------------
  ;Values to write to Port 64h, followed by reading/writing the KCCB
  ;  (Keyboard Controller Command Byte) from Port 60h.
  ;----------------------------------------------------------------------------
  KCCBRead  EQU 20h ;Tells Controller to place KCCB in Port 60h to Read
  KCCBWrite EQU 60h ;Tells Controller to read KCCB byte from Port 60h


;==============================================================================
;USB Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Maximum number of Devices we can control
  ;----------------------------------------------------------------------------
  MaxDevices EQU 8

  ;----------------------------------------------------------------------------
  ;Maximum number of entries in Bad (incompatible) Device Table
  ;----------------------------------------------------------------------------
  MaxBadDevices EQU 12

  ;----------------------------------------------------------------------------
  ;General
  ;----------------------------------------------------------------------------
  Int14RequestSize EQU 64 ;Size of Int 14 USB Request Data Structure

  ;----------------------------------------------------------------------------
  ;Maximum Descriptor Sizes
  ;----------------------------------------------------------------------------
  MaxDescriptorSize EQU 512 ;Max size of Device/Config/HID Descriptor
;  MaxDescriptorSize EQU 200 ;Max size of Device/Config/HID Descriptor
;  MaxReportSize     EQU 150 ;Max size of Report Descriptor
  MaxReportSize     EQU 600 ;Max size of Report Descriptor
    ;MS Wireless 6000 "Mouse" is 571 bytes long (but appears as a generic
    ;  HID Device, not a Mouse).

  ;----------------------------------------------------------------------------
  ;USB Descriptor Types
  ;----------------------------------------------------------------------------
  DescrTypeDevice    EQU 01h ;Device
  DescrTypeConfig    EQU 02h ;Configuration
  DescrTypeString    EQU 03h ;String
  DescrTypeInterface EQU 04h ;Interface
  DescrTypeEndPoint  EQU 05h ;EndPoint
  DescrTypeDvcQual   EQU 06h ;Device Qualifier
  DescrTypeOtherSpd  EQU 07h ;Other Speed Configuration
  DescrTypeIntfPower EQU 08h ;Interface Power
  DescrTypeOnTheGo   EQU 09h ;On-the-Go
  DescrTypeDebug     EQU 0Ah ;Debug
  DescrTypeIntfAssoc EQU 0Bh ;Interface Association
  ;Types 12 - 17 (0Ch - 11h) are added for Wireless USB
  DescrTypeWSecurity EQU 0Ch ;Wireless USB Security
  DescrTypeWKey      EQU 0Dh ;Wireless USB Key
  DescrTypeWEncrType EQU 0Eh ;Wireless USB Encryption Type
  DescrTypeWBOS      EQU 0Fh ;Wireless USB Binary-device Object Store
  DescrTypeWDvcCap   EQU 10h ;Wireless USB Device Capability
  DescrTypeWEndPtCmp EQU 11h ;Wireless USB Endpoint Companion

  ;HID Class-Specific Descriptor Types
  DescrTypeHID         EQU 21h ;Human Interface Device
  DescrTypeHIDReport   EQU 22h ;Report (from HID)
  DescrTypeHIDPhysical EQU 23h ;Physical Descriptor (from HID)

  ;----------------------------------------------------------------------------
  ;Values in SRRequest for HID Devices
  ;----------------------------------------------------------------------------
  HIDRQGetReport   EQU  1 ;Get Report
  HIDRQGetIdle     EQU  2 ;Get Idle Factor
  HIDRQGetProtocol EQU  3 ;Get Protocol Type
  HIDRQSetReport   EQU  9 ;Set Report
  HIDRQSetIdle     EQU 10 ;Set Idle Factor
  HIDRQSetProtocol EQU 11 ;Set Protocol Type

  ;----------------------------------------------------------------------------
  ;Data for GetReport/SetReport
  ;----------------------------------------------------------------------------
  HIDReportTypeNone    EQU 0 ;None
  HIDReportTypeInput   EQU 1 ;Input
  HIDReportTypeOutput  EQU 2 ;Output
  HIDReportTypeFeature EQU 3 ;Feature

  ;----------------------------------------------------------------------------
  ;Data for GetProtocol/SetProtocol
  ;----------------------------------------------------------------------------
  HIDProtocolBoot   EQU 0 ;Boot Protocol
  HIDProtocolReport EQU 1 ;Report Protocol

  ;----------------------------------------------------------------------------
  ;USB-Defined Device and Interface Classes, SubClasses, and Protocols
  ;----------------------------------------------------------------------------
  DvcClassNone         EQU 0 ;Device Class = None
  DvcSubClassNone      EQU 0 ;Device SubClass = None
  DvcProtocolNone      EQU 0 ;Device Protocol = None
  IntfClassHID         EQU 3 ;Interface Class = HID (Human Interface Device)
  IntfSubClassNone     EQU  0 ;Interface SubClass = None
  IntfSubClassAny      EQU -1 ;Interface SubClass = Any
  IntfSubClassHIDBoot  EQU  1 ;Interface SubClass = Boot
  IntfProtocolNone     EQU  0 ;Interface SubClass = None
  IntfProtocolAny      EQU -1 ;Interface SubClass = Any
  IntfProtocolHIDMouse EQU 2 ;Interface Protocol = Mouse

  ;----------------------------------------------------------------------------
  ;Possible Error Codes to return to calling program on completion of TD
  ;----------------------------------------------------------------------------
  TDStsOK             EQU 0000h ;ACK Received (TD completed OK / no errors)
  TDStsNAKReceived    EQU 0001h ;NAK Received
  TDStsStalled        EQU 0002h ;TD is Stalled
  TDStsTimeout        EQU 0004h ;TD has timed out (Bulk/Control)
  TDStsOverDue        EQU 0008h ;TD is OverDue (Int/Isoch)
  TDStsShortPacket    EQU 0010h ;TD Short Packet Detected
  TDStsBabbleDetected EQU 0020h ;Babble Detected
  TDStsCRCTOReceived  EQU 0040h ;CRC/TimeOut Error Received
  TDStsBitStuffError  EQU 0080h ;Rx Data contained > 6 ones in a row
  TDStsDataBuffErr    EQU 0100h ;Data Buffer Error
  TDStsControlSetup   EQU 1000h ;Error actually occurred during Control Setup
  TDStsDvcRemoved     EQU 2000h ;Device removed while TD was in progress
  TDStsLargeCallErr   EQU 8000h ;Error during Large Call

  ;----------------------------------------------------------------------------
  ;Statuses sent by Host to USB Device Owners
  ;----------------------------------------------------------------------------
  ;Device Connect/Disconnect
  OwnerCallNewDvc   EQU 0001h ;A new Matching Device has been attached
  OwnerCallDvcDisc  EQU 0002h ;Registered Device has been disconnected
  OwnerCallDvc0Disc EQU 0003h ;Matching Unregistered Device Disconnected
  OwnerCallDontLook EQU 0007h ;Stop Looking for New Devices for ~5 seconds
  ;Device Enable/Disable/Reset/Suspend/Resume/Power
  OwnerCallDvcEnable   EQU 0008h ;Device Enabled
  OwnerCallDvcDisable  EQU 0009h ;Device Disabled
  OwnerCallDvcReset    EQU 000Ah ;Device Resetting
  OwnerCallDvcSuspend  EQU 000Bh ;Device Suspended
  OwnerCallDvcResume   EQU 000Ch ;Device Resumed
  OwnerCallDvcPwrOn    EQU 000Dh ;Device Power On
  OwnerCallDvcPwrOff   EQU 000Eh ;Device Power Off
  OwnerCallDvcPwrReset EQU 000Fh ;Device Power Reset
  ;Host Connect/Disconnect/Error
  OwnerCallNewHost     EQU 0011h ;A new Host Driver has been Installed
  OwnerCallHostDisc    EQU 0012h ;An existing Host Driver has been Uninstalled
  OwnerCallHostHWErr   EQU 0017h ;Host System/Processor Error (Host will Reset)
  ;Host Run/Stop/Reset/Suspend/Resume
  OwnerCallHostRun     EQU 0018h ;Host Running
  OwnerCallHostStop    EQU 0019h ;Host Stopped
  OwnerCallHostReset   EQU 001Ah ;Host Reset
  OwnerCallHostSuspend EQU 001Bh ;Host Suspended
  OwnerCallHostResume  EQU 001Ch ;Host Resumed
  OwnerCallHostDebug   EQU 001Dh ;Host in Debug Mode
  OwnerCallHostSST     EQU 001Eh ;Host Single-Stepped
  ;Timing Changes
  OwnerCallTimingChg   EQU 0021h ;Timing Change on Host
  ;Upstream Device Changes
  OwnerCallDvcDiscUS     EQU (OwnerCallDvcDisc OR 0080h)     ;Dvc Disc Upstream
  OwnerCallDvcEnableUS   EQU (OwnerCallDvcEnable OR 0080h)   ;Dvc Enable Upstream
  OwnerCallDvcDisableUS  EQU (OwnerCallDvcDisable OR 0080h)  ;Dvc Disable Upstream
  OwnerCallDvcResetUS    EQU (OwnerCallDvcReset OR 0080h)    ;Dvc Reset Upstream
  OwnerCallDvcSuspendUS  EQU (OwnerCallDvcSuspend OR 0080h)  ;Dvc Suspend Upstream
  OwnerCallDvcResumeUS   EQU (OwnerCallDvcResume OR 0080h)   ;Dvc Resume Upstream
  OwnerCallDvcPwrOnUS    EQU (OwnerCallDvcPwrOn OR 0080h)    ;Dvc PwrOn Upstream
  OwnerCallDvcPwrOffUS   EQU (OwnerCallDvcPwrOff OR 0080h)   ;Dvc PwrOff Upstream
  OwnerCallDvcPwrResetUS EQU (OwnerCallDvcPwrReset OR 0080h) ;Dvc PwrReset Upstream


;==============================================================================
;USB Report Descriptor Related
;  HID Reports Start with a 1-byte Tag, followed by 0 or more bytes of Data
;  Bits 0-1 are actual size (0-3 bytes of data following the Tag)
;  Bits 2-3 are Tag Type: 0h = Main, 4h = Global, 8h = Local, Ch = Reserved
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Miscellaneous
  ;----------------------------------------------------------------------------
  RptTagLongData EQU 00FCh ;Long Data Item (more than 3 bytes)
                           ;Lower 2 bits are always 10h (Size of 2)
                           ;Next byte is Actual Data Size (0-255)
                           ;Next Byte is Tag (Description)
                           ;Followed by up to 255 bytes of "real" data

  ;----------------------------------------------------------------------------
  ;Main
  ;----------------------------------------------------------------------------
  RptTagInput              EQU   80h ;Input
  RPtTagOutput             EQU   90h ;Output
  RptTagFeature            EQU  0B0h ;Feature
    ;These apply to Input, Output, and Feature
    RptMainIOFConstant     EQU   01h ;Constant
     RptMainIOFData        EQU   00h ; Data
    RptMainIOFVariable     EQU   02h ;Variable
     RptMainIOFArray       EQU   00h ; Array
    RptMainIOFRelative     EQU   04h ;Relative
     RptMainIOFAbsolute    EQU   00h ; Absolute
    RptMainIOFWrap         EQU   08h ;Wrap
     RptMainIOFNoWrap      EQU   00h ; No Wrap
    RptMainIOFNonLinear    EQU   10h ;Non-Linear
     RptMainIOFLinear      EQU   00h ; Linear
    RptMainIOFNoPrefer     EQU   20h ;No Preferred State
     RptMainIOFPrefer      EQU   00h ; Preferred State
    RptMainIOFNull         EQU   40h ;Null State
     RptMainIOFNoNull      EQU   00h ; No Null State
    RptMainIOFVolatile     EQU   80h ;Volatile
     RptMainIOFNonVolatile EQU   00h ; Non-Volatile
    RptMainIOFBuffered     EQU  100h ;Buffered Bytes
     RptMainIOFBitField    EQU  000h ; Bit-Field

  RptTagCollection         EQU  0A0h ;Collection
    RptMainColPhysical     EQU   00h ;Physical (Group of Axes)
    RptMainColApplication  EQU   01h ;Application (Mouse/Keyboard)
    RptMainColLogical      EQU   02h ;Logical (interrelated Data)

  RptTagEndCollection      EQU  0C0h ;End Collection

  ;----------------------------------------------------------------------------
  ;Global
  ;----------------------------------------------------------------------------
  RptTagUsagePage          EQU   04h ;Usage Page
    RptGlbUPGenericDeskTop EQU   01h ;Generic Desktop
      RptGDPointer         EQU   01h ;Pointing Device
      RptGDMouse           EQU   02h ;Mouse
      ;03h = Reserved
      RptGDJoystick        EQU   04h ;Joystick
      RptGDGamePad         EQU   05h ;GamePad
      RptGDKeyboard        EQU   06h ;Keyboard
      RptGDKeyPad          EQU   07h ;KeyPad
      RptGDMultiAxisCtl    EQU   08h ;Multi-Axis Controller
       ;09h - 2Fh = Reserved
      RptGDXAxis           EQU   30h ;X-axis
      RptGDYAxis           EQU   31h ;Y-axis
      RptGDZAxis           EQU   32h ;Z-axis
      RptGDRotateX         EQU   33h ;Rotation around X-axis
      RptGDRotateY         EQU   34h ;Rotation around Y-axis
      RptGDRotateZ         EQU   35h ;Rotation around Z-axis
      RptGDSlider          EQU   36h ;Slider
      RptGDDial            EQU   37h ;Dial
      RptGDWheel           EQU   38h ;Wheel
      RptGDHatSwitch       EQU   39h ;Hat-Switch
      RptGDCountedBuff     EQU   3Ah ;Counted Buffer
      RptGDByteCount       EQU   3Bh ;Byte Count
      RptGDMotionWakeup    EQU   3Ch ;Wakeup on Motion
      RptGDStart           EQU   3Dh ;Start
      RptGDSelect          EQU   3Eh ;Select
      ;3Fh Reserved
      RptGDVectorX         EQU   40h ;X-Axis Vector
      RptGDVectorY         EQU   41h ;Y-Axis Vector
      RptGDVectorZ         EQU   42h ;Z-Axis Vector
      RptGDVectorBRX       EQU   43h ;X-Axis Vector, Relative to the Body
      RptGDVectorBRY       EQU   44h ;Y-Axis Vector, Relative to the Body
      RptGDVectorBRZ       EQU   45h ;Z-Axis Vector, Relative to the Body
      RptGDVectorNoD       EQU   46h ;Non-Directional Vector (e.g., Tempurature)
      ;47h = ??
      RptGDResMult         EQU   48h ;Resolution Multiplier
      ;49h-7Fh = Reserved
      RptGDSysCtl          EQU   80h ;System Control
      RptGDSysPowerDown    EQU   81h ;System Power Down
      RptGDSysSleep        EQU   82h ;System Sleep
      RptGDSysWakeUp       EQU   83h ;System Wake Up
      RptGDSysMenuContext  EQU   84h ;System Menu Context
      RptGDSysMenuMain     EQU   85h ;System Menu Main
      RptGDSysMenuApp      EQU   86h ;System Menu Application
      RptGDSysMenuHelp     EQU   87h ;System Menu Help
      RptGDSysMenuExit     EQU   88h ;System Menu Exit
      RptGDSysMenuSelect   EQU   89h ;System Menu Select
      RptGDSysMenuRight    EQU   8Ah ;System Menu Right
      RptGDSysMenuLeft     EQU   8Bh ;System Menu Left
      RptGDSysMenuUp       EQU   8Ch ;System Menu Up
      RptGDSysMenuDown     EQU   8Dh ;System Menu Down
      RptGDSysColdStart    EQU   8Eh ;System Cold Restart
      RptGDSysWarmStart    EQU   8Fh ;System Warm Restart
      RptGDDPadUp          EQU   90h ;D-Pad Up
      RptGDDPadDown        EQU   91h ;D-Pad Down
      RptGDDPadRight       EQU   92h ;D-Pad Right
      RptGDDPadLeft        EQU   93h ;D-Pad Left
      ;94h - 9Fh = Reserved
      RptGDSysDock         EQU  0A0h ;System Dock
      RptGDSysUndock       EQU  0A1h ;System Undock
      RptGDSysSetup        EQU  0A2h ;System Setup
      RptGDSysBreak        EQU  0A3h ;System Break
      RptGDSysDbgBrk       EQU  0A4h ;System Debugger Break
      RptGDSysAppBrk       EQU  0A5h ;Application Break
      RptGDSysAppDbgBrk    EQU  0A6h ;Application Debugger Break
      RptGDSysMute         EQU  0A7h ;System Speaker Mute
      RptGDSysHibernate    EQU  0A8h ;System Hibernate
      ;A9h - AFh = Reserved
      RptGDSysDispInvt     EQU  0B0h ;System Display Invert
      RptGDSysDispInt      EQU  0B1h ;System Display Internal
      RptGDSysDispExt      EQU  0B2h ;System Display External
      RptGDSysDispBoth     EQU  0B3h ;System Display Both
      RptGDSysDispDual     EQU  0B4h ;System Display Dual
      RptGDSysDispTgl      EQU  0B5h ;System Display Toggle Internal/External
      RptGDSysDispSwap     EQU  0B6h ;System Display Swap Primary/Secondary
      RptGDSysDispAuto     EQU  0B7h ;System Display LCD Autoscale
      ;B8h - FFFFh = Reserved

    RptGlbUPSimulationCtl  EQU   02h ;Simulation Controls
    RptGlbUPVRCtl          EQU   03h ;Virtual Reality? Controls
    RptGlbUPSportCtl       EQU   04h ;Sport Controls
    RptGlbUPGameCtl        EQU   05h ;Game Controls
    ;06h = ?
    RptGlbUPKeyboard       EQU   07h ;Keyboard/Keypad (Usage Type = Selector)
    RptGlbUPLED            EQU   08h ;LEDs (Light Emitting Diodes)
    RptGlbUPButton         EQU   09h ;Buttons
    RptGlbUPOrdinal        EQU   0Ah ;Ordinal
    RptGlbUPConsumerDvc    EQU   0Ch ;Consumer Devices
      RptConsACPan         EQU 0238h ;AC Pan (used for Horizontal Wheel)

  RptTagLogicalMin         EQU   14h ;Logical Minumum
  RptTagLogicalMax         EQU   24h ;Logical Maximum
  RptTagPhysicalMin        EQU   34h ;Physical Minimum
  RptTagPhysicalMax        EQU   44h ;Physical Maximum
  RptTagUnitExponent       EQU   54h ;Unit Exponent (base 10)
  RptTagUnit               EQU   64h ;Units

  RptTagReportSize         EQU   74h ;Size of Report Field in (bits)
  RptTagReportID           EQU   84h ;Report ID
  RptTagReportCount        EQU   94h ;Report Count
  RptTagPush               EQU  0A4h ;Push Global State Table onto the Stack
  RptTagPop                EQU  0B4h ;Pop Global State Table from the Stack

  ;----------------------------------------------------------------------------
  ;Local
  ;----------------------------------------------------------------------------
  RptTagUsage              EQU   08h ;Usage
  RptTagUsageMin           EQU   18h ;Usage Minimum
  RptTagUsageMax           EQU   28h ;Usage Maximum
  RptTagDesignatorIndex    EQU   38h ;Designator Index (Body Part)
  RptTagDesignatorMin      EQU   48h ;Designator Minimum
  RptTagDesignatorMax      EQU   58h ;Designator Maximum
  RptTagStringIndex        EQU   78h ;String Index
  RptTagStringMin          EQU   88h ;String Minimum
  RptTagStringMax          EQU   98h ;String Maximum
  RptTagDelimiter          EQU  0A8h ;1 = Open Set, 2 = Close Set


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;STRUCTURES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;==============================================================================
;TSR Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;This is a Structure to use at the beginning of Interrupt Routines in
  ;  TSR's.  Using this header, along with the appropriate code,
  ;  will allow the installation and removal of TSR's (actually,
  ;  the redirected interrupt vectors of a TSR) in any order.
  ;Note that the real Interrupt code MUST IMMEDIATELY follow this Header!!
  ;----------------------------------------------------------------------------
  IntHdr STRUC
    HdwreRst  DB      0CBh ;Hardware Reset Routine (CBh = RETF)
    CodeJmp   DW    010EBh ;Jump to real Interrupt Handler (JMP ENDS)
    OldVector DD        ?  ;Old interrupt Vector
    Signature DW    0424Bh ;Signature for this type of Interrupt routine
    EOIFlag   DB        0  ;=80h if we issue End-of-Interrupt, else 0
    HdwreJmp  DW    0F4EBh ;Jump to Hardware Reset Routine (JMP HdwreRst)
    FutureUse DB 7 DUP (0) ;Reserved for future use
   ENDS
  IntOfst EQU OFFSET CodeJmp ;Offset from beginning of our Structure


;==============================================================================
;DOS Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Program Segment Prefix
  ;----------------------------------------------------------------------------
  PSPStruc      STRUC
    PSPDOSExit     DW       20CDh ;Int 20h Call
    PSPNextSegment DW          ?  ;Segment of First Byte of Memory beyond Program
    PSPCPMCall5    DB   6 DUP (?) ;CPM Far Call 5
    PSPInt22Addr   DD          ?  ;Stored Int 22h Handler
    PSPInt23Addr   DD          ?  ;Stored Int 23h (Ctrl-Break) Handler
    PSPInt24Addr   DD          ?  ;Stored Int 23h (Critical Error) Handler
    PSPParentPSP   DW          ?  ;Segment of Parent PSP
    PSPJFT         DB  20 DUP (?) ;Job File Table (20 File Handles)
    PSPEnvirSeg    DW          ?  ;Environment Segment for PSP
    PSPSSSPOnEntry DD          ?  ;Processes SS:SP on Entry to last Int 21h Call
    PSPJFTSize     DW          ?  ;Number of entries in JFT (default = 20)
    PSPJFTPointer  DD          ?  ;Pointer to JFT (Default = PSP:0018h)
    PSPPreviousPSP DD          ?  ;Pointer to previous PSP (Default = FFFF:FFFF)
    PSPFiller1     DB  18 DUP (?) ;Misc stuff used by SHARE, Windows, etc.
                                  ;Memory Below here available to TSR!
    PSPFiller2     DB   2 DUP (?) ;Unused in DOS <= 6.00
    PSPInt21       DW       21CDh ;Int 21h Call
    PSPRETF        DB        0CBh ;RETF Call
    PSPFiller3     DB   9 DUP (?) ;Can be used to make Extended FCB from FCB1
    PSPFCB1        DB  16 DUP (?) ;First File Control Block
    PSPFCB2        DB  16 DUP (?) ;Second File Control Block
    PSPFiller4     DB   4 DUP (?) ;Unused
    PSPCmdLineLen  DB          ?  ;Command Line Length & DTA
    PSPCmdLine     DB 127 DUP (?) ;Command Line String
   ENDS
  PSPStrucSize EQU (TYPE PSPStruc) ;Should be 256 bytes

  ;----------------------------------------------------------------------------
  ;Memory Control Block
  ;----------------------------------------------------------------------------
  MCBStruc STRUC
    MCBID              DB        ?  ;Memory Control Block ID
      MCBIDValid      EQU       'M' ;Memory Block is Valid
      MCBIDLastInList EQU       'Z' ;Memory Block is Last in List
    MCBOwnerID         DW        ?  ;PSP Segment of Owner
                                    ;   0 = Owns Self
                                    ;   6 = DR-DOS XMS UMB
                                    ;   7 = DR-DOS Excluded Upper Memory
                                    ;   8 = Belongs to DOS
                                    ;FFFx = 386Max
    MCBSize            DW        ?  ;Size in Paragraphs
    MCBFiller          DB 3 DUP (?) ;Filler
    MCBOwnerName       DB 8 DUP (?) ;Owner Name
   ENDS
  MCBStrucSize EQU (TYPE MCBStruc) ;Should be 16 bytes

  ;----------------------------------------------------------------------------
  ;Structure needed to Initialize DOS EXEC Function
  ;----------------------------------------------------------------------------
  ParamBlockStruc STRUC
    PBEnvirSegment   DW ? ;Environment Segment to use (0 = Copy Callers Segment)
    PBCmdTailPtr     DD ? ;Pointer to Command Tail
    PBFCB1Offset     DD ? ;Pointer to File Control Block #1
    PBFCB2Offset     DD ? ;Pointer to File Control Block #2
    PBSSSPOnReturn   DD ? ;Holds Childs Initial SS:SP on Return (if AL = 01)
    PBCSIPOnReturn   DD ? ;Holds Entry Point (CS:IP) on Return (if AL = 01)
   ENDS
  ParamBlockSize EQU (TYPE ParamBlockStruc) ;Size of EXEC Parameter Block

  ;----------------------------------------------------------------------------
  ;DTA (Disk Transfer Area) after Find First Matching File (INT 21h, Func 4Eh)
  ;The first several bytes of the DTA are DOS version specific, and
  ;  we don't care about that data anyway.  We only care about the data
  ;  that is documented and the same across all versions of DOS.
  ;----------------------------------------------------------------------------
  FindFirstStruc   STRUC            ;Find First Matching File
    FFVerSpecific     DB 21 DUP (0) ;DOS Version-specific Data
    FFAttribs         DB         ?  ;File Attributes
      FAttrReadOnly  EQU        01h ;Read-Only
      FAttrHidden    EQU        02h ;Hidden
      FAttrSystem    EQU        04h ;System
      FAttrVolume    EQU        08h ;Volume Label
      ;Bit 3 = Execute-only (Novell Netware)
      FAttrDirectory EQU        10h ;Directory
      FAttrArchive   EQU        20h ;Archive
      FAttrUnused    EQU        40h ;Unused
                                    ;DOS 3+ returns this bit set if file
                                    ;  is actually a character device
                                    ;DOS Window underneath Windows XP doesn't
      ;Bit 7 = Delete Pending (OpenDOS, NovellDOS)
      ;      = Shareable (Novell Netware)
   FFTime            DW         ?  ;File Creation Time
    FFDate            DW         ?  ;File Creation Date
    FFSize            DD         ?  ;File Size
    FFName            DB 13 DUP (?) ;ASCIIZ File Name & Extension
   ENDS

   FAttrToUse EQU FAttrHidden+FAttrSystem
     ;Attributes to use in DOS's Find First Matching File Function
     ;DOS ignores the Read-Only and and Archive bits, so they don't matter


;==============================================================================
;USB/Mouse Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;This structure keeps track of each of the several individual USB Devices
  ;----------------------------------------------------------------------------
  DeviceInfoStruc    STRUC
    DIFlags             DB         ?  ;Misc Yes/No Flags
      DIFlagInUse      EQU        01h ;This entry currently being used?
      DIFlagDisabled   EQU        02h ;This individual Device is Disabled
      DIFlagSwapBtns   EQU        04h ;Swap L/R Buttons
      DIFlagDisconnect EQU        08h ;Device in process of Disconnecting
      DIFlagX2Bytes    EQU        10h ;X Data is 2 bytes long
      DIFlagY2Bytes    EQU        20h ;Y Data is 2 bytes long
      DIFlagZ12Bytes   EQU        40h ;Wheel 1 Data is 2 bytes long
      DIFlagZ22Bytes   EQU        80h ;Wheel 2 (or Z) Data is 2 bytes long
    DIHostIndex         DB         ?  ;Host Index this Device is on
    DIDvcAddress        DB         ?  ;Device Address of Device
    DIEndPoint          DB         ?  ;End Point Number
    DIInterfaceNum      DB         ?  ;Interface number of Device (usually 0)
    DINumButtons        DB MinButtons ;Number of Buttons
    DIBtnDataLastTime   DB         ?  ;Button data last time through
    DIXRemainder        DB         ?  ;Leftover from X Movements
    DIYRemainder        DB         ?  ;Leftover from Y Movements
    DISysReportID       DB         ?  ;System (Keyboard) Report ID
    DISysLastTime       DB         ?  ;System (Keyboard) Data last time
    DINumWheels         DB         0  ;Number of wheels (0, 1, or 2)
    DIReportID          DB         ?  ;Report ID (if needed)
    DIReportIDData      DB         ?  ;Report ID from Data (if provided)
                                      ;  MUST IMMEDIATELY precede DIRawData!!!
    DIRawData           DB  8 DUP (0) ;Raw Device Data
      DIBtnOffset      EQU ((OFFSET DIRawData)+0) ;Button Data (first byte)
      DIXDataPS2       EQU ((OFFSET DIRawData)+1) ;X Data (for PS/2 Mouse)
      DIYDataPS2       EQU ((OFFSET DIRawData)+2) ;Y Data (for PS/2 Mouse)
      DIZ1DataPS2      EQU ((OFFSET DIRawData)+3) ;Wheel 1 Data (for PS/2 Mouse)
      DIZ2DataPS2      EQU ((OFFSET DIRawData)+4) ;Wheel 2 Data (for PS/2 Mouse)
    DIXOffset           DW         0  ;Offset of X Data
    DIYOffset           DW         0  ;Offset of Y Data
    DIZ1Offset          DW         0  ;Offset of Wheel 1 Data
    DIZ2Offset          DW         0  ;Offset of Wheel 2 (or Z) Data
    DIRegHandle         DW         ?  ;Handle Number of Owner Registry
    DIIntHandle         DW         ?  ;Handle number of Periodic Interrupt
    DIStsData           DB  8 DUP (0) ;Raw Device Data for Status
   ENDS
  DeviceInfoStrucSize EQU (TYPE DeviceInfoStruc)

  ;----------------------------------------------------------------------------
  ;This structure keeps track of Bad (incompatible) Devices
  ;----------------------------------------------------------------------------
  BadDeviceStruc    STRUC
    BDFlags            DB  ?  ;Misc Yes/No Flags
      BDFlagInUse     EQU 01h ;This entry currently being used?
    BDInterfaceNum     DB  ?  ;Interface Number of Bad Device
    BDHostIndex        DB  ?  ;Host Index of Bad Device
    BDDvcAddress       DB  ?  ;Device Address of Bad Device
    BDFiller           DB  ?  ;To align data at even addresses
    BDNewDeviceStage   DB  ?  ;Bad Device Stage
    BDNewDeviceError   DW  ?  ;Bad Device Error Code
   ENDS
  BadDeviceStrucSize EQU (TYPE BadDeviceStruc)

  ;----------------------------------------------------------------------------
  ;This is the format for a request sent to Int 14h to Send/Receive Data
  ;It contains the type of request, etc.
  ;----------------------------------------------------------------------------
  Int14RequestStruc         STRUC      ;Structure for an Int 14 Request
    I14RRequestType            DB   ?  ;Type of Request
      I14RRTHostClass         EQU  00h ;Host/System/OS Class
        I14RRTGetHostSWInfo   EQU  01h ;Get Host Software Info
        I14RRTGetHostHWInfo   EQU  02h ;Get Host Hardware Info
        I14RRTGetHostVendInfo EQU  03h ;Get Host Vendor Info
        I14RRTGetHostStatus   EQU  04h ;Get Current Host Status
        I14RRTHostRun         EQU  08h ;Start/Run/Resume Host
        I14RRTHostStop        EQU  09h ;Stop Host
        I14RRTHostReset       EQU  0Ah ;Reset Host
        I14RRTHostSuspend     EQU  0Bh ;Global Suspend Host
        I14RRTHostResume      EQU  0Ch ;Force Global Resume on Host
      I14RRTTimingClass       EQU  10h ;Frame Timing Class
        I14RRTRegTmgOwner     EQU  11h ;Register as Timing Owner
        I14RRTUnRegTmgOwner   EQU  12h ;UnRegister as Timing Owner
        I14RRTIncTiming       EQU  13h ;Increment (Slow Down) Frame Timing
        I14RRTDecTiming       EQU  14h ;Decrement (Speed Up) Frame Timing
        I14RRTChangeTiming    EQU  15h ;Change Frame Timing (by Large Amount)
      I14RRTHubClass          EQU  20h ;Hub Class
        I14RRTGetDvcHubInfo   EQU  21h ;Get Hub Info for Device
        I14RRTNewDvcConn      EQU  24h ;Hub has Detected new Device
        I14RRTDvcDisc         EQU  25h ;Device has been Disconnected
        I14RRTSendHubChar     EQU  27h ;Send Hub Characteristics to Host
        I14RRTEnableHubPort   EQU  28h ;Enable Device given Hub & Port
        I14RRTDisableHubPort  EQU  29h ;Disable Device given Hub & Port
        I14RRTResetHubPort    EQU  2Ah ;Reset Device given Hub & Port
        I14RRTSuspendHubPort  EQU  2Bh ;Suspend Device given Hub & Port
        I14RRTResumeHubPort   EQU  2Ch ;Resume Device given Hub & Port
        I14RRTPwrOnHubPort    EQU  2Dh ;Power On Device given Hub & Port
        I14RRTPwrOffHubPort   EQU  2Eh ;Power Off Device given Hub & Port
        I14RRTPwrResetHubPort EQU  2Fh ;Power Reset Device given Hub & Port
      I14RRTTPowerClass       EQU  30h ;Power Class
        I14RRTGetDvcPowerInfo EQU  31h ;Get Power Info for Device
        I14RRTGetHubPowerDraw EQU  32h ;Get Power Draw for Hub
        I14RRTPwrOnDevice     EQU  3Dh ;Power On Device given Dvc Addr
        I14RRTPwrOffDevice    EQU  3Eh ;Power Off Device given Dvc Addr
        I14RRTPwrResetDevice  EQU  3Fh ;Power Reset Device given Dvc Addr
      I14RRTDeviceClass       EQU  40h ;Device Class
        I14RRTGetDvcClassInfo EQU  41h ;Get Device Class Info
        I14RRTGetDvcVendInfo  EQU  42h ;Get Device Vendor Info
        I14RRTGetDvcStatus    EQU  43h ;Get Device Status
        I14RRTEnableDevice    EQU  48h ;Enable/Resume Device given Dvc Addr
        I14RRTDisableDevice   EQU  49h ;Disable Device given Dvc Addr
        I14RRTResetDevice     EQU  4Ah ;Reset Device given Dvc Addr
        I14RRTSuspendDevice   EQU  4Bh ;Suspend Device given Dvc Addr
        I14RRTResumeDevice    EQU  4Ch ;Resume Device given Dvc Addr
      I14RRTConfigClass       EQU  50h ;Configuration Class
        I14RRTConfigingIntf   EQU  51h ;Driver is Configuring an Interface
        I14RRTConfigIntfDone  EQU  52h ;Interface Configuration is Complete
        I14RRTSetNewConfig    EQU  58h ;Set/Change Device Config Value
      I14RRTInterfaceClass    EQU  60h ;Interface Class
        I14RRTFindRegIntf     EQU  62h ;Look for Registered Interface
        I14RRTFindUnRegIntf   EQU  63h ;Look for Unregistered Interface
        I14RRTRegIntfOwner    EQU  64h ;Register as Interface Owner
        I14RRTUnRegIntfOwner  EQU  65h ;Unregister as Interface Owner
        I14RRTIntfDontLook    EQU  68h ;Existing Interface Owner Don't Look
      I14RRTAltIntfClass      EQU  70h ;Alternate Interface Class
        I14RRTGetAltIntfInfo  EQU  71h ;Get Alternate Interface Info
      I14RRTEndPointClass     EQU  80h ;End Point Class
        I14RRTGetDataX        EQU  81h ;Get Current DataX Value
        I14RRTIncDataX        EQU  88h ;Increment/Toggle DataX Value
      I14RRTPacketClass       EQU  90h ;Packet Class
        I14RRTDoIsoch         EQU  94h ;Schedule Isochronous Transaction
        I14RRTDoInterruptPer  EQU  95h ;Schedule Periodic Interrupt
        I14RRTDoControl       EQU  96h ;Schedule Control/Setup Request
        I14RRTDoBulk          EQU  97h ;Schedule Bulk Transaction
        I14RRTDoInterrupt1T   EQU  98h ;Schedule One-Time Interrupt
        I14RRTCloseHandle     EQU  9Ch ;Close/Remove Scheduled Transaction
        I14RRTChangeIntPer    EQU  9Dh ;Change Periodicity of Interrupt
        I14RRTGetTransStatus  EQU  9Fh ;Get Status of Packet Transaction
      I14RRTMiscClass         EQU 0A0h ;Miscellaneous Class
        I14RRTLargeCallDone   EQU 0A1h ;Large (Complicated) Call Complete
        I14RRTBeepSpeaker     EQU 0AFh ;Beep the Speaker
      I14RRTInterHostClass    EQU 0E0h ;Inter-Host Communication Class
      I14RRTInternalClass     EQU 0F0h ;Host Internal/Troubleshooting Class
    I14RFlags                  DB   ?  ;Bit-level flags
      I14RFlagIn              EQU  01h ;In Direction
      I14RFlagLowSpeed        EQU  02h ;Low-Speed Device
      I14RFlagHiSpeed         EQU  04h ;High-Speed Device
      I14RFlagNoRetries       EQU  10h ;No Auto Retries for Control
      I14RFlagShortPktOK      EQU  20h ;No Retries for Short Packets
      I14RFlagSpecificFrame   EQU  40h ;Use Specific Frame Number
      I14RFlagAddrIsPhys      EQU  80h ;Data Address is Physical
    I14RHostIndex              DB   ?  ;Host Index
    I14RDeviceAddress          DB   ?  ;USB Device Address
      I14RHubAddress          EQU (OFFSET I14RDeviceAddress)
    I14REndPoint               DB   ?  ;EndPoint or Port or Alt Interface
      I14RHubPort             EQU (OFFSET I14REndPoint)
      I14RAltInterface        EQU (OFFSET I14REndPoint)
    I14RConfigValue            DB   ?  ;Configuration Value
      I14RCloseID             EQU (OFFSET I14RConfigValue)
      I14RDataX               EQU (OFFSET I14RConfigValue)
    I14RInterfaceNum           DB   ?  ;Interface Number
    I14RSearchIndex            DB   ?  ;Search Index
    I14RVendorID               DW   ?  ;Vendor ID
    I14RProductID              DW   ?  ;Product ID
    I14RDvcClass               DB   ?  ;Device Class
    I14RDvcSubClass            DB   ?  ;Device SubClass
    I14RDvcProtocol            DB   ?  ;Device Protocol
    I14RIntfClass              DB   ?  ;Interface Class
    I14RIntfSubClass           DB   ?  ;Interface SubClass
    I14RIntfProtocol           DB   ?  ;Interface Protocol
    I14RRequestHandle          DW   ?  ;Request Handle Number
    I14RPeriodicity            DW   ?  ;Interrupt Periodicity/Duration
      I14RBeepFrequency       EQU (OFFSET I14RPeriodicity)
    I14RTimeout                DW   ?  ;Transaction Time Out Value
    I14RDataAddress            DD   ?  ;Data Address
    I14RDataSize               DW   ?  ;Size of Data (Bytes)
    I14RCallBackAddr           DD   ?  ;Call Back Address (CS:IP format)
      I14RLargeCallRtnCode    EQU (OFFSET I14RCallBackAddr)
    I14RUserPktID              DW   ?  ;User Packet ID
    I14RSetupReqData           DQ   ?  ;Setup Request Data (8 bytes)
    I14RFrameTiming            DW   ?  ;Frame Timing Value (def = 12000)
      I14RFrameIndex          EQU (OFFSET I14RFrameTiming)
                                       ;Starting Frame of Isoch Schedule
    I14RIsochSchedAddr         DD   ?  ;Address of Isoch Schedule
    I14RFiller  DB (Int14RequestSize-$) DUP (0)

   ENDS

  ;----------------------------------------------------------------------------
  ;Device Request is a structure containing data required by a USB Setup Packet
  ;----------------------------------------------------------------------------
  SetupRequest           STRUC     ;Structure for a Standard Setup Pkt
    SRRequestType           DB  ?  ;Request Type
      SRRTOut              EQU 00h ;0=Out (Host to Device)
      SRRTIn               EQU 80h ;1=In (Device to Host)
      SRRTTypeMask         EQU 60h ;Mask for Type
        SRRTTypeStandard   EQU 00h ;Type 0 = Standard
        SRRTTypeClass      EQU 20h ;Type 1 = Class
        SRRTTypeVendor     EQU 40h ;Type 2 = Vendor
                                   ;Type 3 = Reserved
      SRRTRecipientMask    EQU 1Fh ;Mask for Recipient Type
        SRRTRecipDevice    EQU 00h ;Recipient Type 0 = Device
        SRRTRecipInterface EQU 01h ;Recipient Type 1 = Interface
        SRRTRecipEndPoint  EQU 02h ;Recipient Type 2 = EndPoint
        SRRTRecipOther     EQU 03h ;Recipient Type 3 = Other
                                   ;Types 4-31 = Reserved
    SRRequest               DB  ?  ;Specific Request
      SRRQGetStatus        EQU  0  ;Request  0 = Get Status
      SRRQClearFeature     EQU  1  ;Request  1 = Clear Feature
      SRRQGetState         EQU  2  ;Request  2 = Get State
      SRRQSetFeature       EQU  3  ;Request  3 = Set Feature
                                   ;Request  4 = Reserved
      SRRQSetAddress       EQU  5  ;Request  5 = Set Address
      SRRQGetDescriptor    EQU  6  ;Request  6 = Get Descriptor
      SRRQSetDescriptor    EQU  7  ;Request  7 = Set Descriptor
      SRRQGetConfig        EQU  8  ;Request  8 = Get Configuration
      SRRQSetConfig        EQU  9  ;Request  9 = Set Configuration
      SRRQGetAltInterface  EQU 10  ;Request 10 = Get Alternate Interface
      SRRQSetAltInterface  EQU 11  ;Request 11 = Set Alternate Interface
      SRRQSynchFrame       EQU 12  ;Request 12 = Synch Frame
    SRValue                 DW  ?  ;Value depends on Request Type
    SRIndex                 DW  ?  ;Index depends on Request Type
      ;If EndPoint,  Bits 3:0 = EndPoint Number
      ;              Bit    7 = 1 if IN EndPoint, 0 if OUT EndPoint
      ;              All other bits are 0
      ;If Interface, Bits 7:0 = Interface Number (low word)
      ;              All other bits are 0 (high word)
    SRLength                DW  ?  ;Length of Data (if there is Data)

   ENDS
  SetupRequestSize EQU TYPE (SetupRequest)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get Device Descriptor
  ;----------------------------------------------------------------------------
  DeviceDescriptor   STRUC    ;Structure for a Device Descriptor
    DDLength            DB  ? ;Size of Descriptor in bytes
    DDDescrType         DB DescrTypeDevice
    DDUSBRelease        DW  ? ;USB Compliance  bcd (0210h=v2.10)
    DDDvcClass          DB  ? ;Device Class (defined by USB-IF)
    DDDvcSubClass       DB  ? ;Device SubClass (defined by USB-IF)
    DDDvcProtocol       DB  ? ;Device Protocol (defined by USB-IF)
    DDMaxPktSize        DB  ? ;Max Packet Size for EndPoint 0
                              ;  MUST be 8, 16, 32, or 64
    DDVendorID          DW  ? ;Vendor ID (defined by USB-IF)
    DDProductID         DW  ? ;Product ID (defined by manufacturer)
    DDDeviceRelease     DW  ? ;Device Release Number (bcd)
    DDManufacturerIndx  DB  ? ;Index of Manufacturer String
    DDProductIndx       DB  ? ;Index of Product String
    DDSerialNumberIndx  DB  ? ;Index of Serial Number String
    DDNumConfigs        DB  ? ;Number of possible Configurations
   ENDS
  DeviceDescriptorSize EQU TYPE (DeviceDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get Configuration Descriptor
  ;----------------------------------------------------------------------------
  ConfigDescriptor     STRUC     ;Structure for a Config Descriptor
    CDLength              DB  ?  ;Size of Descriptor in bytes
    CDDescrType           DB DescrTypeConfig
    CDTotalLength         DW  ?  ;Total length of data returned for
                                 ;  this configuration (ALL data)
    CDNumInterfaces       DB  ?  ;Number of Interfaces in this Config
    CDConfigValue         DB  ?  ;Value to use in SetRequest to
                                 ;  set this configuration
    CDConfigurationIndx   DB  ?  ;Index of Configuration String
    CDAttributes          DB  ?  ;Configuration Attributes
      CDAttrBit7         EQU 80h ;Bit 7 must be set to 1
      CDAttrSelfPowered  EQU 40h ;Self Powered
      CDAttrRemoteWakeup EQU 20h ;Remote Wakeup Supported
      CDAttrBatteryPower EQU 10h ;Battery Powered
      ;Bits 3:0 = Reserved (0)
    CDMaxBusPower         DB  ?  ;Maximum Power Consumption FROM BUS
                                 ;  in 2mA units (*2 to get mA)
   ENDS
  ConfigDescriptorSize EQU TYPE (ConfigDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get Interface Descriptor
  ;----------------------------------------------------------------------------
  InterfaceDescriptor   STRUC     ;Structure for an Interface Descr
    IDLength               DB  ?  ;Size of Descriptor in bytes
    IDDescrType            DB DescrTypeInterface
    IDInterfaceNumber      DB  ?  ;Interface Number (0-based Index)
    IDAlternateSetting     DB  ?  ;Value used to select this alternate
                                  ;  setting for the Interface Number
    IDNumEndPoints         DB  ?  ;Number of Interfaces, excl 0
    IDInterfaceClass       DB  ?  ;Interface Class (defined by USB-IF)
    IDInterfaceSubClass    DB  ?  ;Interface SubClass (def by USB-IF)
    IDInterfaceProtocol    DB  ?  ;Interface Protocol (def by USB-IF)
    IDInterfaceIndx        DB  ?  ;Index of Interface String
   ENDS
  InterfaceDescriptorSize EQU TYPE (InterfaceDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by a Get EndPoint Descriptor
  ;----------------------------------------------------------------------------
  EndPointDescriptor    STRUC     ;Structure for an EndPoint Descr
    EDLength               DB  ?  ;Size of Descriptor in bytes
    EDDescrType            DB DescrTypeEndPoint
    EDEndPointNumber       DB  ?  ;EndPoint Number
      EDDirectionIn       EQU 80h ;Direction = In (Ignored in Control EP's)
      ;Bits 6:4 = Reserved (0)
      EDNumberMask        EQU 0Fh ;EndPoint Number (0-15)
    EDAttributes           DB  ?  ;EndPoint attributes
      ;Bits 7:6 = Reserved (0)
      EDUsageTypeMask     EQU 30h ;Bits 5:4 = Usage Type
      EDUsageTypeSft      EQU  4  ;Bits to Shift by
        EDAttrImplicitFB  EQU 20h ;2 = Implicit Feedback Data EndPoint
        EDAttrFeedback    EQU 10h ;1 = Feedback EndPoint
        EDAttrData        EQU 00h ;0 = Data EndPoint
      EDSynchTypeMask     EQU 0Ch ;Bits 3:2 = Synchronization Type
      EDSynchTypeSft      EQU  2  ;Bits to Shift By
        EDAttrSynch       EQU 0Ch ;3 = Synchronous
        EDAttrAdaptive    EQU 08h ;2 = Adaptive
        EDAttrAsynch      EQU 04h ;1 = Asynchronous
      EDAttrTransTypeMask EQU 03h ;Bits 1:0 = Transfer Type
        EDAttrInterrupt   EQU 03h ;3 = Interrupt
        EDAttrBulk        EQU 02h ;2 = Bulk
        EDAttrIsoch       EQU 01h ;1 = Isochronous
        EDAttrControl     EQU 00h ;0 = Control
    EDMaxPktSize           DW  ?  ;Max Packet Size EndPoint can handle
    EDPollInterval         DB  ?  ;Polling Interval for Data Transfers
    ;The following two bytes only appear in some Isochronous Applications
    EDRefreshRate          DB  ?  ;Refresh Rate (1-9, power of 2 mS)
    EDSynchEndPoint        DB  ?  ;Synchronization EndPoint
   ENDS
  EndPointDescriptorSize EQU TYPE (EndPointDescriptor)

  ;----------------------------------------------------------------------------
  ;Format of data returned by an HID (Human Interface Device) Descriptor
  ;----------------------------------------------------------------------------
  HIDDescriptor       STRUC     ;Structure for an HID Descriptor
    HIDDLength           DB ?   ;Size of Descriptor in bytes
    HIDDDescrType        DB DescrTypeHID
    HIDDClassRelease     DW ?   ;HID Class Release Spec (BCD)
    HIDDCountryCode      DB ?   ;Country Code
                                ;0 = None/NA, 21h = US
    HIDDNumClasses       DB ?   ;Number of Class Descriptors
    HIDDDescriptorType   DB ?   ;Type of Class Descriptor
                                ;DescrTypeHIDReport (22h) or
                                ;DescrTypeHIDPhysical (23h)
    HIDDDescriptorLength DW ?   ;Length of Class Descriptor
     ;Repeat Type and Length for each Class Descriptor
   ENDS
  HIDDescriptorSize EQU 9
   ;HIDDescriptorSize EQU TYPE (HIDDescriptor)


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;MACROS
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

  JUMP MACRO     ;Do a NEAR (SHORT) JMP instead of a Local JMP
    JMP SHORT #1
   #EM

  SwapDSES MACRO ;Swap DS and ES with each other
    PUSH DS
    PUSH ES
    POP  DS
    POP  ES
   #EM


;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
;TSR DATA
;ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ

;==============================================================================
;TSR Related
;==============================================================================

  ;------------------------------------------------------------------------------
  ;INT2F Data (this needs to be the first data at the top of the file)
  ;If the user does a "TYPE USBMOUSE.COM" at the command line, this will
  ;  just show the program name and version, instead of sending all kinds of
  ;  annoying happy faces and BELs to the screen the way most programs do.
  ;This is also the same data that is used to test if the program is
  ;  already installed in memory.
  ;------------------------------------------------------------------------------
  NoType1:     DB               CR  ;To handle TYPE from command line
  ProgName:    DB        'USBMOUSE' ;Program Name
  ProgNameLen EQU       $-ProgName  ;Length of name
  ProgVerTxt:  DB           ' 0.10' ;Program version in ASCII
  Author:      DB '  Bret Johnson' ;Authors name
  AuthorLen   EQU         $-Author  ;Length of Author
  NoType2:     DB              EOF  ;To handle TYPE from command line
  ProgVer      DW             0010h ;Program version as a word
  HandleNum    DB              0C0h ;TSR multiplex handle number

  ;------------------------------------------------------------------------------
  ;Miscellaneous Yes/No TSR Flags
  ;------------------------------------------------------------------------------
  TSRFlags          DB 00h ;Miscellaneous Yes/No Flags
    Int15Enabled   EQU 01h ;Int 15 Far Call function Enabled?
    ScalingFactor2 EQU 02h ;Scaling Factor is 2:1?
    Int14ReqInUse  EQU 04h ;Int 14 Request currently being used
    FarCallInProg  EQU 08h ;Mouse User Far Call already in progress
    SwapBtns       EQU 10h ;Swap L/R Buttons Globally
    NoBeep         EQU 20h ;Don't Beep the Speaker
    EmuHardware    EQU 40h ;Use Hardware Emulation (rather than BIOS)
    EOIIssued      EQU 80h ;End-of-Interrupt (OUT A0h,20h) occured

  TSRFlags2      DB 00h ;Miscellaneous Yes/No Flags #2
    DisableAll  EQU 01h ;Disable All Mice (used while showing RawData)
    Int08InUse  EQU 02h ;Int 08h already in use
    Int70InUse  EQU 04h ;Int 70h already in use
    NoBeep1Time EQU 08h ;No Beep this One Time


;==============================================================================
;Stack Related
;==============================================================================
  StackInUse DB 0 ;Counter to keep track of "setup stack" calls
  OldSS      DW ? ;Original Stack Segment
  OldSP      DW ? ;Original Stack Pointer
  TempBX     DW ? ;Temporary Storage for BX while setting up Stack
  TempFlags  DW ? ;Temporary Storage for Flags while setting up Stack


;==============================================================================
;INT 70h (System Delay) Related
;==============================================================================
  OldA1           DB ? ;Original Port A1h Contents (PIC2 IRQ Mask)
;  ChangeThisByte: DB 0 ;Byte of memory for Event Timer to change


;==============================================================================
;PS2 Port BIOS Function related (Int 15h)
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Code to Call when another Program "Registers" as Int 15h Owner
  ;----------------------------------------------------------------------------
  EVEN 2
  MouseUserFarCall DD ? ;Mouse User Interrupt Handler

  ;----------------------------------------------------------------------------
  ;Device ID we return on calls (keeps track of IntelliMouse enables)
  ;----------------------------------------------------------------------------
  DeviceID DB DvcIDWheelNo

  ;----------------------------------------------------------------------------
  ;This keeps track of the user trying to Enable the Wheel of a PS2 Mouse
  ;To enable the Wheel, the user must Set the Sample rate to 200, 100, 80,
  ;  in that order, with no other Int 15h C2xxh calls in between
  ;To enable the 2nd Wheel, the user must Set the Sample rate to 200, 200, 80,
  ;  in that order, with no other Int 15h C2xxh calls in between
  ;----------------------------------------------------------------------------
  EnableWheelCounter  DB 0 ;Needed to know when user wants to Enable Wheel
  EnableWheelCounter2 DB 0 ;Needed to know when user wants to Enable 2nd Wheel

  ;;----------------------------------------------------------------------------
  ;;Timer to keep track of IntelliMouse enables
  ;;If there is too much time elapsed between the first and last Enable
  ;;  setting (200-100-80 or 200-200-80), we will ignore the enable.
  ;;----------------------------------------------------------------------------
  ;IntelliTimer DB ?

  ;----------------------------------------------------------------------------
  ;Size of Data returned by Int15 calls
  ;----------------------------------------------------------------------------
  Int15DataSize DB 3 ;3 if user does NOT want to see Wheel data,
                     ;4 they DO want to see it

  ;----------------------------------------------------------------------------
  ;Data needed to store Current Mouse Resolution (Counts/mm)
  ;----------------------------------------------------------------------------
  Resolution DB Resolution4 ;Resolution in Counts/mm (Default = 4)

  ;----------------------------------------------------------------------------
  ;Data needed to Store Current Sample Rate (Periodicity)
  ;----------------------------------------------------------------------------
  SampleRateCode DB SampleCode100 ;Default Sample Rate = 100Hz

  ;----------------------------------------------------------------------------
  ;Data needed to Set/Change Sample Rate (Periodicity)
  ;The Int 15h functions are issued with a Code Number (0-6)
  ;This Lookup Table converts the Code to an actual Sample Rate, and to a
  ;  Periodicity Factor (Divisor) needed for the USB 1000Hz Interrupt Rate
  ;----------------------------------------------------------------------------
  SampleRateTable:
    DB   SampleCode010 ;Code from Int 15 Function
      DB            10 ;Corresponding Interrupt Rate (Hz)
      DB           100 ;Periodicity Factor (1000Hz divisor)
    DB   SampleCode020
      DB            20
      DB            50
    DB   SampleCode040
      DB            40
      DB            25
    DB   SampleCode060
      DB            60
      DB            17
    DB   SampleCode080
      DB            80
      DB            13
    DB   SampleCode100
      DB           100
      DB            10
   ; DB   SampleCode200
   ;   DB           200
   ;   DB             5
    DB   SampleCode200   ;We actually use 100 as the maximum,
      DB           100   ;  since 200 doesn't seem to work
      DB            10   ;  very well!!
    DB -1                ;End of Table
      DW -1

  ;----------------------------------------------------------------------------
  ;Data needed to compensate for 2:1 Scaling Factor
  ;Instead of blindly multiplying everything by 2, setting the Scaling
  ;  Factor to 2:1 actually uses special algorithm when the amount of
  ;  movement is small (<=5).  Only when the amount of movement is
  ;  large (>5) does the Mouse actually multiply the Actual movement
  ;  factor by 2.
  ;This Table contains the data needed by the Scaling Factor algorithm
  ;  data for small movements.  The Table contains the Reported Movement
  ;  when the Actual Movement is small.  The line number of the Table
  ;  (0-5) is the Actual movement factor.
  ;If the Scaling Factor is 1:1 (the default), this Table is not used.
  ;----------------------------------------------------------------------------

  ScalingTbl:
;Reported Actual
;-------- ------
    DB 0 ;0
    DB 1 ;1
    DB 1 ;2
    DB 3 ;3
    DB 6 ;4
    DB 9 ;5

  ;----------------------------------------------------------------------------
  ;Int 15h PS2 Port SubFunctions (in AL on Int 15h entry)
  ;----------------------------------------------------------------------------
  Int15FunctionTbl:
    DB I15ALEnableDisable    ;Enable/Disable
     DW PS2EnableDisable
    DB I15ALReset            ;Reset
     DW PS2Reset
    DB I15ALSetSampling      ;Set Sampling Rate
     DW PS2SetSampleRate
    DB I15ALSetResolution    ;Set Resolution
     DW PS2SetResolution
    DB I15ALGetDeviceID      ;Get Device ID
     DW PS2GetDeviceIDBH
    DB I15ALInitialize       ;Initialize
     DW PS2Initialize
    DB I15ALStatusScaling    ;Get Status / Set Scaling
     DW PS2GetStatus
    DB I15ALSetHandler       ;Set Handler Address
     DW PS2SetHandlerAddress
    ;Functions 8 (Write to Port) and 9 (Read from Port)
    ;  not supported by us!
    DB I15ALGetHandler       ;Get Handler Address
     DW PS2GetHandlerAddress
    DB -1                    ;End of Table
     DW -1

;==============================================================================
;Mouse Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Method to use to "type" a Mouse Data Byte
  ;0 = Not Configured yet, 1-3 = valid Methods
  ;----------------------------------------------------------------------------
  Method DB 0

  ;----------------------------------------------------------------------------
  ;Data needed to send a Data Byte using Method 3
  ;----------------------------------------------------------------------------
  EVEN 2
  Int01Vect    DD  ? ;Old Interrupt 01 (SST) Vector
  IntXXFlags   DW  ? ;For Interrupt Stack Calls
  IntXXVect    DD  ? ;For Interrupt Stack Calls
  DoNextTime   DB No ;Fake the Byte again next time?
  ByteToSend   DB  ? ;Data Byte to send
  Int01Counter DB  0 ;Counter for recursive calls to Int01
  InstrCounter DW  0 ;Instruction Counter for Int 01h (for OUT A0h,20h)

  ;----------------------------------------------------------------------------
  ;Circular Buffer to store Data Bytes we need to send.
  ;Each time something happens, we will either send 3 or 4 data bytes
  ;  (whether 3 or 4 depends on how the MOuse is configured).
  ;Our process involves filling this buffer with Data Bytes as we process the
  ;  USB Codes, and then "typing" the contents of the buffer after we're
  ;  done.
  ;This is a circular buffer, which is safer than a linear buffer
  ;  (it is "protected" from us trying to fill it with too much data)
  ;----------------------------------------------------------------------------
  Buffer:      DB BufferSize DUP (?)  ;Buffer for Scan Codes
  BufferHead   DW Buffer+1            ;Pointer to Buffer Head
  BufferTail   DW Buffer              ;Pointer to Buffer Tail
  BufferCount  DB 0                   ;Number of codes currently in buffer
  BufferEnd   EQU Buffer+BufferSize-1 ;End of buffer

;  ;----------------------------------------------------------------------------
;  ;INT 09 (Keyboard) Vector
;  ;The Keyboard and the PS2 Mouse share the same hardware, including the
;  ;  same I/O Ports (60h-64h).
;  ;When performing Hardware Emulation, we "fake" keystrokes to port 60h,
;  ;  which for some unknown reason get intercepted by the keyboard handler
;  ;  (IRQ 01/INT 09) instead of the PS2 Mouse Handler (IRQ 12/INT 74h).
;  ;While doing our HW emulation, we temporarily intercept INT 09 ourselves
;  ;  to make sure our codes don't get intercepted by the keyboard handler
;  ;  by mistake.
;  ;----------------------------------------------------------------------------
;  Int09Vect DD ?


;==============================================================================
;USB Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Handle Number of New Device (Device 0) Registry
  ;----------------------------------------------------------------------------
  Reg0Handle DW ?

  ;----------------------------------------------------------------------------
  ;Int08 Countdown Timers
  ;----------------------------------------------------------------------------
  Int08Counter    DB 5 ;Countdown Timer to periodically check for new Devices
                       ;Initial Delay is 5 timer ticks (about 1/4 second)
                       ;Delays after initial one will be about 1 second
                       ;Setting to 0 Disables the program
  DontLookCounter DW 0 ;Countdown Timer to not Look for new Devices

  ;----------------------------------------------------------------------------
  ;Calls that the USB Host Driver can send to us after we're "registered"
  ;Our code is called with this function code in AX
  ;----------------------------------------------------------------------------
  OwnerCallFunctionTbl:
   ;Device Connect/Disconnect
    DB OwnerCallNewDvc        ;New Device Connected
     DW DeviceAdded
    DB OwnerCallDvcDisc       ;Existing Device Disconnected
     DW DeviceDisconnected
    DB OwnerCallDvc0Disc      ;Unregistered Device Disconnected
     DW Device0Disconnected
    DB OwnerCallDontLook      ;Don't Look for New Interface Temporarily
     DW StartDontLook
   ;Device Enable/Disable/Reset/Suspend/Resume/PwrOn/PwrOff/PwrReset
    DB OwnerCallDvcEnable     ;Device Enabled
     DW DeviceEnabled
    DB OwnerCallDvcDisable    ;Device Disabled
     DW DeviceDisabled
    DB OwnerCallDvcReset      ;Device Reset
     DW DeviceReset
    DB OwnerCallDvcSuspend    ;Device Suspend
     DW DeviceSuspend
    DB OwnerCallDvcResume     ;Device Resume
     DW DeviceResume
    DB OwnerCallDvcPwrOn      ;Device Power On
     DW DevicePowerOn
    DB OwnerCallDvcPwrOff     ;Device Power Off
     DW DevicePowerOff
    DB OwnerCallDvcPwrReset   ;Device Power Reset
     DW DevicePowerReset
   ;Host Connect/Disconnect
    DB OwnerCallNewHost       ;New Host has been Added
     DW NewHostAdded
    DB OwnerCallHostDisc      ;Existing Host Disconnected
     DW HostDisconnected
    DB OwnerCallHostHWErr     ;Host System/Processor Error
     DW HostHWErr
   ;Host Run/Stop/Reset/Suspend/Resume/Debug/SingleStep
    DB OwnerCallHostRun       ;Host Running
     DW HostRun
    DB OwnerCallHostStop      ;Host Stopped
     DW HostStop
    DB OwnerCallHostReset     ;Host Reset
     DW HostReset
    DB OwnerCallHostSuspend   ;Host Suspended
     DW HostSuspend
    DB OwnerCallHostResume    ;Host Resumed
     DW HostResume
    DB OwnerCallHostDebug     ;Host in Debug Mode
     DW HostDebug
    DB OwnerCallHostSST       ;Host Single-Stepped
     DW HostSST
   ;Timing Changes
    DB OwnerCallTimingChg     ;Host Timing Changed
     DW TimingChange
   ;Upstream Device Changes
    DB OwnerCallDvcDiscUS     ;Device Disconnect Upstream
     DW DeviceDisconnectedUS
    DB OwnerCallDvcEnableUS   ;Device Enable Upstream
     DW DeviceEnabledUS
    DB OwnerCallDvcDisableUS  ;Device Disable Upstream
     DW DeviceDisabledUS
    DB OwnerCallDvcResetUS    ;Device Reset Upstream
     DW DeviceResetUS
    DB OwnerCallDvcSuspendUS  ;Device Suspend Upstream
     DW DeviceSuspendUS
    DB OwnerCallDvcResumeUS   ;Device Resume Upstream
     DW DeviceResumeUS
    DB OwnerCallDvcPwrOnUS    ;Device Power On Upstream
     DW DevicePowerOnUS
    DB OwnerCallDvcPwrOffUS   ;Device Power Off Upstream
     DW DevicePowerOffUS
    DB OwnerCallDvcPwrResetUS ;Device Power Reset Upstream
     DW DevicePowerResetUS
   ;End of Table
    DB -1                     ;End of Table
     DW -1

  ;----------------------------------------------------------------------------
  ;Table to keep track of individual Devices
  ;----------------------------------------------------------------------------
  EVEN 2
  DeviceInfoTable:
    DB ((MaxDevices+1)*DeviceInfoStrucSize) DUP (0) ;Table of Dvc Data
    DIPS2Offset EQU (DeviceInfoTable+(MaxDevices*DeviceInfoStrucSize))
                                                    ;Last entry is PS2 Mouse

  ;----------------------------------------------------------------------------
  ;Table to keep track of "bad" (incompatible) Devices.
  ;This table will keep track of Host Indexes and Address of incompatible
  ;  Devices that we find.
  ;If we don't keep track of them, we will get caught in an endless
  ;  loop of looking at the bad one every time we check (if it happens
  ;  to be the first one), and will never skip over it and be
  ;  able to find a "good" (compatible) Device.
  ;----------------------------------------------------------------------------
  EVEN 2
  BadDeviceTable:
    DB (MaxBadDevices*BadDeviceStrucSize) DUP (0)

  ;----------------------------------------------------------------------------
  ;Offset in InfoTable of Last Device we found (needed to print Descriptors)
  ;----------------------------------------------------------------------------
  LastDeviceFound DW DeviceInfoTable

  ;----------------------------------------------------------------------------
  ;Stage & Error Code where New Device search failed (for troubleshooting)
  ;----------------------------------------------------------------------------
  NewDeviceStage DB ?
  NewDeviceError DW ?

  ;----------------------------------------------------------------------------
  ;Data Structures needed to issue Requests to the USB Host
  ;----------------------------------------------------------------------------
  EVEN 2
  Int14Request:
    DB Int14RequestSize DUP (0)
  Int14Request2:
    DB Int14RequestSize DUP (0)

  ;----------------------------------------------------------------------------
  ;Storage Area for Data returned by USB Descriptors and Reports
  ;----------------------------------------------------------------------------
  DvcDescrData:
    DB DeviceDescriptorSize DUP (?) ;Device Descriptor Data
  DescriptorData:
    DB MaxDescriptorSize    DUP (?) ;Actual Descriptor Data
  ReportData:
    DB MaxReportSize        DUP (?) ;Actual Report Data


;==============================================================================
;USB Mouse/HID Class-Specific Control Requests
;==============================================================================
  EVEN 2

  ;----------------------------------------------------------------------------
  ;Get Report Descriptor
  ;----------------------------------------------------------------------------
  GetReportDescrReq:
    DB SRRTIn+SRRTTypeStandard+SRRTRecipInterface
    DB SRRQGetDescriptor
    DB ? ;Report ID
    DB DescrTypeHIDReport
    DB ? ;Interface Number
    DB 0
    DW ? ;Number of Bytes

  I14RGetReportDescrRptID EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RGetReportDescrIntf  EQU I14RSetupReqData.SRIndex[0] ;Byte
  I14RGetReportDescrSize  EQU I14RSetupReqData.SRLength   ;Word

  ;----------------------------------------------------------------------------
  ;Set Idle Rate
  ;  Silences Report on Interrupt In EndPoint until new event or specified time
  ;  Recommended DEFAULT idle rate is 500ms for keyboards (delay before
  ;    first rpt) and infinity for joysticks and mice
  ;----------------------------------------------------------------------------
  SetHIDIdleReq:
    DB SRRTOut+SRRTTypeClass+SRRTRecipInterface
    DB HIDRQSetIdle
    DB ? ;Report ID (0 = AllReports)
    DB ? ;Duration in 4ms increments (+/- 10% + 2ms)
         ;0 = infinite (only report changes)
         ;If less than polling rate, polling rate is used
    DB ? ;Interface Number
    DB 0
    DW 0

  I14RSetHIDIdleRptID    EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RSetHIDIdleDuration EQU I14RSetupReqData.SRValue[1] ;Byte
  I14RSetHIDIdleIntf     EQU I14RSetupReqData.SRIndex[0] ;Byte

;  ;----------------------------------------------------------------------------
;  ;Set Protocol
;  ;----------------------------------------------------------------------------
;  SetHIDProtocolReq:
;    DB SRRTOut+SRRTTypeClass+SRRTRecipInterface
;    DB HIDRQSetProtocol
;    DB HIDProtocolReport ;Protocol Type = Report (we do not use Boot Protocol)
;    DB 0
;    DB ? ;Interface Number
;    DB 0
;    DW 0
;
;  I14RSetHIDProtocolIntf EQU I14RSetupReqData.SRIndex[0] ;Byte


;==============================================================================
;USB General-Purpose Control Requests
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Get Device Descriptor
  ;----------------------------------------------------------------------------
  GetDvcDescrReq:
    DB SRRTIn+SRRTTypeStandard+SRRTRecipDevice
    DB SRRQGetDescriptor
    DB 0
    DB DescrTypeDevice
    DW 0
    DW DeviceDescriptorSize

  ;----------------------------------------------------------------------------
  ;Get Configuration Descriptor
  ;----------------------------------------------------------------------------
  GetCfgDescrReq:
    DB SRRTIn+SRRTTypeStandard+SRRTRecipDevice
    DB SRRQGetDescriptor
    DB 0 ;Configuration Value to Get (always 0?)
    DB DescrTypeConfig
    DW 0
    DW ConfigDescriptorSize ;Size will change for a Complete Config

  I14RGetCfgDescrValue EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RGetCfgDescrSize  EQU I14RSetupReqData.SRLength   ;Word

  ;----------------------------------------------------------------------------
  ;Set Alternate Interface
  ;----------------------------------------------------------------------------
  SetAltIntfReq:
    DB SRRTOut+SRRTTypeStandard+SRRTRecipInterface
    DB SRRQSetAltInterface
    DB ? ;Alternate Value to set Interface to
    DB 0
    DB ? ;Interface to Set
    DB 0
    DW 0

  I14RSetAltIntfAltIntf EQU I14RSetupReqData.SRValue[0] ;Byte
  I14RSetAltIntfIntf    EQU I14RSetupReqData.SRIndex[0] ;Byte


;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
;TSR CODE
;ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ

;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE INT 2Fh (TSR Multiplex)
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HAVE INTERRUPT 2Fh RUN THIS PROGRAM EVERY TIME IT OCCURS
;Inputs:  AH = Function (Handle) number, C0h-FFh
;         AL = Subfunction number
;          (If MuxGetProgName): DI = 0
;Outputs:  (AL = MuxInstallChk):  AL = FFh if installed
;          (AL = MuxGetProgName): ES:[DI] = Pointer to ProgName
;          (AL = MuxGetProgVer):  AX = ProgVer (Program version number, AH=Major)
;          (AL = MuxGetAuthor):   ES:[DI] = Pointer to Author
;Changes:
;------------------------------------------------------------------------------
Int2FHdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int2F:
  STI                               ;Enable Interrupts
  CMP  AH,CS:HandleNum              ;Is this our handle?
  JE   Int2FInstall                 ;If so, jump to handle it
  CMP  W CS:Int2FHdr.OldVector[2],0 ;Is the Old Vector valid?
  JE   Int2FDone                    ;If not, handle it
  JMP  D CS:Int2FHdr.OldVector      ;If so, go to the next one in the chain
Int2FDone:                          ;Invalid vector before us
  IRET

Int2FInstall:
  CMP  AL,MuxInstallChk  ;Is it Install Check?
  JNZ  Int2FProgName     ;If not, try the next possibility
  MOV  AL,-1             ;Mark the flag byte
  IRET

Int2FProgName:
  CMP  AL,MuxGetProgName ;Is it Get Program Name?
  JNE  Int2FProgVer      ;If not, try the next possibility
  OR   DI,DI             ;Does DI=0?
  JNZ >N90               ;If not, it's not for us
  MOV  ES,CS             ;If so, point ES:[DI]
  MOV  DI,ProgName       ;  at ProgName
N90:                     ;Done
  IRET

Int2FProgVer:
  CMP  AL,MuxGetProgVer ;Is it Get Program Version?
  JNE  Int2FAuthor      ;If not, try the next one
  MOV  AX,CS:ProgVer    ;If so, do it
  IRET

Int2FAuthor:
  CMP  AL,MuxGetAuthor ;Is it Get Author?
  JNE >A90             ;If not, we're done
  MOV  ES,CS           ;If so, point ES:[DI]
  MOV  DI,Author       ;  at Author
A90:                   ;Done
  IRET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO LOOK FOR NEW USB DEVICES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;CHECK AND SEE IF THERE IS A NEW DEVICE CONNECTED, AND HANDLE IT IF IT IS
;Inputs:  DS = ES = Our Data Area
;         CLD already issued
;Outputs:
;Changes: Looks for new, unowned (unconfigured) Devices
;         If one is found, it is Configured and pointed at us
;NOTES:   When we first install this program into memory, we Registered with
;           the USB Drivers that we wanted to be informed as soon as a new
;           Device was attached.  We also poll the hosts periodically for a
;           new Device, just in case we somehow missed the Host informing us.
;         Whether this is in response to the Host telling us, or whether we
;           polled to find out, this section of code is called.
;         This section of code is actually a series of short pieces of code,
;           which are disjointed in time.  One section schedules a USB Request
;           with the Host Driver, and gives the Host Driver the address
;           of the NEXT section of code to perform when the Request is
;           completed.
;         Requests sent to a USB Host can take anywhere from a few
;           milliseconds to several hundred milliseconds to complete.
;         It is very bad for a TSR to just sit around waiting for something
;           to happen, especially when it can be a long time in coming.
;         This back-and-forth approach between the Host Driver and the Device
;           Driver makes the TSR run pretty efficiently.
;------------------------------------------------------------------------------
LookForNewDevice:
  CALL FindEmptyInfoBX               ;Look for an empty Info Table Entry (BX)
  JZ  >L901                          ;If none, just quit
  TEST TSRFlags,Int14ReqInUse        ;Is the Request Structure already in use?
  JNZ >L901                          ;If so, just quit
  OR   TSRFlags,Int14ReqInUse        ;If not, mark it as in use now
  MOV  SI,Int14Request               ;Point at our Int 14 Structure
  MOV  [SI].I14RRequestType,I14RRTFindUnRegIntf ;Request Type = Find Interface
  MOV  [SI].I14RHostIndex,-1         ;Any/All Hosts
  MOV  [SI].I14RSearchIndex,0        ;Start with Search Index 0
L10:                                 ;Loop to here to find Device
  CALL DoInt14CallCXDXSI             ;Do it (uses DS:SI, CH = Addr, CL = Host,
                                     ;  DL = Interface, DH = Alt Interface)
  JC  >L80                           ;If no device, we're done
  CALL TestBadDevice                 ;If one there, is it in our Bad List?
  JC  >L20                           ;If not, continue
  INC  [SI].I14RSearchIndex          ;If so, Increment the Search Index
  JMP  L10                           ;Keep looking
L20:                                 ;Found a possible Device!
  MOV  [SI].I14RRequestType,I14RRTConfigingIntf ;Request Type = Configing
  MOV  W [SI].I14RHostIndex,CX       ;Store Host Index & Device Address
  CALL DoInt14CallSaveAllSI          ;Do it
  JNC >L30                           ;If OK, continue
  MOV  Int08Counter,2                ;If not, Look again Soon
  JMP >L80                           ;Done for now
L901:                                ;To avoid JMP > 128
  JMP >L90
L30:                                 ;OK to Configure Interface
  MOV  NewDeviceStage,0              ;Stage
  MOV  NewDeviceError,0              ;  Zero
  CALL TestAltIntfMouse              ;Is it possibly a Mouse?
  JC  >L75                           ;If not, mark it as Bad
  MOV  NewDeviceStage,1              ;Update Stage
  OR   [BX].DIFlags,DIFlagInUse      ;Mark Info Table entry as in use
  MOV  W [BX].DIHostIndex,CX         ;Store Host Index & Device Address
  MOV  [BX].DIInterfaceNum,DL        ;Store Interface Number
  MOV  [SI].I14RUserPktID,BX         ;Set User Pkt ID to Info Table Offset
  MOV  [SI].I14RRequestType,I14RRTDoControl ;Request Type = Control
;  MOV  [SI].I14RInterfaceNum,DL      ;Store Interface Number
  MOV  [SI].I14REndPoint,0           ;EndPoint 0
  MOV  CX,SetAltIntfReq              ;Copy
  CALL CopySetupReqToInt14SI         ;  Request Pkt
  MOV  B [SI].I14RSetAltIntfIntf,DL          ;Store Interface in Request
  MOV  B [SI].I14RSetAltIntfAltIntf,DH       ;Store Alt Interface in Request
  MOV  W [SI].I14RCallBackAddr[0],AltIntfSet ;Go here when done
  OR   [SI].I14RFlags,I14RFlagNoRetries ;No retries
  CALL DoInt14CallAXSI               ;Do it
  JNC >L90                           ;If it worked, we're done
L70:                                 ;Error
  MOV  NewDeviceStage,2              ;Update Stage
L75:                                 ;Mark as Bad
  CALL FinishUpBadDevice             ;If not, mark it as bad
  JMP >L90                           ;Done
L80:                                 ;No New Devices
  AND  TSRFlags,(NOT Int14ReqInUse)  ;Mark the request as available
L90:                                 ;Done
  RET                                ;NOT RETF here!

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;NOTE: Some Devices will not accept a Set Alternate Interface Request,
;        so an error here does not necessarily indicate a bad Device!
;------------------------------------------------------------------------------
AltIntfSet:
  MOV  NewDeviceStage,10     ;Update Stage
  CALL ClearDescriptorData   ;Clear out the data buffer
  MOV  SI,Int14Request       ;Point at Int14 Request Struc
  AND  [SI].I14RFlags,(NOT I14RFlagNoRetries) ;Allow retries again
  MOV  CX,GetDvcDescrReq     ;Copy
  CALL CopySetupReqToInt14SI ;  Request Pkt
  MOV  W [SI].I14RDataAddress[0],DvcDescrData    ;Put data here
  MOV  W [SI].I14RCallBackAddr[0],GotDeviceDescr ;Go here when done
  CALL DoInt14CallAXSI       ;Do it
  JNC >I90                   ;If it worked, we're done
  MOV  NewDeviceStage,11     ;Update Stage
I70:                         ;Error
  CALL FinishUpBadDevice     ;Mark it as Bad
I90:                         ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
GotDeviceDescr:
  MOV  NewDeviceStage,20     ;Update Stage
  JNZ >D70                   ;If Error, handle it
  MOV  SI,Int14Request       ;Point at Int14 Request Structure
  MOV  CX,GetCfgDescrReq     ;Copy
  CALL CopySetupReqToInt14SI ;  Request Pkt
  MOV  W [SI].I14RDataAddress[0],DescriptorData  ;Put data here
  MOV  W [SI].I14RCallBackAddr[0],GotConfigDescr ;Go here when done
  CALL DoInt14CallAXSI       ;Do it
  JNC >D90                   ;If it worked, we're done
  MOV  NewDeviceStage,21     ;Update Stage
D70:                         ;Error
  CALL FinishUpBadDevice     ;Mark it as Bad
D90:                         ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
GotConfigDescr:
  MOV  NewDeviceStage,30        ;Update Stage
  JNZ >P70                      ;If Error, handle it
;  JNZ >O70                      ;If Error, handle it
;  MOV  SI,Int14Request          ;Point at Int14 Request Structure
;  MOV  CX,SetHIDProtocolReq     ;Copy
;  CALL CopySetupReqToInt14SI    ;  Request Pkt
;  MOV  AL,[SI].I14RInterfaceNum               ;Set
;  MOV  B [SI].I14RSetHIDProtocolIntf,AL       ;  Interface Number
;  MOV  W [SI].I14RCallBackAddr[0],ProtocolSet ;Go here when done
;  CALL DoInt14CallAXSI          ;Do it
;  JNC >O90                      ;If it worked, we're done
;  MOV  NewDeviceStage,31        ;Update Stage
;O70:                            ;Error
;  CALL FinishUpBadDevice        ;Mark it as Bad
;O90:                            ;Done
;  RETF
;
;;------------------------------------------------------------------------------
;;Inputs: [BX] = Info Table Pointer
;;------------------------------------------------------------------------------
;ProtocolSet:
;  MOV  NewDeviceStage,40             ;Update Stage
;  JNZ >P70                           ;If Error, handle it
;  MOV  NewDeviceStage,41             ;Update Stage
  MOV  NewDeviceStage,31             ;Update Stage
  MOV  DI,DescriptorData             ;Point at Config Descriptor Data
  MOV  SI,Int14Request               ;Point at Int14 Request Structure
  MOV  AX,[DI].CDTotalLength         ;Get length of entire Descriptor
  CMP  AX,MaxDescriptorSize          ;Will it fit in our buffer?
  JAE >P70                           ;If not, error
  MOV  CX,GetCfgDescrReq             ;Copy
  CALL CopySetupReqToInt14SI         ;  Request Pkt
  MOV  W [SI].I14RGetCfgDescrSize,AX ;Store the number of bytes
  MOV  W [SI].I14RCallBackAddr[0],GotCompleteConfig ;Go here when done
  CALL DoInt14CallAXSI               ;Do it
  JNC >P90                           ;If it worked, we're done
;  MOV  NewDeviceStage,42             ;Update Stage
  MOV  NewDeviceStage,32             ;Update Stage
P70:                                 ;Error
  CALL FinishUpBadDevice             ;Mark it as Bad
P90:                                 ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
GotCompleteConfig:
  MOV  NewDeviceStage,50                ;Update Stage
  JNZ >C70                              ;If Error, handle it
  MOV  DI,DescriptorData                ;Point at Config Descriptor Data
  MOV  SI,Int14Request                  ;Point at Int14 Request Structure
  MOV  CX,SetHIDIdleReq                 ;Copy
  CALL CopySetupReqToInt14SI            ;  Request Pkt
  MOV  AL,[SI].I14RInterfaceNum         ;Set
  MOV  B [SI].I14RSetHIDIdleIntf,AL     ;  Interface Number
  ;MOV  B [SI].I14RSetHIDIdleRptID,0     ;Report ID 0 (All Reports)
  CALL CalcIdleRate                     ;Get Idle Rate (AL)
  MOV  B [SI].I14RSetHIDIdleDuration,AL ;Store it
  MOV  W [SI].I14RCallBackAddr[0],GetReportDescr ;Go here when done
  OR   [SI].I14RFlags,I14RFlagNoRetries ;No retries
  CALL DoInt14CallAXSI                  ;Do it
  JNC >C90                              ;If it worked, we're done
  MOV  NewDeviceStage,51                ;Update Stage
C70:                                    ;Error
  CALL FinishUpBadDevice                ;Mark it as Bad
C90:                                    ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;NOTE: Some Devices will not accept a Set HID Idle Duration Request,
;        so an error here does not necessarily indicate a bad Device!
;      The program still works OK even if the Mouse's Report Rate doesn't
;        match our Interrupt polling rate, so this isn't a real big deal.
;------------------------------------------------------------------------------
GetReportDescr:
  MOV  NewDeviceStage,200                   ;Update Stage
  MOV  SI,Int14Request                      ;Point at Int14 Request Structure
  AND  [SI].I14RFlags,(NOT I14RFlagNoRetries) ;Allow retries again
  MOV  DI,DescriptorData                    ;Point at Config Data
  MOV  BL,[BX].DIInterfaceNum               ;Interface Number to look for
R10:                                        ;Loop to find Interface Descriptor
  MOV  AL,DescrTypeInterface                ;Look for an Interface
  CALL FindDescrType                        ;  Descriptor (rtns AX, DI)
  JC  >R70                                  ;If none found, Error
  CMP  [DI].IDInterfaceNumber,BL            ;Is it the Correct Interface?
  JE  >R20                                  ;If so, continue
  ADD  DI,AX                                ;If not, point at the next Descr
  JMP  R10                                  ;Keep looking
R20:                                        ;Found the Interface Descriptor
  MOV  NewDeviceStage,201                   ;Update Stage
  MOV  AL,DescrTypeHID                      ;Look for an HID
  CALL FindDescrType                        ;  Descriptor (rtns AX, DI)
  JC  >R70                                  ;If none found, Error
  MOV  NewDeviceStage,202                   ;Update Stage
  MOV  AX,[DI].HIDDDescriptorLength         ;Get Report Size
  CMP  AX,MaxReportSize                     ;Is it too big for our buffer?
  JAE >R70                                  ;If so, error
  MOV  CX,GetReportDescrReq                 ;Copy
  CALL CopySetupReqToInt14SI                ;  Request Pkt
  MOV  W [SI].I14RGetReportDescrSize,AX     ;Store number of bytes
  MOV  B [SI].I14RGetReportDescrIntf,BL     ;Store Interface Number
  ;MOV  B [SI].I14RGetReportDescrRptID,0     ;Report ID 0 (all reports)
  MOV  W [SI].I14RDataAddress[0],ReportData ;Where to put data
  MOV  W [SI].I14RCallBackAddr[0],SetupInts ;Go here when done
  CALL ClearReportData                      ;Clear out Report Data area
  MOV  NewDeviceStage,61                    ;Update Stage
  CALL DoInt14CallAXSI                      ;Do it
  JNC >R90                                  ;If it worked, we're done
R70:                                        ;Error
  CALL FinishUpBadDevice                    ;Mark it as Bad
R90:                                        ;Done
  RETF

;------------------------------------------------------------------------------
;Inputs: [BX] = Info Table Pointer
;------------------------------------------------------------------------------
SetupInts:
  MOV  NewDeviceStage,70            ;Update Stage
  JNZ >I701                         ;If error, mark it as bad
  MOV  NewDeviceStage,210           ;Update Stage
  MOV  SI,Int14Request              ;Point SI at Int14 Request
  CALL DecipherReportDescr          ;Get Device Info from Report
  JC  >I701                         ;If invalid, quit
  MOV  NewDeviceStage,211           ;Update Stage
  CALL DecipherReportDescrSys       ;If OK, look for System Keys
  MOV  DI,DescriptorData            ;Point DI at Config Data
  MOV  CL,[BX].DIInterfaceNum       ;Interface Number to look for
I10:                                ;Loop to find Interface Descr
  MOV  AL,DescrTypeInterface        ;Look for an Interface
  CALL FindDescrType                ;  Descriptor (rtns AX, DI)
  JC  >I701                         ;If none found, Error
  MOV  NewDeviceStage,212           ;Update Stage
  CMP  [DI].IDInterfaceNumber,CL    ;Is it the Correct Interface?
  JE  >I20                          ;If so, continue
  ADD  DI,AX                        ;If not, point at the next Descr
  JMP  I10                          ;Keep looking
I20:                                ;Found the Interface Descriptor
  MOV  AL,DescrTypeEndPoint         ;Look for an EndPoint
  CALL FindDescrType                ;  Descriptor (rtns AX, DI)
  JC  >I701                         ;If none found, Error
  MOV  NewDeviceStage,213           ;Update Stage
  MOV  [SI].I14RRequestType,I14RRTDoInterruptPer ;Periodic Interrupt
  MOV  [SI].I14RFlags,I14RFlagIn    ;Assume Direction = In
  CALL CalcPeriodicity              ;Get Periodicity to use (AX)
  MOV  [SI].I14RPeriodicity,AX      ;Store it
  MOV  NewDeviceStage,214           ;Update Stage
  MOV  AX,[DI].EDMaxPktSize         ;Get Data Size
  CMP  AX,3                         ;Is it too small?
  JB  >I70                          ;If so, error
  CMP  AX,8                         ;Is it too big?
  JBE >I30                          ;If not, continue
I701:                               ;To avoid JMP > 128
  JMP >I70
I30:                                ;Data Size is OK
  MOV  [SI].I14RDataSize,AX         ;If OK, store it
  MOV  NewDeviceStage,215           ;Update Stage
  MOV  AL,[DI].EDEndPointNumber     ;Get EP Address & Direction
  TEST AL,EDDirectionIn             ;Is it In (it better be!)
  JZ  >I70                          ;If not, error
  MOV  NewDeviceStage,216           ;Update Stage
  AND  AL,EDNumberMask              ;Store the
  MOV  [SI].I14REndPoint,AL         ;  EndPoint
  MOV  [BX].DIEndPoint,AL           ;  Number
  LEA  AX,[BX].DIRawData            ;Where to
  MOV  W [SI].I14RDataAddress[0],AX ;  put data
  CMP  [BX].DIReportID,0            ;Is there a Report ID?
  JE  >I40                          ;If not, Data Address is OK
  DEC  W [SI].I14RDataAddress[0]    ;If so, Data will include ID
I40:                                ;Data Address is OK
  MOV  W [SI].I14RCallBackAddr[0],DeviceChangeCode ;Code to call
  MOV  NewDeviceStage,73            ;Update Stage
  CALL DoInt14CallAXCXSI            ;Do it
  JC  >I70                          ;If error, quit
  MOV  [BX].DIIntHandle,CX          ;Save the Interrupt Handle
  MOV  LastDeviceFound,BX           ;Mark this as the Last Device
  MOV  [SI].I14RRequestType,I14RRTRegIntfOwner ;Register as Owner
  MOV  W [SI].I14RCallBackAddr[0],DvcOwnerFarCall ;Owner Address
  CALL DoInt14CallAXCXSI            ;Do it
  MOV  [BX].DIRegHandle,CX          ;Store the Registry Handle
  JNC >I80                          ;If it worked, we're done
  MOV  NewDeviceStage,75            ;Update Stage
  MOV  AX,[BX].DIIntHandle          ;If not,
  MOV  [SI].I14RRequestHandle,AX    ;  we need
  MOV  [SI].I14RRequestType,I14RRTCloseHandle ;  to stop the
  CALL DoInt14CallAXSI              ;  Periodic Interrupt
I70:                                ;Error
  CALL FinishUpBadDevice            ;Mark it as Bad
  JMP >I90                          ;Done
I80:                                ;Device is OK
  MOV  [SI].I14RRequestType,I14RRTConfigIntfDone ;Request = Done Configuring
  CALL DoInt14CallSaveAllSI         ;Do it
  CMP  Int08Counter,0               ;Disabled?
  JE  >I85                          ;If so, quit
  MOV  Int08Counter,2               ;Look for another Device soon
I85:                                ;Done for now
  AND  TSRFlags,(NOT Int14ReqInUse) ;Mark the request as available
I90:                                ;Done
  RETF


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;SUPPORT FUNCTIONS NEEDED BY LOOKFORNEWDEVICE PROCESS
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;TEST AND SEE IF THE DEVICE COULD POSSIBLY BE A MOUSE
;Inputs:  DS = ES = TSR Data Area
;         [SI] = Int 14 Request to use
;                  Host Index & Device Address filled in
;         DL = Interface Number
;         DH = Alternate Interface Number
;Outputs: CF = Clear if OK (Device is, or could be, a Mouse)
;            = Set if Not (cannot be a Mouse)
;                TSRFlags2.NoBeep1Time Set
;Changes: [SI] filled in with Interface, Alt Interface, & Request Type
;NOTES: The only thing this tests is the Interface SubClass & Protocol
;------------------------------------------------------------------------------
TestAltIntfMouse:
  PUSH BX,CX                    ;Save used registers
  MOV  [SI].I14RRequestType,I14RRTGetAltIntfInfo ;Request = Get Alt Intf Info
  MOV  [SI].I14RInterfaceNum,DL ;Store Interface
  MOV  [SI].I14RAltInterface,DH ;Store Alternate Interface
  CALL DoInt14CallBXCXSI        ;Issue the Request
  CMP  BH,IntfSubClassHIDBoot   ;Interface SubClass = None or Boot?
  JA  >F70                      ;If neither, Error
  JB  >F30                      ;If None, continue
F10:                            ;SubClass = Boot
  CMP  CL,IntfProtocolHIDMouse  ;Is it a Mouse?
  JE  >F80                      ;If so, continue
  JMP >F70                      ;If not, Error
F30:                            ;SubClass = None
  OR   CL,CL                    ;Protocol = None?
  JZ  >F80                      ;If so, continue
F70:                            ;Error
  OR   TSRFlags2,NoBeep1Time    ;Don't want to Beep
  STC                           ;Set return flag
  JMP >F90                      ;Done
F80:                            ;OK
  CLC                           ;Set return flag
F90:                            ;Done
  POP  CX,BX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FINISH UP A BAD DEVICE ENTRY
;Inputs:  BX = Info Table Offset
;         (Info Table Entry contains Device Data)
;         DS = ES = TSR Data Area
;         Int14Request (contains Host/Address/Interface Data)
;Outputs: Resets DeviceInfoTable Entry
;         Marks Int14Request as available (not in use any more)
;         Updates NewDeviceStage & NewDeviceError
;         Puts Device info in BadDevice Table
;         Issues Request to Beep the Speaker
;Changes: DI,SI
;------------------------------------------------------------------------------
FinishUpBadDevice:
  MOV  SI,Int14Request              ;Point at Int14 Request Struc
  MOV  NewDeviceError,AX            ;Store Error Code
  CALL ResetDvcEntryBX              ;Reset the Dvc Info Entry that we Reserved
  MOV  [SI].I14RRequestType,I14RRTGetDvcStatus ;Request = Get Dvc Status
                                               ;(Make sure Device is still
                                               ;  plugged in)
  CALL DoInt14CallSaveAllSI         ;Issue the Request
  JC  >B10                          ;If error (no Device), don't add to Bad Tbl
  CALL AddBadDevice                 ;Add it to Bad Table
B10:                                ;Bad Device added to table, if appropriate
  TEST TSRFlags,NoBeep              ;Are we allowed to Beep the Speaker?
  JNZ >B20                          ;If not, skip down
  TEST TSRFlags2,NoBeep1Time        ;Are we allowed to Beep the Speaker?
  JNZ >B20                          ;If not, skip down
  MOV  [SI].I14RRequestType,I14RRTBeepSpeaker ;Want to beep the Speaker
  MOV  [SI].I14RHostIndex,-1        ;Any Host
  MOV  B [SI].I14RTimeout,3         ;Beep length = 3 DeciSeconds
  MOV  W [SI].I14RBeepFrequency,300 ;Frequency = 300Hz
  CALL DoInt14CallSaveAllSI         ;Do it
  MOV  B [SI].I14RTimeOut,0         ;Reset Timeout Value
B20:                                ;Done Handling Beep
  AND  TSRFlags2,(NOT NoBeep1Time)  ;Mark as OK to Beep next time
  MOV  [SI].I14RRequestType,I14RRTConfigIntfDone ;Request = Done Configuring
  CALL DoInt14CallSaveAllSI         ;Do it
  CMP  Int08Counter,0               ;Disabled?
  JE  >B90                          ;If so, Done
  MOV  Int08Counter,2               ;Look for another Device soon
B90:                                ;Done
  AND  TSRFlags,(NOT Int14ReqInUse) ;Mark the request as available
  RET

;------------------------------------------------------------------------------
;CLEAR OUT THE OLD DATA FROM THE DESCRIPTOR DATA BUFFER
;Inputs:  ES = TSR Data Area
;         CLD already issued
;Outputs:
;Changes: DescriptorData becomes all zeroes
;------------------------------------------------------------------------------
ClearDescriptorData:
  PUSH AX,CX,DI        ;Save used registers
  MOV  DI,DvcDescrData ;Point at buffer
  MOV  CX,((DeviceDescriptorSize+MaxDescriptorSize)/2) ;Number of words to write
  XOR  AX,AX           ;Write zeroes
  REP  STOSW           ;Do it
  POP  DI,CX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CLEAR OUT THE OLD DATA FROM THE REPORT DATA BUFFER
;Inputs:  ES = TSR Data Area
;Outputs:
;Changes: ReportData becomes all zeroes
;------------------------------------------------------------------------------
ClearReportData:
  PUSH AX,CX,DI             ;Save used registers
  MOV  DI,ReportData        ;Point at buffer
  MOV  CX,(MaxReportSize/2) ;Number of words to write
  XOR  AX,AX                ;Write zeroes
  REP  STOSW                ;Do it
  POP  DI,CX,AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND A PARTICULAR TYPE OF DESCRIPTOR IN THE COMPLETE CONFIGURATION DESCRIPTOR
;Inputs:  DS = ES = TSR Data Area
;         [DI] = Pointer to start/middle of Configuration Descriptor
;          AL  = Descriptor Type to find
;Outputs: CF = Set if OK (Descriptor Found)
;              [DI] points at beginning of Descriptor
;               AX = Descriptor Length
;            = Clear if Error (Descriptor not found)
;              AX, DI unchanged
;Changes:
;------------------------------------------------------------------------------
FindDescrType:
  PUSH BX,SI               ;Save used registers
  MOV  SI,DI               ;Point SI at the starting Pointer
  XOR  BH,BH               ;Initialize high byte of Size to 0
Y10:                       ;Loop here to find Descriptor
  MOV  BL,[SI].DDLength    ;Put the Length in BL
  OR   BL,BL               ;Valid Descriptor?
  JZ  >Y70                 ;If not, Error
  CMP  AL,[SI].DDDescrType ;The Descriptor we're looking for?
  JE  >Y80                 ;If so, we're done
  ADD  SI,BX               ;If not, point at next Descriptor
  CMP  SI,(DescriptorData+MaxDescriptorSize) ;Gone past the end of the buffer?
  JBE  Y10                 ;If not, keep looking
Y70:                       ;Error
  STC                      ;Set Error Flag
  JMP >Y90                 ;Done
Y80:                       ;OK
  MOV  DI,SI               ;Update Pointer
  MOV  AX,BX               ;Put length in AX
  CLC                      ;Set OK Flag
Y90:                       ;Done
  POP  SI,BX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A REQUEST (CONTROL) STRUCTURE INTO THE INT14 STRUCTURE
;Inputs:  [CX] = Request Structure to Copy
;         ES = TSR Data Area (contains both Request & Int14 Request)
;         [DI] = Int14 Request Structure to copy it to
;      or [SI] = Int14 Request Structure to copy it to
;Outputs:
;Changes: Int14 Structure contains Request Structure Data
;------------------------------------------------------------------------------
CopySetupReqToInt14SI:
  PUSH DI                    ;Save used registers
  MOV  DI,SI                 ;Put pointer in DI
  CALL CopySetupReqToInt14DI ;Copy it
  POP  DI                    ;Restore used registers
  RET

CopySetupReqToInt14DI:
  PUSH CX,DI,SI,DS                ;Save used registers
  MOV  DS,ES                      ;Point DS at TSR Data Area
  ADD  DI,OFFSET I14RSetupReqData ;Point at correct part of structure
  MOV  SI,CX                      ;Point at structure to copy
  MOV  CX,(SetupRequestSize/2)    ;Number of words to copy
  REP  MOVSW                      ;Copy it
  POP  DS,SI,DI,CX                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DECIPHER THE REPORT DESCRIPTOR TO DETERMINE THE DEVICE CHARACTERISTICS
;Inputs:  ReportData
;         DS = ES = TSR Data Area
;         [BX] = Info Table Entry
;Outputs: CF = Clear if OK (found some valid Data)
;            = Set if Error (not a Valid Device)
;          NumButtons, HasWheel, DeviceID, ReportID
;Changes:
;NOTES: To keep this code simple, we will assume that the Device and the
;         Report Descriptor don't do or have anything really weird.
;       We will assume Buttons are listed first, followed by X-Axis and
;         Y-Axis, which can either be bytes or words.  Any Axis (Wheel)
;         data following the X- and Y-Axes will be assumed to be the same
;         data length (byte or word) as the first two Axes.
;       X and Y can be followed by Z, which will be Wheel1, unless the Z is
;         actually followed by an actual Wheel, in which case Z will be
;         Wheel2 (the Horizontal Wheel) and the Wheel will be Wheel 1 (the
;         Vertical Wheel).
;       The only "unusual" thing we account for is the possibility of a
;         Report ID.  A regular, stand-alone Mouse would never need a
;         Report ID.
;       A Mouse integrated with a Keyboard (like one I currently have) may
;         need to use Report IDs, though.  The second Report coming from the
;         Mouse on my particular Keyboard, though, should actually be part
;         of the Keyboard Report rather than the Mouse Report.  The Report
;         is for the System Keys (Power Off, etc.) that are actually on the
;         keyboard, and in reality have NOTHING at all to do with the Mouse.

;Assumptions:
;  Data is never more than 8 bytes.  Low-Speed Mice (the vast majority) are
;    limited to 8 bytes by the USB spec, so that is guaranteed.  Some newer
;    (expensive) mice are full-speed, so theoretically can have more
;    than 8 bytes of data.  If a mouse sends more than 8 bytes of data, a
;    custom driver will need to be built.
;  Buttons are at Offset 0.  If number of buttons is not a multiple of 8,
;    there are "unused" bits afterwards to keep it byte aligned.  We will
;    allow only 1 button, although every PC Mouse I've ever seen has
;    at least two buttons.  This will allow us to use an Apple Mouse
;    (which has only 1 button).
;  I've never seen a USB Mouse that doesn't have a Wheel, but suppose
;    it's possible.  It's definitely possible to use a PS/2 Mouse, which
;    doesn't have Wheel, with one of the PS/2-to-USB coverter cables.
;  If there was no Z-Axis, we look for an AC Pan Label
;    (we do not look for a second Wheel Label).  If it is found, it is used
;    as the second (Horizontal) Wheel with a size of one byte.
;  We ignore Resolution Multipliers.  The user can to change the Resolution
;    to adjust for this, if needed.  If it turns out that the Resolution is
;    unable to adjust things enough to make the mouse usable, a custom
;    driver will need to be built.
;------------------------------------------------------------------------------
D701:
  JMP  NEAR DecipherReportDescrErr       ;To avoid JMP > 128

DecipherReportDescr:
  PUSH AX,CX,DX,DI,SI                    ;Save used registers
  MOV  SI,ReportData                     ;Point at Data
  MOV  DI,(ReportData+MaxReportSize)     ;Point at end of data
;   mov  SI,TestDescr
;   mov  DI,(TestDescr+250)
  OR   TSRFlags2,NoBeep1Time                      ;Assume No Beeps
  MOV  AX,RptTagUsagePage+(RptGlbUPGenericDesktop SHL 8) ;Usage Page
  CALL TestReportTagByte                 ;  Generic Desktop?
  JC   D701                              ;If not, error
D10:                                     ;Loop to here to find Usage
  MOV  AL,RptTagUsage                    ;Look for
  CALL LookForReportTag                  ;  a Usage
  JC   D701                              ;If none found, quit
  CMP  AL,RptGDMouse                     ;Is it a Mouse?
  JNE  D10                               ;If not, keep looking
  AND  TSRFlags2,(NOT NoBeep1Time)       ;Allow Beeps Again
  MOV  AL,RptTagReportID                 ;Look for
  CALL LookForReportTag                  ;  a Report ID
  JC  >D20                               ;If none, continue (ReportID = 0)
  MOV  [BX].DIReportID,AL                ;If found, store it
D20:                                     ;Report ID Handled
  MOV  AL,RptTagReportCount              ;Look for
  CALL LookForReportTag                  ;  the Button Count
  JC   D701                              ;If not found, error
  CMP  AL,MinButtons                     ;Is it big enough?
  JB   D701                              ;If not, error
  MOV  [BX].DINumButtons,AL              ;If OK, store it
  MOV  CH,AL                             ;Store it in our Offset Tracker
  DEC  CH                                ;Subtract 1,
  MOV  CL,3                              ;  divide by 8,
  SHR  CH,CL                             ;  and add 1
  INC  CH                                ;  to convert it to the X Axis Offset
  MOV  B [BX].DIXOffset,CH               ;Store the Offset
  MOV  AX,RptTagUsagePage+(RptGlbUPGenericDesktop SHL 8) ;Look for another
  CALL TestReportTagByte                 ;  Generic Desktop (end of Button Page)
  JC   D701                              ;If not found, error
  MOV  DX,SI                             ;Save Report Pointer
  MOV  AL,RptTagReportSize               ;Look for the
  CALL LookForReportTag                  ;  Report Size (applies to at least XY)
  JC   D701                              ;If not found, error
  MOV  SI,DX                             ;If found, restore Report Pointer
  MOV  CL,1                              ;Assume one byte
  CMP  AL,8                              ;Is it one byte?
  JE  >D30                               ;If so, continue
  CMP  AL,16                             ;Is it one word?
  JNE  D701                              ;If not, error
  INC  CL                                ;If so, increment offset size
  OR   [BX].DIFlags,(DIFlagX2Bytes+DIFlagY2Bytes) ;Mark X & Y as Words
D30:                                     ;CL contains X, Y, and Z Size
  MOV  AX,RptTagUsage+(RptGDPointer SHL 8) ;Skip over Pointer Usage
  CALL TestReportTagByte                 ;  (happens on some mice)
  MOV  AX,RptTagUsage+(RptGDXAxis SHL 8) ;Usage
  CALL TestReportTagByte                 ;  X-Axis?
  JC  >D70                               ;If not, Error
  MOV  AX,RptTagUsage+(RptGDYAxis SHL 8) ;Usage
  CALL TestReportTagByte                 ;  Y-Axis?
  JC  >D70                               ;If not, Error
  ADD  CH,CL                             ;Adjust Offset Tracker
  MOV  B [BX].DIYOffset,CH               ;Store it
  ADD  CH,CL                             ;Adjust Offset Tracker
  MOV  AX,RptTagUsage+(RptGDZAxis SHL 8) ;Usage
  CALL TestReportTagByte                 ;  Z-Axis?
  JC  >D35                               ;If not, look for a Wheel
D31:                                     ;There is a Z-Axis
  INC  [BX].DINumWheels                  ;Mark as having one wheel
  PUSH SI                                ;Save pointer
  MOV  AX,RptTagUsage+(RptGDWheel SHL 8) ;Usage Wheel
  CALL TestReportTagByte                 ;  after Usage Z-Axis?
  POP  SI                                ;Restore Pointer
  JNC >D33                               ;If so, handle it
D32:                                     ;No Wheel following Z-Axis
  MOV  B [BX].DIZ1Offset,CH              ;Store the Offset
  CMP  CL,2                              ;Is it a Word or a Byte?
  JB  >D80                               ;If a Byte, we're done
  OR   [BX].DIFlags,DIFlagZ12Bytes       ;If a Word, mark as a Word
  JMP >D80                               ;We're done
D33:                                     ;Wheel following Z-Axis (Z is Wheel2)
  INC  [BX].DINumWheels                  ;Mark as having two wheels
  MOV  B [BX].DIZ2Offset,CH              ;Store Offset for Z-Axis (Wheel2)
  ADD  CH,CL                             ;Adjust Offset Tracker
  MOV  B [BX].DIZ1Offset,CH              ;Store Offset for Wheel (Wheel1)
  ;No need to test for data length here, since we'd have more than 8 bytes!
  JMP >D80                               ;Done
D35:                                     ;No Z-Axis
  MOV  AX,RptTagUsage+(RptGDWheel SHL 8) ;Usage
  CALL TestReportTagByte                 ;  Wheel?
;  JNC >D40                               ;If so, handle it
;  CMP  [BX].DINumWheels,1                ;If not, was there a Z Axis?
;  JE  >D70                               ;If so, Error
;  JMP >D80                               ;If not, done
  JC  >D80                               ;If not, done
D40:                                     ;Have a Wheel
  INC  [BX].DINumWheels                  ;Mark as having one wheel
  MOV  B [BX].DIZ1Offset,CH              ;Store the Offset
;  CMP  [BX].DINumWheels,2                ;Was there a Z Axis (Horizontal Wheel)?
;  JE  >D80                               ;If so, done
;  INC  CH                                ;If not, adjust Offset Tracker
  ADD  CH,CL                             ;Adjust Offset Tracker
  CMP  CL,2                              ;Is it a Byte?
  JB  >D42                               ;If so, continue
  OR   [BX].DIFlags,DIFlagZ12Bytes       ;Mark as a Word
D42:                                     ;Word size handled, if apprpriate
  MOV  AX,RptTagUsagePage+(RptGlbUPConsumerDvc SHL 8) ;Look for the
  CALL TestReportTagByte                 ;  Consumer Device Page
  JC  >D80                               ;If not found, done
  MOV  AX,RptTagUsage+((RptConsACPan AND 0FFh)SHL 8) ;Look for the
  CALL TestReportTagByte                 ;  AC Pan Usage
  JC  >D80                               ;If not found, done
  CMP  B [SI-1],(RptConsACPan SHR 8)     ;Is it really the AC Pan Usage?
  JNE >D80                               ;If not, done
D50:                                     ;Has two Wheels
  INC  [BX].DINumWheels                  ;Mark as having two wheels
  MOV  B [BX].DIZ2Offset,CH              ;Store the Offset
  JMP >D80                               ;Done
DecipherReportDescrErr:
D70:                                     ;Error
  STC                                    ;Set Error Flag
  JMP >D90                               ;Done
D80:                                     ;OK
  CALL AddBaseOffsets                    ;Add Base Offsets for the Axes
  CLC                                    ;Set OK Flag
D90:                                     ;Done
  POP  SI,DI,DX,CX,AX                    ;Restore used registers
  RET

;Look for more than 8 buttons, Z Axis, Wheel, AC Pan, & Resolution Multiplier?
;Some mice are full speed, so can have more than 8 bytes of data!

;------------------------------------------------------------------------------
;ADD THE BASE (DIRawData) OFFSET TO THE AXES OFFSETS
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Entry
;         [BX].DIXOffset, [BX].DIYOffset, [BX].DIZ1Offset, [BX].DIZ2Offset
;            all contain offset within DIRawData (0 if not used)
;         CLD already issued
;Outputs: [BX].DIXOffset, [BX].DIYOffset, [BX].DIZ1Offset, [BX].DIZ2Offset
;            are all "adjusted" to a real Offset (Pointer) Value
;Changes:
;------------------------------------------------------------------------------
AddBaseOffsets:
  PUSH AX,CX,DI          ;Save used registers
  LEA  DI,[BX].DIXOffset ;Point at the X Offset
  LEA  AX,[BX].DIRawData ;Put Base Offset (what we need to Add) in AX
  MOV  CX,4              ;Need to do 4 Axes
B10:                     ;Loop to here for each Axis
  CMP  W [DI],0          ;Is this a valid offset?
  JE  >B40               ;If not, skip it
  ADD  [DI],AX           ;If so, add the DIRawData base offset
B40:                     ;Done with this one
  ADD  DI,2              ;Point at the next Axis
  LOOP B10               ;Keep going until we're done
  POP  DI,CX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DECIPHER THE REPORT DESCRIPTOR TO FIND THE SYSTEM KEYS (IF THEY ARE THERE)
;Inputs:  ReportData
;         DS = ES = TSR Data Area
;         [BX] = Info Table Entry (already contains Interface Number)
;         ReportData already deciphered for Mouse data
;Outputs: DISysReportID
;         Sets Idle Rate for System Keys Report ID to 10ms
;Changes: Int14Request
;NOTES: A Mouse integrated with a Keyboard (like one I currently have) may
;         use Report IDs.  The second Report coming from the Mouse on my
;         Keyboard/Mouse combo is for the System Keys on the Keyboard
;         (Power/Sleep/WakeUp), which really have nothing at all to do
;         with the Mouse.  We will do a simple deciphering of the Mouse
;         Report Decriptor, and if we find a System Control Report ID,
;         we will handle it.  At least for now, we will assume all Reports
;         will be in the same format as the one on my Keyboard.
;------------------------------------------------------------------------------
DecipherReportDescrSys:
  PUSH AX,CX,DI,SI                          ;Save used registers
  CMP  [BX].DIReportID,0                    ;Is there a second Report ID?
  JE  >Y90                                  ;If no, we're done
  MOV  SI,ReportData                        ;Point at Data
  MOV  DI,(ReportData+MaxReportSize)        ;Point at end of data
Y10:                                        ;Look for System Control here
  MOV  AL,RptTagUsage                       ;Look for
  CALL LookForReportTag                     ;  Usage
  JC  >Y90                                  ;If none, Quit
  CMP  AL,RptGDSysCtl                       ;Is it System Control?
  JNE  Y10                                  ;If not, keep looking
  MOV  AL,RptTagReportID                    ;Look for
  CALL LookForReportTag                     ;  a Report ID
  JC  >Y90                                  ;If none, Quit
  MOV  [BX].DISysReportID,AL                ;If found, store it
Y40:                                        ;Set Idle Rate for Keys
  MOV  SI,Int14Request                      ;Point at Request Structure
  MOV  [SI].I14RRequestType,I14RRTDoControl ;Type = Control
  MOV  CX,SetHIDIdleReq                     ;Point at Idle Request Struc
  CALL CopySetupReqToInt14SI                ;Copy Setup Pkt (CX)
  MOV  B [SI].I14RSetHIDIdleRptID,AL        ;Store Report ID
  MOV  B [SI].I14RSetHIDIdleDuration,2      ;Idle Rate = 2 (10ms)
  MOV  AL,[BX].DIInterfaceNum               ;Copy the
  MOV  B [SI].I14RSetHIDIdleIntf,AL         ;  Interface Number
  MOV  W [SI].I14RCallBackAddr[0],-1        ;Do nothing when done
  CALL DoInt14CallSaveAllSI                 ;Do it
Y90:                                        ;Done
  POP  SI,DI,CX,AX                          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;LOOK FOR A SPECIFIC REPORT TAG AND VALUE FOR THE REPORT TAG
;Inputs:  [SI] = Report Pointer
;         [DI] = End of Report Data Area
;         AL   = Report Tag to Look for
;         AH   = Report Tag Value to look for (The Byte after the Tag)
;Outputs: CF = Clear if Tag was found
;             [SI] = Points to next Tag after the one found
;            = Set if Tag was not Found
;              SI = Unchanged
;Changes:
;NOTES: This finds the first matching Report Tag, and tests the value (byte)
;         immediately following that Report Tag.  It does not continue looking
;         in the Report Descriptor after the first Report Tag it finds.
;         In addition, if the Data does not match, it returns SI unchanged.
;------------------------------------------------------------------------------
TestReportTagByte:
  PUSH AX,BX            ;Save used registers
  MOV  BX,SI            ;Save original Pointer
  CALL LookForReportTag ;Look for the Tag
  JC  >B70              ;If not found, error
  CMP  AL,AH            ;Does the Value match?
  JE  >B80              ;If so, it's a match
  MOV  SI,BX            ;Restore original Pointer
B70:                    ;No match
  STC                   ;Set no match flag
  JMP >B90              ;Done
B80:                    ;It matches
  CLC                   ;Set matches flag
B90:                    ;Done
  POP  BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;LOOK FOR A SPECIFIC REPORT TAG IN THE REPORT DESCRIPTOR
;Inputs:  [SI] = Report Pointer
;         [DI] = End of Report Data Area
;         AL   = Report Tag to Look for
;Outputs: CF = Clear if Tag was found
;              AL  = Data Byte immediately following the Tag
;             [SI] = Points to next Tag after the one found
;            = Set if Tag was not Found
;              AL = 0
;              SI = Unchanged
;Changes:
;NOTES: This works under the premise that Data is at most one byte
;         after a Report Tag (which all Mouse Data should be!)
;------------------------------------------------------------------------------
LookForReportTag:
  PUSH BX,CX,DX     ;Save used registers
  MOV  DX,SI        ;Save original pointer
  XOR  CH,CH        ;Size Adder is always a Byte
  MOV  BL,AL        ;Save Tag we're looking for
L10:                ;Loop to here to find the Tag
  LODSB             ;Get the next Tag
  OR   AL,AL        ;Legitimate Tag?
  JZ  >L70          ;If not, error
  MOV  CL,AL        ;Get the
  AND  CL,03h       ;  Data Size
  AND  AL,(NOT 03h) ;Mask out the Size so we can test it
  CMP  AL,BL        ;Is it the Tag we're looking for?
  JE  >L80          ;If so, we're done
  ADD  SI,CX        ;If not, point at the next one
  CMP  SI,DI        ;Are we past the end of the buffer?
  JB   L10          ;If not, keep looking
L70:                ;Error
  XOR  AL,AL        ;AL = 0
  MOV  SI,DX        ;Restore original Pointer
  STC               ;Set Error Flag
  JMP >L90          ;Done
L80:                ;Found it
  MOV  AL,[SI]      ;Put the Data Byte in AL
  ADD  SI,CX        ;Update the Pointer
  CLC               ;Set OK Flag
L90:                ;Done
  POP  DX,CX,BX     ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE MOUSE MOVEMENTS, MOUSE BUTTON PRESSES, AND SYSTEM KEYS
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;THIS CODE IS PROCESSED EVERY TIME AN INTERRUPT COMES FROM THE DEVICE
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Offset (contains DeviceData)
;Outputs: Processes "Raw" Mouse data into an appropriate format
;         If appropriate, calls the "User" Code telling it what has changed
;Changes: Lots of things
;NOTES: If the Report that comes back is for System Keys,
;         we cannot send periodically reliable data to USBKEYB, since the
;         polling rate we use is determined by what the user wants the
;         Mouse to do, not by what we want the keyboard to do.
;       In addition, moving the Mouse while pressing one of the Keys
;         causes delays and interruptions in reporting the Keys, which
;         can create all kinds of grief.
;       Therefore, all we will be able to do is notify USBKEYB of presses
;         and releases, with no timing information while it is pressed.
;         Any Typematic Timing will need to be handled by USBKEYB.
;------------------------------------------------------------------------------
DeviceChangeCode:
  OR   CX,CX                  ;Did we get any data?
  JZ  >V90                    ;If not, quit
  MOV  AL,[BX].DIReportIDData ;Is it the Mouse
  CMP  AL,[BX].DIReportID     ;  Report ID?
  JNE >V20                    ;If not, try the next possibility
  CALL DeviceChangeMouse      ;If so, handle it
  JMP >V90                    ;Done
V20:                          ;Not Mouse ID
  CMP  AL,[BX].DISysReportID  ;Is it the System Keys Report ID?
  JNE >V90                    ;If not, quit
  CALL DeviceChangeSys        ;If so, handle it
V90:                          ;Done
  RETF                        ;Quit


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO PROCESS SYSTEM KEYS
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HANDLE THE DEVICE NOTIFICATION FOR THE SYSTEM KEYS REPORT ID
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Offset (contains DeviceData)
;Outputs: Processes "Raw" System Keys Data, sending call to the
;           USB Keyboard processor (USBKEYB) if appropriate.
;Changes: Lots of things, including possibly all Registers
;NOTES: We cannot send periodically reliable data to USBKEYB, since the
;         polling rate we use is determined by what the user wants the
;         Mouse to do, not by what we want the keyboard to do.
;       In addition, moving the Mouse while pressing one of the System Keys
;         causes delays and interruptions in reporting the Keys, which
;         can create all kinds of grief.
;       Therefore, all we will be able to do is notify USBKEYB of presses
;         and releases, with no timing information while it is pressed.
;         If a key needs Typematic functions, the timing will need to be
;         handled by USBKEYB.  System Keys shouldn't need to be Typematic.
;------------------------------------------------------------------------------
DeviceChangeSys:
  CALL MakeStack         ;Set up our own Stack Space (CS = TSR Data Area!)
  CALL DoDeviceChangeSys ;Handle the Keys
  CALL ReturnStack       ;Restore Original Stack Space
  RET

DoDeviceChangeSys:
  PUSH DX                    ;Save used registers
  MOV  DL,[BX].DISysLastTime ;Get the Data byte last time
  XOR  DH,DH                 ;Assume Uninstalled/Disabled
  TEST TSRFlags2,DisableAll  ;All Devices Disabled?
  JNZ >K30                   ;If so, continue
  TEST [BX].DIFlags,(DIFlagDisabled+DIFlagDisconnect) ;Disabled or Disconnected?
  JNZ >K30                   ;If so, continue
  MOV  DH,[BX].DIBtnOffset   ;If not, get the Real Data Byte
K30:                         ;DH contains Data this time
  MOV  [BX].DISysLastTime,DH ;Save it for next time
  CMP  DL,DH                 ;Anything changed since last time?
  JE  >K90                   ;If not, just quit
  CALL Int16InstallCheck     ;Appropriate INT 16h Function installed?
  JC  >K90                   ;If not, we're done
  CALL ProcessSysKeyReleases ;Process Key Releases
  CALL ProcessSysKeyPresses  ;Process Key Presses
K90:                         ;Done
  POP  DX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;REMOVE ALL "HANGING" KEYSTROKES FOR A MOUSE (JUST DISCONNECTED OR DISABLED)
;Inputs:   ES  = TSR Data Area
;         [DI] = Device Info Buffer
;Outputs: DIFlagDisconnect is set
;         Issues Release codes to USBKEYB
;Changes:
;NOTES: This routine sets DIFlagDisconnect at the beginning, but does not
;         UnSet it when it is done.  It is up to the calling routine
;         to do that.  This is because the calling routine will need to
;         Disable/Reset/whatever the Device, which may take several hundred
;         milliseconds to complete.  During that time, there may be movement
;         data continuing to come from the mouse, which would continue to be
;         processed if DIFlagDisconnect is Unset by us.
;------------------------------------------------------------------------------
RlsPressedSysESDI:
  PUSH BX,DS             ;Save used registers
  PUSHF                  ;Save flags
  CLI                    ;Disable interrupts
  MOV  DS,ES             ;Point DS at TSR Data Area
  MOV  BX,DI             ;Point BX at the Device Info Entry
  OR   [BX].DIFlags,DIFlagDisconnect ;Mark as being Disconnected
  CALL DoDeviceChangeSys ;Send Key Releases
  POPF                   ;Restore flags
  POP  DS,BX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PROCESS SYSTEM KEY PRESSES & RELEASES
;Inputs:  DS = ES = TSR Data Area
;         DH = System Key bit map (1 = Press) This Time
;         DL = System Key bit map (1 = Press) Last Time
;Outputs: Notifies USB Keyboard Driver (via INT 16h Functions) as needed
;Changes:
;------------------------------------------------------------------------------
ProcessSysKeyPresses:
  PUSH AX,DX                ;Save used registers
  XOR  AL,AL                ;Mark as a Press
  JMP >Y00                  ;Do it
ProcessSysKeyReleases:
  PUSH AX,DX                ;Save used registers
  MOV  AL,-1                ;Mark as a Release
  NOT  DX                   ;Work with Releases
;  JMP >Y00                  ;Do it
Y00:
  PUSH CX                   ;Save used registers
  OR   DH,DL                ;Figure out which buttons
  XOR  DH,DL                ;  have been changed since last time (DH)
  JZ  >Y90                  ;If no changes, quit
  MOV  DL,RptGDSysPowerDown ;Start with the first possible key
  MOV  CX,8                 ;Test 8 bits
Y10:                        ;Loop to here for each bit
  ROR  DH,1                 ;Is bit 0 set?
  JNC >Y50                  ;If not, don't send anything
  OR   AL,AL                ;Are we doing presses?
  JNZ >Y30                  ;If not, handle a Release
Y20:                        ;Need to send a Press
  CALL SendSysKeyPress      ;Send a Press
  JMP >Y50                  ;Continue
Y30:                        ;Need to send a Release
  CALL SendSysKeyRelease    ;Send a Release
Y50:                        ;Key handled
  INC  DL                   ;Increment Key Code
  LOOP Y10                  ;Keep going until we're done
Y90:                        ;Done
  POP  CX                   ;Restore used registers
  POP  DX,AX                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PERFORM AN INSTALL CHECK ON THE INT 16H USB SYSTEM KEY FUNCTIONS
;Inputs:
;Outputs: CF = Clear if Function worked
;            = Set if not
;Changes:
;------------------------------------------------------------------------------
Int16InstallCheck:
  PUSH AX,DX       ;Save used registers
  MOV  AL,30h      ;AL = Function = System Keys Install Check
  MOV  DX,1B1Bh    ;DX = 1B1Bh (Test value)
  CALL DoInt16Func ;Do it (returns CF)
  POP  DX,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SEND A SYSTEM KEY PRESS OR RELEASE SIGNAL TO THE USB KEYBOARD DRIVER
;Inputs:  DL = Key Code to send
;Outputs: CF = Clear if Function worked
;            = Set if not
;Changes:
;------------------------------------------------------------------------------
SendSysKeyPress:
  PUSH AX          ;Save used registers
  MOV  AL,31h      ;AL = Function = Send USB System Key Press
  JMP >Y00         ;Do it
SendSysKeyRelease:
  PUSH AX          ;Save used registers
  MOV  AL,32h      ;AL = Function = Send USB System Key Release
;  JMP >Y00         ;Do it
Y00:
  PUSH DX          ;Save used registers
  XOR  DH,DH       ;Make sure DH = 0
  CALL DoInt16Func ;Do it (returns CF)
  POP  DX          ;Restore used registers
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PERFORM ONE OF THE INT 16H "SEND USB SYSTEM KEY" FUNCTIONS
;Inputs:  DX = Value to send in DX
;         AL = Subfunction to use
;Outputs: CF = Clear if Function worked
;            = Set if not
;Changes:
;------------------------------------------------------------------------------
DoInt16Func:
  PUSHA            ;Save all registers
  PUSH DS,ES       ;Save used registers
  MOV  AH,1Bh      ;Set AH to correct function
  MOV  BX,'KE'     ;BX = 'KE'
  MOV  CX,'YB'     ;CX = 'YB'
  STC              ;Preset error flag
  INT  16h         ;Do it
  JC  >X70         ;If error, not installed
  OR   AX,AX       ;Error?
  JNZ >X70         ;If so, Error
  CMP  BX,'YB'     ;BX what it should be?
  JNE >X70         ;If not, Error
  CMP  CX,'KE'     ;CX what it should be?
  JE  >X80         ;If so, it's OK
X70:               ;Not installed
  STC              ;Set not installed flag
  JMP >X90         ;Done
X80:               ;Installed
  CLC              ;Set installed flag
X90:               ;Done
  POP  ES,DS       ;Restore used registers
  POPA             ;Restore all registers
  RET

;------------------------------------------------------------------------------
;HANDLE THE DEVICE NOTIFICATION FOR THE MOUSE REPORT ID
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Offset (contains DeviceData)
;Outputs: Processes "Raw" Mouse data into an appropriate format
;         If appropriate, calls the "User" Code telling it what has changed
;Changes: Lots of things, including possibly all Registers
;Stack should look like this during the call:
;  No Wheel  Wheel
;  --------  -----
;  Buttons   Buttons & X Data
;  X Data    Y Data
;  Y Data    Z Data
;  0         0
;------------------------------------------------------------------------------
DeviceChangeMouse:
  CALL MakeStack                      ;Set up our own Stack Space
  CALL CopyRawData                    ;Copy the Raw Data for Status
  CALL GetPS2HWInfo                   ;Get the PS2 Data if emulating HW
  AND  B [BX].DIBtnOffset,RawAll5Btns ;Mask out all unused buttons
  CMP  DeviceID,DvcIDWheel2Yes        ;Are all 5 buttons enabled?
  JE  >M10                            ;If so, continue
  AND  B [BX].DIBtnOffset,RawAll3Btns ;If not, mask out all but the last 3 btns
M10:                                  ;Unused buttons are masked out
  CALL SwapUSBBtnsBX                  ;Swap L/R Buttons, if needed
  CALL HandleDataWords                ;Handle Data that's words insted of Bytes
  CALL NegateData                     ;Negate Data that's backwards
  CALL ProcessUSBScaling              ;Compensate for Scaling Factor, if needed
  CALL ProcessUSBResolution           ;Compensate for Resolution, if needed
  CALL TestUSBChanges                 ;Has anything actually changed?
  JC  >M90                            ;If not, quit
  TEST TSRFlags,FarCallInProg         ;Call already in Progress?
  JNZ >M90                            ;If so, we're done
  OR   TSRFlags,FarCallInProg         ;If not, mark as In Progress
  CALL CalcHandlerStatusByte          ;Calculate Status Byte (in AL, AH = 0)
M15:                                  ;AL contains Button Data, AH = 0
  CALL DoHWEmulation                  ;Process hardware emulation, if needed
  JNC >M80                            ;If processed, we're done
  MOV  SI,[BX].DIXOffset              ;Point at X Data
  CMP  DeviceID,DvcIDWheel1Yes        ;Has the Wheel been Enabled?
  JB  >M20                            ;If not, don't load the Wheel Data
  MOV  AH,[SI]                        ;If so, put X data in AH
  MOV  SI,[BX].DIYOffset              ;Point at Y Data
M20:                                  ;Wheel data Loaded, if appropriate
  PUSH AX                             ;Put Btns (& X if Wheel) on the stack
  XOR  AH,AH                          ;All high bytes are 0 from now on
  MOV  AL,[SI]                        ;Put X (Y if Wheel)
  PUSH AX                             ;  on the stack
  CMP  DeviceID,DvcIDWheel1Yes        ;Is there a Wheel?
  JAE >M40                            ;If so, jump to handle it
  MOV  SI,[BX].DIYOffset              ;If not, Point at Y
  MOV  AL,[SI]                        ;Get the Y Data
  JMP >M50                            ;Jump to put it on the stack
M40:                                  ;Wheel(s) are enabled
  CALL GetZDataForStack               ;Get Wheel Data
M50:                                  ;AX contains third word for stack
  PUSH AX                             ;Put Y Data (Z if Wheel) on the stack
M60:                                  ;3 bytes of Stack loaded for Far Call
  XOR  AX,AX                          ;Last Word of Stack
  PUSH AX                             ;  is 0
  CALL D MouseUserFarCall             ;Call the User Handler
M65:                                  ;Done with the User Handler, if needed
  POP  AX,AX,AX,AX                    ;Restore the Stack pointer
M80:                                  ;Done processing all Codes
  AND  TSRFlags,(NOT FarCallInProg)   ;Mark as done
M90:                                  ;Done
  CALL ReturnStack                    ;Restore Original Stack Space
  RET

;------------------------------------------------------------------------------
;COPY THE RAW KEY CODE DATA TO THE STATUS AREA, IN CASE WE NEED TO SHOW RAW STS
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Info Table Offset
;         CLD already issued
;         ReportID (if used) is already verified, to make sure we're using
;           valid data
;Outputs: Copies RawData to StatusData
;Changes:
;------------------------------------------------------------------------------
CopyRawData:
  PUSH CX,DI,SI          ;Save used registers
  LEA  SI,[BX].DIRawData ;Point SI at the Raw Data Area
  LEA  DI,[BX].DIStsData ;Point DI at the Status Data Area
  MOV  CX,(8/2)          ;Number of words to copy
  REP  MOVSW             ;Copy the Raw Data to the Status Data
W90:                     ;Done
  POP  SI,DI,CX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;UPDATE THE INFORMATION ABOUT THE PS2 MOUSE IF DOING HW EMULATION
;Inputs:  DS = ES = TSR Data Area
;         TSRFlags.EmuHardware
;         PS2 Device Info Entry
;         Int15Hdr.OldVector
;Outputs: DeviceID, Resolution, SampleRateCode, PS2 Button Data (Buttons 1-3)
;Changes:
;------------------------------------------------------------------------------
GetPS2HWInfo:
  PUSH AX,BX,CX,DX,DI,SI            ;Save used registers
  MOV  DI,DIPS2Offset               ;Point [DI] at the PS2 Data Area
  MOV  SI,OFFSET TSRFlags           ;Point [SI] at TSRFlags
  TEST B [SI],EmuHardware           ;Are we emulating Hardware?
  JZ  >I90                          ;If not, don't do anything
;  TEST [DI].DIFlags,DIFlagInUse     ;Is there even a PS2 Mouse Installed?
;  JZ  >I90                          ;If not, don't do anything
  MOV  AX,0C200h+I15ALGetDeviceID   ;Function = Get Device ID
  STC                               ;Preset Error Flag
  PUSHF                             ;Do it
  CALL D Int15Hdr.OldVector         ;  (returns AX, BH)
  JC  >I90                          ;If error, quit
  CMP  BH,DvcIDWheelNo              ;Is it valid?
  JE  >I20                          ;If so, store it
  CMP  BH,DvcIDWheel1Yes            ;Is it valid?
  JB  >I30                          ;If not, don't store it
  CMP  BH,DvcIDWheel2Yes            ;Is it valid?
  JA  >I30                          ;If not, don't store it
I20:                                ;DeviceID is valid
  MOV  DeviceID,BH                  ;Store the Device ID
I30:                                ;DeviceID handled
  MOV  AX,0C200h+I15ALStatusScaling ;Function = Get Status/Set Scaling
  XOR  BH,BH                        ;SubFunction = 0 = Get Status
  PUSHF                             ;Do it (assume no error)
  CALL D Int15Hdr.OldVector         ;  (returns AX, BL, CL, DL)
  TEST BL,StatusScaling2            ;Scaling Factor of 2:1?
  JNZ >I40                          ;If so, handle it
I35:                                ;1:1 Scaling
  AND  B [SI],(NOT ScalingFactor2)  ;Mark as 1:1 Scaling
  JMP >I50                          ;continue
I40:                                ;2:1 Scaling
  OR   B [SI],ScalingFactor2        ;Mark as 2:1 Scaling
I50:                                ;Scaling Factor handled
  CALL ConvertBtnsBL                ;Convert the Buttons so we can use them
  MOV  [DI].DIRawData[0],BL         ;Store the Button Status
  MOV  Resolution,CL                ;Store the Resolution
  CALL ConvertSamplingDL            ;Convert the Resolution to a Code
  MOV  SampleRateCode,DL            ;Store it
I90:                                ;Done
  POP  SI,DI,DX,CX,BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT PS2 "GET STATUS" BUTTONS TO STACK-COMPATIBLE FORMAT
;Inputs:  BL = Button Status (from PS2 Get Status Call)
;              (Buttons are in different order than those returned on the stack)
;Outputs: BL = Raw Button Mask (as used in Stack or USB Mice)
;Changes:
;------------------------------------------------------------------------------
ConvertBtnsBL:
  PUSH AX            ;Save used registers
  XOR  AL,AL         ;Start with no buttons pressed
B10:                 ;Process Left Button
  TEST BL,StatusLBtn ;Left Button pressed?
  JZ  >B20           ;If not, continue
  OR   AL,RawLBtn    ;If so, mark Left button as pressed
B20:                 ;Process Right Button
  TEST BL,StatusRBtn ;Right Button pressed?
  JZ  >B30           ;If not, continue
  OR   AL,RawRBtn    ;If so, mark Right button as pressed
B30:                 ;Process Center Button
  TEST BL,StatusCBtn ;Center Button pressed?
  JZ  >B90           ;If not, we're done
  OR   AL,RawCBtn    ;If so, mark Center button as pressed
B90:                 ;Done
  MOV  BL,AL         ;Store the result in BL
  POP  AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT PS2 "GET STATUS" SAMPLE RATE (HZ) TO A SAMPLE RATE CODE
;Inputs:  DS = ES = TSR Data Area
;         SampleRateTable
;         DL = Sample Rate in Hz (from PS2 Get Status Call)
;Outputs: DL = Sample Rate Code corresponding to Sample Rate
;Changes:
;NOTES: For some unknown reason, the "Get Status" request for the PS2 Mouse
;         BIOS returns the actual sample rate (in Hertz), rather than
;         the code (from 0 to 6) that is used in the "Set Sampling" call.
;       In this code, we simply assume that the sample rate value is a
;         valid number, and do not actually test it.
;------------------------------------------------------------------------------
ConvertSamplingDL:
  PUSH SI                     ;Save used registers
  MOV  SI,(SampleRateTable+1) ;Point at the conversion table
P10:                          ;Loop to here for each table entry
  CMP  B [SI-1],SampleCode200 ;Is this the last table entry?
  JE  >P90                    ;If so, we're done
  CMP  [SI],DL                ;Is this the correct table entry?
  JE  >P90                    ;If so, we're done
  ADD  SI,3                   ;If not, point at the next table entry
  JMP  P10                    ;Keep looking
P90:                          ;Done
  MOV  DL,[SI-1]              ;Store the Sampel Rate Code in DL
  POP  SI                     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PUT THE DATA FOR THIS MOUSE INTO THE HARDWARE EMULATION BUFFER
;Inputs:  DS = ES = TSR Data Area
;         AL = Button Data Byte to send (combined button statuses)
;        [BX] = Device Info Entry of Mouse we're processing
;         All registers saved to stack (we don't need to save anything)
;         TSRFlags.EmuHardware
;         DeviceID
;Outputs: CF = Clear if we processed things (TSRFlags.EmuHardware = False)
;              Buffer contains appropriate data
;            = Set if we did not process things (TSRFlags.EmuHardware = True)
;Changes: Puts some data in Buffer
;         CHanges several registers
;------------------------------------------------------------------------------


;How do we know if we're supposed to send anything if we don't use the BIOS?
;We're sending things with no Driver installed.


DoHWEmulation:
  TEST TSRFlags,EmuHardware    ;Should we do anything
  JZ  >H70                     ;If not, don't
  CLI                          ;If so, disable interrupts
  CALL PutBufferAL             ;Put the Button Data into the Buffer
  MOV  SI,[BX].DIXOffset       ;Put the
  LODSB                        ;  X Data
  CALL PutBufferAL             ;  into the Buffer
  MOV  SI,[BX].DIYOffset       ;Put the
  LODSB                        ;  Y Data
  CALL PutBufferAL             ;  into the Buffer
  CMP  DeviceID,DvcIDWheel1Yes ;Should we process the wheel(s)?
  JB  >H80                     ;If not, we're done
  CALL GetZDataForStack        ;If so, get the Wheel Data (AL)
  CALL PutBufferAL             ;Put the Wheel Data into the Buffer
  JMP >H80                     ;Done
H70:                           ;Did not process things
  STC                          ;Set return flag
  JMP >H90                     ;Done
H80:                           ;Did process things
  CALL EnableInt70             ;Enable INT 70h (CMOS Timer)
  CLC                          ;Set return flag
H90:                           ;Done
  STI                          ;Enable interrupts
  RET

;------------------------------------------------------------------------------
;GET THE APPROPRIATE Z DATA VALUE TO PUT ON THE STACK FOR DEVICECHANGEMOUSE
;Inputs:   DS = ES = TSR Data Area
;         [BX] = Info Table Entry we're using
;          AH  = 0
;          DeviceID = DvcIDWheel1Yes or DvcIDWheel2Yes
;Outputs:  AX  = Value to put on stack (AL = appropriate Z Data, AH = 0)
;Changes:  SI
;------------------------------------------------------------------------------
GetZDataForStack:
  PUSH CX                 ;Save used registers
  XOR  AL,AL              ;Assume no wheels
  XOR  CL,CL              ;Assume no Second Wheel
  CMP  [BX].DINumWheels,1 ;How many Wheels does our Mouse have?
  JB  >M90                ;If none, we're done
  MOV  SI,[BX].DIZ1Offset ;If at least one Wheel, put the
  MOV  AL,[SI]            ;  Vertical Wheel Data in AL
  JE  >M05                ;If only one Wheel, continue
  MOV  SI,[BX].DIZ2Offset ;If two Wheels, put the
  MOV  CL,[SI]            ;  Horizontal Wheel Data in CL
M05:                      ;AL = Z1 data, CL = Z2 Data
  CMP  DeviceID,DvcIDWheel1Yes ;How many Wheels are Enabled?
  JA  >M20                ;If 2, jump to handle it
M10:                      ;Only One Wheel is Enabled (AL = Wheel Data)
  CMP  AL,7               ;Is it between 0 and 7?
  JBE >M90                ;If so, done
  JG  >M15                ;If positive and more than 7, jump to handle it
  CMP  AL,-7              ;Is it between 0 and -7?
  JGE >M90                ;If so, done
M12:                      ;Z Data is too negative
  MOV  AL,-7              ;Make it -7
  JMP >M90                ;Done
M15:                      ;Z Data is too positive
  MOV  AL,7               ;Make it 7
  JMP >M90                ;Done
M20:                      ;Two Wheels are Enabled
  TEST AL,AL              ;Is Z1 Data Positive, Negative, or 0?
  JZ  >M40                ;If 0, look for Z2 Data
  JS  >M25                ;If negative, jump to handle it
M22:                      ;Z1 Data is positive
  MOV  AL,1               ;Make it 1
  JMP >M60                ;Jump to add in the Buttons
M25:                      ;Z1 Data is negative
  MOV  AL,-1              ;Make it -1
  JMP >M60                ;Jump to add in the Buttons
M40:                      ;Z1 data = 0
  MOV  AL,CL              ;Put second Wheel data in AL
  TEST AL,AL              ;Is Z1 Data Positive, Negative, or 0?
  JZ  >M60                ;If 0, it's Ok like it is
  JS  >M45                ;If negative, jump to handle it
M42:                      ;Z1 Data is positive
  MOV  AL,2               ;Make it 2
  JMP >M60                ;Jump to handle it
M45:                      ;Z1 Data is negative
  MOV  AL,-2              ;Make it -2
M60:                      ;AL contains Z Data, need to add in Buttons 4 & 5
  AND  AL,0Fh             ;Mask out the high nibble
  MOV  CL,AL              ;Save it for a second
  CALL GetAllBtns45       ;Get Buttons 4 & 5 Status
  OR   AL,CL              ;Mask it in with the Z Data
M90:                      ;Done
  POP  CX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SWAP THE L/R BUTTONS IN A STATUS BYTE, IF APPROPRIATE
;Inputs:  AL/[BX] = Mouse Button Status to work with
;         TSRFlags
;Outputs: Swaps L/R buttons in Status Byte, if needed
;Changes:
;------------------------------------------------------------------------------
SwapAllBtnsAL:
  TEST TSRFlags,SwapBtns           ;Need to swap the buttons?
  JZ  >A90                         ;If not, we're done
  CALL SwapBtnsGo                  ;If so, swap 'em
A90:                               ;Done
  RET

SwapUSBBtnsBX:
  PUSH AX                          ;Save used registers
  TEST [BX].DIFlags,DIFlagSwapBtns ;Need to swap the buttons?
  JZ  >U90                         ;If not, we're done
  MOV  AL,[BX].DIBtnOffset         ;Get the Raw Button Data
  CALL SwapBtnsGo                  ;Swap L/R Buttons
  MOV  [BX].DIBtnOffset,AL         ;Store the new Button Status
U90:                               ;Done
  POP  AX                          ;Restore used registers
  RET

SwapBtnsGo:
  PUSH BX                          ;Save used registers
  MOV  BL,AL                       ;Put current status
  MOV  BH,AL                       ;  in BL and BH
  AND  BL,NOT (RawLBtn+RawRBtn)    ;Mask out existing Button Status
  TEST BH,RawLBtn                  ;Left Button pressed?
  JZ  >G20                         ;If not, skip it
  OR   BL,RawRBtn                  ;If so, make it a right button
G20:                               ;Left Button handled
  TEST BH,RawRBtn                  ;Left Button pressed?
  JZ  >G30                         ;If not, skip it
  OR   BL,RawLBtn                  ;If so, make it a right button
G30:                               ;Right Button Handled
  MOV  AL,BL                       ;Put it in AL for the return
  POP  BX                          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;NEGATE THE RAW DATA THAT IS BACKWARDS
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Device Info Entry to change
;Outputs: [BX].DIYOffset
;         [BX].DIZ1Offset
;Changes:
;NOTES: We currently only change the Y and Z1 Data.  The X Data does not need
;         to be negated.  I have never personally seen a mouse with Z2 data,
;         so don't know if it needs to be negated or not, and it may depend
;         on the mouse itself.
;       Also, if a mouse actually calls the Wheel a Z-Axis rather than a Wheel
;         (and it doesn't have a Wheel), it may not need to be negated.
;------------------------------------------------------------------------------
NegateData:
  PUSH DI                 ;Save used registers
  MOV  DI,[BX].DIYOffset  ;Point [DI] at the Y Data
  NEG  B [DI]             ;Negate it
  CMP  [BX].DINumWheels,0 ;Does the mouse have a Wheel?
  JE  >N90                ;If not, we're done
  MOV  DI,[BX].DIZ1Offset ;Point [DI] at the Vertical Wheel Data
  NEG  B [DI]             ;Negate it
N90:                      ;Done
  POP  DI                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT RAW DATA AXES THAT ARE WORDS INTO BYTES
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Device Info Entry to change
;                [BX].DIFlags
;Outputs: [BX].DIXOffset
;         [BX].DIYOffset
;         [BX].DIZ1Offset
;         [BX].DIZ2Offset
;Changes:
;------------------------------------------------------------------------------
HandleDataWords:
  PUSH AX,CX,DI,SI       ;Save used registers
  MOV  CX,4              ;Need to do 4 Axes
  MOV  AL,DIFlagX2Bytes  ;Start with
  LEA  SI,[BX].DIXOffset ;  the X Data
D10:                     ;Loop to here for each Axis
  MOV  DI,[SI]           ;Get the Axis Offset
  CALL HandleDataWord    ;Process it
  SHL  AL,1              ;Move over to the
  ADD  SI,2              ;  next Axis
  LOOP D10               ;Keep going until we're done
  POP  SI,DI,CX,AX       ;Restore used registers
  RET

HandleDataWord:
  PUSH AX              ;Save used registers
  OR   DI,DI           ;Valid Data?
  JZ  >W90             ;If not, we're done
  TEST [BX].DIFlags,AL ;If so, is the Data a word?
  JZ  >W90             ;If not, we're done
  MOV  AX,[DI]         ;If so, get the Raw Data
  OR   AX,AX           ;Is it negative?
  JS  >W30             ;If so, handle it
W20:                   ;Data is positive
  CMP  AX,127          ;Is it too positive?
  JLE >W90             ;If not, we're done
  MOV  AL,127          ;If so, maximize it
  JMP >W80             ;Jump to store it
W30:                   ;Data is negative
  CMP  AX,-127         ;Is it too negative?
  JGE >W90             ;If not, we're done
  MOV  AL,-127         ;If so, maximize it
W80:                   ;Store modified data
  MOV  [DI],AL         ;Store it
W90:                   ;Done
  POP  AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COMPENSATE THE RAW DATA FOR THE 2:1 SCALING FACTOR
;Inputs:  DS = ES = TSR Data Area
;         [BX] = Device Info Entry to change
;          TSRFlags.ScalingFactor2
;Outputs: [BX].DIXOffset (X Scaling)
;         [BX].DIYOffset (Y Scaling)
;         compensated for 2:1 Scaling Factor, if necessary
;Changes:
;------------------------------------------------------------------------------
ProcessUSBScaling:
  PUSH DI                ;Save used registers
  MOV  DI,[BX].DIXOffset ;Get the X Data Offset
  CALL DoScaling2        ;Compenstate for Scaling Factor, if needed
  MOV  DI,[BX].DIYOffset ;Get the Y Data Offset
  CALL DoScaling2        ;Compensate for Scaling Factor, if needed
  POP  DI                ;Restore used registers
  RET

DoScaling2:
  PUSH AX,BX         ;Save used registers
  TEST TSRFlags,ScalingFactor2 ;Do we need to do anything?
  JZ  >S90           ;If not, quit
  XOR  AH,AH         ;Assume positive data
  MOV  AL,[DI]       ;Get Raw data
  TEST AL,AL         ;Is it positive?
  JNS >S10           ;If so, continue
  DEC  AH            ;Mark as negative
  NEG  AL            ;Make positive
S10:                 ;Data is positive
  CMP  AL,5          ;Is it small enough to even be in the Table?
  JA  >S50           ;If not, handle it
  MOV  BX,ScalingTbl ;Point at Scaling Table
  XLATB              ;Convert Raw Data to Scaled Data
  JMP >S60           ;Done
S50:                 ;Too big to be in the table
  SHL  AL,1          ;Just multiply by 2
S60:                 ;Convert back to negative, if necessary
  OR   AH,AH         ;Was it positive?
  JZ  >S80           ;If so, we're done
  NEG  AL            ;If not, make it negative again
S80:                 ;Done storing
  MOV  [DI],AL       ;Store the compensated Resolution
S90:                 ;Done
  POP  BX,AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COMPENSATE RAW DATA FOR RESOLUTION FACTOR
;Inputs:  [BX] = InfoTable Entry
;         (Contains XRemainder, YRemainder, DeviceData)
;         Resolution
;Outputs: Compensates DeviceData (X & Y Data) for Resolution
;         XRemainder, YRemainder
;Changes:
;------------------------------------------------------------------------------
ProcessUSBResolution:
  PUSH DI,SI                ;Save used registers
  MOV  SI,[BX].DIXOffset    ;Point [SI] at the X Data
  LEA  DI,[BX].DIXRemainder ;Point [DI] at X Remainder
  CALL DoResolution1        ;Convert it
  MOV  SI,[BX].DIYOffset    ;Point [SI] at the Y Data
  INC  DI                   ;Point [DI] at Y Remainder
  CALL DoResolution1        ;Convert it
  POP  SI,DI                ;Restore used registers
  RET

DoResolution1:
  PUSH AX,BX,CX,DX    ;Save used registers
  MOV  AL,[DI]        ;Get remainder from last time
  CBW                 ;Convert it to a word
  MOV  BX,AX          ;Put it in BX
  MOV  AL,[SI]        ;Get Raw Data
  CBW                 ;Convert it to a word
  ADD  AX,BX          ;Add Remainder from last time
  XOR  CH,CH          ;Assume positive
  TEST AX,AX          ;Is it positive?
  JNS >R10            ;If so, continue
  DEC  CH             ;If not, mark as negative
  NEG  AX             ;Make it positive
R10:                  ;AX = New Data + Remainder (positive), CH = +/- Flag
  MOV  BX,AX          ;Store total movement in BX
  MOV  CL,Resolution8 ;Convert Resolution
  SUB  CL,Resolution  ;  to a Shift Counter
  SHR  AX,CL          ;Compensate for Resolution Factor
  MOV  DX,AX          ;Store it in DX
  SHL  AX,CL          ;Calculate the
  SUB  BX,AX          ;  remainder for next time
  OR   CH,CH          ;Was it positive?
  JZ  >R20            ;If so, we're done
  NEG  BX             ;If not, make it
  NEG  DX             ;  negative
R20:                  ;DL = Compensated Data, BL = Remainder
  MOV  [SI],DL        ;Store Compensated Factor
  MOV  [DI],BL        ;Store remainder for next time
  POP  DX,CX,BX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST THE RAW USB MOUSE DATA TO SEE IF ANYTHING HAS ACTUALLY CHANGED
;Inputs:  [BX] = Device Info Table Entry
;         (Already Compensated/Swapped as needed)
;         MouseUserFarCall
;         TSRFlags.Int15Enabled
;         TSRFlags.EmuHardware
;Outputs: CF = Clear if something has changed
;            = Set if nothing has actually changed since last time
;         [BX].DIBtnDataLastTime
;Changes:
;------------------------------------------------------------------------------
TestUSBChanges:
  PUSH AX,SI                       ;Save used registers
  MOV  AL,[BX].DIBtnOffset         ;Get Button Data
  AND  AL,RawAll5Btns              ;Mask out buttons 6-8
  CMP  DeviceID,DvcIDWheel2Yes     ;Do we care about buttons 4 & 5?
  JE  >C10                         ;If so, continue
  AND  AL,RawAll3Btns              ;If not, mask out buttons 4 & 5
C10:                               ;Unwanted buttons are masked out
  CMP  AL,[BX].DIBtnDataLastTime   ;Have the buttons changed since last time?
  MOV  [BX].DIBtnDataLastTime,AL   ;Save it for next time
  JNE >C30                         ;If buttons have changed, continue
  MOV  SI,[BX].DIXOffset           ;Has the X
  CMP  B [SI],0                    ;  Data changed?
  JNE >C30                         ;If so, continue
  MOV  SI,[BX].DIYOffset           ;Has the Y
  CMP  B [SI],0                    ;  Data changed?
  JNE >C30                         ;If so, continue
  CMP  DeviceID,DvcIDWheel1Yes     ;If so, are the wheel(s) enabled?
  JB  >C70                         ;If not, there are no changes
  CMP  [BX].DINumWheels,1          ;If so, do we have a Wheel?
  JB  >C70                         ;If not, there are no changes
  MOV  SI,[BX].DIZ1Offset          ;Has the Vertical Wheel
  CMP  B [SI],0                    ;  Data changed?
  JNE >C30                         ;If so, continue
  CMP  DeviceID,DvcIDWheel2Yes     ;If not, is the second wheel enabled?
  JNE >C70                         ;If not, there are no changes
  CMP  [BX].DINumWheels,2          ;If so, do we have a second Wheel?
  JB  >C70                         ;If not, there are no changes
  MOV  SI,[BX].DIZ2Offset          ;Has the Horizontal Wheel
  CMP  B [SI],0                    ;  Data changed?
  JE  >C70                         ;If not, there are no changes
C30:                               ;Something has changed since last time
  TEST TSRFlags,EmuHardware        ;Are we emulating hardware?
  JNZ >C40                         ;If so, we don't care about BIOS status
  TEST TSRFlags,Int15Enabled       ;INT 15h Functions even enabled?
  JZ  >C70                         ;If not, we're done
  CMP  W MouseUserFarCall[2],0     ;Is there some User Code to perform?
  JE  >C70                         ;If not, we're done
C40:                               ;High-level functions are enabled
  TEST TSRFlags2,DisableAll        ;All Devices Disabled?
  JNZ >C70                         ;If so, pretend nothing has changed
  TEST [BX].DIFlags,DIFlagDisabled ;This particular Device Disabled?
  JZ  >C80                         ;If not, it's OK to do things
C70:                               ;Nothing has changed since last time
  STC                              ;Set no-change flag
  JMP >C90                         ;Done
C80:                               ;Something has changed
  CLC                              ;Set change flag
C90:                               ;Done
  POP  SI,AX                       ;Restore used regsiters
  RET

;------------------------------------------------------------------------------
;CALCULATE THE CORRECT STATUS WORD TO PUT ON THE STACK FOR THE USER FAR CALL
;Inputs:  [BX] = Info Table Entry
;         (contains DeviceData)
;Outputs: AX = Status Word to put on stack for User Far Call
;Changes:
;------------------------------------------------------------------------------
CalcHandlerStatusByte:
  PUSH CX,DI             ;Save used registers
  MOV  CX,0008h          ;Start with Default Value
  CALL GetAllBtns123     ;Get Buttons 1-3 Status
  OR   CL,AL             ;Add it to the Status Byte
  MOV  DI,[BX].DIXOffset ;Point at the X Data
  TEST B [DI],80h        ;Is the Data Negative?
  JZ  >H30               ;If not, handle the Y data
  OR   CL,HandlerXNeg    ;Mark it as Negative
H30:                     ;Test Y Direction
  MOV  DI,[BX].DIYOffset ;Point at the Y Data
  TEST B [DI],80h        ;Is the Data Negative?
  JZ  >H50               ;If not, we're done
  OR   CL,HandlerYNeg    ;Mark it as Negative
H50:                     ;Done
  MOV  AX,CX             ;Put Status Byte in AX
  POP  DI,CX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET COMBINED STATUS OF BUTTONS ON ALL MICE
;Inputs:  ES = TSR Data Area
;Outputs: AL = Raw Buttons Status (all mice combined)
;              Includes all 5 buttons, if appropriate
;Changes:
;------------------------------------------------------------------------------
GetAllBtns45:
  CALL GetAllRawBtns ;Get all 5 buttons
  AND  AL,18h        ;Mask out buttons 1-3
  SHL  AL,1          ;Move it over so we can use it as a mask
  RET

GetAllBtns123:
  CALL GetAllRawBtns  ;Get all 5 buttons
  AND  AL,RawAll3Btns ;Mask out buttons 4&5
  RET

GetAllRawBtns:
  PUSH CX,DI,DS                    ;Save used registers
  MOV  DS,ES                       ;Point DS at TSR Data Area
  XOR  AL,AL                       ;Start with no buttons pressed
  MOV  CX,(MaxDevices+1)           ;Number of table entries to test
  MOV  DI,DeviceInfoTable          ;Point at Table
A20:                               ;Loop to here for each Table Entry
  TEST [DI].DIFlags,DIFlagInUse    ;Valid Device here?
  JZ  >A30                         ;If not, try next entry
  TEST TSRFlags2,DisableAll        ;All Devices Disabled?
  JNZ >A30                         ;If so, skip to the next entry
  TEST [DI].DIFlags,DIFlagDisabled ;This Device Disabled?
  JNZ >A30                         ;If so, try next entry
  OR   AL,[DI].DIBtnOffset         ;If not, mask it in to our running total
A30:                               ;Go to next entry
  ADD  DI,DeviceInfoStrucSize      ;Point at next Table entry
  LOOP A20                         ;Keep looking
  AND  AL,RawAll5Btns              ;Mask out unwanted buttons
  CMP  ES:DeviceID,DvcIDWheel2Yes  ;Do we care about all 5 buttons?
  JE  >A90                         ;If so, we're done
  AND  AL,RawAll3Btns              ;If not, mask out buttons 4 & 5
A90:                               ;Done
  CALL SwapAllBtnsAL               ;Swap L/R Buttons, if needed
  POP  DS,DI,CX                    ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO PROCESS NOTIFICATIONS FROM THE HOST DRIVER REGARDING DEVICE CHANGES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HANDLE A NOTIFICATION FROM THE HOST DRIVER FOR SOMETHING RELATED TO THE DEVICE
;Inputs:  AX = Device Owner Call Code (various possibilities)
;         [BX] = Info Table Offset
;         CX, DX = Varies by exact type of call
;         DS = ES = TSR Data Area
;Outputs: Depends on Call Type
;         Calls appropriate "subfunction"
;Changes: Nearly all registers
;------------------------------------------------------------------------------
DvcOwnerFarCall:
  CLD                          ;Go forward with string functions
  MOV  DI,BX                   ;Point DI at the Info Table Entry
  MOV  BX,OwnerCallFunctionTbl ;Point at the Function table to search
  CALL SearchByteToWordTbl     ;Look for the function (in AL)
  JC  >H90                     ;If invalid function, quit
  CALL BX                      ;If OK, do it
H90:
  RETF

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST SAYING THAT A NEW DEVICE HAS BEEN ADDED
;Inputs:  [DI] = Info Table Entry
;         DS = ES = TSR Data Area
;         CL = Host Index
;         CH = Device Address
;         DL = Interface
;         DH = Alternate Interface
;Outputs:
;Changes: Prompts us to look for a new Device earlier than normal
;NOTES:   Originally, this routine directly called LookForNewDevice.
;         For some reason, this did not work with some Devices (they would
;           return bad descriptors).  This is probably due to the Devices not
;           being given enough time to "reorganize" themselves after the
;           enumeration process of the Host Driver, which downloads the same
;           descriptors we download again here.
;         So now, instead of calling LookForNewDevice directly, this routine
;           sets the Int08Counter to a small number so that we look for
;           the new Device after a small delay.  Doing it after a small
;           delay, instead of doing it immediately, seems to work better
;           (at least with some Devices).
;------------------------------------------------------------------------------
DeviceAdded:
  CMP  Int08Counter,0 ;Disabled?
  JE  >A90            ;If so, quit
  MOV  Int08Counter,2 ;If not, look for a new Device right away
A90:                  ;Done
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST SAYING THAT A DEVICE HAS BEEN DISCONNECTED
;Inputs:  [DI] = Info Table Entry
;         DS = ES = TSR Data Area
;         CL = Host Index
;         CH = Device Address
;Outputs:
;Changes: Removes Device from Configuration
;NOTES:   This is called if a REGISTERED Device has been disconnected
;------------------------------------------------------------------------------
DeviceDisconnected:
  CALL RlsPressedSysESDI ;Release any pending keystrokes
  CALL ResetDvcEntryDI   ;Delete entry from our table
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST SAYING THAT AN UNREGISTERED DEVICE IS DISCO'D
;Inputs:  DS = ES = TSR Data Area
;         CL = Host Index
;         CH = Device Address
;Outputs:
;Changes:
;NOTES: This is called if an UNREGISTERED Device has been disconnected,
;         so it will NOT be in DeviceInfoTable.  It may or may not be
;         in the BadDeviceTable.
;------------------------------------------------------------------------------
Device0Disconnected:
  CALL DeleteBadDevices ;Delete the Bad Entries from our Table, if there
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM THE HOST TO TEMPORARILY STOP LOOKING FOR DEVICES
;Inputs:  DS = ES = TSR Data Area
;         CL = Host Index
;         CH = Device Address
;         DL = Interface Number
;         DH = TimeOut Value to use (deciseconds)
;         DI = Device Info Table Entry
;Outputs: DontLookCounter
;Changes:
;NOTES: This should only be called if a Device-specific Driver is trying
;         to take control of an already registered device from us.
;       We actually stop looking for ALL new Interfaces, rather than just
;         the one that is being requested.
;------------------------------------------------------------------------------
StartDontLook:
  PUSH AX,BX,DX,SI        ;Save used registers
  MOV  DontLookCounter,-1 ;Start not looking
  MOV  SI,Int14Request2   ;Point at Request Structure
  CALL RlsPressedSysESDI  ;Release any pending keystrokes
  CALL RlsDvcNoResetDI    ;Release the Device, don't Reset it
  MOV  AL,DH              ;Put seconds in AL
  XOR  AH,AH              ;Multiply
  MOV  BX,18206           ;  seconds
  MUL  BX                 ;  by
  MOV  BX,10000           ;  1.8206
  DIV  BX                 ;  (number of timer ticks per decisecond)
  CMP  DX,5000            ;Do we need to round up?
  JB  >S90                ;If not, continue
  INC  AX                 ;If so, round up
S90:                      ;Done
  INC  AX                 ;Add one more Timer Tick for good measure
  MOV  DontLookCounter,AX ;Store the Counter Value
  POP  SI,DX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE A REQUEST FROM A HOST SAYING THAT IT'S BEEN UNINSTALLED
;Inputs:  CL = USB Host Index that isn't there any more
;         DS = ES = TSR Data Area
;Outputs:
;Changes:
;------------------------------------------------------------------------------
HostDisconnected:
  PUSH BX,DI                    ;Save used registers
  MOV  DI,DeviceInfoTable       ;Point at table
  MOV  BL,MaxDevices            ;Number of table entries
D20:                            ;Loop to here for each table entry
  TEST [DI].DIFlags,DIFlagInUse ;Empty Table Entry?
  JZ  >D40                      ;If so, skip it
  CMP  [DI].DIHostIndex,CL      ;Is it the Host this Device is on?
  JNE >D40                      ;If not, skip it
  CALL RlsPressedSysESDI        ;If so, release any pending keystrokes
  CALL ResetDvcEntryDI          ;Delete it from the Info Table
D40:                            ;Done with this table entry
  ADD  DI,DeviceInfoStrucSize   ;Point at the next Table Entry
  DEC  BL                       ;Decrement loop counter
  JNZ  D20                      ;If not done yet, keep looking
  POP  DI,BX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE VARIOUS REQUESTS FROM THE USB HOST DRIVER ABOUT UPSTREAM CHANGES
;Inputs:  Depends on Call
;Outputs:
;Changes: We don't care about any of these calls, since they don't directly
;           affect our program.  Any critical changes to upstream devices
;           are eventually reflected to us as a call directly addressing
;           one of our Devices, which is how we handle those things.
;------------------------------------------------------------------------------
DeviceEnabled:        ;Device Enabled
DeviceDisabled:       ;Device Disabled
DeviceReset:          ;Device Reset
DeviceSuspend:        ;Device Suspended
DeviceResume:         ;Device Resumed
DevicePowerOn:        ;Device Power On
DevicePowerOff:       ;Device Power Off
DevicePowerReset:     ;Device Power Reset
NewHostAdded:         ;New Host Added
HostHWErr:            ;Host System/Processor Error
HostRun:              ;Host Running
HostStop:             ;Host Stopped
HostReset:            ;Host Reset
HostSuspend:          ;Host Suspended
HostResume:           ;Host Resumed
HostDebug:            ;Host in Debug Mode
HostSST:              ;Host Single-Stepped
TimingChange:         ;Host Timing Change
DeviceDisconnectedUS: ;Device Disconnect Upstream
DeviceEnabledUS:      ;Device Enable Upstream
DeviceDisabledUS:     ;Device Disable Upstream
DeviceResetUS:        ;Device Reset Upstream
DeviceSuspendUS:      ;Device Suspend Upstream
DeviceResumeUS:       ;Device Resume Upstream
DevicePowerOnUS:      ;Device Power On Upstream
DevicePowerOffUS:     ;Device Power Off Upstream
DevicePowerResetUS:   ;Device Power Reset Upstream
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;SUPPORT CODE TO RELEASE/RESET A DEVICE
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;COMPLETELY RELEASE A DEVICE THAT WE CURRENTLY OWN (POSSIBLY INCLUDING A RESET)
;Inputs:  DS = ES = TSR Data Area
;         [DI] = Device Info Entry to Release
;         [SI] = Int 14 Request to use
;Outputs: Releases Device Entry
;           Closes Periodic Handle
;           Unregisters as Interface Owner
;           Resets Device (if appropriate)
;           Resets Device Info Entry
;Changes:
;------------------------------------------------------------------------------
RlsDvcNoResetDI:
  PUSH BX                                        ;Save used registers
  XOR  BL,BL                                     ;Mark as No Reset
  JMP >R00                                       ;Do it
RlsDvcDoResetDI:
  PUSH BX                                        ;Save used registers
  MOV  BL,-1                                     ;Mark as a Reset
;  JMP >R00                                       ;Do it
R00:
  PUSH AX                                        ;Save used registers
  MOV  W [SI].I14RCallBackAddr[0],-1             ;Set CallBackAddr to nothing
  MOV  AX,W [DI].DIHostIndex                     ;Copy Host Index and
  MOV  W [SI].I14RHostIndex,AX                   ;  Device Address
  MOV  AX,[DI].DIIntHandle                       ;Copy
  MOV  [SI].I14RRequestHandle,AX                 ;  Interrupt Handle Number
  MOV  [SI].I14RRequestType,I14RRTCloseHandle    ;Request = Close Handle
  CALL DoInt14CallSaveAllSI                      ;Do it
  MOV  AX,[DI].DIRegHandle                       ;Copy
  MOV  [SI].I14RRequestHandle,AX                 ;  Registry Handle
  MOV  [SI].I14RRequestType,I14RRTUnRegIntfOwner ;Request = UnRegister
  CALL DoInt14CallSaveAllSI                      ;Do it
  OR   BL,BL                                     ;Need to Reset the Device?
  JZ  >R80                                       ;If not, we're done
  MOV  [SI].I14RRequestType,I14RRTResetDevice    ;Request = Reset
  CALL DoInt14CallSaveAllSI                      ;Do it
R80:                                             ;Done with Host
  CALL ResetDvcEntryDI                           ;Delete the Device Info Entry
  POP  AX                                        ;Restore used registers
  POP  BX                                        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESET A DEVICE INFO TABLE ENTRY
;Inputs:  DS = ES = TSR Data Area
;        [BX] or [DI] = Info Table Entry to reset
;         CLD already issued
;Outputs:
;Changes: Resets (clears out) Info Table Entry
;NOTES:   This preserves the Disabled/Enabled and Exchange status of
;           the Device, so that the user does not need to worry about
;           re-setting it after a temporary Disconnect or Host Reset.
;         Unlike most routines, this one preserves Flags across the Call!
;------------------------------------------------------------------------------
ResetDvcEntryBX:
  PUSH DI              ;Save used registers
  MOV  DI,BX           ;Point DI at the Table Entry
  CALL ResetDvcEntryDI ;Reset the Entry
  POP  DI              ;Restore used registers
  RET

ResetDvcEntryDI:
  PUSH AX,CX,DI                               ;Save used registers
  PUSHF                                       ;Save flags
  CLI                                         ;Disable interrupts
  AND  B [DI],(DIFlagDisabled+DIFlagSwapBtns) ;Reset non-preserved flags
  INC  DI                                     ;Point at next Table Byte
  XOR  AX,AX                                  ;Need to store zeroes
  STOSB                                       ;Reset it
  MOV  CX,((DeviceInfoStrucSize-2)/2)         ;Number of words to store
  REP  STOSW                                  ;Reset the reset of the table
  POPF                                        ;Restore flags
  POP  DI,CX,AX                               ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO FIND/STORE ENTRIES IN VARIOUS STRUCTURE-BASED TABLES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;CONVERT A DEVICE INFO INDEX TO AN OFFSET
;Inputs:  ES = Data Segment where data is located
;         CL = Device Index, or BL = Device Index (if DIIndex2OffsetBL)
;Outputs: DI = Offset to Table Entry (Assuming ES is the segment)
;Changes:
;------------------------------------------------------------------------------
DIIndex2OffsetBL:
  PUSH CX             ;Save used registers
  MOV  CL,BL          ;Put Index in CL
  CALL DIIndex2Offset ;Do it
  POP  CX             ;Restore used registers
  RET

DIIndex2Offset:
  PUSH AX,CX,DX               ;Save used registers
  XOR  CH,CH                  ;Index is actually a byte
  MOV  DI,DeviceInfoTable     ;Offset to beginning of Table
  MOV  AX,DeviceInfoStrucSize ;Size of each entry
  MUL  CX                     ;Multiply Index by the size of each entry
  ADD  DI,AX                  ;Add the offset of the beginning of the table
  POP  DX,CX,AX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND AN EMPTY INFO TABLE ENTRY
;Inputs:  DS = ES = TSR Data Area
;Outputs: ZF = Clear if OK (found an empty Table entry)
;              [BX] = Table Entry
;            = Set if error (Table is full)
;               BX  = 0
;Changes:
;------------------------------------------------------------------------------
FindEmptyInfoBX:
  PUSH CX                       ;Save used registers
  MOV  BX,DeviceInfoTable       ;Point at first Table Entry
  MOV  CX,MaxDevices            ;Number of Table Entries
E10:                            ;Loop to here to find empty Table Entry
  TEST [BX].DIFlags,DIFlagInUse ;Empty Table entry?
  JZ  >E90                      ;If so, continue
  ADD  BX,DeviceInfoStrucSize   ;Point at next table entry
  LOOP E10                      ;Keep looking
  XOR  BX,BX                    ;Table is full!
E90:                            ;Done
  OR   BX,BX                    ;Set Return Flag
  POP  CX                       ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;SUPPORT CODE FOR BAD DEVICE TABLE
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;ADD A BAD (INCOMPATIBLE) DEVICE HOST & ADDRESS TO THE TABLE
;Inputs:  DS:[SI] = Int 14 Structure containing Bad Device Data (Host/Addr/Intf)
;Outputs: Adds Host & Address to Bad Device Table
;Changes:
;------------------------------------------------------------------------------
AddBadDevice:
  PUSH AX,BX,CX                 ;Save used registers
  PUSHF                         ;Save Flags
  CLI                           ;Disable Interrupts
  MOV  BX,BadDeviceTable        ;Point at Table
  MOV  CX,MaxBadDevices         ;Number of Entries in Table
A20:                            ;Loop here to find empty table entry
  TEST [BX].BDFlags,BDFlagInUse ;This entry already used?
  JNZ >A50                      ;If so, skip it
  OR   [BX].BDFlags,BDFlagInUse ;If not, mark it as used
  MOV  AX,W [SI].I14RHostIndex  ;Store the Host Index
  MOV  W [BX].BDHostIndex,AX    ;  & Device Address
  MOV  AL,[SI].I14RInterfaceNum ;Store the
  MOV  [BX].BDInterfaceNum,AL   ;  Interface Number
  MOV  AX,NewDeviceError        ;Store the
  MOV  [BX].BDNewDeviceError,AX ;  Error Code
  MOV  AL,NewDeviceStage        ;Store the
  MOV  [BX].BDNewDeviceStage,AL ;  New Device Stage
  JMP >A90                      ;Done
A50:                            ;Entry not Empty
  ADD  BX,BadDeviceStrucSize    ;Point at next entry
  LOOP A20                      ;Keep going until we're done
A90:                            ;Done
  POPF                          ;Restore Flags
  POP  CX,BX,AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DELETE BAD (INCOMPATIBLE) DEVICE HOSTS & ADDRESSES FROM THE TABLE
;Inputs:  CL = Host Index, CH = Address
;Outputs: Deletes Host & Address from Bad Device Table
;Changes:
;NOTES: This is called as the result a a Device Disconnect, so we need to
;         delete all Interfaces of the Device.  There will probably only
;         be one Entry per Address, but we need to check all of them.
;------------------------------------------------------------------------------
DeleteBadDevices:
  PUSH AX,BX,CX,DX,DI            ;Save used registers
  PUSHF                          ;Save Flags
  MOV  AX,CX                     ;AL = Host Index, AH = Device Address
  MOV  DL,-1                     ;Don't care about the Interface Number
  CLI                            ;Disable Interrupts
D10:                             ;Loop here for each Table Entry
  CALL FindBadDeviceEntry        ;Any entries in the Table (returns BX)?
  JC  >D90                       ;If not, we're done
  PUSH AX                        ;If so, save used registers
  XOR  AX,AX                     ;Write zeroes
  MOV  CX,(BadDeviceStrucSize/2) ;Number of words to write
  MOV  DI,BX                     ;Point DI at Table Entry
  REP  STOSW                     ;Write them
  POP  AX                        ;Restore used registers
  JMP  D10                       ;Keep looking for more Entries
D90:                             ;Done
  POPF                           ;Restore Flags
  POP  DI,DX,CX,BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COMPARE A HOST AND ADDRESS TO SEE IF IT'S IN OUR BAD DEVICE TABLE
;Inputs:  CL = Host, CH = Address, DL = Interface, DH = Alt Interface
;Outputs: CF = Set if it's not in the Bad Device Table
;            = Clear if it is in Table
;Changes:
;NOTES: This ignores the Alt Interface Setting (we don't care about it).
;------------------------------------------------------------------------------
TestBadDevice:
  PUSH AX,BX              ;Save used registers
  MOV  AX,CX              ;AL = Host Index, AH = Device Address
                          ;DL = Interface Number
  CALL FindBadDeviceEntry ;Is it in the Table (returns BX)?
  POP  BX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND A MATCHING HOST/ADDRESS/INTERFACE ENTRY IN THE BAD DEVICE TABLE
;Inputs:  AL = Host Index to find
;         AH = Device Address to find
;         DL = Interface Number to find
;              if -1, we don't care about the Interface Number
;Outputs: CF = Clear if Entry Found
;              BX = Entry Offset
;            = Set if Entry not found
;              BX = Undefined
;Changes:
;------------------------------------------------------------------------------
FindBadDeviceEntry:
  PUSH CX                       ;Save used registers
  PUSHF                         ;Save flags
  MOV  BX,BadDeviceTable        ;Point at Table
  MOV  CX,MaxBadDevices         ;Number of Entries in Table
  CLI                           ;Disable Interrupts
E20:                            ;Loop here to find empty table entry
  TEST [BX].BDFlags,BDFlagInUse ;Valid Entry?
  JZ  >E50                      ;If not, skip it
  CMP  W [BX].BDHostIndex,AX    ;Do the Host Index & Device Address match?
  JNE >E50                      ;If not, skip it
  CMP  DL,-1                    ;Do we care about the Interface Number?
  JE  >E80                      ;If not, we've found it!
  CMP  [BX].BDInterfaceNum,DL   ;Does the Interface Number match?
  JE  >E80                      ;If so, we've found it!
E50:                            ;Go to next entry
  ADD  BX,BadDeviceStrucSize    ;Point at the next entry
  LOOP E20                      ;Keep looking
E70:                            ;Error
  POPF                          ;Restore Flags
  STC                           ;Set Not Found Flag
  JMP >E90                      ;Done
E80:                            ;OK
  POPF                          ;Restore Flags
  CLC                           ;Set Found Flag
E90:                            ;Done
  POP  CX                       ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO ISSUE INT 14h CALLS TO THE HOST DRIVER
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;ISSUE AN INT 14 CALL TO THE USB HOST
;Inputs:  DS:[DX] = Int 14 Request (filled with appropriate data)
;      OR DS:[DI]
;      OR DS:[SI]
;Outputs: CF = Set if error (host stalled, etc.)
;            = Clear if OK
;Changes: Sends the appropriate info to the USB Host via Int 14 call
;------------------------------------------------------------------------------
DoInt14CallSaveAllSI:       ;Save all changed registers
                            ;  [SI] = Int14 Struc
  PUSH DX                   ;Save used registers
  MOV  DX,SI                ;Point DX at the Data Offset
  CALL DoInt14CallSaveAllDX ;Do it
  POP  DX                   ;Restore used registers
  RET

DoInt14CallAXSI:     ;Save all changed registers except AX
                     ;  [SI] = Int14 Struc
  PUSH DX            ;Save used registers
  MOV  DX,SI         ;Point DX at the Data Offset
  CALL DoInt14CallAX ;Do it
  POP  DX            ;Restore used registers
  RET

DoInt14CallCXSI:     ;Save all changed registers except CX,
                     ;  [SI] = Int14 Structure
  PUSH DX            ;Save used registers
  MOV  DX,SI         ;Point DX at the Data Offset
  CALL DoInt14CallCX ;Do it
  POP  DX            ;Restore used registers
  RET

DoInt14CallCXDI:     ;Save all changed registers except CX,
                     ;  [DI] = Int14 Structure
  PUSH DX            ;Save used registers
  MOV  DX,DI         ;Point DX at the Data Offset
  CALL DoInt14CallCX ;Do it
  POP  DX            ;Restore used registers
  RET

DoInt14CallAXCXSI:     ;Save all changed registers except AX & CX
                       ;  [SI] = Int14 Structure
  PUSH DX              ;Save used registers
  MOV  DX,SI           ;Point DX at the Data Offset
  CALL DoInt14CallAXCX ;Do it
  POP  DX              ;Restore used registers
  RET

DoInt14CallBXCXSI:     ;Save all changed registers except BX & CX
                       ;  [SI] = Int14 Structure
  PUSH DX              ;Save used registers
  MOV  DX,SI           ;Point DX at the Data Offset
  CALL DoInt14CallBXCX ;Do it
  POP  DX              ;Restore used registers
  RET

DoInt14CallCXDXSI:     ;Save all changed registers except CX & DX
                       ;  [SI] = Int14 Structure
  MOV  DX,SI           ;Point DX at the Data Offset
  CALL DoInt14CallCXDX ;Do it
  RET

DoInt14CallSaveAllDX: ;Save all changed registers
                      ;  [DX] = Int14 Structure
  PUSH AX,BX,CX,DX    ;Save registers that Int14 changes!
  CALL DoInt14Call    ;Do it
  POP  DX,CX,BX,AX    ;Restore used registers
  RET

DoInt14CallAX:
  PUSH BX,CX,DX    ;Save registers that Int14 changes, except AX!!
  CALL DoInt14Call ;Do it
  POP  DX,CX,BX    ;Restore used registers
  RET

DoInt14CallCX:
  PUSH AX,BX,DX    ;Save registers that Int14 changes, except CX!!
  CALL DoInt14Call ;Do it
  POP  DX,BX,AX    ;Restore used registers
  RET

DoInt14CallAXCX:
  PUSH BX,DX       ;Save registers that Int14 changes, except AX & CX!!
  CALL DoInt14Call ;Do it
  POP  DX,BX       ;Restore used registers
  RET

DoInt14CallBXCX:
  PUSH AX,DX       ;Save registers that Int14 changes, except BX & CX
  CALL DoInt14Call ;Do it
  POP  DX,AX       ;Restore used registers
  RET

DoInt14CallCXDX:
  PUSH AX,BX       ;Save registers that Int14 changes, except CX & DX!!
  CALL DoInt14Call ;Do it
  POP  BX,AX       ;Restore used registers
  RET

;Int 14 returns various values in AX, BX, CX, & DX
DoInt14Call:
  STI           ;Enable Interrupts
  MOV  AX,5001h ;Set Function
  MOV  BX,'US'  ;Set
  MOV  CX,'B!'  ;  Registers
  STC           ;Preset Error Flag
  INT  14h      ;Do it
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE NEEDED TO STORE AND GET THINGS FROM THE CIRCULAR BUFFER
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;-----------------------------------------------------------------------------
;PUT A BYTE AT THE END OF THE CIRCULAR BUFFER
;Inputs:  DS = ES = TSR Data Area
;         BufferHead, BufferTail = Head and Tail of the circular buffer
;         AL = Byte to put in the Buffer
;Outputs: Adds a Byte to the end of the Buffer
;         CF = Clear if it worked OK
;            = Set if it didn't work
;Changes: BufferTail, BufferCount
;-----------------------------------------------------------------------------
PutBufferAL:
  PUSH DI            ;Save used registers
  CLI                ;Disable interrupts
  CMP  BufferCount,BufferSize ;Is the buffer already full
  JAE >P70           ;If so, Error
  MOV  DI,BufferTail ;If OK, get the buffer Tail
  INC  DI            ;Point at the next space in the buffer
  CMP  DI,BufferEnd  ;Will this put us past the end of the Buffer?
  JBE >P20           ;If not, our pointer is OK
  MOV  DI,Buffer     ;If so, point at the beginning of buffer
P20:                 ;Buffer Pointer is correct
  MOV  [DI],AL       ;Store the code
  MOV  BufferTail,DI ;Store the new Tail
  INC  BufferCount   ;Increment the counter
  JMP >P80           ;Done
P70:                 ;The buffer is already full
  STC                ;Set the "buffer is full" flag
  JMP >P90           ;We're done
P80:                 ;It worked OK
  CLC                ;Set the "it worked" flag
P90:                 ;We're done
  STI                ;Enable interrupts
  POP  DI            ;Restore used registers
  RET

;-----------------------------------------------------------------------------
;GET A BYTE FROM THE BEGINNING OF THE CIRCULAR BUFFER
;Inputs:  DS = ES = TSR Data Area
;         BufferHead, BufferTail = Head and Tail of the circular buffer
;Outputs: Gets the next Byte from the buffer
;         BL = Byte from the buffer
;         CF = Clear if it worked OK
;            = Set if it didn't work
;Changes: BufferHead (if there is anything in the buffer)
;-----------------------------------------------------------------------------
GetBufferBL:
  PUSH DI            ;Save used registers
  CLI                ;Disable interrupts
  CMP  BufferCount,0 ;Does the buffer have anything in it?
  JE  >G70           ;If not, Error
  MOV  DI,BufferHead ;If so, point at the buffer Head
  MOV  BL,[DI]       ;Get the Code
  INC  DI            ;Point at the next space in our buffer
  CMP  DI,BufferEnd  ;Will this put us past the end of the Buffer?
  JBE >G10           ;If not, our pointer is OK
  MOV  DI,Buffer     ;If so, point at the beginning of the Buffer
G10:                 ;Buffer Pointer is OK
  MOV  BufferHead,DI ;Store the new Head
  DEC  BufferCount   ;Decrement the counter
  JMP >G80           ;Done
G70:                 ;The buffer is empty
  STC                ;Set the "buffer was empty" flag
  JMP >G90           ;We're done
G80:                 ;There's a scan code in AL
  CLC                ;Set the "it worked" flag
G90:                 ;We're done
  STI                ;Enable interrupts
  POP  DI            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SEND THE BYTES THAT ARE IN THE BUFFER
;Inputs:  DS = ES = TSR Data Area
;         Buffer
;Outputs:
;Changes: Sends the Code(s) using Hardware Emulation
;------------------------------------------------------------------------------
SendCodes:
  PUSH AX,BX               ;Save used registers
  CMP  BufferCount,0       ;Any codes to send?
  JE  >S95                 ;If not, quit
;  MOV  AL,9                ;Point INT 09 at
;  MOV  BX,OFFSET Int09Vect ;  our Code that
;  CALL SwapIntVect         ;  doesn't do anything

  CALL DisableKeybIRQ      ;Disable the Keyboard IRQ
  CMP  Method,1            ;Using Method 1?
  JE  >S10                 ;If so, we need the Mouse IRQ Enabled
  CALL DisableMouseIRQ     ;If not, Disable the Mouse IRQ

S10:                       ;Loop to here for each Scan Code in buffer
  CALL GetBufferBL         ;Get Keystroke from Buffer (BL)
  JC  >S90                 ;If buffer is Empty, we're done
  CALL FakeIt              ;If OK, send the Code
  JMP  S10                 ;Keep looking for more codes
S90:                       ;Done, restore INT 09

  CALL EnableKeybIRQ       ;Enable the Keyboard IRQ
;  CMP  Method,1            ;Using Method 1?
;  JE  >S10                 ;If so, we didn't Disable the Mouse IRQ
  CALL EnableMouseIRQ      ;If not, Enable the Mouse IRQ



;  MOV  BX,OFFSET Int09Vect ;Restore INT 08 back
;  CALL SwapIntVect         ;  to its original setting
S95:                       ;Done
  POP  BX,AX               ;Restore used registers
  RET

;;------------------------------------------------------------------------------
;;PERFORM THIS CODE EVERY TIME A KEYBOARD INTERRUPT HAPPENS DURING HW EMULATION
;;Inputs:
;;Outputs:
;;Changes: Simply process the Keyboard Interrupt, doing nothing with it
;;------------------------------------------------------------------------------
;Int09:
;  PUSH AX     ;Save used registers
;  STI         ;Enable Interrupts
;  IN   AL,60h ;Get the Scan Code
;  MOV  AX,AX  ;Delay a little bit
;;  CLI         ;Disable
;;  IN   AL,61h ;  interrupts
;;  PUSH AX     ;  and
;;  XOR  AL,80h ;  acknowledge
;;  OUT  61h,AL ;  the
;;  POP  AX     ;  key-
;;  OUT  61h,AL ;  board
;  MOV  AL,20h ;Tell the interrupt controller
;  OUT  20h,AL ;  we're done
;;  STI         ;Enable interrupts
;  POP  AX     ;Restore used registers
;  IRET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code needed to Emulate Hardware Codes, using the various Methods
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;SEND THE NEXT CODE
;Inputs:  DS = ES = TSR Data Area
;         BL = Code To Send
;         Method
;Outputs:
;Changes: Emulates the Code
;------------------------------------------------------------------------------
FakeIt:
;  CALL ClearKeyboard ;Clear out the Keyboard Buffer
  CMP  Method,2      ;Which Method to use?
  JE  >F20           ;If Method 2, handle it
  JA  >F30           ;If Method 3, handle it
  CALL FakeIt1       ;Do Method 1
  JMP >F90           ;Done
F20:                 ;Method 2
  CALL FakeIt2       ;Do Method 2
  JMP >F90           ;Done
F30:                 ;Method 3
  CALL FakeIt3       ;Do Method 3
F90:                 ;Done
;  CALL ClearKeyboard ;Clear out the Keyboard Buffer
  RET

;ClearKeyboard:
;  PUSH AX     ;Save used registers
;  IN   AL,61h ;Get curent Keyboard Control Register
;  PUSH AX     ;Save it
;  XOR  AL,80h ;Toggle bit 7 (this clears the keyboard)
;  OUT  61h,AL ;Send it
;  POP  AX     ;Restore the
;  OUT  61h,AL ;  original value
;  POP  AX     ;Restore used registers
;  RET


;NOTE: All PORT 60h commands to Mouse must be preceded by D4h or they will be
;  sent to the Keyboard!  Don't know if this applies to us or not!

;------------------------------------------------------------------------------
;FAKE A CODE USING METHOD 1 (WORKS WITH WINDOWS?)
;Inputs:  DS = ES = TSR Data Area
;         BL = Code to Send
;Outputs: Sends the Code to the keyboard controller to fake a Byte
;Changes:
;------------------------------------------------------------------------------
FakeIt1:
  PUSH AX              ;Save used registers
  CLI                  ;Disable interrupts
  CALL WaitKbdInEmpty  ;Wait for the Input Buffer to Clear
  MOV  AL,0D3h         ;When clear, function D3H (Fake a Code with our Byte)
  OUT  64h,AL          ;Send it
  CALL WaitKbdInEmpty  ;Wait for the Controller to take it in
  MOV  AL,BL           ;Send our ScanCode to the keyboard
  OUT  60h,AL          ;  (IRQ will be generated automatically)
  STI                  ;Enable interrupts (let INT 74h occur)
  CALL WaitKbdInEmpty  ;Wait for the Input Buffer to Clear
  CALL WaitKbdOutEmpty ;Wait for the Output Buffer to Clear
  POP  AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FAKE A KEYBOARD KEYSTROKE USING METHOD 2 (WORKS WITH MOST AT KEYBOARDS)
;Inputs:  DS = ES = TSR Data Area
;         BL = Scan Code to fake
;         Mouse and Keyboard IRQ's already Disabled
;Outputs: Sends the Scan Code to the keyboard controller to fake a keystroke
;Changes:
;------------------------------------------------------------------------------
FakeIt2:
  PUSH AX              ;Save used registers
  PUSHF                ;Save Flags
  CLI                  ;Disable interrupts
;  CALL DisableMouseIRQ ;Disable real mouse IRQ
;  CALL DisableKeybIRQ  ;Disable real Keyboard IRQ
  CALL WaitKbdOutEmpty ;Wait for Keyboard Output Buffer to Clear
  JNZ >F90             ;If it never clears, quit
  CALL WaitKbdInEmpty  ;Wait for Keyboard Input Buffer to Clear
;  JNZ >F90             ;If it never clears, quit
  MOV  AL,KCCBWrite    ;Tell Keyboard Controller to
  OUT  64h,AL          ;  read the next byte as the KCCB
  CALL WaitKbdInEmpty  ;Wait for the Controller to take it in
;  JNZ >F90             ;If it didn't work, quit
  MOV  AL,BL           ;Send our scan code
  OUT  60h,AL          ;  to the keyboard as the KCCB
  CALL WaitKbdInEmpty  ;Wait for the buffer to Empty
  MOV  AL,KCCBRead     ;Tell Keyboard Controller to
  OUT  64h,AL          ;  output the KCCB to Port 60h
  CALL WaitKbdOutFull  ;Wait for the Keyboard to take it in
  MOV  AL,KCCBWrite    ;Tell Keyboard Controller to
  OUT  64h,AL          ;  read the next byte as the KCCB
  CALL WaitKbdInEmpty  ;Wait for the Controller to take it in
  MOV  AL,45h          ;Tell the Keyboard Controller to
  OUT  60h,AL          ;  work normally again
  IN   AL,60h          ;This prevents some computers from generating INT's
  INT  74h             ;Perform the Int 74h Code
;  CALL WaitKbdInEmpty  ;Wait for the Input Buffer to Clear
;  MOV  AL,0A8h         ;Enable the Mouse Port
;  OUT  64h,AL          ;  just to be safe
F90:                   ;Done
;  CALL EnableKeybIRQ   ;Reenable real keyboard IRQ
;  CALL EnableMouseIRQ  ;Reenable real mouse IRQ
  POPF                 ;Restore Flags
  POP  AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WAIT FOR THE KEYBOARD INPUT/OUTPUT BUFFER TO BE FULL/EMPTY
;Inputs:  DS = ES = TSR Data Area
;Outputs: If WaitXXFull:
;           ZF = Clear if OK (Buffer is Full)
;              = Set if Error (timed out waiting for Buffer to Fill)
;         If WaitXXEmpty:
;           ZF = Set if OK (Buffer is Empty)
;              = Clear if Error (timed out waiting for Buffer to Empty)
;Changes:
;------------------------------------------------------------------------------
WaitKbdInFull:
  PUSH AX                ;Save used registers
  MOV  AH,P64InBuffFull  ;AH = bit to test for
  JMP >F00               ;Do it
WaitKbdOutFull:
  PUSH AX                ;Save used registers
  MOV  AH,P64OutBuffFull ;AH = bit to test for
;  JMP >F00               ;Do it
F00:
  PUSH CX                ;Save used registers
  PUSHF                  ;Save flags
  CLI                    ;Disable interrupts
  MOV  CX,200            ;Try it 200 times before timing out
F10:                     ;Loop to here to wait for buffer to be full
  IN   AL,64h            ;Get the data from the controller
  TEST AL,AH             ;Is it full yet?
  LOOPZ F10              ;If not, keep waiting
F90:                     ;Done
  POPF                   ;Restore flags
  POP  CX                ;Restore used registers
  POP  AX                ;Restore used registers
  RET

WaitKbdInEmpty:
  PUSH AX                ;Save used registers
  MOV  AH,P64InBuffFull  ;AH = bit to test for
  JMP >E00               ;Do it
WaitKbdOutEmpty:
  PUSH AX                ;Save used registers
  MOV  AH,P64OutBuffFull ;AH = bit to test for
;  JMP >E00               ;Do it
E00:
  PUSH CX                ;Save used registers
  PUSHF                  ;Save flags
  CLI                    ;Disable interrupts
  MOV  CX,200            ;Try it 200 times before timing out
E10:                     ;Loop to here to wait for buffer to be full
  IN   AL,64h            ;Get the data from the controller
  TEST AL,AH             ;Is it empty yet?
  LOOPNZ E10             ;If not, keep waiting
E90:                     ;Done
  POPF                   ;Restore flags
  POP  CX                ;Restore used registers
  POP  AX                ;Restore used registers
  RET

;-----------------------------------------------------------------------------
;FAKE A KEYBOARD INTERRUPT WITh METHOD 3
;Inputs:  DS = ES = TSR Data Area
;         BL = Code to fake
;Outputs:
;Changes: Fakes a keyboard interrupt
;-----------------------------------------------------------------------------
FakeIt3:
  PUSH AX,SI                    ;Save used registers
F30:                            ;Our loop test flag is set
  MOV  ByteToSend,BL            ;Put the new Code where Int01 can find it
  MOV  InstrCounter,1000        ;Initialize Maximum Instruction Counter
  AND  TSRFlags,(NOT EOIIssued) ;Mark as not receiving End-of-Interrupt yet
  CALL StartInt01               ;If so, initialize the Single Step Vector
  CALL PutCode                  ;Fake the code
  CALL StopInt01                ;If not recursive, reset the Single Step Vector
  MOV  InstrCounter,0           ;Reset OUT A0h,20h Instruction Counter
F90:                            ;Done
  POP  SI,AX                    ;Restore used registers
  RET

;-----------------------------------------------------------------------------
;DISABLE KBD INT & CHANGE INT 01 (SST) CODE TO OURS
;Inputs:  DS = ES = TSR Data Area
;         Mouse and Keyboard IRQ's already disabled
;Outputs:
;Changes:
;-----------------------------------------------------------------------------
StartInt01:
  PUSH AX,BX               ;Save used registers
  INC  Int01Counter        ;Increment Recursive Counter
  CMP  Int01Counter,1      ;First time?
  JNE >T90                 ;If not, just quit
;  CALL DisableMouseIRQ     ;Don't allow real mouse movements
;  CALL DisableKeybIRQ      ;Don't allow real keyboard presses
  MOV  AL,1                ;Change INT 01 vector
  MOV  BX,OFFSET Int01Vect ;Point to its new value
  CALL SwapIntVect         ;Swap it
T90:                       ;Done
  POP  BX,AX               ;Restore used registers
  RET

;-----------------------------------------------------------------------------
;CHANGE INT 01 (SST) BACK TO ORIGINAL & ENABLE KBD INT
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes: INT 01 Code, Enables Keyboard Interrupt
;-----------------------------------------------------------------------------
StopInt01:
  PUSH AX,BX               ;Save used registers
  CMP  Int01Counter,0      ;Recursive Counter already 0?
  JE  >P90                 ;If so, just quit
  DEC  Int01Counter        ;If not, decrement it
  JNZ >P90                 ;If not 0 yet, keep waiting
  MOV  AL,1                ;Change INT 01 vector
  MOV  BX,OFFSET Int01Vect ;Point to its original value
  CALL SwapIntVect         ;Swap it back
;  CALL EnableKeybIRQ       ;Allow real keyboard presses again
;  CALL EnableMouseIRQ      ;Allow real mouse movements again                   Q
P90:                       ;DOne
  POP  BX,AX               ;Restore used registers
  RET

;-----------------------------------------------------------------------------
;DISABLE MOUSE OR KEYBOARD IRQ
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes: Disables Mouse or Keyboard IRQ
;-----------------------------------------------------------------------------
DisableMouseIRQ:
  PUSH AX,DX           ;Save used registers
  MOV  DX,(PIC2Addr+1) ;Point at the second PIC IRQ Mask Port
  MOV  AH,10h          ;Mask in bit 4 = IRQ 12 = PS2 Mouse
  JMP >D00             ;Do it
DisableKeybIRQ:
  PUSH AX,DX           ;Save used registers
  MOV  DX,(PIC1Addr+1) ;Point at the second PIC IRQ Mask Port
  MOV  AH,02h          ;Mask in bit 1 = IRQ 1 = Keyboard
;  JMP >D00             ;Do it
D00:
  PUSHF                ;Save flags
  CLI                  ;Disable interrupts
  IN   AL,DX           ;Get current Hardware Interrupt status byte
  OR   AL,AH           ;Disable appropriate IRQ
  OUT  DX,AL           ;Do it
  POPF                 ;Restore flags
  POP  DX,AX           ;Restore used register
  RET

;-----------------------------------------------------------------------------
;ENABLE MOUSE OR KEYBOARD IRQ
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes: Enables Mouse or Keyboard IRQ
;-----------------------------------------------------------------------------
EnableMouseIRQ:
  PUSH AX,DX           ;Save used registers
  MOV  DX,(PIC2Addr+1) ;Point at the second PIC IRQ Mask Port
  MOV  AH,(NOT 10h)    ;Mask out bit 4 = IRQ 12 = PS2 Mouse
  JMP >E00             ;Do it
EnableKeybIRQ:
  PUSH AX,DX           ;Save used registers
  MOV  DX,(PIC1Addr+1) ;Point at the second PIC IRQ Mask Port
  MOV  AH,(NOT 02h)    ;Mask out bit 1 = IRQ 1 = Keyboard
;  JMP >E00             ;Do it
E00:
  PUSHF                ;Save flags
  CLI                  ;Disable interrupts
;  CALL EnableMousePort ;Make sure the Mouse Port is Enabled
  IN   AL,DX           ;Get current Hardware Interrupt status byte
  AND  AL,AH           ;Enable appropriate IRQ
  OUT  DX,AL           ;Do it
  POPF                 ;Restore flags
  POP  DX,AX           ;Restore used register
  RET

;------------------------------------------------------------------------------
;ISSUE THE COMMAND TO THE KEYBOARD CONTROLLER TO DISABLE/ENABLE KEYBOARD/MOUSE
;Inputs:
;Outputs:
;Changes: Disables/Enalbe the Keyboard or Mouse
;------------------------------------------------------------------------------
DisableMousePort:
  PUSH AX             ;Save used registers
  MOV  AL,0A7h        ;AL = A7h = Disable Mouse Port
  JMP >A00            ;Continue
EnableMousePort:
  PUSH AX             ;Save used registers
  MOV  AL,0A8h        ;AL = A8h = Enable Mouse Port
  JMP >A00            ;Continue
DisableKeybPort:
  PUSH AX             ;Save used registers
  MOV  AL,0ADh        ;AL = ADh = Disable Keyboard Port
  JMP >A00            ;Continue
EnableKeybPort:
  PUSH AX             ;Save used registers
  MOV  AL,0AEh        ;AL = AEh = Enable Keyboard Port
;  JMP >A00            ;Continue
A00:
  PUSHF               ;Save flags
  CLI                 ;Disable interrupts
  CALL WaitKbdInEmpty ;Wait for the Keyboard Input buffer to clear
;  JNZ >A90            ;If it never clears, quit
  OUT  64h,AL         ;Send the command
;  CALL WaitKbdInEmpty ;Wait for the Keyboard Controller to accept it
A90:                  ;Done
  POPF              ;Restore flags
  POP  AX             ;Restore used registers
  RET

;-----------------------------------------------------------------------------
;"FAKE" INT 09 INTO ACCEPTING OUR SCAN CODE (NOT FROM KEYBOARD)
;Inputs:  DS = ES = TSR Data Area
;         ByteToSend = Code to use
;Outputs:
;Changes: Performs a "fake" INT 09 using the Scan Code
;-----------------------------------------------------------------------------
PutCode:
  PUSH AX,BX               ;Save used registers
  MOV  AL,74h              ;Want to work with INT 74h
  MOV  BX,OFFSET IntXXVect ;Point to Data area
  CALL MoveFromIntVect     ;Put INT XX Vector in storage
  PUSHF                    ;Get the
  POP  AX                  ;  flags
  OR   AH,((IntFlag+TrapFlag)SHR 8) ;Do INT with Int & Trap flags On
  MOV  IntXXFlags,AX       ;Set Flags the way we want them to be
  POP  BX,AX               ;Restore used registers
  CALL DoIntXX             ;Perform INT w/ Int 01 Enabled
  RET

;-----------------------------------------------------------------------------
;DO INT XX WITH OUR FLAGS
;Inputs:  IntXXFlags, IntXXVect
;Outputs:
;Changes: Performs INT XX with our Flags, Returns to this code
;-----------------------------------------------------------------------------
DoIntXX:
  PUSHF            ;Save current Flags
  PUSH CS          ;Push our CS for the return
  CALL DoIntXXPush ;Push things and do the INT
  RET

DoIntXXPush:
  PUSH W CS:IntXXFlags   ;Push the Flags
  PUSH W CS:IntXXVect[2] ;Push the CS
  PUSH W CS:IntXXVect[0] ;Push the IP
  IRET                   ;Do it

;-----------------------------------------------------------------------------
;SWAP OR CHANGE INTERRUPT VECTOR TO/FROM OUR CODE
;Inputs:  AL = Interrupt Number to Change
;         CS:[BX] = What we want it swapped with
;Outputs:
;Changes: Interrupt vector ->, <-, or <-> New Address
;-----------------------------------------------------------------------------
SwapIntVect:
  PUSH CX              ;Save used register
  XOR  CL,CL           ;If Swap, CL=00
  JMP >V10             ;Jump to do it
;MoveToIntVect:
;  PUSH CX              ;Save used register
;  MOV  CL,01           ;If Move To, CL=01
;  JMP >V10             ;Jump to do it
MoveFromIntVect:
  PUSH CX              ;Save used register
  MOV  CL,02           ;If Move From, CL=02
V10:                   ;Swap or change Int vect
  PUSH AX,BX,DI,ES     ;Save used registers
  MOV  CH,AL           ;Save Int # in CH
  XOR  AX,AX           ;AX=0000
  MOV  ES,AX           ;ES=0000 (Int Vector Table)
  MOV  AL,CH           ;Get Int # Back
  ADD  AX,AX           ;Multiply Int #
  ADD  AX,AX           ;  by 4 (to get offset)
  MOV  DI,AX           ;Put it in DI
  CLI                  ;Disable Interrupts
  CALL ChangeIntVectDo ;Change the IP
  INC  BX,2            ;Point to New CS
  INC  DI,2            ;Point to old CS
  CALL ChangeIntVectDo ;Change the CS
  STI                  ;Enable Interrupts
  POP  ES,DI,BX,AX     ;Restore used registers
  POP  CX              ;Restore used register
  RET

;-----------------------------------------------------------------------------
;SWAP OR CHANGE ES:[DI] TO/FROM CS:[BX]
;Inputs:  CL=00   ES:[DI] <-> CS:[BX]
;         CL=01   ES:[DI] <-  CS:[BX]
;         CL=02   ES:[DI]  -> CS:[BX]
;Outputs:
;Changes: AX
;-----------------------------------------------------------------------------
ChangeIntVectDo:
  CMP  CL,02      ;Are we Moving From?
  JNE >D10        ;If so, skip the next few steps
  MOV  AX,ES:[DI] ;If no, put old vector in AX
  JMP >D20        ;Jump to change it
D10:              ;Move to or Swap
  MOV  AX,CS:[BX] ;If Move To or Swap, Put New Vector in AX
  XCHG AX,ES:[DI] ;Swap new & old vectors
  CMP  CL,01      ;Are we Moving To?
  JE  >D90        ;If so, skip the next step
D20:              ;Move from or Swap
  MOV  CS:[BX],AX ;If Move From or Swap, Put old vector in CS:[BX]
D90:              ;We're done
  RET

;-----------------------------------------------------------------------------
;INTERRUPT 01 (SINGLE STEP):  PERFORMED AFTER EACH STEP IN IRQ 12
;Inputs:  ByteToSend =  Code to "fake" into INT 74h
;Outputs:
;Changes: Single-Steps through IRQ 12 CALL
;-----------------------------------------------------------------------------


;Need to monitor both OUT 20h,20 & OUT A0h,20h for EOI?


Int01:
  PUSH BP               ;Save used register
  MOV  BP,SP            ;Want to work with the stack
  PUSH AX,BX,DS,ES      ;Save used registers
  MOV  DS,CS            ;Make CS & DS the same
  LES  BX,SS:[BP+2]     ;ES = Current CS, BX = Current IP
  MOV  AX,ES:[BX]       ;Put the OpCode in AX
  DEC  InstrCounter     ;Decrement the Counter
  JNZ >Z10              ;If not 0 yet, keep working
Z05:                    ;Too many insructions - Give it up
  AND  B SS:[BP+7],((NOT TrapFlag)SHR 8) ;Unset Trap flag
;  CALL EnableMouseIRQ   ;Enable the Mouse Interrupt
;  CALL EnableKeybIRQ    ;Enable the Keyboard Interrupt
  JMP >Z90              ;Done
Z10:                    ;Not timed out yet
  CMP  DoNextTime,Yes   ;Do we need to do it again?
  MOV  DoNextTime,No    ;Reset the NextTime flag
  JNE >Z20              ;If not, continue
  MOV  AL,ByteToSend    ;Get our Code
  CMP  AL,SS:[BP-2]     ;Did our code work last time?
  JE  >Z90              ;If so, no need to do it again
  MOV  SS:[BP-2],AL     ;If not, do it again
  JMP >Z90              ;Done
Z20:                    ;Need to test actual OpCode
  CMP  AL,OpCodeInt1Min ;Is the OpCode in the range we care about?
  JB  >Z90              ;If not, we're done
  CALL I1TestIN60h      ;Is it IN 60h?
  JNC >Z90              ;If so, we're done
  CALL I1TestOUTA0h     ;Is it OUT A0h?
  JNC >Z90              ;If so, we're done
  CALL I1TestOUT60h     ;Is it OUT 60h?
  JNC >Z90              ;If so, we're done
  CALL I1TestOUT64h     ;Is it OUT 64h?
;  JNC >Z90              ;If so, we're done
;  CMP  AL,OpCodeINTxx   ;Is it an INT xx?
;  JE  >Z99              ;If so, handle it
;  JNE >Z90              ;If not, we're done
;  CALL I1INTxxTest      ;If so, should we run it with Trap Set?
;  JC  >Z99              ;If with Trap Set, handle it
Z90:                    ;Done
  POP  ES,DS,BX,AX      ;Restore used registers
  POP  BP               ;Restore used registers
  IRET

;Int01Int:
;Z99:
;  MOV  AL,AH               ;Get the interrupt number
;  MOV  BX,OFFSET IntXXVect ;Point at our data area
;  CALL MoveFromIntVect     ;Store our interrupt pointer
;  MOV  AX,SS:[BP+06]       ;Get the original flags
;  MOV  IntXXFlags,AX       ;Store them for this interrupt
;  ADD  W SS:[BP+2],2       ;Skip past the INT for next time through
;  POP  ES,DS,BX,AX         ;Restore used registers
;  POP  BP                  ;Restore used registers
;  JUMP DoIntXXPush         ;Do the interrupt

;------------------------------------------------------------------------------
;TEST THE NEXT OPCODE TO SEE IF IT'S AN IN 60h (Get ScanCode)
;Inputs:  CS = DS = TSR Data Area
;         AX        = Next OpCode to be performed
;         SS:[BP-2] = Original AX
;         SS:[BP+2] = Original IP
;         SS:[BP+4] = Original CS
;         SS:[BP+6] = Original Flags
;         ES:[BX]   = Original CS:IP
;Outputs: CF = Clear if it is an IN 60h
;              Modifies Flags/Counters as appropriate
;            = Set if not an IN 60h
;Changes:
;------------------------------------------------------------------------------
I1TestIN60h:
  PUSH AX              ;Save used registers
  CMP  AL,OpCodeINALDX ;Is it IN AL,DX?
  JE  >I10             ;If so, handle it
  CMP  AL,OpCodeINAXDX ;Is it IN AX,DX?
  JNE >I20             ;If not, try the next possibility
I10:                   ;Is IN AL,DX or IN AX,DX
  CMP  DX,60h          ;If so, does DX=60h?
  JE  >I50             ;If so, handle it
  JMP >I70             ;If not, quit
I20:                   ;Test IN AL,60h
  CMP  AX,(OpCodeINALxx+(60h SHL 8)) ;Is it IN AL,60h?
  JE  >I25                           ;If so, handle it
  CMP  AX,(OpCodeINAXxx+(60h SHL 8)) ;Is it IN AX,60h?
  JNE >I70             ;If not, we're done
I25:                   ;Is IN AL,60h or IN AX,60h
  INC  W SS:[BP+2]     ;Increment IP to skip the IN
I50:                   ;Increment IP, set ScanCode
  INC  W SS:[BP+2]     ;Increment IP to skip the IN
  MOV  DoNextTime,Yes  ;Mark as needing to do it again next time
  MOV  AL,ByteToSend   ;Set AL to
  MOV  SS:[BP-2],AL    ;  our Code
  JMP >I80             ;Done
I70:                   ;Not handled
  STC                  ;Set not handled flag
  JMP >I90             ;Done
I80:                   ;Handled
  CLC                  ;Set handled flag
I90:                   ;Done
  POP  AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST THE NEXT OPCODE TO SEE IF IT'S AN OUT XX
;Inputs:  CS = DS = TSR Data Area
;         AX        = Next OpCode to be performed
;         SS:[BP-2] = Original AX
;         SS:[BP+2] = Original IP
;         SS:[BP+4] = Original CS
;         SS:[BP+6] = Original Flags
;         ES:[BX]   = Original CS:IP
;Outputs: CF = Clear if it is an OUT xx that we care about
;              Modifies Flags/Counters as appropriate
;            = Set if not an OUT xx that we care about
;Changes:
;------------------------------------------------------------------------------
I1TestOUTA0h:
  PUSH BX                 ;Save used registers
  MOV  BX,0A0h            ;Mark as Port A0h
  JMP >U00                ;Do it
I1TestOUT60h:
  PUSH BX                 ;Save used registers
  MOV  BX,60h             ;Mark as Port 60h
  JMP >U00                ;Do it
I1TestOUT64h:
  PUSH BX                 ;Save used registers
  MOV  BX,64h             ;Mark as Port 64h
;  JMP >U00                ;Do it
U00:
  PUSH CX,DX              ;Save used registers
  MOV  DX,SS:[BP-2]       ;Put original calling program's AX in DX
  MOV  CX,1               ;Assume OUT DX,AL or OUT DX,AX
  CMP  AL,OpCodeOUTDXAL   ;Is it OUT DX,AL?
  JE  >U10                ;If so, handle it
  CMP  AL,OpCodeOUTDXAX   ;Is it OUT DX,AX?
  JNE >U20                ;If not, try the next possibility
U10:                      ;Is OUT DX,AL or OUT DX,AX
  CMP  DX,BX              ;Is it the Port we're looking for?
  JE  >U30                ;If so, continue
  JMP >U70                ;If not, quit
U20:                      ;Test OUT xx,AL our OUT xx,AX
  INC  CX                 ;Assume OUT xx,AL or OUT xx,AX
  CMP  AL,OpCodeOUTxxAL   ;Is it OUT xx,AL?
  JE  >U25                ;If so,handle it
  CMP  AL,OpCodeOUTxxAX   ;Is it OUT xx,AX?
  JNE >U70                ;If not, we're done
U25:                      ;Is correct OpCode
  CMP  AH,BL              ;Is it the Port we're looking for?
  JNE >U70                ;If not, quit
U30:                      ;Is correct OpCode & Port
  CMP  BL,60h             ;Which Port is it?
  JA  >U60                ;If Port A0h, handle it
  JE  >U50                ;If Port 60h, handle it
U40:                      ;Is OUT 64h
  CMP  DL,0A7h            ;Is it function A7h (Disable Mouse Port)?
  JE  >U80                ;If so, we're done
  CMP  DL,0A8h            ;Is it function A8h (Enable Mouse Port)?
  JE  >U80                ;If so, we're done
U50:                      ;Is OUT 60h or OUT 64h,A7h or OUT 64h,A8h
  TEST TSRFlags,EOIIssued ;Have we received the End-of-Interrupt yet?
  JZ  >U80                ;If not, we're done
  AND  B SS:[BP+7],((NOT TrapFlag)SHR 8) ;Unset Trap flag for next time
  MOV  InstrCounter,0     ;Reset the Instruction Counter
;  CALL EnableMouseIRQ     ;Enable the Mouse
;  CALL EnableKeybIRQ      ;Enable the Keyboard
  JMP >U80                ;Done
U60:                      ;Is OUT A0h
  CMP  DL,20h             ;Does AL=20h (non-specific EOI)?
  JE  >U65                ;If so, handle it
  CMP  DL,64h             ;Does AL=64h (specific EOI, IRQ 12)?
  JNE >U70                ;If not, we're done
U65:                      ;Is OUT A0h (EOI)
  OR   TSRFlags,EOIIssued ;Mark us as having gotten the OUT A0h,20h
  MOV  InstrCounter,100   ;Set the Instruction Counter
  ADD  SS:[BP+2],CX       ;Increment IP to skip the OUT
  JMP >U80                ;Done
U70:                      ;Not handled
  STC                     ;Set not handled flag
  JMP >U90                ;Done
U80:                      ;Handled
  CLC                     ;Set handled flag
U90:                      ;Done
  POP  DX,CX              ;Restore used registers
  POP  BX                 ;Restore used registers
  RET

;;------------------------------------------------------------------------------
;;TEST THE NEXT OPCODE TO SEE IF IT'S AN INT XX
;;Inputs:  CS = DS = TSR Data Area
;;         AX        = Next OpCode to be performed (AL = INT OpCode, AH = INT #)
;;         SS:[BP-2] = Original AX
;;         SS:[BP+2] = Original IP
;;         SS:[BP+4] = Original CS
;;         SS:[BP+6] = Original Flags
;;         ES:[BX]   = Original CS:IP
;;         Already tested for the INT xx OpCode (AL)
;;Outputs: CF = Clear if the INT is one we know about
;;                (should run "normally" (without the Trap Flag Set)
;;            = Set if we the INT is unknown
;;                (should run with the Trap Flag Set)
;;Changes:
;;NOTE: INT's that we "don't know about" could possibly ask for an IN AL,60h
;;        which we will need to simulate.  For the INT's that we do know about
;;        (PrintScreen, SystemRequest, Break, Bootstrap, and Translate
;;        Code), we know the Code has already been received and
;;        processed, so we will not need to simulate the Code again
;;        during the INT.
;;------------------------------------------------------------------------------
;I1IntXXTest:
;  PUSH AX           ;Save used registers
;  MOV  AL,AH        ;Put INT # in AL (makes Code smaller)
;  MOV  AH,SS:[BP-1] ;Put original AH (Interrupt Function Number) in AH
;I10:                ;Test for PrintScreen
;  CMP  AL,05h       ;Is it INT 05h (PrintScreen)?
;  JE  >I80          ;If so, it's one we know about
;I20:                ;Test for Break
;  CMP  AL,1Bh       ;Is it INT 1Bh (Control-Break Handler)?
;  JE  >I80          ;If so, it's one we know about
;I30:                ;Test for Translate Scan Code & System Request
;  CMP  AL,15h       ;Is it INT 15h (BIOS Services)?
;  JNE >I40          ;If not, keep testing
;  CMP  AH,4Fh       ;Is it Function 4Fh (Translate Scan Code)?
;  JE  >I80          ;If so, it's one we know about
;  CMP  AH,85h       ;Is it Function 85h (Alt-System Request Handler)?
;  JE  >I80          ;If so, it's one we know about
;  JMP >I70          ;If not, we don't know about it
;I40:                ;Test for Flush Disk Buffers (part of Ctrl-Alt-Del)
;  CMP  AL,21h       ;Is it Int 21h (DOS Services)?
;  JNE >I70          ;If not, we don't know about it
;  CMP  AH,0Dh       ;Is it Function 0Dh (Flush Disk Buffers)?
;  JE  >I80          ;If so, it's one we know about
;I70:                ;Not an INT we know about
;  STC               ;Set return flag
;  JMP >I90          ;Done
;I80:                ;Is an INT we know about
;  CLC               ;Set return flag
;I90:                ;Done
;  POP  AX           ;Restore used registers
;  RET









;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE INT 11h
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HAVE INTERRUPT 11h (GET EQUIPMENT LIST) RUN THIS PROGRAM EVERY TIME IT OCCURS
;Inputs:
;Outputs: {E}AX = Equipment List, making sure it shows a PS2 Port is Installed
;Changes:
;------------------------------------------------------------------------------
Int11Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int11:
  PUSHF                        ;Call the
  CALL D CS:Int11Hdr.OldVector ;  Old Handler (returns {E}AX)
  TEST CS:TSRFlags,EmuHardware ;Emulating the BIOS?
  JNZ >V90                     ;If not, we're done
  OR   AL,Int11PS2Installed    ;If so, mark PS2 Port as installed
V90:                           ;Done
  IRET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE INT 15h
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HAVE INTERRUPT 15h (BIOS SERVICES) RUN THIS PROGRAM EVERY TIME IT OCCURS
;Inputs:
;Outputs: Emulates a PS2 Port with a Mouse Attached to it
;         Depending on Exact Call SubType (value in AL), will return
;           various values in AX, BX, CX and/or DX
;Changes:
;------------------------------------------------------------------------------
Int15Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int15:
  PUSHF                            ;Save flags
  STI                              ;Enable interrupts
  CLD                              ;Go forward with string functions
  TEST CS:TSRFlags,EmuHardware     ;Are we emulating hardware?
  JNZ  Int15Old                    ;If so, let the old handler process it
  CMP  AH,Int15PS2PortCall         ;Is it one of the PS2 functions?
  JNE  Int15Old                    ;If not, it's not for us
  CMP  AL,I15ALSetHandler          ;Valid Function Number?
  JBE >F05                         ;If so, handle it
  CMP  AL,I15ALGetHandler          ;Valid Function Number?
  JE  >F05                         ;If so, handle it
  MOV  AH,Int15ErrInvalidFunction  ;If not,
  JMP >F75                         ;  it's an error
F05:                               ;It's for us!
  CALL MakeStack                   ;Set up our own Stack Space
  PUSH BP,DS,ES                    ;Save used registers
  MOV  BP,ES                       ;Save users ES on the call (may need it)
  MOV  DS,CS                       ;Point DS at our data area
  MOV  ES,CS                       ;Point ES at our data area
  CALL DoInt15Function             ;Do our thing
  JNC >F80                         ;If no error, jump to handle it
F70:                               ;Error
  POP  ES,DS,BP                    ;Restore used registers
  CALL ReturnStack                 ;Restore original Stack Space
F75:                               ;Error, no saved registers
  POPF                             ;Restore flags
  PUSH BP                          ;Save used registers
  MOV  BP,SP                       ;Point BP at the stack
  OR   B SS:[BP+6],CarryFlag       ;Set Carry Flag for the return
  JMP >F90                         ;Done
F80:                               ;No Error
  POP  ES,DS,BP                    ;Restore used registers
  CALL ReturnStack                 ;Restore original Stack Space
  POPF                             ;Restore flags
  PUSH BP                          ;Save used registers
  MOV  BP,SP                       ;Point BP at the stack
  AND  B SS:[BP+6],(NOT CarryFlag) ;Clear Carry Flag for the return
F90:                               ;Done
  POP  BP                          ;Restore used registers
  IRET

Int15Old:
  POPF                             ;Restore flags
  JMP  D CS:Int15Hdr.OldVector     ;Let the Old Interrupt Handler do it

;------------------------------------------------------------------------------
;DO APPROPRIATE INT 15 (PS2/MOUSE) SUBFUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AL = SubFunction to Perform
;         BP = Caller's ES
;         BX = Unchanged from Caller
;Outputs: CF = Clear if OK
;           Returns various values in AX, BX, CX and/or DX, depending on call
;         CF = Set if Error
;           AH = Error Code, all other registers unchanged
;Changes:
;------------------------------------------------------------------------------
DoInt15Function:
  PUSH DI                         ;Save used registers
  MOV  DI,BX                      ;Save original BX
  MOV  AH,Int15ErrInvalidFunction ;Assume invalid function
  MOV  BX,Int15FunctionTbl        ;Point at function table
  CALL SearchByteToWordTbl        ;Look for our function
  XCHG  BX,DI                     ;Put Function Code in DI,
                                  ;  restore original BX
  JC  >I70                        ;If error, we're done
  CMP  AL,I15ALSetSampling        ;Is it Set Sample Rate?
  JE  >I10                        ;If so, continue
  MOV  W EnableWheelCounter,0     ;If not, Reset Enable Wheel Counters
I10:                              ;Do Function
  XOR  AH,AH                      ;Assume no errors from now on
  CALL DI                         ;Call the function
  JNC >I80                        ;If no error, we're done
  OR   AH,AH                      ;Did it return a valid error code?
  JNZ >I70                        ;If so, we're done
  MOV  AH,Int15ErrNeedToResend    ;If not, default = Resend
I70:                              ;Error
  STC                             ;Set Error Flag
  JMP >I90                        ;Done
I80:                              ;OK
  CLC                             ;Set OK flag
I90:                              ;Done
  POP  DI                         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DO THE PS2 ENABLE/DISABLE HANDLER FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AL = Original INT 15h Function Code (Enable/Disable)
;         AH = 0
;         BH = Function to Perform (0 = Disable, 1 = Enable)
;Outputs: CF = Set if error
;              AH = Error Code
;            = Clear if OK
;              AH = Unchanged
;              Int15Enabled changed as appropriate
;Changes:
;------------------------------------------------------------------------------
PS2EnableDisable:
  CMP  BH,1                        ;Is it Enable or Disable?
  JA  >E70                         ;If neither, Error
  JE  >E20                         ;If Enable, jump to handle it
E10:                               ;Disable
  AND  TSRFlags,(NOT Int15Enabled) ;Mark as Disabled
  JMP >E80                         ;Done
E20:                               ;Enable
  CMP  W MouseUserFarCall[2],0     ;Is there a registered Handler address?
  JNE >E30                         ;If so, it's OK to Enable
  MOV  AH,Int15ErrNoDeviceHandler  ;If not, Error = No Handler
  JMP >E75                         ;Done
E30:                               ;OK to Enable
  OR   TSRFlags,Int15Enabled       ;Mark as Enabled
  CALL ResetTotals                 ;Reset Movement Counters
  JMP >E80                         ;Done
E70:                               ;Error
  MOV  AH,Int15ErrInvalidInput     ;Error = Invalid value in BH
E75:                               ;Error, AH = Error Code
  STC                              ;Set Error Flag
  JMP >E90                         ;Done
E80:                               ;No Error
  CLC                              ;Set OK flag
E90:                               ;Done
  RET

;------------------------------------------------------------------------------
;DO THE PS2 RESET FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AH = 0
;Outputs: CF = Set if error
;              AH = Error Code
;            = Clear if OK
;              AH = Unchanged (0)
;              BH = Device ID (0)
;              BL = Device Type (Mouse)
;              Mouse has been Reset
;Changes:
;------------------------------------------------------------------------------
PS2Reset:
;  MOV  BH,3                    ;Default Data Size = 3 Bytes
  MOV  BH,Int15DataSize        ;Data Size = Unchanged
  CALL PS2Initialize           ;Do a Soft Initialization
  ;MOV  DeviceID,DvcIDWheelNo   ;Disable Wheel(s)
  MOV  W MouseUserFarCall[2],0 ;Reset Far Call Address
  ;CALL RePointPS2Handler       ;RePoint PS2 handler at us
  MOV  BX,DvcTypeMouse         ;Rtn BH = Device ID = 0 (No Wheels)
                               ;Rtn BL = Device Type = Mouse
  CLC                          ;Set Return Flag
  RET

;------------------------------------------------------------------------------
;DO THE PS2 INITIALIZE FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AH = 0
;         BH = Data Package Size to use (1-8 bytes)
;              We will only allow a size of 3 or 4 for the Mouse
;Outputs: CF = Set if error
;              AH = Error Code
;            = Clear if OK
;              AH = Unchanged (0)
;              Mouse Software has been Initialized
;Changes:
;------------------------------------------------------------------------------
PS2Initialize:
  PUSH BX                            ;Save used registers
  CMP  BH,4                          ;Is data valid?
  JA  >I70                           ;If not, error
  CMP  BH,3                          ;Is data valid?
  JB  >I70                           ;If not, error
  MOV  Int15DataSize,BH              ;Store it
  MOV  DeviceID,DvcIDWheelNo         ;Disable Wheel(s)
  MOV  SampleRateCode,SampleCode100  ;Store the Default Sample Rate Code
  MOV  BL,DefaultPeriodicity         ;Set the
  CALL ChangePeriodicity             ;  Default Periodicity
  AND  TSRFlags,(NOT ScalingFactor2) ;Default Scaling Factor is 1:1
  MOV  Resolution,Resolution4        ;Default Resolution is 4 counts/mm
  AND  TSRFlags,(NOT Int15Enabled)   ;Default = Disabled
  JMP >I80                           ;Done
I70:                                 ;Error
  MOV  AH,Int15ErrInvalidInput       ;Error Code
  STC                                ;Error Flag
  JMP >I90                           ;Done
I80:                                 ;OK
  CALL ResetTotals                   ;Reset Movement Counters
  CLC                                ;Set OK Flag
I90:                                 ;Done
  POP  BX                            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DO THE PS2 SET SAMPLE RATE FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AL = Original INT 15h Function Code (Set Sample Rate)
;         AH = 0
;         BH = Sample Rate Code (See SampleRateTable)
;Outputs: CF = Set if error
;              AH = Error Code (Invalid Input)
;            = Clear if OK
;              AH = Unchanged
;              Sample Rate changed as appropriate
;Changes:
;------------------------------------------------------------------------------
PS2SetSampleRate:
  PUSH BX,DX                   ;Save used registers
  CMP  BH,SampleCode200        ;Is it a valid Function Code?
  JA  >S70                     ;If not, error
  CMP  BH,SampleCode080        ;Is it the possible end of a Setup?
  JNE >S05                     ;If not, it's OK
  MOV  BH,SampleCode010        ;If so, send a Code to make sure it doesn't
  CALL DoPS2MouseFunction      ;  Change the PS2 Mouse Setup
  MOV  BH,SampleCode080        ;Restore original Sample Code
S05:                           ;PS2 Mouse handled
  CALL DoPS2MouseFunction      ;If OK, send it to the PS2 Mouse
  MOV  DX,W EnableWheelCounter ;Put Wheel Counter in DX
  CMP  BH,SampleCode200        ;Is it a valid Function Code?
  JE  >S30                     ;If so, see if it's part of a Wheel Enable
  CMP  BH,SampleCode100        ;Possible Wheel enable?
  JE  >S20                     ;If so, handle it
  CMP  BH,SampleCode080        ;Possible Wheel enable?
  JNE >S50                     ;If not, continue
S10:                           ;Setting 80
  CMP  DL,2                    ;End of Wheel Enable?
  JNE >S15                     ;If not, try next possibility
  MOV  DeviceID,DvcIDWheel1Yes ;If so, Enable Wheel
  JMP >S40                     ;Reset Wheel Enable Counter
S15:                           ;Not enabling 1 wheel
  CMP  DH,2                    ;End of Wheels Enable?
  JNE >S40                     ;If not, reset Wheel Enable Counters
  MOV  DeviceID,DvcIDWheel2Yes ;If so, Enable Wheels
  JMP >S40                     ;Reset Wheel Enable Counter
S20:                           ;Setting 100
  CMP  DL,1                    ;Possible Wheel Enable?
  JNE >S40                     ;If not, reset Wheel Enable Counter
  INC  DL                      ;If so, Increment the Wheel Enable Counter
  JMP >S50                     ;Continue
S30:                           ;Setting 200
  OR   DX,DX                   ;Possible Wheel(s) Enable?
  JNZ >S35                     ;If not, try next possibility
  ADD  DX,0101h                ;If so, increment both Wheel Counters
  ;MOV  IntelliTimer,5          ;Initialize Timeout Counter
  JMP >S50                     ;Continue
S35:                           ;Not enabling 1 wheel
  CMP  DH,1                    ;Possible enable Wheels?
  JNE >S40                     ;If not, reset Wheel Enable Counters
  INC  DH                      ;If so, increment Wheels Counter
  XOR  DL,DL                   ;Reset Wheel Counter
  JMP >S50                     ;Continue
S40:                           ;Not part of Wheel enable
  XOR  DX,DX                   ;Reset Wheel Enable Counters
S50:                           ;Wheel enable Accounted for
  MOV  W EnableWheelCounter,DX ;Store Counters for next time
  MOV  SampleRateCode,BH       ;If OK, store it
  PUSH AX                      ;Save AX for a second
  MOV  AL,BH                   ;Convert Sample Rate Code
  MOV  BX,SampleRateTable      ;  Code to a
  CALL SearchByteToWordTbl     ;  Periodicity factor (BH)
  POP  AX                      ;Restore AX
  JC  >S70                     ;Quit if error
  MOV  BL,BH                   ;If OK,
  CALL ChangePeriodicity       ;  Change Periodicity
  JMP >S80                     ;Done
S70:                           ;Error
  MOV  AH,Int15ErrInvalidInput ;Error = Invalid value in BH
  STC                          ;Set Error Flag
  JMP >S90                     ;Done
S80:                           ;No Error
;  XOR  AH,AH                   ;Error Code = None
  CLC                          ;Set OK flag
S90:                           ;Done
  POP  DX,BX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DO THE PS2 SET RESOLUTION FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AL = Original INT 15h Function Code (Set Resolution)
;         AH = 0
;         BH = Resolution Code
;              0 = 1 Counts/mm
;              1 = 2 Counts/mm
;              2 = 4 Counts/mm
;              3 = 8 Counts/mm
;         AL = Original Function Code (Set Resolution)
;Outputs: CF = Set if error
;              AH = Error Code (Invalid Input)
;            = Clear if OK
;              AH = Unchanged
;              Resolution changed as appropriate
;Changes:
;------------------------------------------------------------------------------
PS2SetResolution:
  CMP  BH,Resolution8          ;Is it a valid function code?
  JA  >R70                     ;If not, error
;  CALL DoPS2MouseFunction      ;If OK, pass on to PS2 Handler
  MOV  Resolution,BH           ;Store it
  JMP >R80                     ;Done
R70:                           ;Error
  MOV  AH,Int15ErrInvalidInput ;Error = Invalid value in BH
  STC                          ;Set Error Flag
  JMP >R90                     ;Done
R80:                           ;OK
  CLC                          ;Set OK Flag
R90:                           ;Done
  RET

;------------------------------------------------------------------------------
;DO THE PS2 GET DEVICE ID FUNCTION
;Inputs:  ES = TSR Data Area
;         AL = Original INT 15h Function Code (Get Device ID)
;         AH = 0
;Outputs: CF = Clear
;              BH = Device ID (DvcIDWheelNo, Wheel1Yes, or Wheel2Yes)
;Changes:
;------------------------------------------------------------------------------
PS2GetDeviceIDBH:
  MOV  BH,ES:DeviceID ;Set BH = Device ID
  CLC                 ;Set OK Flag
  RET

;------------------------------------------------------------------------------
;DO THE PS2 GET STATUS/EXTENDED COMMANDS FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         BH = 0 for Get Status
;            = 1 for Set 1:1 Scaling
;            = 2 for Set 2:1 Scaling
;         AL = Original INT 15h Function Code (Get Status)
;         AH = 0
;Outputs: CF = Set if error
;              AH = Error Code
;            = Clear if OK
;              AH = Unchanged
;              Appropriate Function performed
;              If Get Status:
;                BL = Enabled, Scaling, Buttons 1-3
;                CL = Resolution
;                DL = Sampling Rate
;Changes:
;NOTES: If Function = Get Status, does not pass on to PS2 Handler.
;       If Function = Set Scaling, does pass on to PS2 Handler.
;------------------------------------------------------------------------------
PS2GetStatus:
  PUSH SI                            ;Save used registers
  CMP  BH,2                          ;Is it a valid subfunction?
  JA  >E70                           ;If not, error
  JE  >E40                           ;If function 2 (Set 2:1 Scaling), handle it
  OR   BH,BH                         ;Is it function 1 (Set 1:1 Scaling)?
  JNZ >E30                           ;If so, handle it
E10:                                 ;Function 0 (Get Status)
  CALL CalcStatus                    ;Calculate correct status (Returns BL)
  MOV  CL,Resolution                 ;Get Resolution
  CALL CalcSampleRate                ;Calculate the Sample Rate (Returns DL)
  JMP >E80                           ;Done
E30:                                 ;Set Scaling Factor to 1:1
;  CALL DoPS2MouseFunction            ;Pass on to PS2 Handler
  AND  TSRFlags,(NOT ScalingFactor2) ;Set Scaling Factor at 1:1
  JMP >E80                           ;Done
E40:                                 ;Set Scaling Factor at 2:1
;  CALL DoPS2MouseFunction            ;Pass on to PS2 Handler
  OR   TSRFlags,ScalingFactor2       ;Set Scaling Factor at 2:1
  JMP >E80                           ;Done
E70:                                 ;Error
  MOV  AH,Int15ErrInvalidInput       ;Error Code
  STC                                ;Error Flag
  JMP >E90                           ;Done
E80:                                 ;OK
  CLC                                ;Set OK Flag
E90:                                 ;Done
  POP  SI                            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DO THE PS2 SET HANDLER ADDRESS FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AL = Original INT 15h Function Code (Set Handler Address)
;         AH = 0
;         BP:BX = New Far Call Address (0 if resetting)
;Outputs: CF = Set if error
;              AH = Error Code
;            = Clear if OK
;              New Device Address Stored
;Changes:
;NOTES: If Handler is already Disabled (0), it is an Error to Disable it
;       If Set, it is an Error to Set it Again
;------------------------------------------------------------------------------
PS2SetHandlerAddress:
  OR   BP,BP                      ;Are we resetting the Address?
  JZ  >H30                        ;If so, handle it
  CMP  W MouseUserFarCall[2],0    ;If not, is it currently 0 (Disabled)?
  JNE >H70                        ;If not, error
  JMP >H50                        ;If so, it's OK
H30:                              ;Resetting
  OR   BX,BX                      ;Is the Offset also 0?
  JNZ >H70                        ;If not, error
  CMP  W MouseUserFarCall[2],0    ;Is it already 0?
  JE  >H70                        ;If so, there's an error
H50:                              ;OK to store it
  MOV  W MouseUserFarCall[2],BP   ;Store Far Call Segment
  MOV  W MouseUserFarCall[0],BX   ;Store Far Call Offset
  JMP >H80                        ;Done
H70:                              ;Error
  MOV  AH,Int15ErrNoDeviceHandler ;Error Code
  STC                             ;Error flag
  JMP >H90                        ;Done
H80:                              ;OK
  CLC                             ;Set OK Flag
H90:                              ;Done
  RET

;------------------------------------------------------------------------------
;DO THE PS2 GET HANDLER ADDRESS FUNCTION
;Inputs:  DS = ES = TSR Data Area
;         AL = Original INT 15h Function Code (Get Handler Address)
;         AH = 0
;Outputs: CF = Clear
;              CX:DX = Handler Address
;Changes:
;------------------------------------------------------------------------------
PS2GetHandlerAddress:
  MOV  CX,W MouseUserFarCall[2] ;CX = Far Call Segment
  MOV  DX,W MouseUserFarCall[0] ;DX = Far Call Offset
  CLC                           ;Set OK Flag
  RET

;;------------------------------------------------------------------------------
;;RE-POINT PS2 (BIOS) HANDLER AT OUR USER CODE
;;Inputs:  ES = TSR Data Area
;;         PS2DeviceID
;;Outputs:
;;Changes: Resets PS2 Mouse, and enables IntelliMouse functions if there
;;         Re-Points PS2 (BIOS) Handler to our Code
;;------------------------------------------------------------------------------
;RePointPS2Handler:
;  PUSH AX,BX,DS              ;Save used registers
;  MOV  DS,ES                 ;Point DS at TSR Data Area
;  MOV  BX,PS2FarCall         ;Re-Point
;  MOV  AL,I15ALSetHandler    ;  PS2 (BIOS) at us
;  CALL DoPS2MouseFunction    ;Do it
;  MOV  BH,1                  ;SubFunction 1 (Enable)
;  MOV  AL,I15ALEnableDisable ;Function = Enable/Disable
;  CALL DoPS2MouseFunction    ;Do it
;H90:                         ;Done
;  POP  DS,BX,AX              ;Restore used registers
;  RET

;------------------------------------------------------------------------------
;SEND A REQUEST TO THE PS2 MOUSE (NOT THE USB MICE)
;Inputs:  DS = ES = TSR Data Area
;         AL = Function to call
;         Other registers (typically BH/BX) as needed for call
;         Int15Hdr.OldVector
;Outputs: Performs function for PS2 Mouse (not USB Mice)
;         Preserves AX, BX, CX, DX, and Flags across the call
;Changes:
;------------------------------------------------------------------------------
DoPS2MouseFunction:
  PUSH AX,BX,CX,DX          ;Save used registers
  PUSHF                     ;Save flags
  TEST B [DIPS2Offset].DIFlags,DIFlagInUse ;Is there a PS2 Mouse installed?
  JZ  >P90                  ;If not, quit
  MOV  AH,Int15PS2PortCall  ;Set correct value for AH
  PUSHF                     ;Do
  CALL D Int15Hdr.OldVector ;  it
P90:                        ;Done
  POPF                      ;Restore flags
  POP  DX,CX,BX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE CURRENT STATUS BYTE
;Inputs:  DS = ES = TSR Data Area
;         Various Registers (mostly in Int14 Request)
;Outputs: BL = Current Status (as needed by Int 15h GetStatus Request)
;Changes:
;------------------------------------------------------------------------------
CalcStatus:
  PUSH AX                      ;Save used registers
  XOR  BL,BL                   ;Default = 0
  TEST TSRFlags,Int15Enabled   ;Enabled?
  JZ  >S10                     ;If not, continue
  OR   BL,StatusEnabled        ;If so, mark it as Enabled
S10:                           ;Test Buttons
  CALL GetAllRawBtns           ;Get combined Buttons Status (AL, 5 buttons)
S20:                           ;Test Left Button
  TEST AL,RawLBtn              ;Left Button pressed?
  JZ  >S30                     ;If not, continue
  OR   BL,StatusLBtn           ;If so, mark it as pressed
S30:                           ;Test Right Button
  TEST AL,RawRBtn              ;Right Button pressed?
  JZ  >S40                     ;If not, continue
  OR   BL,StatusRBtn           ;If so, mark it as pressed
S40:                           ;Test Center Button
  TEST AL,RawCBtn              ;Center Buton pressed?
  JZ  >S50                     ;If not, continue
  OR   BL,StatusCBtn           ;If so, mark it as pressed
S50:                           ;Test Scaling
  TEST TSRFlags,ScalingFactor2 ;Scaling Factor at 2:1?
  JZ  >S60                     ;If not, continue
  OR   BL,StatusScaling2       ;If so, mark as 2:1 Scaling
S60:                           ;Test Stream/Remote Mode
                               ;Mode Always Stream!
S90:                           ;Done
  POP  AX                      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE CURRENT SAMPLE RATE
;Inputs:  DS = ES = TSR Data Area
;         Periodicity contains last Periodicity rate set
;Outputs: DL = Sample Rate (Reports/Second)
;Changes:
;------------------------------------------------------------------------------
CalcSampleRate:
  PUSH AX,BX               ;Save used registers
  MOV  AL,SampleRateCode   ;Get current Sample Rate Code
S10:                       ;Test for 100
  CMP  AL,SampleCode100    ;Is it 100 or 200 (if so, we're really 80)?
  JB  >S40                 ;If not, just use the table
  JA  >S20                 ;If 200, jump to handle it
  MOV  BL,100              ;Tell the user it's 100
  JMP >S80                 ;Done
S20:                       ;It's 200
  MOV  BL,200              ;If so, tell the user it's 200
  JMP >S80                 ;Done
S40:                       ;Convert Code using the Table
  MOV  BX,SampleRateTable  ;Convert the Code
  CALL SearchByteToWordTbl ;  to an Interrupt Rate
S80:                       ;BL contains Sampel Rate
  MOV  DL,BL               ;Put it in DL for the Return
  POP  BX,AX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE CURRENT IDLE RATE FACTOR TO USE
;Inputs:  DS = ES = TSR Data Area
;         SampleRateCode
;Outputs: AL = Idle Rate to set Device to
;         (Periodicity = 2+4*IdleRate)
;Changes:
;------------------------------------------------------------------------------
CalcIdleRate:
  PUSH BX              ;Save used registers
  MOV  BH,AH           ;Save AH
  CALL CalcPeriodicity ;Get Periodicity (AL)
I20:                   ;Periodicity >= 10
  SUB  AL,2            ;Subtract 2
  SHR  AL,1            ;Divide
  SHR  AL,1            ;  by 4
  MOV  AH,BH           ;Restore AH
  POP  BX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE CURRENT PERIODICITY FACTOR TO USE
;Inputs:  DS = ES = TSR Data Area
;         SampleRateCode
;Outputs: AX = Periodicity
;Changes:
;------------------------------------------------------------------------------
CalcPeriodicity:
  PUSH BX                  ;Save used registers
  MOV  AL,SampleRateCode   ;Get current Sample Rate Code
  MOV  BX,SampleRateTable  ;Convert the Code
  CALL SearchByteToWordTbl ;  to an Interrupt Rate
  MOV  AL,BH               ;Put Periodicity
  XOR  AH,AH               ;  in AX
  POP  BX                  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CHANGE THE SAMPLE RATE TO A NEW PERIODICITY
;Inputs:  DS = ES = TSR Data Area
;         BL = New Periodicity to use
;Outputs:
;Changes: Modifies Interrupt Periodicity (for all mice)
;         Modifies Idle Rate of Device (for all mice)
;NOTES: Thos does NOT change anything on the PS2 Mouse, only on the USB Mice.
;------------------------------------------------------------------------------
ChangePeriodicity:
  PUSH AX,BX,CX,DI,SI                          ;Save used registers
  MOV  SI,Int14Request2                        ;Point at Request Structure
  MOV  [SI].I14REndPoint,0                     ;End Point 0
  XOR  BH,BH                                   ;Store Periodicity
  MOV  [SI].I14RPeriodicity,BX                 ;  in Request
  MOV  CX,SetHIDIdleReq                        ;Point at Idle Request Struc
  CALL CopySetupReqToInt14SI                   ;Copy Setup Pkt (CX)
  MOV  CL,2                                    ;Calculate Idle Rate
  SUB  BL,CL                                   ;Calculate Idle Rate to use
  SHR  BL,CL                                   ;  (2ms + 4 * this number)
  MOV  B [SI].I14RSetHIDIdleDuration,BL        ;Store it in the Request
  MOV  W [SI].I14RCallBackAddr[0],-1           ;Do nothing when done
  MOV  CX,MaxDevices                           ;Number of Table Entries
  MOV  DI,DeviceInfoTable                      ;Point at Table
P10:                                           ;Loop to here for each Table Entry
  TEST [DI].DIFlags,DIFlagInUse                ;Valid Table Entry?
  JZ  >P50                                     ;If not, skip it
  MOV  AX,W [DI].DIHostIndex                   ;Copy the Host Index
  MOV  W [SI].I14RHostIndex,AX                 ;  and Device Address
  MOV  AX,[DI].DIIntHandle                     ;Copy the
  MOV  [SI].I14RRequestHandle,AX               ;  Interrupt Handle Number
  MOV  AL,[DI].DIInterfaceNum                  ;Copy the
  MOV  B [SI].I14RSetHIDIdleIntf,AL            ;  Interface Number
  MOV  AL,[DI].DIReportID                      ;Copy the Report ID
  MOV  B [SI].I14RSetHIDIdleRptID,AL           ;  (will be 0 if no ID's used)
  MOV  [SI].I14RRequestType,I14RRTChangeIntPer ;Request = Change Periodicity
  CALL DoInt14CallSaveAllSI                    ;Do it
  MOV  [SI].I14RRequestType,I14RRTDoControl    ;Type = Control
  CALL DoInt14CallSaveAllSI                    ;Do it
P50:                                           ;Done with this Table Entry
  ADD  DI,DeviceInfoStrucSize                  ;Point at next Table Entry
  LOOP P10                                     ;Keep going until we're done
  POP  SI,DI,CX,BX,AX                          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESET MOVEMENT AND BUTTON VALUES TO 0
;Inputs:  DS = ES = TSR Data Area
;Outputs: XTotal, YTotal
;Changes:
;------------------------------------------------------------------------------
ResetTotals:
  PUSH AX,CX,DI               ;Save used registers
  XOR  AX,AX                  ;Reset to zeroes
  MOV  CX,(MaxDevices+1)      ;Number of Table entries to do
  MOV  DI,DeviceInfoTable     ;Point at Table
R10:                          ;Loop to here for each table entry
  MOV  W [DI].DIXRemainder,AX ;Reset Remainders
  MOV  W [DI].DIRawData[0],AX ;Reset
  MOV  W [DI].DIRawData[2],AX ;  All
  MOV  W [DI].DIRawData[4],AX ;  Raw
  MOV  W [DI].DIRawData[6],AX ;  Data
  ADD  DI,DeviceInfoStrucSize ;Point at next entry
  LOOP R10                    ;Keep going
  POP  DI,CX,AX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SEARCH A BYTE-TO-WORD LOOKUP TABLE FOR A MATCH
;Inputs:  ES:[BX] = Table to search
;         AL      = Byte to search for
;Outputs: CF = Clear if a byte match as found in table
;              BX = Word from Table
;            = Set if no match found in table
;              BX = unchanged
;Changes: Last Entry in Table (end-of-table marker) = DB -1, DW -1
;------------------------------------------------------------------------------
SearchByteToWordTbl:
  PUSH AX,SI,DS  ;Save used registers
  CLD            ;Go forward with string functions
  MOV  DS,ES     ;Point DS at correct data area
  MOV  SI,BX     ;Put pointer in SI
  MOV  AH,AL     ;Put byte to check in AH
B10:             ;Loop to here for each table entry
  LODSB          ;Get the next table entry
  CMP  AL,-1     ;Is it possibly the end of the table?
  JNE >B20       ;If not, continue
  CMP  W [SI],-1 ;Is it actually the end of the table?
  JE  >B70       ;If so, there's no match
B20:             ;Not end of table
  CMP  AL,AH     ;Is it a match?
  JE  >B80       ;If so, handle it!
  INC  SI,2      ;If not, skip over this table entry
  JMP  B10       ;And keep looking
B70:             ;No entry found
  STC            ;Set the not found flag
  JMP >B90       ;Quit
B80:             ;Entry found [SI]
  MOV  BX,[SI]   ;Point BX at the table entry
  CLC            ;Set the found flag
B90:             ;Done
  POP  DS,SI,AX  ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE INT 08h (Timer Tick)
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HAVE INTERRUPT 08 (TIMER) RUN THIS EVERY TIME IT OCCURS
;Inputs:
;Outputs: Searches for Newly attached Device
;Changes:
;NOTES: In theory, we shouldn't even need to do this since we are registering
;         and will be notified automatically if a new Device is attached.
;       We will do this just in case we miss it or are busy for some reason.
;------------------------------------------------------------------------------
Int08Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int08:
  PUSHF                          ;Process the
  CALL D CS:Int08Hdr.OldVector   ;  Old Interrupt Handler
  STI                            ;Enable interrupts
  CLD                            ;Go forward with string functions
  CALL MakeStack                 ;Set up our own Stack Space
  PUSH BX,DI,SI,DS,ES            ;Save used registers
  MOV  DS,CS                     ;Point DS and
  MOV  ES,CS                     ;  ES at our data area
  CALL EnableIRQ8                ;Enable IRQ 8 (CMOS Timer)
  CALL EnableInt70               ;Enable INT 70h (CMOS Timer)
  ;CALL DoIntelliTimer            ;Count down the IntelliMouse Timer
  MOV  BX,OFFSET TSRFlags2       ;Point [BX] at TSRFlags2
  TEST B [BX],Int08InUse         ;Still processing the previous IRQ?
  JNZ >E95                       ;If so, quit
  OR   B [BX],Int08InUse         ;If not, mark as processing
E10:                             ;OK to process
  MOV  DI,OFFSET Int08Counter    ;Point DI at Int08Counter
  MOV  SI,OFFSET DontLookCounter ;Point BX at DontLookCounter
  CMP  W [SI],0                  ;Need to Decrement DontLookCounter?
  JE  >E20                       ;If not, continue
  DEC  W [SI]                    ;If so, Decrement it
  JNZ >E90                       ;If not 0 yet, quit
  CMP  B [DI],0                  ;Disabled?
  JE  >E90                       ;If so, quit
  MOV  B [DI],1                  ;If not, look for Device right away
E20:                             ;Done with DontLookCounter
  CMP  B [DI],0                  ;Are we supposed to look yet?
  JE  >E90                       ;If not, just quit
  DEC  B [DI]                    ;Decrement the Delay Counter
  JNZ >E90                       ;If not 0 yet, quit
  MOV  B [DI],182                ;If 0, reset for next time (approx 10 seconds)
  PUSH AX,BX,CX,DX,BP            ;Save used registers
  CALL LookForNewDevice          ;Look for a new Device
  POP  BP,DX,CX,BX,AX            ;Restore used registers
E90:                             ;Done, mark as not processing any more
  AND  B [BX],(NOT Int08InUse)   ;Mark as done processing
E95:                             ;Done
  POP  ES,DS,SI,DI,BX            ;Restore used registers
  CALL ReturnStack               ;Restore Original Stack Space
  IRET

;;------------------------------------------------------------------------------
;;COUNTDOWN THE INTELLIMOUSE COUNTDOWN TIMER, IF NECESSARY
;;Inputs:  DS = ES = TSR Data Area
;;         IntelliTimer
;;Outputs: IntelliTimer
;;         EnableWheelCounter, EnableWheelCounter2
;;Changes:
;;------------------------------------------------------------------------------
;DoIntelliTimer:
;  CMP  IntelliTimer,0         ;Do we need to do anything?
;  JE  >I90                    ;If not, quit
;  DEC  IntelliTimer           ;If so, decrement by 1
;  JNZ >I90                    ;If not 0 yet, we're done
;  MOV  W EnableWheelCounter,0 ;If timed out, reset tracking Counters
;I90:                          ;Done
;  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE INT 70h (CMOS Delay Timer)
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;;------------------------------------------------------------------------------
;;STARTS THE EVENT WAIT INTERVAL (INT 70h's DON'T GET ISSUED OTHERWISE)
;;Inputs:  DS = ES = TSR Data Area
;;Outputs: CF = Set if error or function already busy
;;            = Clear if it worked (or nothing to type)
;;Changes: Starts the Event Wait Handler (allows Int 70h to occur)
;;         Int 70h will enable high bit of CS:ChangeThisByte)
;;------------------------------------------------------------------------------
;StartEvent:
;  PUSH AX,BX,CX,DX       ;Save used registers
;  XOR  CX,CX             ;CX:DX = number of microseconds
;  MOV  DX,950            ;  to delay
;  MOV  BX,ChangeThisByte ;Event Wait Handler will set the high bit of
;                         ;  ES:[BX] when the delay is over
;  MOV  B [BX],0          ;Make sure ChangeThisByte is 0 when we start
;  MOV  AX,8300h          ;Function 83h, subfun 00h (Set Event Interval)
;  INT  15h               ;Do it
;  POP  DX,CX,BX,AX       ;Restore used registers
;  RET

;------------------------------------------------------------------------------
;HAVE INTERRUPT 70h (RTC Interrupt) RUN THIS PROGRAM WHEN IT OCCURS
;Inputs:
;Outputs:
;Changes: Types all the keys in our Buffer
;------------------------------------------------------------------------------
Int70Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int70:
  CALL MakeStack                   ;Create our own stack space
  PUSH AX,BX,DS,ES                 ;Save used registers
  MOV  DS,CS                       ;Point DS
  MOV  ES,CS                       ;  and ES at our data area
  STI                              ;Enable interrupts
  CMP  W Int70Hdr.OldVector[2],0   ;Is the Old Vector Valid?
  JE  >S20                         ;If not, handle it
  CMP  W Int70Hdr.OldVector[2],70h ;Is the Old Vector DOS (FreeDOS bug)?
  JE  >S20                         ;If not, handle it
  PUSHF                            ;Call the Old Interrupt Vector
  CALL D Int70Hdr.OldVector        ;  (this will issue the End-of-Interrupt)
  JMP >S40                         ;Continue
S20:                               ;Old Vector is invalid
  CALL IssueEOI2                   ;Issue End-of-Interrupt
S40:                               ;Done handling End-of-Interrupt
  MOV  BX,OFFSET TSRFlags2         ;Point [BX] at TSRFlags2
  TEST B [BX],Int70InUse           ;Still processing the last IRQ?
  JNZ >S90                         ;If so, quit
  OR   B [BX],Int70InUse           ;If not, mark as processing
  CALL SendCodes                   ;Send the Codes
  CALL EnableIRQ8                  ;Make sure IRQ 8 remains enabled in PIC
  CALL EnableInt70                 ;Make sure Int 70h remains enabled in CMOS
  AND  B [BX],(NOT Int70InUse)     ;Mark as done processing
S90:                               ;Done
  POP  ES,DS,BX,AX                 ;Restore used registers
  CALL ReturnStack                 ;Restore original Stack Space
  IRET

;------------------------------------------------------------------------------
;ISSUE AN END-OF-INTERRUPT SIGNAL TO THE PICs FOR AN IRQ ON THE SECOND PIC
;Inputs:
;Outputs:
;Changes: Issues End-of-Interrupt Signal for an IRQ on the second PIC
;------------------------------------------------------------------------------
IssueEOI2:
  PUSH AX                                   ;Save used registers
  PUSHF                                     ;Save flags
  CLI                                       ;Disable interrupts
  MOV  AL,OCW2SelectOCW2+OCW2NonSpecificEOI ;Non-specific EOI
  OUT  PIC2Addr,AL                          ;Send to PIC 2
  MOV  AL,OCW3SelectOCW3+OCW3ReadISR        ;Get the
  OUT  PIC2Addr,AL                          ;  Interrupt In-Service Register
  MOV  AX,AX                                ;  from
  IN   AL,PIC2Addr                          ;  PIC 2
  OR   AL,AL                                ;Other Ints still being serviced?
  JNZ >P90                                  ;If so, don't reset PIC 1 yet
  MOV  AL,OCW2SelectOCW2+OCW2NonSpecificEOI ;If not, Non-specific EOI
  OUT  PIC1Addr,AL                          ;Send to PIC 1
P90:                                        ;Done
  POPF                                      ;Restore flags
  POP  AX                                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ENABLE IRQ 8 (INT 70h)
;Inputs:
;Outputs:
;Changes: Makes sure IRQ 8 (INT 70h) is Enabled
;NOTES: We need to call this after every call to INT 70h, since many BIOSes
;       will Disable it (the Periodic Interrupt) when they are called.
;------------------------------------------------------------------------------
EnableIRQ8:
  PUSH AX              ;Save used registers
  PUSHF                ;Save flags
  CLI                  ;Disable Interrupts
  IN   AL,(PIC2Addr+1) ;Get the PIC2 IRQ Mask
  AND  AL,(NOT 01h)    ;Enable IRQ8
  OUT  (PIC2Addr+1),AL ;  (Int 70h)
  POPF                 ;Restore Flags
  POP  AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ENABLE INT 70h PERIODIC INTERRUPT, IF APPOPRIATE
;Inputs:  ES:CPUClass
;Outputs:
;Changes: Int 70 Frequency Divider & Periodic Interrupt Flag
;------------------------------------------------------------------------------
EnableInt70:
  PUSH AX             ;Save used registers
  MOV  AH,CMOSPortA   ;Get the current
  CALL GetCMOS        ;  frequency divider
  CMP  AL,Period1024  ;Is it 1024Hz?
  JE  >E30            ;If so, test the periodic interrupt flag
  MOV  AL,Period1024  ;If not, set it
  CALL SetCMOS        ;  to 1024Hz
E30:                  ;Enable periodic interrupt flag
  MOV  AH,CMOSPortB   ;Get the current
  CALL GetCMOS        ;  Periodic Interrupt status
  TEST AL,PeriodicInt ;Periodic Interrupts already enabled?
  JNZ >E80            ;If so, we're done
  OR   AL,PeriodicInt ;If not,
  CALL SetCMOS        ;  Enable Periodic Interrupt
E80:                  ;Done changing CMOS
  CALL FinishCMOS     ;Clean up the CMOS data so Int's are reissued
E90:                  ;We're done
  POP  AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CLEAN UP THE CMOS AFTER AN INT 70 IS ISSUED SO IT CAN GET ISSUED AGAIN
;Inputs:
;Outputs:
;Changes: "Resets" CMOS IRQ 08 Stuff
;NOTES: I ran across some documentation (thanks to a SLOWDOWN user) that says
;         after an IRQ 08/Int 70h occurs, CMOS Port C must be read or Periodic
;         Interrupts won't be re-issued (this supposedly tells the CMOS that
;         the Interrupt processing is complete).  The documentation also
;         stated that after done messing with the CMOS, you should leave the
;         CMOS pointed at CMOS Port D.  There was never a specific, practical
;         reason stated for doing this, but I guess is just good practice.
;         This little subroutine simply reads CMOS ports C & D, but doesn't
;         do anything with the data.
;       The documentation also stated that one should disable NMI while
;         messing with the CMOS.  The reason stated for this was that if the
;         NMI caused a reboot, and you were in the middle of changing
;         something in the CMOS, the computer could reboot to an unknown
;         state.  This is because the CMOS retains its state even through a
;         reboot.  While booting, most computers do not reset the CMOS to
;         a known state, because the CMOS contains things like the Date & Time
;         and BIOS configuration items.
;       Well, what we're changing here, even though it is in the CMOS, is
;         reset at reboot.  All we do to the CMOS is set the Periodic Interrupt
;         rate to 1024Hz (which is what it's supposed to be after a reboot)
;         and Enable (allow) the Periodic Interrupts to occur (which is
;         also supposed to be reset after a reboot, even if nothing else is).
;       Because Disabling NMI is a little bit cumbersome to do, and because
;         it really doesn't do anything useful anyway, we won't worry about
;         it!!
;------------------------------------------------------------------------------
FinishCMOS:
  PUSH AX           ;Save used registers
  MOV  AH,CMOSPortC ;Read Port C
  CALL GetCMOS      ;  (this supposedly allows Periodic Int's to keep happening)
  MOV  AH,CMOSPortD ;Read Port D
  CALL GetCMOS      ;  (this supposedly sets things to a "normalized" state)
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET A BYTE FROM THE CMOS
;Inputs:  AH = CMOS port number to get
;Outputs: AL = Byte from CMOS
;Changes:
;------------------------------------------------------------------------------
GetCMOS:
  PUSHF       ;Save flags
  CLI         ;Disable interrupts
  MOV  AL,AH  ;Put Port number in AL
  OUT  70h,AL ;Tell CMOS which port number we want
;  PUSH AX     ;Wait for
;  POP  AX     ;  output
  IN   AL,71h ;Get the byte from the CMOS
  POPF        ;Restore flags
  RET

;------------------------------------------------------------------------------
;SET A BYTE IN THE CMOS
;Inputs:  AH = CMOS port number to change
;         AL = What to change the CMOS byte to
;Outputs:
;Changes: Byte in CMOS
;------------------------------------------------------------------------------
SetCMOS:
  PUSHF       ;Save flags
  CLI         ;Disable interrupts
  XCHG AL,AH  ;Tell CMOS which
  OUT  70h,AL ;  port number we want
  XCHG AL,AH  ;Get what we want to change it to
  OUT  71h,AL ;Change the CMOS byte
  POPF        ;Restore flags
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO HANDLE THE ORIGINAL (REAL) PS2 MOUSE BIOS SOFTWARE
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HANDLE THE PS2 FAR CALL (FROM THE ORIGINAL PS2 BIOS HANDLER)
;Inputs:
;Outputs:
;Changes: If appropriate, passes Far Call along to the Int 33h Handler
;           after modifying the data locations and "ORing" in the
;           buttons from the USB Mice
;------------------------------------------------------------------------------
PS2FarCall:
  PUSH BP                          ;Save used registers
  MOV  BP,SP                       ;Get Stack Pointer
                                   ;[BP+00] = Original BP
                                   ;[BP+02] = PS2 Far Call Return Addr
                                   ;[BP+06] = Stack Word 4 (Always 0)
                                   ;[BP+08] = Stack Word 3 (Y, Z if Wheel)
                                   ;[BP+10] = Stack Word 2 (X, Y if Wheel)
                                   ;[BP+12] = Stack Word 1 (Btns, & X if Wheel)
  PUSHA                            ;Save all registers
  PUSH DS,ES                       ;Save used registers
  MOV  DS,CS                       ;Point DS and
  MOV  ES,CS                       ;  ES at TSR Data Area
  MOV  BX,DIPS2Offset              ;Point at the PS2 Table Entry
  MOV  DI,12                       ;Point SS:[BP+DI] at buttons on stack
  MOV  AL,SS:[BP+DI]               ;Get the Buttons off the stack
  AND  AL,RawAll3Btns              ;Mask out unwanted bits
  CMP  [BX].DINumWheels,2          ;Are there any more buttons?
  JB  >F10                         ;If not, we're done with the buttons
  MOV  AH,SS:[BP+DI].StackZWheel   ;If so, get the Button Data
  AND  AH,30h                      ;Mask out everything but buttons 4 & 5
  SHR  AH,1                        ;Move it over so we can mask it in
  OR   AL,AH                       ;Mask it in
F10:                               ;AL contains button status
  MOV  [BX].DIBtnOffset,AL         ;Store the Buttons
  CMP  [BX].DINumWheels,0          ;Any Wheels?
  JA  >F20                         ;If so, jump to handle it
  MOV  AL,SS:[BP+DI].StackXNoWheel ;If not, put X Data in AL
  MOV  AH,SS:[BP+DI].StackYNoWheel ;  and Y Data in AH
  JMP >F30                         ;Continue
F20:                               ;Has at least one wheel
  MOV  AL,SS:[BP+DI].StackXWheel   ;Put X Data in AL
  MOV  AH,SS:[BP+DI].StackYWheel   ;  and Y Data in AH
F30:                               ;AL = X Data, AH = Y Data
  NEG  AH                          ;Make Y Data backwards
  MOV  W [BX].DIXDataPS2,AX        ;Store X & Y Data
  MOV  AL,SS:[BP+DI].StackZWheel   ;Get the Wheel Data
  XOR  AH,AH                       ;Start with Z2 Data at 0
  CMP  [BX].DINumWheels,1          ;How many wheels are there?
  JB  >F90                         ;If none, we're ready to send it
  JE  >F80                         ;If 1, it's OK to send it
F40:                               ;PS2 Mouse has 2 Wheels
  AND  AL,0Fh                      ;Mask out the buttons
  TEST AL,08h                      ;Is it Negative?
  JZ  >F45                         ;If not, continue
  OR   AL,0F0h                     ;If so, convert it to a byte
F45:                               ;Nibble converted to byte
  CMP  AL,1                        ;Is it 0, 1, or 2?
  JBE >F80                         ;If 0 or 1, it's OK like it is
  JG  >F60                         ;If 2, jump to handle it
  CMP  AL,-1                       ;Is it -2 or -1?
  JE  >F80                         ;If -1, it's OK like it is
F50:                               ;Z Data is -2
  MOV  AH,-1                       ;Mark Z2 as moved
  JMP >F70                         ;Continue
F60:                               ;Z Data is 2
  MOV  AH,1                        ;Mark Z2 as moved
F70:                               ;Need to make Z1 Data 0
  XOR  AL,AL                       ;Z1 = 0
F80:                               ;AX contains Wheels Data
  MOV  W [BX].DIZ1DataPS2,AX       ;Store the Wheels Data
  NEG  B [BX].DIZ1DataPS2          ;Make the Vertical Wheel
  ;NEG  B [BX].DIZ2Data             ;  Data backwards
F90:                               ;Data has been stored
  CALL DeviceChangeMouse           ;Do it
  POP  ES,DS                       ;Restore used registers
  POPA                             ;Restore all registers
  POP  BP                          ;Restore used registers
  RETF


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;STACK-RELATED FUNCTIONS
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;CREATE/DELETE OUR OWN STACK
;Inputs:  CS = TSR Data Area
;         StackInUse
;Outputs: Interrupts Enabled
;Changes: SS,SP to/from our stack
;------------------------------------------------------------------------------
MakeStack:
  CLI                  ;Disable interrupts
  PUSHF                ;Save
  POP  CS:TempFlags    ;  Flags
  INC  CS:StackInUse   ;Increment number-of-calls counter
  CMP  CS:StackInUse,1 ;Is this the first call?
  JNE >M90             ;If not, the stack is already in use
  MOV  CS:TempBX,BX    ;  BX in memory
  POP  BX              ;Save MakeStack's return address in BX
  MOV  CS:OldSS,SS     ;Save original SS
  MOV  CS:OldSP,SP     ;Save original SP
  MOV  SP,CS           ;Put our CS
  MOV  SS,SP           ;  into SS
  MOV  SP,LastTSRByte  ;SP = Top of Stack
  PUSH BX              ;Restore MakeStack's return address to stack
  MOV  BX,CS:TempBX    ;Restore BX from memory
M90:                   ;Done
  PUSH CS:TempFlags    ;Restore
  POPF                 ;  Flags
  STI                  ;Enable interrupts
  RET

ReturnStack:
  CLI                ;Disable interrupts
  PUSHF              ;Save
  POP  CS:TempFlags  ;  Flags
  DEC  CS:StackInUse ;Decrement number-of-calls counter
  JNZ >R90           ;If sack is still in use, quit
  MOV  CS:TempBX,BX  ;Save BX in memory
  POP  BX            ;Save MakeStack's return address in BX
  MOV  SS,CS:OldSS   ;Restore original SS
  MOV  SP,CS:OldSP   ;Restore original SP
  PUSH BX            ;Restore MakeStack's return address to stack
  MOV  BX,CS:TempBX  ;Restore BX from memory
R90:                 ;Done
  PUSH CS:TempFlags  ;Restore
  POPF               ;  Flags
  STI                ;Enable interrupts
  RET


;;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;;DEBUGGING CODE - NOT NEEDED IN FINAL RELEASE OF PROGRAM!!!
;;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;
;;  VideoOffsetFirst EQU ((80*2*27)+(50*2)) ;Row 27, Column 50
;  VideoOffsetFirst EQU ((80*2*27)+(10*2)) ;Row 27, Column 10
;  VideoOffsetLast  EQU ((80*2)*50)        ;End of Screen (51st Row)
;
;  VideoOffset  DW VideoOffsetFirst ;Video Offset to use this time
;
;;Inputs: DS:DI = Buffer Entry Pointer
;WritePS2Buttons:
;  PUSH AX,DI,ES
;  PUSHF
;  CLD
;  CALL GetVideoOffset ;(ES:DI)
;  MOV  AL,PS2Buttons
;  CALL WriteALByte
;  POPF
;  POP  ES,DI,AX
;  RET
;
;WriteCL:
;  PUSH AX,DI,ES
;  PUSHF
;  CLD
;  CALL GetVideoOffset ;(ES:DI)
;  MOV  AL,CL
;  CALL WriteALByte
;  POPF
;  POP  ES,DI,AX
;  RET
;
;;WriteRawData:
;;  PUSH AX,CX,DI,SI,ES     ;Save used registers
;;  PUSHF                   ;Save flags
;;  CLD                     ;Go forward with string functions
;;  LEA  SI,[DI].DIRawData  ;Point SI at Raw Data
;;  CALL GetVideoOffset     ;Get video offset to use (ES:DI)
;;  MOV  CX,MaxKeybDataSize ;Number of bytes in buffer
;;B10:                      ;Loop to here for each byte in buffer
;;;  MOVSB                   ;Copy the character into video memory
;;  LODSB                   ;Write the
;;  CALL WriteALByte        ;  Buffer Contents
;;;  XOR  B ES:[DI],077h     ;Toggle the color
;;;  INC  DI                 ;Point at next character in video memory
;;  LOOP B10                ;Keep going until we're done
;;  POPF                    ;Restore flags
;;  POP  ES,SI,DI,CX,AX     ;Restore used registers
;;  RET
;
;
;;WriteAXBXCXDX:
;;  PUSH AX,DI,ES       ;Save used registers
;;  PUSHF               ;Save Flags
;;  CALL GetVideoOffset ;Get video offset to use (ES:DI)
;;  CALL WriteAXWord    ;Write AX
;;  MOV  AX,BX          ;Write
;;  CALL WriteAXWord    ;  BX
;;  MOV  AX,CX          ;Write
;;  CALL WriteAXWord    ;  CX
;;  MOV  AX,DX          ;Write
;;  CALL WriteAXWord    ;  DX
;;  POPF                ;Restore flags
;;  POP  ES,DI,AX       ;Restore used registers
;;  RET
;;
;;WriteAXWord:
;;  PUSH AX,CX         ;Save used registers
;;  MOV  CH,4          ;Need to write 4 characters
;;  MOV  CL,4          ;Need to shift 4 bits at a time
;;W30:                 ;Loop to here for each Nibble we write
;;  ROL  AX,CL         ;Put bits to write in low nibble
;;  CALL WriteNibbleC  ;Write the Nibble
;;  XOR  B ES:[DI],77h ;Toggle the Color
;;  INC  DI            ;Skip over the Attribute Byte
;;  DEC  CH            ;Decrement the Loop Counter
;;  JNZ  W30           ;If not 0 yet, keep writing
;;  MOV  AL,' '        ;Write a
;;  STOSB              ;  Space
;;  XOR  B ES:[DI],77h ;Toggle the Color
;;  INC  DI            ;Skip over the Attribute Byte
;;  POP  CX,AX         ;Restore used registers
;;  RET
;
;WriteALByte:
;  PUSH AX,CX         ;Save used registers
;  MOV  CH,2          ;Need to write 4 characters
;  MOV  CL,4          ;Need to shift 4 bits at a time
;W30:                 ;Loop to here for each Nibble we write
;  ROL  AL,CL         ;Put bits to write in low nibble
;  CALL WriteNibbleC  ;Write the Nibble
;  XOR  B ES:[DI],77h ;Toggle the Color
;  INC  DI            ;Skip over the Attribute Byte
;  DEC  CH            ;Decrement the Loop Counter
;  JNZ  W30           ;If not 0 yet, keep writing
;  MOV  AL,' '        ;Write a
;  STOSB              ;  Space
;  XOR  B ES:[DI],77h ;Toggle the Color
;  INC  DI            ;Skip over the Attribute Byte
;  POP  CX,AX         ;Restore used registers
;  RET
;
;WriteAX:
;  PUSH AX,CX,DI,ES    ;Save used registers
;  PUSHF               ;Save Flags
;  CALL GetVideoOffset ;Get video offset to use (ES:DI)
;  MOV  CH,4           ;Need to write 4 characters
;  MOV  CL,4           ;Need to shift 4 bits at a time
;F20:                  ;Loop to here for each Nibble we write
;  ROL  AX,CL          ;Put bits to write in low nibble
;  CALL WriteNibbleC   ;Write the Nibble
;  XOR  B ES:[DI],77h  ;Toggle the Color
;  INC  DI             ;Skip over the Attribute Byte
;  DEC  CH             ;Decrement the Loop Counter
;  JNZ  F20            ;If not 0 yet, keep writing
;  POPF                ;Restore flags
;  POP  ES,DI,CX,AX    ;Restore used registers
;  RET
;
;WriteNibbleC:
;  PUSH AX             ;Save used register
;  AND  AL,0Fh         ;Get rid of the high nibble
;  ADD  AL,'0'         ;Convert it to a number
;  CMP  AL,'9'         ;Should it be a number?
;  JBE >N90            ;If so, we're done
;  ADD  AL,('A'-'9'-1) ;If not, convert it to a letter
;N90:                  ;We're done
;  STOSB               ;Write it to the string
;  POP  AX             ;Restore used register
;  RET                 ;We're done
;
;;Outputs:  ES = 0B800h (Video Segment)
;;          [DI] = Video Offset to use
;GetVideoOffset:
;  MOV  DI,0B800h                       ;Point ES at
;  MOV  ES,DI                           ;  Video Memory
;  MOV  DI,CS:VideoOffset               ;Point at Video Offset we want to change
;  ADD  CS:VideoOffset,160              ;Go to next row for next time
;  CMP  CS:VideoOffset,VideoOffsetLast  ;Gone too far yet?
;  JB  >V90                             ;If not, we're done
;  MOV  CS:VideoOffset,VideoOffsetFirst ;If so, reset it to the top
;V90:                                   ;Video Offset updated for next time
;  RET
;
;  TRow EQU 20 ;Row to Toggle Color in (1-based)
;  TCol EQU  1 ;Column to Toggle Color in (1-based)
;
;ToggleColor:
;  PUSH BX,DS        ;Save used registers
;  PUSHF             ;Save Flags
;  MOV  BX,0B800h    ;Point DS at
;  MOV  DS,BX        ;  video memory
;  MOV  BX,((TRow-1)*160)+((TCol-1)*2)
;  INC  B [BX]       ;Change the character
;  XOR  B [BX+1],77h ;Toggle the Color
;  POPF              ;Restore flags
;  POP  DS,BX        ;Restore used registers
;  RET
;
;
;;WriteFrameNum:
;;  PUSHF                        ;Save Flags
;;  PUSH AX,CX,DX,DI,DS,ES       ;Save used registers
;;  MOV  DS,CS                   ;Point DS at Local data Area
;;  MOV  DI,Int14Request2        ;Point at Request Structure
;;  MOV  [DI].I14RRequestType,I14RRTGetHostStatus ;Get Host Status
;;  MOV  [DI].I14RHostIndex,15   ;Host Index 15
;;  MOV  DX,DI                   ;Do the Call
;;  CALL DoInt14CallCX           ;  (Returns CX = Current Frame Index)
;;  MOV  AX,CX                   ;Put Frame Nubmer in AX
;;  MOV  DI,0B800h               ;Point ES at
;;  MOV  ES,DI                   ;  Video Memory
;;  MOV  DI,4160                 ;Point at Character we want to change
;;  MOV  CH,4                    ;Need to write 4 characters
;;  MOV  CL,4                    ;Need to shift 4 bits at a time
;;F10:                           ;Loop to here for each Nibble we write
;;  ROL  AX,CL                   ;Put bits to write in low nibble
;;  CALL WriteNibbleC            ;Write the Nibble
;;  XOR  B ES:[DI],77h           ;Toggle the Color
;;  INC  DI                      ;Skip over the Attribute Byte
;;  DEC  CH                      ;Decrement the Loop Counter
;;  JNZ  F10                     ;If not 0 yet, keep writing
;;  POP  ES,DS,DI,DX,CX,AX       ;Restore used registers
;;  POPF                         ;Restore flags
;;  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;SPACE FOR OUR TSR STACK
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

  EVEN 16
  DB (StackSize/8) DUP ('TSRStack')


LastTSRByte:


;ÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛ
;ÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛ


;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
;DATA NEEDED TO INSTALL/UPDATE/SHOW STATUS (NOT NEEDED DIRECTLY IN TSR)
;ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ

;==============================================================================
;TSR Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Table of Interrupt Numbers that the TSR Intercepts
  ;First byte of each entry is the Interrupt Number
  ;Second word is the Pointer to our Interrupt Handler Code Header
  ;----------------------------------------------------------------------------
  IntNumTable:
    DB 70h       ;Real-time Clock
     DW Int70Hdr
    DB 2Fh       ;TSR Multiplex
     DW Int2FHdr
    DB 15h       ;BIOS Services
     DW Int15Hdr
    DB 11h       ;Get Equipment List
     DW Int11Hdr
    DB 08h       ;Timer Tick
     DW Int08Hdr
    DB 0         ;End of Table

  ;----------------------------------------------------------------------------
  ;Data needed to install as a TSR, Hooking into the Multiplex Int
  ;----------------------------------------------------------------------------
  FirstHandle DB 0 ;First available user handle for Int 2Fh


;==============================================================================
;Miscellaneous
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Miscellaneous Yes/No flags (individual bits)
  ;----------------------------------------------------------------------------
  ProgFlags        DB 00h ;Yes/No Test bits #1
    FoundSuptFile EQU 01h ;Found Support File in the Path
    Uninstall     EQU 02h ;Uninstall resident program?
    InMemory      EQU 04h ;Already installed in memory?
    UseLowMemory  EQU 08h ;User does not want us in High Memory
    MemoryMoved   EQU 10h ;TSR Memory has been moved by our Auto-Code
    PS2ResetDone  EQU 20h ;PS2 Mouse has already been reset at least once
    NoInstall     EQU 40h ;Don't Install as TSR

  ProgFlags2   DB 00h ;Yes/No Test bits #2
    Help      EQU 01h ;User wants help?
    DoEnvir   EQU 02h ;Parsing/Testing Environment variable
    DoStdIn   EQU 04h ;Parsing/Testing StdIn buffer
    Write2Err EQU 08h ;Write to ERR instead of Pause/CON

  StatusFlags    DB 00h ;Yes/No Test bits #2
    Report      EQU 01h ;Show HID Report Data
    Status      EQU 02h ;Show Status
    Descriptors EQU 04h ;Show Device/Config Descriptors Data
    Reset       EQU 08h ;Do a Hardware Reset
    BadDevices  EQU 10h ;Print Bad Device Table
    Alias       EQU 20h ;Write the Alias List to the Screen
    ErrLvl      EQU 40h ;Show ErrorLevel Table
    RawData     EQU 80h ;Show Raw Data

  ;----------------------------------------------------------------------------
  ;Miscellaneous Strings we need to write to the screen
  ;----------------------------------------------------------------------------
  HexString: DB '0000',0
  CrLfMsg:   DB CR,LF,0
  SingleMsg: DB ' '
  NoMsg:     DB 0
  Space4Msg: DB ' '
  Space3Msg: DB ' '
  Space2Msg: DB ' '
  SpaceMsg:  DB ' ',0

  ;----------------------------------------------------------------------------
  ;Data needed to test which hardware emulation method to use
  ;----------------------------------------------------------------------------
  TestByte DB -1


;==============================================================================
;Pause Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings and Data needed to Pause the screen
  ;----------------------------------------------------------------------------
  PauseMsg:
    DB CR,LF
    DB ' MORE   (Esc stops Pausing, Ctrl-C stops Completely, any other Continues)',CR,0
  PauseNMsg:
    DB ' MORE                                                                    ',CR,LF,0
  PauseDMsg:
    DB '                                                                               ',CR,0
  PauseRows       DW ? ;Number of rows per screen
  PauseHeaderPtr  DW 1 ;Pointer to Header Message (start with a fake one <> 0)
  PauseHeaderSize DW 2 ;Number of lines in the Header Message
  PauseRowCount   DW 2 ;Current Row Counter


;==============================================================================
;DOS Environment Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Variables to search for our Environment Variable
  ;----------------------------------------------------------------------------
  EnvVarString:  DB 'USBMOUSE='    ;Environment Variable String
  EnvVarSize    EQU $-EnvVarString ;Length of String
  EnvVarPointer  DW -1             ;Pointer to Program Environment Variable

  ;----------------------------------------------------------------------------
  ;Variables to search for an extended Command Line String
  ;This should only be valid/needed if the Command Line Size (PSP:[80h]) is 7Eh
  ;----------------------------------------------------------------------------
  CmdLineVarString:  DB 'CMDLINE='         ;Environment Variable String
  CmdLineVarSize    EQU $-CmdLineVarString ;Length of String
  CmdLineVarPointer  DW -1                 ;Pointer to Program Environment Variable


;==============================================================================
;EXEC Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Size of our program stack (in bytes)
  ;----------------------------------------------------------------------------
  ProgStackSize EQU 512

  ;----------------------------------------------------------------------------
  ;Variables to Locate and Run another Program (EXEC Child Program)
  ;----------------------------------------------------------------------------
  PathToUse:      DB MaxPathSize DUP (0) ;Path to Use
  OurPathPointer  DW -1                  ;Pointer to our Path String
  CurrentPath:    DB  0                  ;Current Path (Zero-Length ASCIIZ)
  PathVarPointer  DW -1                  ;Pointer to PATH Environment Variable
  SupportFile:    DB 'USBSUPT1.COM',0    ;Support File Name to Look for
  PathVarString1: DB 'PATH='             ;PATH Environment String Header (DOS)
  PathVarString2: DB 'Path='             ;PATH Environment String Header (NT?)
  PathVarSize    EQU $-PathVarString2    ;Length of String

  ;----------------------------------------------------------------------------
  ;Variables needed to do EXEC Call
  ;----------------------------------------------------------------------------
  CmdTailSize DB CmdTailMax-2           ;Length of command tail (Maximize it)
  CmdTail:    DB CmdTailMax DUP (CR)    ;Command Tail for EXEC Call
  ParamBlock: DB ParamBlockSize DUP (?) ;Parameter Block for EXEC Call
  FCB1:       DB FCBSize DUP (?)        ;File Control Block #1
  FCB2:       DB FCBSize DUP (?)        ;File Control Block #2
  OldSPExec   DW ?                      ;SP may be destroyed by EXEC Call

  ;----------------------------------------------------------------------------
  ;Strings needed to write Descriptor Header Information to the screen
  ;----------------------------------------------------------------------------
  HostIndexMsg:  DB 'USB HOST INDEX: ',0
  DvcAddressMsg: DB 'DEVICE ADDRESS: ',0

  ;----------------------------------------------------------------------------
  ;Strings needed to set up Command Tail for Exec Call
  ;----------------------------------------------------------------------------
  ReportDescrMsg:     DB 'ReportDescriptor '
  ReportDescrMsgAddr: DB 'xxxx:xxxx '
  ReportDescrCallBk:  DB 'xxxx:xxxx'
                      DB  0

  DescriptorMsg:     DB 'Descriptor '
  DescriptorMsgAddr: DB 'xxxx:xxxx '
  DescriptorMsgHost: DB 'xxh '
  DescriptorMsgDvc:  DB 'xxh '
  DescriptorCallBk:  DB 'xxxx:xxxx'
                     DB  0

  TDStatusMsg:    DB 'TDStatusCode '
  TDStatusCode:   DB 'xxxxh '
  TDStatusCallBk: DB 'xxxx:xxxx'
                  DB 0

  Int14ErrMsg:    DB 'Int14ErrorCode '
  Int14ErrCode:   DB 'xxxxh '
  Int14ErrCallBk: DB 'xxxx:xxxx'
                  DB 0

  ;----------------------------------------------------------------------------
  ;Error Message for when the Support File is not found
  ;----------------------------------------------------------------------------
  NoSuptFileMsg:
    DB 'Error! Need Support File ',0


;==============================================================================
;Help Screen Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Data needed if the User has changed the name of the program from USBMOUSE
  ;----------------------------------------------------------------------------
  RealName:
    DB 'USBMOUSE',0 ;Real name of our program (MUST end in 0)
                    ;  & MUST have enough space for 8-char name
  RealNameSz DW 8   ;Size of RealName (not incl 0)

  ;----------------------------------------------------------------------------
  ;Syntax Help String (if user asks for, or otherwise needs, Help)
  ;----------------------------------------------------------------------------
  SyntaxMsgHdr:
    DB 'SYNTAX: ',0
  SyntaxMsg:
    DB '[Options]',CR,LF
    DB LF
    DB '  /?      Í Show this HELP screen',CR,LF
    DB '  /A      Í Show all ALIASES for these command line Options',CR,LF
    DB '  /ErrLvl Í Show all ErrorLevels (DOS Return Codes)',CR,LF
    DB '  /U      Í UNINSTALL from memory',CR,LF
    DB '  /L:Y|N  Í Force installation into LOW memory (Default = No)',CR,LF
    DB LF
    DB '  /S   Í Display current STATUS of Program',CR,LF
    DB '  /C   Í Diplay CONFIGURATION (& other) descriptors of last mouse found',CR,LF
    DB '  /R   Í Show HID REPORT descriptor of last mouse found',CR,LF
    DB '  /Raw Í Display RAW Data from all mice',CR,LF
    DB LF
    DB '  /B        Í Show list of BAD (incompatible) mice found',CR,LF
    DB '  /Beep:Y|N Í BEEP Speaker when bad mouse is found (Default = Yes)',CR,LF
    DB LF
    DB '  /D:# Í DISABLE mouse index # (0-7, 8=PS2 Mouse)',CR,LF
    DB '  /E:# Í ENABLE mouse index # (0-7, 8=PS2 Mouse)',CR,LF
    DB '  /X:# Í eXCHANGE L/R Btns on Mouse Index # (0-7, 8=PS2 Mouse, 255=All)',CR,LF
    DB LF
    DB '  /BIOS:Y|N Í Use BIOS emulation (as opposed to hardware) (Default = Yes)',CR,LF
    DB '  /M:#      Í Hardware emulation METHOD (1-3, Default = Auto)',CR,LF
;    DB '  W:#      Í Number of WHEELS in hardware emulation (0-2, Default = 0)',CR,LF
    DB LF
    DB '  /Reset           Í RESET PS2 Mouse Hardware',CR,LF
    DB '  /Resolution:#    Í Change RESOLUTION to # Counts/mm (1,2,4,8)',CR,LF
    DB '  /SamplingRate:#  Í Change SAMPLING RATE to # Hz (10,20,40,60,80,100,200)',CR,LF
    DB '  /ScalingFactor:# Í Change SCALING FACTOR to #:1 (1,2)',CR,LF
    DB '  /I:Y|N           Í Install TSR into Memory (Default = Yes)'
    DB 0


;==============================================================================
;Program Status Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings needed to print the Status to the screen
  ;----------------------------------------------------------------------------
  NoMouseMsg:
    DB 'No USB or PS2 Mice are Currently Installed.',CR,LF,0
  StatusMsg1:
    DB '  COMBINED PS2/USB MOUSE STATUS             INDIVIDUAL MOUSE STATUSES',CR,LF
    DB '  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ     ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB '  Int 33h Driver Enabled:    ',0
  StatusMsg2:
    DB                                 '     Idx Enbl Xchg Btns Wheels Host Addr Intf',CR,LF
    DB '  Wheel(s) Enabled:          ',0
  StatusMsg3:
    DB                                 '     ÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ',CR,LF
    DB '  Resolution (Counts/mm):    ',0
  StatusMsg4:
    DB '  Scaling Factor:            ',0
  StatusMsg5:
    DB '  Samples/Second:            ',0
  StatusMsg6:
    DB '  Exchange All L/R Buttons:  ',0
  Status1EndMsg:
    DB '  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',0
  EmulationMsg:
    DB '  Emulation Level:      ',0
  MethodMsg:
    DB '  HW Emulation Method:       ',0
  Status2EndMsg:
    DB '                                     ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF,0

  YesMsg:         DB 'Yes',0
  NoMsgS:         DB ' No',0
  Scaling1Msg:    DB '1:1',0
  Scaling2Msg:    DB '2:1',0
  PS2Msg:         DB 'PS2',0
  BtnsUnknownMsg: DB '2~3',0
  Maybe2Msg:      DB ' 2?',0
  Maybe3Msg:      DB ' 3?',0
  Maybe5Msg:      DB ' 5?',0
  NAMsg:          DB ' na',0
  BIOSMsg:        DB '    BIOS',0
  HardwareMsg:    DB 'Hardware',0
  NoPS2Msg:       DB ' * No PS2 Mouse Installed *',0

  ;----------------------------------------------------------------------------
  ;Strings needed to handle NoInstall Option
  ;----------------------------------------------------------------------------
  NoResetMsg:
    DB CR,LF
    DB 'USBMOUSE did NOT Reset the PS/2 Mouse, since doing it',CR,LF
    DB '  could cause your DOS Mouse Driver to stop working.',CR,LF
    DB LF,0
  BIOSUpdateMsg:
    DB 'The appropriate requests have been sent to the PS/2 BIOS.'
  NoInstallMsg:
    DB CR,LF
    DB LF
    DB 'USBMOUSE has NOT been installed into memory.',0
  BIOSErrMsg:
    DB 'Could not locate a PS/2 Mouse.',CR,LF
    DB 'No Mouse Status is available.',0
  EnabledMsg:
    DB CR,LF,
    DB '     PS/2 MOUSE STATUS',CR,LF
    DB 'ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB 'INT 33h Driver Enabled: ',0
  WheelsMsg:
    DB CR,LF
    DB 'Wheel(s) Enabled:       ',0
  ResolutionMsg:
    DB CR,LF
    DB 'Resolution (Counts/mm): ',0
  ScalingMsg:
    DB CR,LF
    DB 'Scaling Factor:         ',0
  SamplesMsg:
    DB CR,LF
    DB 'Samples/Second:         ',0


;==============================================================================
;Raw Mouse Data Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings and Data needed to print Raw Mouse Data to the screen
  ;----------------------------------------------------------------------------
  RawDataHdr:
    DB CR,LF
    DB 'Press any Key to Quit',CR,LF
    DB 'ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ',CR,LF
    DB '                                    RAW DATA',CR,LF
    DB '                   ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB 'Mous Host Dvc Itf  Buttons  Byte Byte Byte Byte Byte Byte Byte',CR,LF
    DB 'Indx Indx Adr Num  LRC45678   2    3    4    5    6    7    8 ',CR,LF
    DB 'ÄÄÄÄ ÄÄÄÄ ÄÄÄ ÄÄÄ  ÄÄÄÄÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ ÄÄÄÄ',CR,LF
    DB (MaxDevices+1) DUP (LF)
    DB 0

  RawBtnMsg: DB 'LRC45678'

  VideoPage DB -1 ;Current Video Page


;==============================================================================
;Bad Device Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings needed to print Bad Device Table to the screen
  ;----------------------------------------------------------------------------
  BadDeviceHdr:
    DB '   BAD (INCOMPATIBLE) MICE',CR,LF
    DB 'ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB  LF
    DB 'HOST  DVC   INTF  STAGE  ERROR',CR,LF
    DB 'INDX  ADDR  NUM    NUM   CODE',CR,LF
    DB 'ÄÄÄÄ  ÄÄÄÄ  ÄÄÄÄ  ÄÄÄÄÄ  ÄÄÄÄÄ',CR,LF
    DB  0

  NoneFoundMsg:
    DB '#ÄÄ#  #ÄÄ#  None  #ÄÄÄ# #ÄÄÄ#',CR,LF,0
  BadBeepMsg:
    DB 'Beep for Bad Mice: ',0


;==============================================================================
;ErrorLevel Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;ErrorLevels, and Table for ErrorLevel Description Strings
  ;----------------------------------------------------------------------------
  ErLvlOption   EQU  1 ;Bad Option on the command line
  ErLvlMemory   EQU  2 ;Memory problem with the computer
  ErLvlUninst   EQU  3 ;Not in memory yet, can't perform Request
  ErLvlVersion  EQU  4 ;Another version of the program already installed
  ErLvlDOSVer   EQU  5 ;DOS Version too old
  ErLvlStdIn    EQU  6 ;StdIn file too big
  ErLvlBadCPU   EQU  7 ;Incompatible CPU
  ErLvlNoHost   EQU  8 ;No USB Host Driver installed
  ErLvlHostInit EQU  9 ;Error while trying to initialize host request
  ErLvlNoData   EQU 10 ;No Report/Descriptor Data to Print
  ErLvlSuptFile EQU 11 ;Could not Find Support File
  ErLvlNoDevice EQU 12 ;No Devices installed, can't show Raw Data

  ErrLvlHdr:
    DB  CR,LF
    DB '                      ERRORLEVELS (DOS RETURN CODES)',CR,LF
    DB '  ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB '   0 No Error.',CR,LF
    DB 0

  ErrLvlTbl:
    DB ErLvlOption
       DB "Bad Command-line Option.",0
    DB ErLvlMemory
       DB "Memory problem with the computer.",0
    DB ErLvlUninst
       DB "USBMOUSE is not installed in memory yet -- can't perform Request.",0
    DB ErLvlVersion
       DB "A different version of USBMOUSE is already installed in memory.",0
    DB ErLvlDOSVer
       DB "DOS must be at least version 3.00.",0
    DB ErLvlStdIn
       DB "The Redirected Input Option File (USBMOUSE < InFile) is too big.",0
    DB ErLvlBadCPU
       DB "This program requires at least an 80286 CPU.",0
    DB ErLvlNoHost
       DB "Could not find a compatible USB Host Driver in memory.",0
    DB ErLvlHostInit
       DB "Could not properly initialize the USB Host Driver.",0
    DB ErLvlNoData
       DB "There is no Report or Configuration Descriptor data to write.",0
    DB ErLvlSuptFile
       DB "Could not find the required USBSUPT1.COM support file.",0
    DB ErLvlNoDevice
       DB "No Mouse is available to show the Raw Data for.",0
    DB -1,-1 ;End of Table


;==============================================================================
;Alias Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Strings needed to print Command Line Aliases to the screen
  ;----------------------------------------------------------------------------
  AliasHdrMsg:
    DB '                   ALIASES FOR COMMAND-LINE OPTIONS',CR,LF
    DB 'ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB 0

  YesNoHdrMsg:
    DB '       ALIASES FOR YES & NO',CR,LF
    DB 'ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB 0

  MouseIdxHdrMsg:
    DB ' ALIASES FOR MICE INDEXES',CR,LF
    DB 'ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ',CR,LF
    DB 0

  ;----------------------------------------------------------------------------
  ;Table needed to temporarily store Aliases.
  ;They are originally sorted in reverse alphabetical order.
  ;To write them in forward alphabetical order, we temporarily store
  ;  the String Pointers, in the original reverse order, in this table.
  ;  We then write the strings, starting with the last entry in this table
  ;  and moving backwards.
  ;The number of entries in this table must be AT LEAST as big as the largest
  ;  number of Aliases for a single command Option.  Most Options don't have
  ;  more than a dozen or so Aliases.
  ;----------------------------------------------------------------------------
  AliasSortTbl: DW 100 DUP (?) ;Table to store Alias Strings (to reverse order)


;==============================================================================
;Command-line (Parsing) Related
;==============================================================================

  ;----------------------------------------------------------------------------
  ;Data needed to perform command-line configuration changes to the Driver
  ;----------------------------------------------------------------------------
  ResolutionValue DB -1 ;User wants to Change Resolution to this Value
  SamplingValue   DB -1 ;User wants to Change Sampling Rate to this Value
  ScalingValue    DB -1 ;User wants to Change Scaling to this Value

  ;----------------------------------------------------------------------------
  ;Extra Memory Segment (used to store compressed contents of STDIN)
  ;----------------------------------------------------------------------------
  ExtraMemSegment DW ?

  ;----------------------------------------------------------------------------
  ;Data needed to Parse the command line for Options
  ;----------------------------------------------------------------------------
  ParseTest  DB Yes ;Testing the command for errors?
  ParseError DB  No ;Error while Parsing Command Line?

  ;----------------------------------------------------------------------------
  ;Various Parsing-related messages we may need to print to the screen
  ;----------------------------------------------------------------------------
  ParseCmdMsg:
    DB 'Error in the command-line Options for USBMOUSE:',CR,LF,0
  ParseEnvMsg:
    DB 'Error in the Environment variable "USBMOUSE":',CR,LF,0
  ParseStdInMsg:
    DB 'Error in the redirected input file ("USBMOUSE < {FileName}"):',0
  ParseErrMsg:
    DB '^ Error',CR,LF,LF,0
  ParseEnvMsg2:
    DB 'Reset the Environment variable (type "SET USBMOUSE=") and then',CR,LF
  ParseErrMsg2:
    DB 'Type "USBMOUSE ?" for Help.',CR,LF,0
  StdInTooBigMsg:
    DB 'The redirected input file ("USBMOUSE < {FileName}") is too big!',CR,LF,0

  Copyright:
    DB "USBMOUSE 0.10, (C) 2007-2009, Bret E. Johnson.",CR,LF
    DB "DOS Driver for up to 8 Generic USB Mice.",CR,LF
    DB  LF,0
  EnvMsg:
    DB 'Environment: ',0
  InstallMsg1:
    DB 'USBMOUSE has been ',0
  InstallMsgAuto:
    DB 'automatically ',0
  InstallMsgHigh:
    DB 'installed into Upper Memory.',0
  InstallMsgHole:
    DB 'installed into a Low Memory "Hole".',0
  InstallMsgNorm:
    DB 'installed into Low Memory.',0
  InstallMsgLast:
    DB CR,LF
    DB 'Type "USBMOUSE U" to Uninstall USBMOUSE from memory.',CR,LF,0
  BadCPUMsg:
    DB 'This program requires at least an 80286 CPU.',0
  DOSVerMsg:
    DB 'You must be using DOS version 3.00 or later to use this program.',0
  NoHostMsg:
    DB 'There must be a compatible USB Host Driver installed in memory',CR,LF
    DB '  (something like USBUHCI.COM) before this program can be installed.',0
  HostInitErrMsg:
    DB 'There was an error while trying to initialize the USB Host.',0
  NoEnvironMsg:
    DB 'There is no DOS Environment.',CR,LF
    DB 'There is something WEIRD going on here!',0
  ExtraMemoryMsg:
    DB 'There is not enough available memory to run this program!',0
  WrongVerMsg:
    DB 'Another version of USBMOUSE was found in memory.',CR,LF
    DB 'Please UNINSTALL the other version before continuing.',0
  ResetPS2Msg:
    DB 'Locating and '
  ResetPS2Msg2:
    DB 'Resetting the PS/2 Mouse (this may take several seconds) ...',CR,LF,0
  NoDescrDataMsg:
    DB 'There is no Descriptor Data to Write!',0
  UninstallMsg:
    DB 'USBMOUSE has been Uninstalled from memory.',CR,LF,0
  NoUninstallMsg:
    DB 'Unable to Uninstall USBMOUSE from memory, but it has been Disabled.',CR,LF
    DB "Remove any TSR's installed after USBMOUSE and try again.",CR,LF
    DB LF
    DB 'To Re-Enable USBMOUSE, simply type "USBMOUSE" at the command-line.',0
  ReEnabledMsg:
    DB 'USBMOUSE has been Re-Enabled.',CR,LF
    DB LF,0
  UninstallErrMsg:
    DB "Can't find USBMOUSE in memory.",CR,LF
    DB 'You must install it before it can be Uninstalled.',0
  NoDeviceMsg:
    DB 'No USB Mice are Currently Installed.',0
  UpdateMsg:
    DB 'Resident USBMOUSE has been updated with new information.',0

  BadDvcErrMsg:
    DB 'Bad Device',0
  RawDataErrMsg:
    DB 'Raw Mouse',0
  ReportErrMsg:
    DB 'Report '
  DescrErrMsg:
    DB 'Descriptor',0
  StatusErrMsg:
    DB 'Program Status',0
  DataErrMsg:
    DB ' Data is not available until after',CR,LF
    DB '  USBMOUSE has been installed into memory.',0


  ;----------------------------------------------------------------------------
  ;Strings needed to write the Command-line error message
  ;----------------------------------------------------------------------------
  MoreLeftMsg:   DB '<<-More}  ',0
  MoreRightMsg:  DB '  {More->>',0
  MoreMsgSize   EQU $-MoreRightMsg

  ;----------------------------------------------------------------------------
  ;Command Line Options with no single-character equivalents
  ;These must be in the same numerical order that we want the
  ;  Alias Option to Sort them in!
  ;----------------------------------------------------------------------------
  OptionBeep       EQU 1 ;Beep the speaker
  OptionBIOS       EQU 2 ;Emulate in BIOS (as opposed to Hardware)
  OptionResolution EQU 3 ;Change Resolution
  OptionRaw        EQU 4 ;Show Raw Data
  OptionErrLvl     EQU 5 ;ErrorLevel Table
  OptionSampling   EQU 6 ;Change Sampling Rate
  OptionReset      EQU 7 ;Reset Drivers & PS2 Hardware
  OptionScaling    EQU 8 ;Change Scaling Factor

  ;----------------------------------------------------------------------------
  ;Command Line Options
  ;----------------------------------------------------------------------------
  SwitchList:
    DB 'Help',0
    DB  0, '?'
      ; ^              Beginning-of-List Marker
      ;    ^   ^        Code(s) to use

    DB 'XchgLtRtButtons',0
    DB 'XchgLtRtBtns',0
    DB 'XchgLtRt',0
    DB 'XchgLRButtons',0
    DB 'XchgLRBtns',0
    DB 'XchgLR',0
    DB 'XchgLeftRightButtons',0
    DB 'XchgLeftRightBtns',0
    DB 'XchgLeftRight',0
    DB 'XchgButtons',0
    DB 'XchgBtns',0
    DB 'Xchg',0
    DB  0, 'X'

    DB 'Wheels',0
    DB 'Wheel',0
    DB  0, 'W'

    DB 'UseLowMemory',0
    DB 'UseLowMem',0
    DB 'UseLowerMemory',0
    DB 'UseLowerMem',0
    DB 'UseLow',0
    DB  0, 'L'

    DB 'UseBIOSEmulation',0
    DB 'UseBIOS',0
    DB  0, OptionBIOS

    DB 'USBRawMouseData',0
    DB 'USBRawData',0
    DB  0, OptionRaw

    DB 'Uninstall',0
    DB  0, 'U'

    DB 'SwapLtRtButtons',0
    DB 'SwapLtRtBtns',0
    DB 'SwapLtRt',0
    DB 'SwapLRButtons',0
    DB 'SwapLRBtns',0
    DB 'SwapLR',0
    DB 'SwapLeftRightButtons',0
    DB 'SwapLeftRightBtns',0
    DB 'SwapLeftRight',0
    DB 'SwapButtons',0
    DB 'SwapBtns',0
    DB 'Swap',0
    DB  0, 'X'

    DB 'StatusTbl',0
    DB 'StatusTable',0
    DB  0, 'S'

    DB 'StatusofMouse',0
    DB 'StatusofMice',0
    DB  0, 'S'

    DB 'Status',0
    DB  0, 'S'

    DB 'Sounds',0
    DB 'Sound',0
    DB  0, OptionBeep

    DB 'ShowStatusTbl',0
    DB 'ShowStatusTable',0
    DB 'ShowStatus',0
    DB  0, 'S'

    DB 'ScalingFactor',0
    DB 'Scaling',0
    DB 'ScaleFactor',0
    DB 'Scale',0
    DB  0, OptionScaling,

    DB 'SamplingRateHz',0
    DB 'SamplingRateHertz',0
    DB 'SamplingRate',0
    DB 'SamplingHz',0
    DB 'SamplingHertz',0
    DB 'Sampling',0
    DB 'SamplesPerSecond',0
    DB 'SamplesPerSec',0
    DB 'SamplesPerS',0
    DB 'Samples/Second',0
    DB 'Samples/Sec',0
    DB 'Samples/S',0
    DB 'Samples',0
    DB 'SampleRateHz',0
    DB 'SampleRateHertz',0
    DB 'SampleRate',0
    DB 'SampleHz',0
    DB 'SampleHertz',0
    DB 'Sample',0
    DB  0, OptionSampling

    DB 'RptDescriptor',0
    DB 'RptDescr',0
    DB 'Rpt',0
    DB  0, 'R'

    DB 'Resolution',0
    DB  0, OptionResolution

    DB 'ResetPS2Hardware',0
    DB 'ResetPS2',0
    DB 'ResetHardware',0
    DB 'Reset',0
    DB  0, OptionReset

    DB 'ReportDescriptor',0
    DB 'ReportDescr',0
    DB 'Report',0
    DB  0, 'R'

    DB 'Remove',0
    DB  0, 'U'

    DB 'RawUSBMouseData',0
    DB 'RawUSBData',0
    DB  0, OptionRaw

    DB 'RawMouseData',0
    DB 'RawData',0
    DB 'Raw',0
    DB  0, OptionRaw

    DB 'PollingRateHz',0
    DB 'PollingRateHertz',0
    DB 'PollingRate',0
    DB 'PollingHz',0
    DB 'PollingHertz',0
    DB 'Polling',0
    DB 'PollRateHz',0
    DB 'PollRateHertz',0
    DB 'PollRate',0
    DB 'PollHz',0
    DB 'PollHertz',0
    DB 'Poll',0
    DB  0, OptionSampling

    DB 'Out',0
    DB  0, 'U'

    DB 'OptionAliases',0
    DB 'OptionAlias',0
    DB  0, 'A'

    DB 'On',0
    DB  0, 'E'

    DB 'Off',0
    DB  0, 'D'

    DB 'NumWheels',0
    DB 'NumWheel',0
    DB 'NumberWheels',0
    DB 'NumberWheel',0
    DB 'NumberOfWheels',0
    DB 'NumberOfWheel',0
    DB  0, 'W'

    DB 'MouseStatusTbl',0
    DB 'MouseStatusTable',0
    DB 'MouseStatus',0
    DB  0, 'S'

    DB 'MouseData',0
    DB  0, OptionRaw

    DB 'MiceStatusTbl',0
    DB 'MiceStatusTable',0
    DB 'MiceStatus',0
    DB  0, 'S'

    DB 'Mthd',0
    DB 'Method',0
    DB  0, 'M'

    DB 'LowMemory',0
    DB 'LowMem',0
    DB 'LowerMemory',0
    DB 'LowerMem',0
    DB 'Low',0
    DB  0, 'L'

    DB 'IntfDescriptor',0
    DB 'IntfDescr',0
    DB 'Intf',0
    DB  0, 'C'

    DB 'InterfaceDescriptor',0
    DB 'InterfaceDescr',0
    DB 'Interface',0
    DB  0, 'C'

    DB 'InstallTSRIntoMemory',0
    DB 'InstallTSR',0
    DB 'InstallProgramIntoMemory',0
    DB 'InstallProgram',0
    DB 'InstallIntoMemory',0
    DB 'Install',0
    DB  0, 'I'

    DB 'IncompatibleMiceList',0
    DB 'IncompatibleMouseList',0
    DB 'IncompatibleMice',0
    DB 'IncompatibleMouse',0
    DB 'Incompatible',0
    DB  0, 'B'

    DB 'Hz',0
    DB  0, OptionSampling

    DB 'Hlp',0
    DB  0, '?'

    DB 'HIDRptDescriptor',0
    DB 'HIDRptDescr',0
    DB 'HIDRpt',0
    DB  0, 'R'

    DB 'HIDReportDescriptor',0
    DB 'HIDReportDescr',0
    DB 'HIDReport',0
    DB  0, 'R'

    DB 'Hertz',0
    DB  0, OptionSampling

    DB 'H',0
    DB  0, '?'

    DB 'ForceLowMemory',0
    DB 'ForceLowMem',0
    DB 'ForceLowerMemory',0
    DB 'ForceLowerMem',0
    DB 'ForceLow',0
    DB  0, 'L'

    DB 'ExchangeLtRtButtons',0
    DB 'ExchangeLtRtBtns',0
    DB 'ExchangeLtRt',0
    DB 'ExchangeLRButtons',0
    DB 'ExchangeLRBtns',0
    DB 'ExchangeLR',0
    DB 'ExchangeLeftRightButtons',0
    DB 'ExchangeLeftRightBtns',0
    DB 'ExchangeLeftRight',0
    DB 'ExchangeButtons',0
    DB 'ExchangeBtns',0
    DB 'Exchange',0
    DB  0, 'X'

    DB 'ErrorLvlTbl',0
    DB 'ErrorLvlTable',0
    DB 'ErrorLvls',0
    DB 'ErrorLvl',0
    DB 'ErrorLevelTbl',0
    DB 'ErrorLevelTable',0
    DB 'ErrorLevels',0
    DB 'ErrorLevel',0
    DB  0, OptionErrLvl

    DB 'ErrLvlTbl',0
    DB 'ErrLvlTable',0
    DB 'ErrLvls',0
    DB 'ErrLvl',0
    DB 'ErrLevelTbl',0
    DB 'ErrLevelTable',0
    DB 'ErrLevels',0
    DB 'ErrLevel',0
    DB  0, OptionErrLvl

    DB 'ErLvlTbl',0
    DB 'ErLvlTable',0
    DB 'ErLvls',0
    DB 'ErLvl',0
    DB 'ErLevelTbl',0
    DB 'ErLevelTable',0
    DB 'ErLevels',0
    DB 'ErLevel',0
    DB  0, OptionErrLvl

    DB 'EndPointDescriptor',0
    DB 'EndPointDescr',0
    DB 'EndPoint',0
    DB  0, 'C'

    DB 'EndPtDescriptor',0
    DB 'EndPtDescr',0
    DB 'EndPt',0
    DB  0, 'C'

    DB 'Enable',0
    DB  0, 'E'

    DB 'Disable',0
    DB  0, 'D'

    DB 'DvcDescriptor',0
    DB 'DvcDescr',0
    DB 'Dvc',0
    DB  0, 'C'

    DB 'DeviceDescriptor',0
    DB 'DeviceDescr',0
    DB 'Device',0
    DB  0, 'C'

    DB 'Descriptors',0
    DB 'Descriptor',0
    DB 'Descrs',0
    DB 'Descr',0
    DB  0, 'C'

    DB 'Data',0
    DB  0, OptionRaw

    DB 'CountsPerMillimeter',0
    DB 'CountsPerMM',0
    DB 'Counts/Millimeter',0
    DB 'Counts/MM',0
    DB 'Counts',0
    DB  0, OptionResolution

    DB 'ConfigurationDescriptor',0
    DB 'ConfigurationDescr',0
    DB 'Configuration',0
    DB  0, 'C'

    DB 'ConfigDescriptor',0
    DB 'ConfigDescr',0
    DB 'Config',0
    DB  0, 'C'

    DB 'CommandLineAliases',0
    DB 'CommandLineAlias',0
    DB  0, 'A'

    DB 'CmdLineAliases',0
    DB 'CmdLineAlias',0
    DB  0, 'A'

    DB 'BIOSEmulation',0
    DB 'BIOS',0
    DB  0, OptionBIOS

    DB 'Beeps',0
    DB 'Beep',0
    DB  0, OptionBeep

    DB 'BadMiceList',0
    DB 'BadMouseList',0
    DB 'BadMice',0
    DB 'BadMouse',0
    DB 'Bad',0
    DB  0, 'B'

    DB 'Aliases',0
    DB 'Alias',0
    DB  0, 'A'

    DB 0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Enable/Disable/Exchange Switches
  ;----------------------------------------------------------------------------
  SwitchList2:
    DB 'PS2Mouse',0
    DB 'PSMouse',0
    DB 'PS2',0
    DB 'PS',0
    DB  0, MaxDevices

    DB 'Global',0
    DB  0, -1

    DB 'AllMice',0
    DB 'All',0
    DB  0, -1

    DB  0            ;End of list


  ;----------------------------------------------------------------------------
  ;Switches for Yes/No Options
  ;----------------------------------------------------------------------------
  SwitchListYesNo:
    DB 'Yes',0
    DB  0, 'Y'

    DB 'True',0
    DB 'T',0
    DB  0, 'Y'

    DB 'On',0
    DB  0, 'Y'

    DB 'Off',0
    DB  0, 'N'

    DB 'No',0
    DB  0, 'N'

    DB 'False',0
    DB 'F',0
    DB  0, 'N'

    DB 'Enabled',0
    DB 'Enable',0
    DB  0, 'Y'

    DB 'Disabled',0
    DB 'Disable',0
    DB  0, 'N'

    DB '1',0
    DB  0, 'Y'

    DB '0',0
    DB  0, 'N'

    DB  0 ;End of list


  ;----------------------------------------------------------------------------
  ;TABLE OF SUBROUTINE OFFSETS TO PROCESS THE VARIOUS COMMAND LINE OPTIONS
  ;----------------------------------------------------------------------------
  SwitchTbl:
    DB  '?'
       DW DoSwitchH          ;Help
    DB  'A'
       DW DoSwitchA          ;Command Line Aliases
    DB  'B'
       DW DoSwitchB          ;Bad Device Table
    DB  'C'
       DW DoSwitchC          ;Device/Config/Interface/HID/EndPoint Descriptors
    DB  'D'
       DW DoSwitchD          ;Disable
    DB  'E'
       DW DoSwitchE          ;Enable
    DB  'I'
       DW DoSwitchI          ;Install
    DB  'L'
       DW DoSwitchL          ;Low Memory
    DB  'M'
       DW DoSwitchM          ;Method
    DB  'R'
       DW DoSwitchR          ;Report Descriptor
    DB  'S'
       DW DoSwitchS          ;Status
    DB  'U'
       DW DoSwitchU          ;Uninstall
;    DB  'W'
;       DW DoSwitchW          ;Number of Wheels
    DB  'X'
       DW DoSwitchX          ;eXchange L/R Buttons

    DB  OptionBeep
       DW DoSwitchBeep       ;Beep the Speaker
    DB  OptionBIOS
       DW DoSwitchBIOS       ;Use BIOS/Hardware Emulation
    DB OptionResolution
       DW DoSwitchResolution ;Change Resolution
    DB  OptionErrLvl
       DW DoSwitchErrLvl     ;ErrorLevel Table
    DB OptionSampling
       DW DoSwitchSampling   ;Change Sampling Rate
    DB OptionRaw
       DW DoSwitchRaw        ;Show Raw Keyboard Data
    DB OptionReset
       DW DoSwitchReset      ;Reset Drivers & PS2 Hardware
    DB OptionScaling
       DW DoSwitchScaling    ;Change Scaling Factor

    DB  0                    ;End of Table


;ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
;PROGRAM CODE (NOT NEEDED IN TSR)
;Needed to initialize/update/show status of TSR, but not actually needed
;  in memory while TSR is running
;ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ

;------------------------------------------------------------------------------
;INITIALIZE/UPDATE THE PROGRAM
;Inputs:
;Outputs: Lots of Stuff
;Changes: Everything
;------------------------------------------------------------------------------
Main:
  CLD                         ;Go forward with string functions
  MOV  SP,LastProgByte        ;Reset Stack Pointer
  CALL ResizeMemory           ;Resize our Memory Allocation
  CALL CopyNameToMCB          ;Copy our Program name to the MCB
  CALL SetupPauseNoHdr        ;Set up the Pause
  MOV  DX,Copyright           ;Write the
  CALL WriteZPause            ;  Copyright message
  CALL SetupExtraMem          ;Set up the Extra Memory Segment (Quits if Err)
  CALL TestMemory             ;See if we're already installed in memory
                              ;QUIT if another version is installed
  CALL GetRealName            ;Get the Real Program Name
  CALL SetupExec              ;Setup Exec Parameters
  CALL GetOurPath             ;Get the Pointer to Our Path
  CALL GetEnvVars             ;Get the Environment Variable Pointers
  CALL ParseIt                ;Parse the command line
  CALL ReleaseExtraMem        ;Release the Extra Memory Segment
  CALL ReEnable               ;Re-Enable the Program, if required
  CALL DoHelp                 ;Print Help and Quit, if necessary
  CALL DoAliases              ;Print Aliases and Quit, if necessary
  CALL DoErrLvls              ;Print ErrorLevel Table and Quit, if necessary
  CALL HandleNoInstall        ;Handle a No-Install Request by the user
  JC  >M45                    ;If we did it, quit
  CALL DoUninstall            ;Uninstall and quit, if necessary
  CALL DoBadDevices           ;Print Bad Devices and Quit, if necessary
  CALL DoRawData              ;Print Raw Data and Quit, if necessary
  CALL DoDescriptors          ;Print USB Descriptors, Quit if error
  CALL DoReport               ;Print Report Data, Quit if Error
  CALL DoStatus               ;Print Status, if necessary
  CALL TestCompatibility      ;Test for compatibility, Quit if not
  TEST ProgFlags,InMemory     ;Are we already Installed in memory?
  JZ  >M50                    ;If not, Install as a TSR
M40:                          ;Already Installed/Don't Install
  CALL ChangeRates            ;Change Configs as user requested
  MOV  DX,UpdateMsg           ;If so, point at Update message
M45:                          ;DX contains Message
  XOR  AL,AL                  ;Errorlevel = 0
  JMP  Exit                   ;QUIT

M50:                          ;First-time installation (install as TSR)
  CALL TestMethod             ;Test which Hardware Emulation Method to use
  CALL DelEnvironment         ;Delete environment space (don't need it now)
  CALL MoveProgramToUMB       ;Move Program to Upper Memory, if possible
   ;All Subroutines after this MUST assume ES is TSR Segment, NOT CS!!
  MOV  DS,ES                  ;Point DS at TSR Data Area
M60:                          ;Ready to install TSR
;  CALL GetPS2Info             ;Get the PS2 Mouse Info (may reset the Mouse)
  CALL ResetPS2Mouse          ;Get the PS2 Mouse Info (Resets the Mouse)
  CALL InitializeInt01        ;Initialize Int 01 Vector
  CALL InitTables             ;Initialize all Tables
  CALL ChangeInts             ;Change the interrupts to point at us
  CALL InitInts               ;Initialize periodic "Search for New Devices"
                              ;  (quits if there's a problem)
  MOV  DS,CS                  ;Point DS at Local Data Area
  CALL ChangeRates            ;Change Configs as user requested

  XOR  AL,AL                  ;ErrorLevel = 0
  MOV  BX,DS                  ;Point BX at the Local Segment
  MOV  CX,ES                  ;Point CX at the TSR Segment
  MOV  DX,InstallMsg1         ;Write the first part of the
  CALL WriteZPause            ;  Install Message
  TEST ProgFlags,MemoryMoved  ;Did we move the TSR Memory?
  JZ  >M90                    ;If not, use TSR Exit
  MOV  DX,InstallMsgAuto      ;Write
  CALL WriteZPause            ;  "automatically"
  MOV  DX,InstallMsgHole      ;Assume we installed in a Low Memory Hole
  CMP  BX,CX                  ;Did we install in a UMB or a Low Memory Hole?
  JA  >M80                    ;If a Low Memory Hole, leave the message as is
  MOV  DX,InstallMsgHigh      ;If a UMB, point at Installed High message
M80:                          ;[DX] = appropriate Memory Message
  CALL WriteZPause            ;Write the Memory Message
  MOV  DX,InstallMsgLast      ;Point at the last part of the Installed Message
  JMP  Exit                   ;Done

M90:                          ;Exit Program as a TSR
  MOV  DX,InstallMsgNorm      ;Assume we're in Low Memory
  CMP  CX,0A000h              ;Are we in Low Memory?
  JB  >M95                    ;If so, continue
  MOV  DX,InstallMsgHigh      ;If not, point at the High Memory message
M95:                          ;[DX] = appropriate Memory Message to write
  CALL WriteZPause            ;Write the Memory Message
  MOV  DX,InstallMsgLast      ;Write the last part of the
  CALL WriteZPause            ;  Install Message
  MOV  DX,(LastTSRByte SHR 4) ;DX = number of TSR paragraphs
  MOV  AH,31h                 ;Function 31h (TSR), ErrorLevel in AL (0)
  INT  21h                    ;Do it

;------------------------------------------------------------------------------
;MAKE SURE THE MCB (MEMORY CONTROL BLOCK) CONTAINS OUR PROGRAM NAME
;Inputs:  DS = Local Data Area
;         DS:RealName = ASCIIZ Real Name of our program
;         CLD Already Issued
;Outputs:
;Changes: Data in MCB
;NOTES:   This makes sure that the MCB (Memory Control Block) for our
;           program contains our program name.  DOS 4+ does this
;           automatically, but DOS versions 1-3 do not.  This code
;           does not verify whihc DOS version is in use, but simply
;           fills in the MCB no matter what.
;         Our program name is needed inthe MCB to make sure other programs
;           can identify that we are installed in memory.  This code should
;           be added to all TSR's, so that memory-mapping programs
;           (like MAPMEM or ISLOADED) can tell which TSR's are installed
;           in memory.  It should also be added to all programs that
;           EXEC other programs, so that the EXEC'd program can figure out
;           who called it.
;         Programs that are not TSR's and do not EXEC other programs
;           do not need this code.
;------------------------------------------------------------------------------
CopyNameToMCB:
  PUSH CX,DI,SI,ES              ;Save used registers
  MOV  CX,DS                    ;Point ES:[DI]
  DEC  CX                       ;  at the Owners Name
  MOV  ES,CX                    ;  in the MCB
  MOV  DI,(OFFSET MCBOwnerName) ;  of our program
  MOV  SI,RealName              ;Point DS:[SI] at our Program Name
  MOV  CX,4                     ;Copy 4 words (8 bytes)
  REP  MOVSW                    ;Copy our program name to the MCB
  POP  ES,SI,DI,CX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SETUP/RELEASE AN EXTRA 64K MEMORY SEGMENT FOR OUR PROGRAM
;Inputs:  DS = Local Data Area
;Outputs: ExtraMemSegment = Segment of New Segment
;         Quits Program with Error Message if Memory Problem
;Changes:
;------------------------------------------------------------------------------
SetupExtraMem:
  PUSH AX,BX                 ;Save used registers
  MOV  BX,4096               ;Number of Paragraphs in 64k Program
  MOV  AH,4Ah                ;Service 4Ah (Modify memory allocation)
  INT  21h                   ;Do it (uses ES = Segment, BX = # Paragraphs)
  JC  >X70                   ;If Error, Quit
  MOV  AH,48h                ;Service 48h (Allocate Memory Block)
  INT  21h                   ;Do it (uses BX = # paragraphs, rtns AX = Sgmt)
  JC  >X70                   ;If Error, Quit
  MOV  CS:ExtraMemSegment,AX ;If OK, store the segment
  JMP >X90                   ;Done
X70:                         ;Error
  MOV  DX,ExtraMemoryMsg     ;Point at Error Message
  MOV  AL,ErLvlMemory        ;ErrorLevel = Memory Problem
  JMP  Exit                  ;Quit
X90:                         ;Done
  POP  BX,AX                 ;Restore used registers
  RET

ReleaseExtraMem:
  PUSH AX,BX,ES ;Save used registers
  MOV  BX,OFFSET ExtraMemSegment ;Point [BX] at the Extra Memory Segment
  CMP  W [BX],0 ;Did we already Release it?
  JE  >X90      ;If so, quit
  MOV  ES,[BX]  ;If not, ES = Segment to Remove
  MOV  AH,49h   ;Service 49h (Free Memory)
  INT  21h      ;Do it
  MOV  W [BX],0 ;Mark it as released
X90:            ;Done
  POP  ES,BX,AX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RE-ENABLE THE PROGRAM IF IT WAS DISABLED DUE TO A FAILED UNINSTALL
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         DS:ProgFlags.InMemory
;         DS:ProgFlags.Uninstall
;         ES:Int08Counter (= 0 if TSR is Disabled)
;Outputs: If Installed as TSR, currently Disabled, and not Uninstalling:
;           ES:Int08Counter = 5
;         If any other status,
;           does nothing
;Changes:
;------------------------------------------------------------------------------
ReEnable:
  PUSH DX                  ;Save used registers
  TEST ProgFlags,InMemory  ;Already installed as TSR?
  JZ  >B90                 ;If not, quit
  TEST ProgFlags,Uninstall ;User wants us to Uninstall?
  JNZ >B90                 ;If so, quit
  CMP  ES:Int08Counter,0   ;Disabled?
  JNE >B90                 ;If not, quit
  MOV  ES:Int08Counter,5   ;Re-enable
  MOV  DX,ReEnabledMsg     ;Notify the user
  CALL WriteZPause         ;  about what we did
B90:                       ;Done
  POP  DX                  ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code needed because we are a TSR.  Needed to find existing Program
;  in memory, Uninstall from memory, etc.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;CHECK MEMORY FOR COPY OF CODE TO SEE IF PROGRAM IS ALREADY INSTALLED
;Inputs:  ProgFlags.InMemory = No
;Outputs: ES = TSR program segment, whether a TSR yet or not
;         InMemory  = Yes if program is already installed as TSR
;                   = No if not
;         HandleNum = Multiplex interrupt handle number for Interrupt 2F
;Changes:
;------------------------------------------------------------------------------
TestMemory:
  PUSHA                   ;Save all registers that might change
  PUSH DS                 ;  (includes everything except SS & SP)
  MOV  AH,0C0h            ;Function C0, (first available user handle)
M10:                      ;Loop to here for each handle number
  CLD                     ;Go forward with string functions
  XOR  BX,BX              ;Make sure
  XOR  CX,CX              ;  all registers
  XOR  DX,DX              ;  are zero
  MOV  HandleNum,AH       ;Store the handle number
  MOV  AL,MuxInstallChk   ;Install Check Function
  INT  2Fh                ;Do it
  CMP  AL,0FFh            ;Is this function installed?
  JNE >M40                ;If not, our code can't be installed yet
M12:                      ;Test Program Name
  MOV  AH,HandleNum       ;Get the handle number back again
  MOV  AL,MuxGetProgName  ;Get Program Name
  XOR  DI,DI              ;  Function
  INT  2Fh                ;Do it
  MOV  SI,ProgName        ;Point at our name
  MOV  CX,ProgNameLen     ;Get the string length
  REPE CMPSB              ;Compare the two strings
  JNE >M50                ;If not the same, it's not us
M14:                      ;Test Author
  MOV  AH,HandleNum       ;Get the handle number back again
  MOV  AL,MuxGetAuthor    ;Get Author Function
  INT  2Fh                ;Do it (returns ES:[DI])
  MOV  SI,Author          ;Point at our name
  MOV  CX,AuthorLen       ;Get the string length
  REPE CMPSB              ;Compare the two strings
  JNE >M50                ;If no match, it's somebody elses TSR, not ours
M16:                      ;Test Program Version
  MOV  AH,HandleNum       ;Get the handle number back again
  MOV  AL,MuxGetProgVer   ;Get Program Version Function
  INT  2Fh                ;Do it
  CMP  AX,ProgVer         ;Is it the same version as us?
  JE  >M80                ;If so, we're already installed in memory
M30:                      ;A different version is installed in memory
  MOV  AL,ErLvlVersion    ;If not, ErrorLevel = Wrong version
  MOV  DX,WrongVerMsg     ;Point to error message
  JMP  Exit               ;Quit
M40:                      ;No function installed at this handle at all
  CMP  FirstHandle,0      ;Have we already located an available handle?
  JNE >M50                ;If so, just keep searching
  MOV  AH,HandleNum       ;If not, store this one
  MOV  FirstHandle,AH     ;  as the first available
M50:                      ;Increment the Handle number and try again
  MOV  DS,CS              ;Make sure another
  MOV  ES,CS              ;  Int 2F installation check routine
  MOV  AH,HandleNum       ;  hasn't changed things
  ADD  AH,1               ;Look at the next handle number
  JNC  M10                ;If still less than FFh, keep looking
  MOV  AH,FirstHandle     ;If we've searched them all, we're not in memory
  MOV  HandleNum,AH       ;  so go ahead and store our handle number
  JMP >M90                ;And we're done
M80:                      ;We've already been installed (ES contains segment)
  OR   ProgFlags,InMemory ;Mark as already installed
M90:                      ;We're done
  POP  DS                 ;Restore
  POPA                    ;  all registers
  RET

;------------------------------------------------------------------------------
;PRINT MESSAGE IN DX AND QUIT WITH ERRORLEVEL IN AL
;Inputs:  AL = Errorlevel to exit program with
;         CS:[DX] = Message to Print (to ERR)
;Outputs:
;Changes: Quits program
;------------------------------------------------------------------------------
Exit:
  MOV  DS,CS           ;Point DS at Segment where Error Message is Located
  CALL ReleaseExtraMem ;Release the Extra memory Segment
  OR   AL,AL           ;Was there an Error?
  JZ  >X80             ;If not, jump to handle a normal termination
  CALL BeepErr         ;If error, Beep Once
  CALL WriteZErr       ;Write message to ERR
  CALL WriteCrLfErr    ;Move down
  JMP >X90             ;Jump to quit
X80:                   ;No Error - Write to CON
  CALL WriteZPause     ;Write the message to CON
  CALL WriteCrLfPause  ;Write a New Line
X90:                   ;We're done!!
  MOV  AH,4Ch          ;Service 4Ch (terminate program)
  INT  21h             ;Do it

;------------------------------------------------------------------------------
;FINISH UP A "CAN'T PERFORM REQUEST BECAUSE WE'RE NOT INSTALLED YET" ERROR
;Inputs:  DS:[DX] = First message to Print
;Outputs: Writes Message pointed at by DS:[DX]
;         Sets Exit ErrorLevel (ErLvlUninst)
;         Writes Second Half of Error Message
;Changes: Terminates Program
;------------------------------------------------------------------------------
FinishDataErr:
  CALL WriteZErr      ;Write first part of Error Message
  MOV  AL,ErLvluninst ;ErrorLevel = not installed yet
  MOV  DX,DataErrMsg  ;Point at second part of Error Message
  JUMP Exit           ;Terminate program

;------------------------------------------------------------------------------
;UNINSTALL RESIDENT PROGRAM
;Inputs:  ES = TSR Code Segment
;Outputs:
;Changes: Attempts to Uninstall Program from memory
;         If it can't be Uninstalled, it is Disabled
;------------------------------------------------------------------------------
DoUninstall:
  TEST ProgFlags,Uninstall  ;Are we supposed to Uninstall?
  JZ  >U90                  ;If not, just quit
  TEST ProgFlags,InMemory   ;If so, are we even installed yet?
  JZ  >U60                  ;If not, there's an error
  MOV  ES:Int08Counter,0    ;Disable the TSR
  CALL UnconfigureDevices   ;Disable all of the Devices that are configured
  MOV  SI,IntNumTable       ;Point at the Interrupt Address Table
  CALL CompareInts          ;Can we Uninstall the Interrupt Vectors?
  JC  >U70                  ;If not, we can't Uninstall
  MOV  AH,49h               ;If not, service 49h (Release Memory, uses ES)
  INT  21h                  ;Do it
  JC  >U70                  ;If unable to do it, quit with error
U20:                        ;OK to Restore Interrupts
  CALL UnRegDvc0Owner       ;Unregister as a Device 0 Owner
  CALL StopIRQ8             ;Restore IRQ 8 back to original status
  CALL RestoreInts          ;Restore Int's back to normal
  TEST ES:[DIPS2Offset].DIFlags,DIFlagInUse ;Was there a PS2 Mouse installed?
  JZ  >U30                  ;If not, continue
  MOV  DX,ResetPS2Msg2      ;Write the
  CALL WriteZPause          ;  Resetting Message
  MOV  AX,0C200h+I15ALReset ;Reset the
  INT  15h                  ;  PS/2 Mouse
U30:                        ;Done resetting old Mouse driver
  MOV  AX,50                ;Wait a little while
  CALL DelayMSAX            ;  for things to take effect
  MOV  CX,((LastTSRByte-StackSize)/2) ;Number of Words in TSR
  XOR  DI,DI                ;Start of TSR Data Area
  XOR  AX,AX                ;Fill with Zeroes
  REP  STOSW                ;Do it
  MOV  DX,UninstallMsg      ;Point to successful uninstall message
  XOR  AL,AL                ;Errorlevel = 0
  JUMP Exit                 ;QUIT
U60:                        ;Not in memory yet - can't Uninstall
  MOV  DX,UninstallErrMsg   ;Point at error message
  MOV  AL,ErLvlUninst       ;Errorlevel = Not Installed Yet
  JUMP Exit                 ;QUIT
U70:                        ;Another TSR is in the way - can't Uninstall
  MOV  DX,NoUninstallMsg    ;Point at error message
  MOV  AL,ErLvlMemory       ;Errorlevel = Memory Problem
  JUMP Exit                 ;QUIT
U90:                        ;No need to Uninstall
  RET

;------------------------------------------------------------------------------
;CLOSE ALL OPEN HANDLES & UNCONFIGURE DEVICES THAT WE'RE CONTROLLING
;Inputs:  ES = TSR Data Segment
;Outputs:
;Changes: Closes Open Handles, Unconfigures Devices
;------------------------------------------------------------------------------
UnconfigureDevices:
  PUSH CX,DI,SI,DS              ;Save used registers
  MOV  DS,ES                    ;Point DS at TSR Data
  MOV  SI,Int14Request          ;Point at Request Structure
  MOV  DI,DeviceInfoTable       ;Point at Table
  MOV  CX,MaxDevices            ;Number of Table Entries
G10:                            ;Loop to here for each Entry
  TEST [DI].DIFlags,DIFlagInUse ;Valid Entry?
  JZ  >G30                      ;If not, skip it
  CALL RlsPressedSysESDI        ;Release pending keystrokes
  CALL RlsDvcDoResetDI          ;Release & Reset the Device
G30:                            ;Done with this entry
  ADD  DI,DeviceInfoStrucSize   ;Point at next entry
  LOOP G10                      ;Keep going until we're done
  POP  DS,SI,DI,CX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;UNREGISTER AS A DEVICE 0 (NEW DEVICE) OWNER
;Inputs:  DS = Local Data Area
;         ES = TSR Data Segment
;Outputs:
;Changes: Unregisters as Device 0 Owner
;NOTES: We do this as a separate item from UnconfigureDevices, and perform it
;         after we've verified that we can actually Uninstall the program.
;       If we are unable to Uninstall and simply Disable ourselves, we don't
;         want to go through the bother of re-Registering as a Device 0 Owner
;         again.  We leave our Device 0 Registration in effect, but effectively
;         just ignore it until we re-Enable ourselves.
;------------------------------------------------------------------------------
UnRegDvc0Owner:
  PUSH AX,SI,DS                      ;Save used registers
  MOV  DS,ES                         ;Point DS at TSR Data
  MOV  SI,Int14Request               ;Point at Request Structure
  MOV  W [SI].I14RCallBackAddr[0],-1 ;Set CallBackAddr to nothing
  MOV  [SI].I14RRequestType,I14RRTUnRegIntfOwner ;Unregister as owner
  MOV  [SI].I14RHostIndex,-1         ;All Hosts
  MOV  [SI].I14RDeviceAddress,0      ;Address 0 (New Connects)
  MOV  AX,Reg0Handle                 ;Handle Number
  MOV  [SI].I14RRequestHandle,AX     ;  returned by Host
  CALL DoInt14CallSaveAllSI          ;Do it
  POP  DS,SI,AX                      ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code to Copy Data from the redirected input file, "compressing" it and
;  storing it in our local memory buffer.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;READ THE DATA FROM STDIN (REDIRECTED INPUT) AND STORE IT IN OUR BUFFER
;Inputs:  ExtraMemSegment
;Outputs:
;Changes: Fills up StdInBuffer
;         As data is read, multiple spaces are converted to single spaces,
;           and EOL's (CR/LF) are also changed to spaces
;         Last character of buffer is a 0
;         Quits with error message if StdIn is too big for buffer
;------------------------------------------------------------------------------
FillStdInBuffer:
  PUSH AX,DI,ES             ;Save used registers
  CALL FlushKbdBuff         ;Don't let keyboard buffer mess with File Input
  MOV  ES,ExtraMemSegment   ;Point ES:DI at
  XOR  DI,DI                ;  our Buffer
  MOV  AL,' '               ;Start the buffer
  STOSB                     ;  with a space
B10:                        ;Loop to here for each character
  CALL ReadStdInChar        ;Get the next character
  JZ  >B80                  ;If end of input, we're done
B15:                        ;AL contains the next character
  CMP  AL,EOF               ;Is it End-of-File?
  JE  >B80                  ;If so, it's the end of input
B20:                        ;Test for Comment
  CMP  AL,';'               ;Is it the start of a comment?
  JNE >B30                  ;If not, try the next possibility
  CALL SkipOverStdInComment ;If so, skip over the comment
  JC  >B80                  ;If EOF, we're done
  JMP  B10                  ;If not EOF, continue
B30:                        ;Test for String
  CMP  AL,'"'               ;Double Quote?
  JE  >B35                  ;If so, handle a string
  CMP  AL,"'"               ;Single Quote?
  JE  >B35                  ;If so, handle a string
  CMP  AL,"`"               ;Left Quote?
  JNE >B40                  ;If not, try the next possibility
B35:                        ;It's a String
  CALL GetStdInString       ;Get the rest of the String
  JC  >B80                  ;If error, just quit (Error handled later)
  JMP  B10                  ;If OK, continue
B40:                        ;Test for EOL
  CMP  AL,CR                ;Carriage Return?
  JE  >B45                  ;If so, handle it
  CMP  AL,LF                ;Carriage Return?
  JNE >B50                  ;If not, it's a regular character
B45:                        ;End of Line
  CALL AddSpace2StdInBuff   ;Add a Space to the Buffer, if needed
  JMP  B10                  ;Continue
B50:                        ;Is a regular character
  CALL AddChar2StdInBuff    ;Add the character to the buffer
  JMP  B10                  ;Continue
B80:                        ;Done, no error
  XOR  AL,AL                ;Mark the end
  STOSB                     ;  of the buffer
B90:                        ;Done
  POP  ES,DI,AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER A COMMENT IN THE REDIRECTED INPUT FILE
;Inputs:  DS = Local Data Area
;         ES:[DI] = StdInBuffer
;         AL = Semicolon (the start of the Comment)
;Outputs: CF = Clear if OK (not EOF)
;            = Set if we've reached EOF
;         Quits Program with error message if Buffer gets full
;Changes:
;NOTES: This looks for either an EOF or LF character to indicate the end of
;         the comment.  If we see a CR, we simply skip it and wait for the LF
;         that should immediately follow the CR.
;       If a line ends in a CR or CR/LF combination, this will not work
;         this will not work correctly.  However, no file we work with
;         should ever be in that format.
;------------------------------------------------------------------------------
SkipOverStdInComment:
  PUSH AX                 ;Save used registers
C10:                      ;Loop to here for each character on the line
  CALL ReadStdInChar      ;Get the next character (AL)
  JZ  >C70                ;If no characters available, EOF
  CMP  AL,EOF             ;End-of-File character?
  JE  >C70                ;If so, we're done
  CMP  AL,CR              ;Carriage Return?
  JE  >C20                ;If so, End of Comment
  CMP  AL,LF              ;Line Feed?
  JNE  C10                ;If not, keep looking
C20:                      ;End of Line
  CALL AddSpace2StdInBuff ;Add a Space to the Buffer if we need it
  JMP >C80                ;Done
C70:                      ;EOF
  STC                     ;Set EOF Flag
  JMP >C90                ;Done
C80:                      ;Not EOF
  CLC                     ;Set not EOF Flag
C90:                      ;Done
  POP  AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A STRING FROM STDIN TO THE STDINBUFFER
;Inputs:  DS = Local Data Area
;         ES:[DI] = StdInBuffer
;         AL = Quote Characer (the start of the String)
;Outputs: CF = Clear if OK (not EOF)
;            = Set if we've reached EOF (Bad String)
;         Quits Program with error message if Buffer gets full
;Changes:
;NOTES: This looks for the matching Quote Character to indicate the end of
;         the String.
;       If we find an EOF, CR, or LF in the string, we return a CF to indicate
;         a bad String.
;------------------------------------------------------------------------------
GetStdInString:
  PUSH AX                 ;Save used registers
  MOV  AH,AL              ;Save quote character
  CALL AddSChar2StdInBuff ;Add the Quote Character to the Buffer
S10:                      ;Loop to here for each character of string
  CALL ReadStdInChar      ;Get the next character (AL)
  JZ  >S70                ;If no characters available, Error
  CMP  AL,EOF             ;End-of-File character?
  JE  >S70                ;If so, Error
  CMP  AL,CR              ;Carriage Return?
  JE  >S70                ;If so, Error
  CMP  AL,LF              ;Line Feed?
  JE  >S70                ;If so, Error
  CALL AddSChar2StdInBuff ;Add the Character to the Buffer
  CMP  AL,AH              ;Is it the end-of-string character?
  JNE  S10                ;If not, keep looking
  JMP >S80                ;Done
S70:                      ;EOF (Bad String)
  STC                     ;Set EOF Flag
  JMP >S90                ;Done
S80:                      ;String is OK
  CLC                     ;Set OK flag
S90:                      ;Done
  POP  AX                 ;Restore used registers
  RET

;------------------------------------------------------------------------------
;READ A CHARACTER FROM STDIN (REDIRECTED INPUT)
;Inputs:
;Outputs: AL = Next character from StdIn
;              ZF Clear
;            = 0 if no character to get
;              ZF Set
;Changes:
;------------------------------------------------------------------------------
ReadStdInChar:
  PUSH DX      ;Save used registers
  MOV  DH,AH   ;Save original AH
  MOV  AH,06h  ;Function 06h
  MOV  DL,0FFh ;  (Direct Console Input)
  INT  21h     ;Do it (returns AL and ZF)
  MOV  AH,DH   ;Restore original AH
  POP  DX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ADD A CHARACTER OR STRING CHARACTER TO THE END OF THE STDIN BUFFER
;Inputs:  DS = Local Data Area
;         ES:[DI] = StdInBuffer
;         AL = Character to add to end of Buffer
;         CLD already issued
;Outputs: Returns to calling program if Character was added OK
;         Quits Program with Error if Buffer is already full
;Changes: DI (maybe)
;------------------------------------------------------------------------------
AddSpace2StdInBuff:
  PUSH AX                ;Save used registers
  MOV  AL,' '            ;Add a Space to the Buffer
  CALL AddChar2StdInBuff ;  if we need it
  POP  AX                ;Restore used registers
  RET

AddChar2StdInBuff:
  CMP  AL,' '            ;Are we adding a space?
  JNE >B00               ;If not, just add it
  CMP  B ES:[DI-1],AL    ;If so, is there already a space?
  JNE >B00               ;If not, just add it
  JMP >B90               ;If so, we're done
AddSChar2StdInBuff:
B00:                     ;Add the character to the Buffer
  CMP  DI,65534          ;Is the Buffer full?
  JAE >B70               ;If so, Error
  STOSB                  ;If not, Store the Character
  JMP >B90               ;Done
B70:                     ;Buffer already Full
  MOV  DX,StdInTooBigMsg ;Point at error message
  MOV  AL,ErLvlStdIn     ;ErrorLevel = problem with StdIn
  JMP  Exit              ;Quit
B90:                     ;Done
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code to Initialize various Tables and Structures
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;INITIALIZE TABLES AND STRUCTURES TO THEIR APPROPRIATE VALUES
;Inputs:  ES = TSR Data Area
;         DS = Area where Int14Requests are stored that we want to configure
;Outputs:
;Changes: Various things
;------------------------------------------------------------------------------
InitTables:
  PUSH DI               ;Save used registers
  MOV  DI,Int14Request  ;Point at first Int 14 Request
  CALL InitInt14Req     ;Fill it up
  MOV  DI,Int14Request2 ;Point at second Int 14 Request
  CALL InitInt14Req     ;Fill it up
  POP  DI               ;Restore used registers
  RET

InitInt14Req:
  MOV  [DI].I14RDvcClass,DvcClassNone             ;Device Class
  MOV  [DI].I14RDvcSubClass,DvcSubClassNone       ;Device SubClass
  MOV  [DI].I14RDvcProtocol,DvcProtocolNone       ;Device Protocol
  MOV  [DI].I14RIntfClass,IntfClassHID            ;Interface Class
;  MOV  [DI].I14RIntfSubClass,IntfSubClassHIDBoot  ;Interface SubClass
  MOV  [DI].I14RIntfSubClass,IntfSubClassAny      ;Interface SubClass
;  MOV  [DI].I14RIntfProtocol,IntfProtocolHIDMouse ;Interface Protocol
  MOV  [DI].I14RIntfProtocol,IntfProtocolAny      ;Interface Protocol
  MOV  [DI].I14RVendorID,-1                       ;Vendor ID = Don't Care
  MOV  [DI].I14RProductID,-1                      ;Product ID = Don't Care
  MOV  W [DI].I14RDataAddress[2],ES               ;Set Segment
  MOV  W [DI].I14RCallBackAddr[2],ES              ;  Registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE NECESSARY INTERRUPTS TO CHECK FOR A NEW DEVICE
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes:
;------------------------------------------------------------------------------
InitInts:
  PUSH CX,DI                    ;Save used registers
I10:                            ;Register Interface 0 Owner
  MOV  DI,Int14Request          ;Point at Request Structure
  MOV  [DI].I14RRequestType,I14RRTRegIntfOwner ;Register as Owner
  MOV  [DI].I14RHostIndex,-1    ;Any/All Hosts
  MOV  [DI].I14RDeviceAddress,0 ;New Device
  MOV  W [DI].I14RCallBackAddr[0],DvcOwnerFarCall ;Code to call
  MOV  [DI].I14RUserPktID,0     ;Packet ID 0
  CALL DoInt14CallCXDI          ;Do it
  JC  >I70                      ;If error, quit
  MOV  Reg0Handle,CX            ;Save Registry Handle
  JMP >I90                      ;Done
I70:                            ;Error
  MOV  DX,HostInitErrMsg        ;If not, point at Err Msg
  MOV  AL,ErLvlHostInit         ;Error Initializing Host
  JMP  Exit                     ;Done
I90:                            ;It worked
  POP  DI,CX                    ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code to save/initialize/restore Interrupt Vectors
;This code is needed in every program that intercepts Interrupt Vectors.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;INITIALIZE ALL APPROPRIATE INTERRUPT VECTORS TO OUR TSR CODE
;Inputs:  ES = Data area to store Interrupt Vectors in (Presumably a UMB)
;         CS = Data Area where Interrupt Number Table is stored
;         CLD Already Issued!
;Outputs:
;Changes: Various Int vectors
;------------------------------------------------------------------------------
ChangeInts:
  PUSH AX,DI,SI,DS    ;Save used registers
  MOV  DS,ES          ;Point DS at TSR Data Area
  MOV  SI,IntNumTable ;Point at our Interrupt Nubmer Table
C10:                  ;Loop to here for each table entry
  MOV  AL,CS:[SI]     ;Get Interrupt Number
  OR   AL,AL          ;Is it the end of the table?
  JZ  >C90            ;If so, we're done
  MOV  DI,CS:[SI+1]   ;If valid, get our Code Header Offset
  CALL ChangeInt      ;Point the Interrupt at our code
  ADD  SI,3           ;Point at the next table entry
  JMP  C10            ;Keep going until we're done
C90:                  ;Done with the Interrupt Table
  CALL StartIRQ8      ;Enable IRQ 8 (INT 70h)
  POP  DS,SI,DI,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CHANGE INTERRUPT VECTOR TO THIS PROGRAMS INTERRUPT ROUTINE, AND SAVE THE OLD
;Inputs:  DS:[DI] = Pointer to our new interrupt header
;         AL = Interrupt number to change
;Outputs:
;Changes: Interrupt vector
;------------------------------------------------------------------------------
ChangeInt:
  PUSH AX,BX,DX,ES            ;Save used registers
  CALL InitIntHdr             ;Initialize our Interrupt Header
  MOV  AH,35h                 ;Service 35h (Get interrupt vector)
  INT  21h                    ;Do it (returns ES:BX)
  MOV  W [DI].OldVector,BX    ;Save it
  MOV  W [DI].OldVector[2],ES ;  in [DI]
  LEA  DX,[DI].CodeJmp        ;Point DS:DX at our code address
  MOV  AH,25h                 ;Service 25h (Set interrupt vector)
  INT  21h                    ;Do it
  POP  ES,DX,BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE THE DATA IN A REMOVABLE INTERRUPT VECTOR HEADER
;Inputs:  DS:[DI] = Pointer to Our Interrupt Header Structure
;Outputs:
;Changes: The Interrupt Header Data
;------------------------------------------------------------------------------
InitIntHdr:
  MOV  [DI].HdwreRst,0CBh   ;Hardware Reset Code = CBh = RETF
  MOV  [DI].CodeJmp,10EBh   ;Jump to Real Code = EBh 10h = JMP ENDS
  MOV  [DI].Signature,424Bh ;Signature for Removable Vector Header = 424Bh
  MOV  [DI].EOIFlag,0       ;We will not be issuing EOI's
  MOV  [DI].HdwreJmp,0F4EBh ;Jump to Hardware Reset = EBh F4h = JMP HdwreRst
  RET

;------------------------------------------------------------------------------
;CHECK THE INTERRUPT VECTORS TO SEE IF THEY'VE BEEN REHOOKED BY ANOTHER PROGRAM
;Inputs:  ES = TSR Data area
;         CLD Already Issued!
;Outputs: CF = Clear if Vectors are OK (We can safely Uninstall our Program)
;            = Set if at least one Vector is unchangeably rehooked
;Changes:
;------------------------------------------------------------------------------
CompareInts:
  PUSH AX,DX,SI          ;Save used registers
  MOV  SI,IntNumTable    ;Point at our interrupt number table
C10:                     ;Loop to here for each table entry
  LODSB                  ;Get the Interrupt Number
  OR   AL,AL             ;Is it the end of the table?
  JZ  >C80               ;If so, we're done
  MOV  DX,[SI]           ;If valid, get our Code Header Offset
  ADD  DX,OFFSET CodeJmp ;Add the Offset to the actual Vector Address
  CALL CompareInt        ;Has it been unchangeably rehooked?
  JC  >C70               ;If so, we can't Uninstall our Program
  INC  SI,2              ;Point at the next table entry
  JMP  C10               ;Keep going until we're done
C70:                     ;At lest one Interupt has been rehooked
  STC                    ;Set the Rehooked flag
  JMP >C90               ;We're done
C80:                     ;Vectors can be Uninstalled
  CLC                    ;Set the OK flag
C90:                     ;We're done
  POP  SI,DX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COMPARE OUR INTERRUPT ADDRESS TO MEMORY SEE IF IT'S BEEN REHOOKED
;Inputs:  ES:[DX] = Vector address to test (our TSR's address)
;         AL = Interrupt number to test
;Outputs: CF = Clear if OK to remove
;         CF = Set if rehooked and can't be removed
;Changes:
;------------------------------------------------------------------------------
CompareInt:
  PUSH BX,CX,ES ;Save used registers
  MOV  CX,ES    ;Point CX:DX at our vector
  CALL FindInt  ;Look for it (returns ES:BX and CF
  POP  ES,CX,BX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESTORE ALL INTERRUPT VECTORS BACK TO THEIR ORIGINAL STATE
;Inputs:  ES = Data Area where original Vector information is stored
;         CLD already issued!
;Outputs:
;Changes: Various Int vectors
;------------------------------------------------------------------------------
RestoreInts:
  PUSH AX,DI,SI       ;Save used registers
  MOV  SI,IntNumTable ;Point at our Interrupt Nubmer Table
R10:                  ;Loop to here for each table entry
  LODSB               ;Get the Interrupt Number
  OR   AL,AL          ;Is it the end of the table?
  JZ  >R90            ;If so, we're done
  MOV  DI,[SI]        ;If valid, get our Code Header Offset
  CALL RestoreInt     ;Restore Interrupt Vector back to original
  INC  SI,2           ;Point at the next table entry
  JMP  R10            ;Keep going until we're done
R90:                  ;We're done
  POP  SI,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;RESTORE INTERRUPT VECTOR TO ITS ORIGINAL PLACE
;Inputs:  ES:[DI] = Pointer to our Interrupt Header
;         AL = Interrupt number to restore
;Outputs:
;Changes: Interrupt vector back to where it belongs
;Notes:   If we got to here, nothing should have intercepted
;           an interrupt that we haven't checked for.
;         We shouldn't have to worry about running into a road-block!
;------------------------------------------------------------------------------
RestoreInt:
  PUSHF                          ;Save flags
  PUSH BX,CX,DX,SI,BP,ES         ;Save used registers
  MOV  SI,W ES:[DI].OldVector    ;Put the address we'll need to store
  MOV  BP,W ES:[DI].OldVector[2] ;  in BP:SI
  MOV  CX,ES                     ;Point CX:DX
  LEA  DX,[DI].CodeJmp           ;  at our address
  CALL FindInt                   ;Search for it (returns ES:BX)
  CLI                            ;Disable interrupts
  MOV  ES:[BX],SI                ;Store the
  MOV  ES:[BX+2],BP              ;  address
  POP  ES,BP,SI,DX,CX,BX         ;Restore used registers
  POPF                           ;Restore flags
  RET

;------------------------------------------------------------------------------
;SEARCH FOR AN INTERRUPT VECTOR ADDRESS (TO SEE IF OUR CODE CAN BE REMOVED)
;Inputs:  CX:DX = Interrupt Vector address to find (address of our TSR code)
;         AL = Interrupt number to find
;Outputs: CF = Clear if OK to remove
;           ES:BX = address where it is stored
;         CF = Set if rehooked and can't be removed
;           ES:BX = unknown state
;Changes:
;------------------------------------------------------------------------------
FindInt:
  PUSH AX                                ;Save used registers
  XOR  AH,AH                             ;Point ES:BX
  ADD  AX,AX                             ;  at the
  ADD  AX,AX                             ;  main
  MOV  BX,AX                             ;  interupt
  XOR  AX,AX                             ;  vector
  MOV  ES,AX                             ;  table
  CMP  ES:[BX],DX                        ;Is the offset the same as ours?
  JNE >F20                               ;If not, it can't be ours
  CMP  ES:[BX+2],CX                      ;Is the segment the same as ours?
  JE  >F80                               ;If so, it's ours - we're done
F20:                                     ;The main interrupt vector has changed
  LES  BX,ES:[BX]                        ;Get the current vector address
F30:                                     ;Loop for each removable interrupt
  CALL TestIntHdr                        ;Is this a removable interrupt?
  JC  >F70                               ;If not, we can't be removed
  CMP  W ES:[BX-IntOfst].OldVector,DX    ;If so, is the old offset ours?
  JNE >F50                               ;If not, keep looking
  CMP  W ES:[BX-IntOfst].OldVector[2],CX ;If so, is old segment ours?
  JNE >F50                               ;If not, keep looking
  ADD  BX,(OFFSET OldVector) - IntOfst   ;If so, ES:BX = old vector pointer
  JMP >F80                               ;And we're done
F50:                                     ;Removable, but doesn't point at us
  LES  BX,ES:[BX-IntOfst].OldVector      ;Get its old vector address
  JMP  F30                               ;And keep looking for our vector
F70:                                     ;Been rehooked and can't be removed
  STC                                    ;Set the "rehooked" flag
  JMP >F90                               ;We're done
F80:                                     ;It's not rehooked, or it's removable
  CLC                                    ;Set the "OK to remove" flag
F90:                                     ;We're done
  POP  AX                                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AN INTERRUPT POINTER TO SEE IF IT'S REMOVABLE
;Inputs:  ES:BX = Interrupt Vector to test
;Outputs: CF = Set if it's not a removable vector
;            = Clear if it is removable
;Changes:
;Notes:   The references to [BX-IntOfst] instead of [BX] are because of
;           our "unusual" Interrupt Header Structure design
;         We have included the (unneeded) Hardware Reset Routine code
;           (a simple RETF) in our Header Structure for efficiency
;------------------------------------------------------------------------------
TestIntHdr:
  PUSH AX,BX,DS                      ;Save used registers
  MOV  DS,ES                         ;Point DS at the interrupt segment
  CMP  B [BX-IntOfst].CodeJmp,0EBh   ;Is the first instruction a Short JMP?
  JNE >H70                           ;If not, it's not removable
  CMP  [BX-IntOfst].Signature,0424Bh ;Is there a Removeable Interrupt Signature?
  JNE >H70                           ;If not, it's not removable
  MOV  AL,[BX-IntOfst].EOIFlag       ;Get the EOI flag
  OR   AL,AL                         ;Is it zero?
  JZ  >H10                           ;If so, continue checking
  CMP  AL,80h                        ;Is it 80h?
  JNE >H70                           ;If not, it's not removable
H10:                                 ;EOI flag is OK
  CMP  B [BX-IntOfst].HdwreJmp,0EBh  ;Is the HdwreJmp instruction a Short JMP?
  JE  >H80                           ;If so, it's removable!
H70:                                 ;Vector is not removable
  STC                                ;Set the "not removable" flag
  JMP >H90                           ;We're done
H80:                                 ;Vector is removable
  CLC                                ;Set the "removable" flag
H90:                                 ;We're done
  POP  DS,BX,AX                      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF AN INTERRUPT VECTOR HAS A VALID ADDRESS (other than 0)
;Inputs:  AL = Interrupt number to test
;Outputs: ZF = Set if invalid address (Segment = 0)
;            = Clear if valid address (Segment <> 0)
;Changes:
;------------------------------------------------------------------------------
TestInt:
  PUSH BX,DS      ;Save used registers
  XOR  BX,BX      ;Point DS at the
  MOV  DS,BX      ;  Interrupt Table
  MOV  BL,AL      ;Point BX
  SHL  BX,1       ;  at the Interrupt
  SHL  BX,1       ;  we're looking for
  CMP  W [BX+2],0 ;Is the Segment valid (set the return flag)?
  POP  DS,BX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE INT 01 VECTOR IN OUR MEMORY TO OUR CODE
;Inputs:  DS = ES = TSR Data Area
;Outputs:
;Changes: Int01Vect
;------------------------------------------------------------------------------
InitializeInt01:
  MOV  W Int01Vect[0],Int01 ;Put our CS:IP into
  MOV  W Int01Vect[2],DS    ;  Int01Vect
;  MOV  W Int09Vect[0],Int09 ;Put our CS:IP into
;  MOV  W Int09Vect[2],DS    ;  Int01Vect
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE NEEDED TO SET UP AND DO SCREEN PAUSES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;SETUP PARAMETERS FOR DOING A SCREEN PAUSE (FOR PRINTING A LARGE TABLE)
;Inputs:  DS = Local Data Area
;         DS:[DX] = ASCIIZ Header to Print at top of each screen
;                   Last character of the Header MUST be an LF
;                   If SetupPauseNoHdr, we "inject" NoMsg as the Header
;Outputs: Pauses the screen with a MORE message every time it gets full
;NOTES:   If a Screen Header is really large (almost taking up the whole
;           screen itself, with little room for data), this procedure
;           may not work correctly.  This procedure does not validate
;           the size of the Header -- it simply assumes that it is a
;           "reasonable" size compared to the size of the screen.  Most
;           anything that needs a Screen Header (a long data table) will
;           only have a few lines of Header, so this should be OK as a
;           general purpose procedure.
;         After this Setup is complete (which should be near the beginning
;           of MAIN, before anything is sent to the screen), WriteZPause
;           must be called for ALL screen writing to CON:, not WriteZCon!
;         The Pausing occurs in "real time", meaning we can send things
;           to the Pause routine a byte, line, or several screens at a
;           time for processing.  Because we do it in "real time",
;           we can end up with extra MORE's and screen Headers appearing
;           on screen than are actually needed.  During a screen
;           transition (or a transition to a new Header) whether we
;           actually need to write anything or not depends on if there
;           is actually any real data coming afterward (in time), which
;           of course we don't know (since it hasn't happened yet).
;           So, we must assume there is more data to come and process the
;           MORE's and Screen Headers accordingly.  This only causes problems
;           (in the form of extraneous MORE's and Screen Headers)
;           when things happen near the bottom of the screen.
;           DOS's MORE utiltiy (and other programs of similar ilk) do not
;           process in "real time", but rather store things in a Temporary
;           File and then process the file.
;------------------------------------------------------------------------------
SetupPauseNoHdr:
  PUSH DX         ;Save used registers
  MOV  DX,NoMsg   ;Point at 0-length Message
  CALL SetupPause ;Setup the Pause
  POP  DX         ;Restore used registers
  RET

SetupPause:
  PUSH AX,BX,CX,DI,SI     ;Save used registers
  MOV  SI,OFFSET PauseHeaderPtr ;Point [SI] at PauseHeaderPtr
  CMP  W [SI],-1          ;Has the user already cancelled us?
  JE  >S90                ;If so, just quit
  CMP  W [SI],0           ;Has the user stopped pausing already?
  JE  >S85                ;If so, just write the header
  CALL TestRedir          ;Is our output redirected?
  JNC >S10                ;If not, setup our Headers
  MOV  W [SI],0           ;Mark as Redirected or Pausing Stopped/Disabled
  JMP >S85                ;Jump to write the Header and Quit
S10:                      ;Output not redirected
  CALL GetScreenRows      ;Store the number of rows
  MOV  PauseRows,AX       ;  on the screen
  CALL CountZLinesCX      ;Calculate the number of lines
  INC  CX,2               ;  in the new Header
  MOV  DI,PauseRowCount   ;Get the Current Row count
  ADD  DI,CX              ;Add the size of the new header
  SUB  DI,3               ;Adjust for Headers
  DEC  AX                 ;Will writing the new Header
  CMP  DI,AX              ;  cause a Pause event?
  JB  >S50                ;If not, handle it
S20:                      ;New Header is too big to fit on screen
  MOV  W [SI],NoMsg       ;Point Header for second page at nothing
  MOV  PauseHeaderSize,2  ;Set Pause Header Size
  PUSH CX                 ;Save New Header Size
  SUB  AX,PauseRowCount   ;Calculate how many rows to fill up the screen
  MOV  CX,AX              ;Put number of rows in CX
  INC  CX                 ;Adjust it
S30:                      ;Loop to here to fill up the screen
  CALL WriteCrLfPause     ;Write a new line
  LOOP S30                ;Keep going until we're done
  POP  CX                 ;Restore New Header Size
  CMP  W [SI],-1          ;Did the user cancel during the header transition?
  JE  >S90                ;If so, we're done
  CMP  W [SI],0           ;Did the user stop Pausing during the transition?
  JE  >S85                ;If so, just write the Header
S50:                      ;Need to adjust current Row Count
  SUB  PauseRowCount,2    ;Adjust things for the previous Header
S80:                      ;Store new Header Info
  MOV  [SI],DX            ;Store the Header Address
  MOV  PauseHeaderSize,CX ;Store the Header Size
  ADD  PauseRowCount,CX   ;Add the Header Size to the Row Count
  MOV  AX,PauseRows       ;Compensate if
  CMP  AX,PauseRowCount   ;  the new header
  JAE >S85                ;  put us past
  DEC  AX                 ;  the end
  MOV  PauseRowCount,AX   ;  of hte screen
S85:                      ;Write Header
  CALL WriteZCon          ;Write the Header
S90:                      ;We're done
  POP  SI,DI,CX,BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CHECK TO SEE IF STDOUT HAS BEEN REDIRECTED
;Inputs:
;Outputs: CF = Clear if STDOUT is normal (usually the screen)
;            = Set if STDOUT been redirected
;Changes:
;------------------------------------------------------------------------------
TestRedir:
  PUSH AX,BX,DX        ;Save used registers
  MOV  AX,4400h        ;Function 4400h (Get Device Info)
  MOV  BX,StdOutHandle ;Device = STDOUT
  INT  21h             ;Do it (returns DX, changes AX)
  JC  >R80             ;If error, assume not Redirected
  AND  DL,(DvcInfoIsDevice+DvcInfoIsStdOut) ;Mask out unwanted bits
  CMP  DL,(DvcInfoIsDevice+DvcInfoIsStdOut) ;Is it still the STDOUT Device?
  JE  >R80             ;If so, it's not Redirected
R70:                   ;STDOUT has been Redirected
  STC                  ;Set the Redirected flag
  JMP >R90             ;Done
R80:                   ;STDOUT is not Redirected
  CLC                  ;Set the Not-Redirected flag
R90:                   ;Done
  POP  DX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NUMBER OF VIDEO ROWS ON THE SCREEN
;Inputs:
;Outputs: AX = Number of text rows on the screen
;                (usually 25, 43, or 50)
;Changes:
;------------------------------------------------------------------------------
GetScreenRows:
  PUSH BX,CX,DS  ;Save used registers
  MOV  AX,40h    ;Point ES to
  MOV  DS,AX     ;  BIOS data area
  MOV  AH,12h    ;Function 12h (EGA Alternate Select)
  MOV  BX,0FF10h ;Subfunction (BL) 10h (Return EGA information)
                 ;  Preset unrealistic return value (BH)
  INT  10h       ;Do it (returns AX, BX, CX)
  CMP  BL,10h    ;Is the function supported (EGA or better)?
  JE  >R50       ;If not, jump to handle MDA/CGA
  CMP  BH,0FFh   ;Is the return value valid?
  JE  >R50       ;If not, jump to handle MDA/CGA
  TEST B [87h],8 ;If so, is bit 3 of the EGA information byte set?
  JNZ >R50       ;If so, EGA is not the active display
  XOR  AX,AX     ;Get # of
  MOV  AL,[84h]  ;  rows - 1
  JMP >R90       ;We're done
R50:             ;Video card is less than EGA (MDA or CGA)
  MOV  AX,24     ;Number of rows must be 25
R90:             ;We're done
  INC  AX        ;Bump the number of rows by 1
  POP  DS,CX,BX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE ASCIIZ STRING(s) TO THE SCREEN, PAUSING WHEN IF NECESSARY
;Inputs:  DS:[DX] = String to write
;         PauseRows, PauseHeaderPtr, PauseHeaderSize, PauseRowCount
;         CS = Local Data Area
;Outputs: PauseRowCount
;Changes: Pauses the screen and waits for user input, if necessary
;NOTES: This should be called instead of WriteZCon every time something
;         is written during the Pausing.
;       A string can be written by WriteZCon during the pausing, as long
;         as the string does not contain a Line Feed.  Any line containing
;         a Line Feed must come through here or the Line Count will get
;         screwed up!
;------------------------------------------------------------------------------
WriteZPauseFar:
  CALL WriteZPause
  RETF

WriteZPause:
  PUSH AX,BX,CX,DX          ;Save used registers
  TEST CS:ProgFlags2,Write2Err ;Should we write to ERR instead of Pause?
  JZ  >P10                  ;If not, continue
  CALL WriteZErr            ;If so, write to ERR
  JMP >P90                  ;Done
P10:                        ;Write to Pause
  MOV  AX,CS:PauseHeaderPtr ;Put Header Pointer in AX
  CMP  AX,-1                ;Has the Output been cancelled by the user?
  JE  >P90                  ;If so, don't write anything at all
  OR   AX,AX                ;Is STDOUT Redirected?
  JZ  >P80                  ;If so, just write it with no Pausing
  CALL CountZLinesCX        ;Count how many Lines are in the string
  MOV  AX,CS:PauseRowCount  ;Add the new lines
  ADD  AX,CX                ;  to the current lines
  CMP  AX,CS:PauseRows      ;Will we be at or past the end of the screen?
  JB  >P70                  ;If not, just write it
  JE  >P20                  ;If at the end of the screen, handle it
  SUB  AX,CS:PauseRows      ;If not, find
  SUB  CX,AX                ;  the end of the String
  CALL FindEndOfLines       ;  we need to write ([BX])
  MOV  AL,[BX]              ;Save the character that's there now
  MOV  B [BX],0             ;Replace it with a 0
  CALL WriteZPause          ;Write it (will Pause correctly)
  MOV  [BX],AL              ;Restore original character
  MOV  DX,BX                ;Use the new string pointer
  CALL WriteZPause          ;Write it (Pausing if necessary)
  JMP >P90                  ;Done
P20:                        ;At end of screen
  CALL WriteZCon            ;Write the String
  CALL HandlePause          ;Handle the Pause (may change Pointers/Counters)
  JMP >P90                  ;We're done for now
P70:                        ;Store new Row Count
  MOV  CS:PauseRowCount,AX  ;Save the new Row Count
P80:                        ;Write the String
  CALL WriteZCon            ;Write the string
P90:                        ;Done
  POP  DX,CX,BX,AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE PAUSE LINE, AND WAIT FOR A KEYSTROKE
;Inputs:  CS = Local Data Area
;         PauseRows, PauseHeaderPtr, PauseHeaderSize, PauseRowCount
;Outputs: PauseRowCount, PauseHeaderPtr
;Changes: Pauses the screen and waits for user input, if necessary
;------------------------------------------------------------------------------
HandlePause:
  PUSH AX,BX,DX,DS        ;Save String Segment
  MOV  DS,CS              ;Point DS at our Data Segment
  MOV  BX,OFFSET PauseHeaderPtr ;Point [BX] at the Pause Header
  MOV  DX,PauseMsg        ;Write the
  CALL WriteZCon          ;  Enter a Keystroke message
  CALL FlushKbdBuff       ;Clear the keyboard buffer
L10:                      ;Loop to here to wait for a keystroke
  CALL GetKey             ;Is there a key in the keystroke buffer?
  JZ   L10                ;If not, keep waiting until there is
  MOV  DX,PauseDMsg       ;Delete the
  CALL WriteZCon          ;  MORE Message
  CMP  AL,CtrlC           ;Was the keystroke a Control-C?
  JE  >L40                ;If so, jump to handle it
  CMP  AL,Escape          ;Was the keystroke an Escape?
  JNE >L20                ;If not, just keep going
  MOV  W [BX],0           ;If so, mark us as redirected (stop Pausing)
  JMP >L90                ;And quit
L20:                      ;Continue with the Pause
  MOV  DX,PauseNMsg       ;Write the
  CALL WriteZCon          ;  MORE Replacement Line
  MOV  DX,[BX]            ;Point DX at the Header
  CALL WriteZCon          ;Write it
  MOV  AX,PauseHeaderSize ;Reset the Row Count
  MOV  PauseRowCount,AX   ;Store the new Row Count
  JMP >L90                ;We're done for now
L40:                      ;Ctrl-C (stop altogether)
  MOV  W [BX],-1          ;Mark as stopped
L90:                      ;Done
  POP  DS,DX,BX,AX        ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE THE NUMBER OF LINES IN AN ASCIIZ STRING (COUNT CR/LF COMBINATIONS)
;Inputs:  DS:[DX] = Pointer to String to measure
;         CLD already issued
;Outputs: CX = Number of LF's in the String (may be 0)
;         ZF = Set if CX = 0
;            = Clear if CX > 0
;Changes:
;------------------------------------------------------------------------------
CountZLinesCX:
  PUSH AX,SI ;Save used registers
  MOV  SI,DX ;Put the pointer in DI
  XOR  CX,CX ;Initialize Lines Counter
Z10:         ;Loop to here for each character
  LODSB      ;Get the next character
  OR   AL,AL ;Is it the end of the string?
  JZ  >Z90   ;If so, we're done
  CMP  AL,LF ;Is it a Line Feed?
  JNE  Z10   ;If not, it's not a new line
  INC  CX    ;If so, Increment the Line Counter
  JMP  Z10   ;Keep looking
Z90:         ;We're done
  OR   CX,CX ;Set return flag
  POP  SI,AX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND THE POINTER AFTER THE FIRST SEVERAL LINES OF A STRING
;Inputs:  DS:[DX] = String to use
;         CX = Number of lines to "skip"
;         CLD already issued
;Outputs: DS:[BX] = "New" String Pointer (after the last line written)
;Changes:
;NOTES: This assumes that the string actually has MORE than the number of
;         lines in AX (it does not look for the end of an ASCIIZ string).
;------------------------------------------------------------------------------
FindEndOfLines:
  PUSH AX,CX  ;Save used registers
  PUSH DI,ES  ;Save used registers
  MOV  BX,CX  ;BX = Line Counter
  MOV  ES,DS  ;Point ES:[DI] at
  MOV  DI,DX  ;  the string
  MOV  AL,LF  ;Look for Line Feeds
  MOV  CX,-1  ;Maximize counter
L10:          ;Loop to here for each new line
  REPNE SCASB ;Look for the end-of-line character
  DEC  BX     ;Decrement the Line Counter
  JNZ  L10    ;If not 0 yet, keep looking
  MOV  BX,DI  ;Store the new string beginning
L90:          ;Done
  POP  ES,DI  ;Restore used registers
  POP  CX,AX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FLUSH KEYBOARD BUFFER
;Inputs:
;Outputs:
;Changes: Flushes the Keyboard Buffer
;------------------------------------------------------------------------------
FlushKbdBuff:
  PUSH AX     ;Save used registers
F10:          ;Loop to here for each key
  CALL GetKey ;Get a key from the keyboard buffer
  JNZ  F10    ;If there was one, get another
  POP  AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;READ CHARACTER FROM THE KEYBOARD BUFFER
;Inputs:
;Outputs: AH = Keyboard scan code
;         AL = ASCII value of keypress (0 if extended ASCII)
;         AX = 0 if no key is waiting
;Changes: ZF = Set if no key in buffer (AX = 0)
;            = Clear if a key was found (AX = key)
;------------------------------------------------------------------------------
GetKey:
  MOV  AH,1  ;Service 1 (Keystroke waiting?)
  INT  16h   ;Do It
  JZ  >K10   ;If no key waiting, we're done
  XOR  AH,AH ;If a key is waiting, service 0 (Get keystroke)
  INT  16h   ;Do It
  JMP >K90   ;We're done
K10:         ;No keystroke waiting
  XOR  AX,AX ;Make sure AX=0
K90:         ;We're done
  OR   AX,AX ;Set the found/not found flag
  RET

;------------------------------------------------------------------------------
;WRITE A CR/LF TO THE CONSOLE WITH A PAUSE
;Inputs:
;Outputs: To Screen, Updates Pause Counters
;Changes:
;------------------------------------------------------------------------------
WriteCrLfPause:
  PUSH DX          ;Save used registers
  MOV  DX,CrLfMsg  ;Write a Cr/Lf to
  CALL WriteZPause ;  the Pause Device
  POP  DX          ;Restore used registers
  RET

WriteCrLfPause2:
  CALL WriteCrLfPause ;Move down
  CALL WriteCrLfPause ;  2 lines
  RET

WriteCrLfErr:
  PUSH DX         ;Save used registers
  MOV  DX,CrLfMsg ;Write a Cr/Lf to
  CALL WriteZErr  ;  the ERR Device
  POP  DX         ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Some generic code to write strings, etc.
;This code is needed in almost every program we write.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;GETS RID OF ENVIRONMENT INFORMATION - NOT NEEDED IN TSR
;Inputs:
;Outputs:
;Changes: Quits if problem releasing memory
;------------------------------------------------------------------------------
DelEnvironment:
  PUSH AX,ES            ;Save used registers
  MOV  ES,[PSPEnvirSeg] ;Point ES at the Environment Segment
  MOV  AH,49h           ;Service 49h (free up memory)
  INT  21h              ;Do it
  JNC >E90              ;If no error occurred, Quit
  MOV  DX,NoEnvironMsg  ;If error, point to message
  MOV  AL,ErLvlMemory   ;Errorlevel = Memory Problem
  JMP  Exit             ;QUIT
E90:                    ;We're done
  POP  ES,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;BEEP THE SPEAKER
;Inputs:
;Outputs: Beeps the speaker
;Changes:
;------------------------------------------------------------------------------
BeepErr:
  PUSH AX,BX    ;Save used registers
  XOR  BX,BX    ;BH = Video page = 0
  MOV  AX,0E07h ;AH = 0Eh = Function = Write TTY
                ;AL = 07h = Character to write = BEL
  INT  10h      ;Do it
  POP  BX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CAPITALIZE A CHARACTER (Byte)
;Inputs:  AL = character to be capitalized
;Outputs: AL = capitalized character
;Changes:
;------------------------------------------------------------------------------
Capitalize:
  CMP  AL,'a'     ;Is it less than 'a'?
  JB  >Z90        ;If so, quit
  CMP  AL,'z'     ;Is it more than 'z'?
  JA  >Z90        ;If so, quit
  SUB  AL,'a'-'A' ;If between 'a' and 'z', capitalize it
Z90:
  RET

;------------------------------------------------------------------------------
;WRITE A SPECIFIC CHARACTER OR SET OF CHARACTERS TO THE SCREEN
;Inputs:
;Outputs: To screen
;Changes:
;------------------------------------------------------------------------------
WriteAL:
  PUSH DX              ;Save used registers
  MOV  DL,AL           ;Write what's in AL
  JMP >C00             ;Do it
WriteBlock:
  PUSH DX              ;Save used registers
  MOV  DL,'±'          ;Write a Block Character
  JMP >C00             ;Do it
WriteHexEnd:
  PUSH DX              ;Save used registers
  MOV  DL,'h'          ;Write an "h"
  JMP >C00             ;Do it
WriteMinus:
  PUSH DX              ;Save used registers
  MOV  DL,'-'          ;Write a Minus
  JMP >C00             ;Do it
WritePlus:
  PUSH DX              ;Save used registers
  MOV  DL,'+'          ;Write a Plus
  JMP >C00             ;Do it
WriteSpace:
  PUSH DX              ;Save used registers
  MOV  DX,SpaceMsg     ;Point at Message
  JMP >C10             ;Do it
Write2Spaces:
  PUSH DX              ;Save used registers
  MOV  DX,Space2Msg    ;Point at Message
  JMP >C10             ;Do it
Write3Spaces:
  PUSH DX              ;Save used registers
  MOV  DX,Space3Msg    ;Point at Message
  JMP >C10             ;Do it
Write4Spaces:
  PUSH DX              ;Save used registers
  MOV  DX,Space4Msg    ;Point at Message
  JMP >C10             ;Do it
C00:
  MOV  SingleMsg[0],DL ;Put the character in the Single Character String
  MOV  DX,SingleMsg    ;Point at Single Character String
C10:                   ;DX is pointed at String
  CALL WriteZPause     ;Write the String
  POP  DX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE SOME SPACES TO THE SCREEN
;Inputs:  CL = Number of spaces to write
;         SpaceMsg
;Outputs: To the screen
;Changes:
;------------------------------------------------------------------------------
WriteSpacesErr:
  PUSH AX          ;Save used registers
  MOV  AL,-1       ;Mark as ERR
  JMP >S00         ;Do it
WriteSpaces:
  PUSH AX          ;Save used registers
  XOR  AL,AL       ;Mark as CON
;  JMP >S00         ;Do it
S00:
  PUSH CX,DX       ;Save used registers
  OR   CL,CL       ;Should we write 0 spaces?
  JZ  >S90         ;If so, we're done
  XOR  CH,CH       ;If OK, change it to a word
  MOV  DX,SpaceMsg ;Point at the message
  OR   AL,AL       ;Write to CON?
  JNZ >S30         ;If not, Handle ERR
S10:               ;Loop to here for each space
  CALL WriteZPause ;Write the space
  LOOP S10         ;Keep going until we're done
  JMP >S90         ;Done
S30:               ;Write to Err
  CALL WriteZErr   ;Write the space
  LOOP S10         ;Keep going until we're done
S90:               ;We're done
  POP  DX,CX       ;Restore used registers
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT NIBBLE, BYTE, OR WORD TO HEX CHARACTER STRING, AND WRITE IT
;Inputs:  AL/AX = Nibble/Byte/Word to be written
;         CLD must be set already!
;Outputs: Writes to the Screen
;Changes: HexString
;------------------------------------------------------------------------------
WriteNibbleHex:
  PUSH BX             ;Save used registers
  MOV  BX,3           ;Need to start of offset 3 in string
  JMP >H00            ;Jump to do it
;WriteByteHex:
;  PUSH BX             ;Save used registers
;  MOV  BX,2           ;Need to start of offset 2 in string
;  JMP >H00            ;Jump to do it
WriteWordHex:
  PUSH BX             ;Save used registers
  XOR  BX,BX          ;Need to start of offset 0 in string
;  JMP >H00            ;Jump to do it
H00:                  ;Start converting the strings
  PUSH DX,DI          ;Save used registers
  MOV  DI,HexString   ;Point DS:[DI] at HexString
  MOV  DX,DI          ;Point DS:[DX] at HexString
  CALL Word2HexString ;Convert the entire Word to a String
  ADD  DX,BX          ;Point DX at the beginning of where we write
  CALL WriteZPause    ;Write it
  POP  DI,DX          ;Restore used registers
  POP  BX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT A BYTE OR WORD TO A HEXADECIMAL STRING (2 OR 4 CHARACTERS)
;Inputs:  AX = Word to Convert to a Hex String
;         DS = Local Data Area
;         CLD already issued
;Outputs: DS:[DI]
;Changes: DI
;------------------------------------------------------------------------------
Byte2HexString:
  PUSH AX,ES            ;Save used registers
  MOV  ES,DS            ;Point ES:[DI] at the String
  CALL Word2HexString10 ;Convert it
  POP  ES,AX            ;Restore used registers
  RET

Word2HexString:
  PUSH AX,BX,ES         ;Save used registers
  MOV  BX,AX            ;Save original Word
  MOV  ES,DS            ;Point ES:[DI] at
  CALL Word2HexString00 ;Convert it
  POP  ES,BX,AX         ;Restore used registers
  RET

Word2HexString00:
  MOV  AL,BH            ;Convert High Byte
  CALL Word2HexString10 ;  to ASCII
  MOV  AL,BL            ;Work with Low Byte
Word2HexString10:       ;Convert Byte to ASCII
  AAM  10h              ;Separate Low & High Nibbles
  XCHG AH,AL            ;Put High Nibble in AL
  CALL Word2HexString20 ;Convert High Nibble to ASCII
  XCHG AH,AL            ;Work with Low Nibble
Word2HexString20:       ;Convert Nibble to ASCII
  ADD  AL,90h           ;Convert BCD
  DAA                   ;  to Decimal
  ADC  AL,40h           ;Convert Decimal
  DAA                   ;  to ASCII
  STOSB                 ;Store the Nibble
  RET

;------------------------------------------------------------------------------
;WRITE A BYTE AS AN INTEGER (+/-)
;Inputs:  AL = Byte to Write
;Outputs:
;Changes:
;------------------------------------------------------------------------------
WriteInteger:
  PUSH AX           ;Save used registers
  OR   AL,AL        ;Is it Positive, negative, or 0?
  JZ  >I40          ;If zero, jump to handle it
  JNS >I30          ;If positive, jump to handle it
I20:                ;Is negative
  NEG  AL           ;Get the Value in AL
  CALL WriteMinus   ;Write the minus sign
  JMP >I50          ;Jump to write the Number
I30:                ;Is Positive
  CALL WritePlus    ;Write a Plus
  JMP >I50          ;Jump to write the Number
I40:                ;Is Zero
  CALL WriteSpace   ;Write a Space
I50:                ;AL contains the Number
  CALL ByteToString ;Write the Number
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE A BYTE OR WORD TO THE SCREEN AS A DECIMAL
;Inputs:  AL/AX = Byte/Word to Write
;Outputs:
;Changes:
;------------------------------------------------------------------------------
WriteByteMin:
  PUSH AX,BX         ;Save used registers
  XOR  AH,AH         ;Make sure high byte is 0
  MOV  BH,1          ;Mark as doing minimum
  JMP >W00           ;Do it
;WriteWordMin:
;  PUSH AX,BX         ;Save used registers
;  MOV  BH,1          ;Mark as doing minimum
;  JMP >W00           ;Do it
ByteToString:
  PUSH AX,BX         ;Save the original word & BP
  XOR  AH,AH         ;Make sure high byte is 0
  XOR  BH,BH         ;BH = No Minimum, Byte
  JMP >W00           ;Jump to start the loop
WordToString:
  PUSH AX,BX         ;Save the original word
  MOV  BH,2          ;BP = No Minimum, Word
W00:                 ;Do a word or a byte
  PUSH CX,DX,SI      ;Save used registers
  MOV  SI,SingleMsg  ;Point at the single character message
  XOR  BL,BL         ;Test for first non-zero character is false
  MOV  CX,10000      ;First divisor is 10,000
W20:                 ;Loop to here for each character
  XOR  DX,DX         ;High word of Dividend = 0
  DIV  CX            ;Divide DX:AX by current divisor
  OR   AX,AX         ;Is this a non-zero character?
  JNZ >W30           ;If so, we need to write it
  OR   BL,BL         ;Has there already been a non-zero character?
  JNZ >W30           ;If so, we need to write it
  CMP  CX,1          ;Is this the last character?
  JE  >W30           ;If so, we need to write it
  TEST BH,1          ;Are we doing a minimum?
  JNZ >W40           ;If not, don't write anything
  MOV  AL,' '        ;Assume a space
  TEST BH,2          ;Doing a word?
  JNZ >W35           ;If so, write the space
  CMP  CX,100        ;If not, doing the hundreds character?
  JBE >W35           ;If so, write the space
  JMP >W40           ;If not, don't write anything
W30:                 ;We need to write this character
  INC  BL            ;Test for first non-zero character is true
  ADD  AL,'0'        ;Convert the Number to ASCII
W35:                 ;Write the character
  MOV  [SI],AL       ;Write the
  CALL WriteZPauseSI ;  character
W40:                 ;Skip this character - just make it a space
  CMP  CX,1          ;Was this the last division to do?
  JE  >W90           ;If so, we're done
  MOV  AX,DX         ;If not, start next loop with remainder of division
  PUSH AX            ;Save register for a second
  XOR  DX,DX         ;So dividend won't be messed up
  MOV  AX,CX         ;Want to divide divisor in CX
  MOV  CX,10         ;Want to divide by 10
  DIV  CX            ;Divide it
  MOV  CX,AX         ;Store it back in CX
  POP  AX            ;Restore register
  JMP  W20           ;And do it again
W90:                 ;We're done
  POP  SI,DX,CX      ;Restore used registers
  POP  BX,AX         ;Restore original word & BP
  RET

;------------------------------------------------------------------------------
;SEARCH A BYTE-TO-STRING LOOKUP TABLE FOR A MATCHING ENTRY
;Inputs:  DS:[BX] = Table to search
;         AL      = Byte to search for
;         CLD already issued
;Outputs: CF = Clear if a byte match as found in table
;              BX = Pointer to the String
;            = Set if no match found in table
;              BX = unchanged
;Changes:
;NOTES: Last Entry in Table (end-of-table marker) = DW -1
;------------------------------------------------------------------------------
SearchByte2StringTbl:
  PUSH AX,SI            ;Save used registers
  CLD                   ;Go forward with string functions
  MOV  SI,BX            ;Put pointer in SI
  MOV  AH,AL            ;Put byte to check in AH
S10:                    ;Loop to here for each table entry
  LODSB                 ;Get the next table entry
  CMP  AL,-1            ;Is it possibly the end of the table?
  JNE >S20              ;If not, continue
  CMP  B [SI],-1        ;Is it actually the end of the table?
  JE  >S70              ;If so, there's no match
S20:                    ;Not end of table
  CMP  AL,AH            ;Is it a match?
  JE  >S80              ;If so, handle it!
  CALL SkipOverStringSI ;If not, skip over the string
  JMP  S10              ;And keep looking
S70:                    ;No entry found
  STC                   ;Set the not found flag
  JMP >S90              ;Quit
S80:                    ;Entry found [SI]
  MOV  BX,SI            ;Point BX at the String
  CLC                   ;Set the found flag
S90:                    ;Done
  POP  SI,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER AN ASCIIZ STRING
;Inputs:  DS:[SI] = ASCIIZ String to Skip Over
;Outputs:
;Changes: [SI] points at character after the 0
;------------------------------------------------------------------------------
SkipOverStringSI:
  PUSH CX            ;Save used registers
  CALL CalcStrSizeSI ;Calculate the size of the string
  ADD  SI,CX         ;Add it to the pointer
  INC  SI            ;Skip over the 0
  POP  CX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE THE LENGTH OF AN ASCIIZ STRING
;Inputs:  DS:[SI] = ASCIIZ String to wrok with
;Outputs: CX = Length of String
;         ZF = Set if string is 0 length
;            = Clear if not
;Changes:
;------------------------------------------------------------------------------
CalcStrSizeSI:
  PUSH DX            ;Save used registers
  MOV  DX,SI         ;Calculate the
  CALL CalcStrSizeDX ;  length of the string
  POP  DX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE AN ASCIIZ STRING, POINTED AT BY SI, TO PAUSE
;Inputs:  DS:[SI] = ASCIIZ String to Write
;Outputs: To Screen (CON)
;Changes:
;------------------------------------------------------------------------------
WriteZPauseSI:
  PUSH DX          ;Save used registers
  MOV  DX,SI       ;Write the
  CALL WriteZPause ;  string
  POP  DX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE ASCIIZ STRING TO CON OR TO ERR
;Inputs:  DS:[DX] = Pointer to string
;         CS = Local Data Area
;Outputs: Writes the string to CON or ERR
;Changes:
;------------------------------------------------------------------------------
WriteZCon:                  ;Write to CON
  CMP  CS:PauseHeaderPtr,-1 ;Should we write anything?
  JE  >Z90                  ;If not, just quit
  PUSH BX                   ;Save used register
  MOV  BX,StdOutHandle      ;Write to STDOUT
  JMP >Z00                  ;Jump to do it
WriteZErr:                  ;Write to ERR
  PUSH BX                   ;Save used register
  MOV  BX,StdErrHandle      ;Write to STDERR
Z00:
  PUSH AX,CX                ;Save used registers
  CALL CalcStrSizeDX        ;Calculate the size of the string (returns CX)
  JZ  >Z80                  ;If nothing to write, just quit
  MOV  AH,40h               ;Function 40h (Write to Device)
  INT  21h                  ;Do it
Z80:                        ;We're done
  POP  CX,AX                ;Restore used registers
  POP  BX                   ;Restore used register
Z90:                        ;We're done
  RET

;------------------------------------------------------------------------------
;CALCULATE THE LENGTH OF AN ASCIIZ STRING
;Inputs:  DS:[DX] = Pointer to the string
;Outputs: CX = Length of the string
;         ZF = Set if string is 0 length
;            = Clear if not
;Changes:
;------------------------------------------------------------------------------
CalcStrSizeDX:
  PUSH AX,DI  ;Save used registers
  PUSH ES     ;Save used registers
  MOV  ES,DS  ;Point ES:[DI]
  MOV  DI,DX  ; at the string
  XOR  AL,AL  ;Look for a 0
  MOV  CX,-1  ;Start with max count for REPNE
  REPNE SCASB ;Find the end of the string
  NOT  CX     ;Calculate the size
  DEC  CX     ;  of the string (sets ZF)
  POP  ES     ;Restore used registers
  POP  DI,AX  ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO PARSE THE COMMAND LINE FOR SWITCHES, AND PRINT ERRORS.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;PARSE THE COMMAND LINE FOR SWITCHES
;Inputs:
;Outputs: All kinds of stuff!
;Changes:
;------------------------------------------------------------------------------
ParseIt:
  CALL FixCmdLineCR    ;Replace CR at end of Command Line with a 0
  CALL FillStdInBuffer ;Copy redirected input to our buffer, if needed
                       ;  Quits if error
  MOV  ParseTest,Yes   ;Test input strings for errors
  CALL ParseThings     ;Do all input strings
  MOV  ParseTest,No    ;Parse string for real
  CALL ParseThings     ;Do all input strings
P90:
  RET

ParseThings:
  PUSH SI                       ;Save used registers
  MOV  SI,EnvVarPointer         ;Point at our Environment Variable
  CMP  SI,-1                    ;Is there an Environment Variable to parse?
  JE  >P30                      ;If no, skip it
  OR   ProgFlags2,DoEnvir       ;Mark as doing the Environment
  CMP  ParseTest,Yes            ;Just doing the Test Run?
  JE  >P20                      ;If so, don't write the variable contents yet
  MOV  DX,EnvMsg                ;Write the first part of the
  CALL WriteZPause              ;  environment message to the screen
  CALL WriteEnvCon              ;Write the environment variable contents
  CALL WriteCrLfPause2          ;Move down
P20:                            ;Do the Environment Variable
  CALL ParseCmdLine             ;Parse the Environment (quits if Error)
P30:                            ;Do StdIn
  AND  ProgFlags2,(NOT DoEnvir) ;Mark as doing Command Line
  OR   ProgFlags2,DoStdIn       ;Parse the
  XOR  SI,SI                    ;  StdIn buffer
  CALL ParseCmdLine             ;  Quit if error
  AND  ProgFlags2,(NOT DoStdIn) ;  is found
  MOV  SI,81h                   ;Assume "real" Command Line
  CMP  CmdLineVarPointer,-1     ;Have a CMDLINE Environment variable?
  JE  >P50                      ;If not, just do regular command line
  MOV  SI,CmdLineVarPointer     ;If so, point at the variable
P50:                            ;Pointed at Command Line Tail
  CALL ParseCmdLine             ;Parse Command Line
  POP  SI                       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PARSE THE COMMAND LINE FOR SWITCHES
;Inputs:  ParseTest
;         [SI] = Option String to Parse
;Outputs:
;Changes: SI
;------------------------------------------------------------------------------
ParseCmdLine:
  PUSH AX             ;Save used registers
P10:                  ;Look for the next switch
  CALL IsSpaceOrEOX   ;Look for a valid character
  JZ  >P90            ;If EOL, we're done
  CMP  AL,'/'         ;Is it a slash?
  JE  >P20            ;If so, jump to handle it
  CMP  AL,'-'         ;Is it a dash?
  JE  >P20            ;If so, jump to handle it
  DEC  SI             ;If not, point back at the first character
  JMP >P30            ;Continue
P20:                  ;It's a slash or a dash
  CALL IsSpaceOrEOL   ;Look for a valid switch character
  JZ  >P70            ;If no character, there's an error
P30:                  ;Look for the switch
  CALL GetSwitch      ;See if it matches one of our switches
  JNZ  P10            ;If a match, look for the next switch
P70:                  ;Error - tell the user where it was
  CALL WriteCmdErr    ;Display the error for the user
  MOV  AL,ErLvlOption ;ErrorLevel = Bad Option on command line
  MOV  DX,NoMsg       ;Point at an empty message
  JMP  Exit           ;Print error message and quit
P90:                  ;We're done
  POP  AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE SWITCH CHARACTERS, AND DO WHATEVER IT IS THEY'RE ASKING
;Inputs:  DS:[SI] = Command line pointer
;Outputs: Lots of stuff!!!
;         ParseError
;         ZF = Set if Error
;            = Clear if OK
;Changes:
;------------------------------------------------------------------------------
GetSwitch:
  PUSH AX,BX,DI,BP         ;Save used registers
  MOV  BX,SwitchList       ;Point at the list of switch code words
  CALL GetCode             ;See if its a match
  JNC >S10                 ;If so, jump to test it
  CALL GetNextChar         ;If not, it's a single character
  CALL Capitalize          ;Capitalize it
S10:                       ;We have a Switch to test!
  MOV  BX,SwitchTbl        ;Point at the Table of Offsets
S20:                       ;Loop to here for each table entry
  MOV  AH,[BX]             ;Get the switch from our table?
  OR   AH,AH               ;Is it the end of the table?
  JZ  >S70                 ;If so, there's an error
  CMP  AL,AH               ;If a valid entry, is it a match?
  JE  >S30                 ;If so, jump to handle it
  ADD  BX,3                ;If not, point at the next table entry
  JMP  S20                 ;And keep looking
S30:                       ;It's a match from the table
  MOV  DI,OFFSET ProgFlags ;Point [DI] at ProgFlags
  MOV  BP,OFFSET TSRFlags  ;Point [BP] at TSRFlags
  MOV  AH,ParseTest        ;Pass the "Testing" flag
  OR   AH,AH               ;Set the ZF for Testing
  CALL W [BX+1]            ;Do the Switch stuff
  JMP >S90                 ;And quit
S70:                       ;There's an error
  MOV  ParseError,Yes      ;Mark the error flag
S90:                       ;We're done
  CMP  ParseError,Yes      ;Set/Clear the error flag
  POP  BP,DI,BX,AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Display Help (Syntax) Message
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchH:
  OR   B [DI+1],Help ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Display ErrorLevel Table
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchErrLvl:
  OR   B [DI+2],ErrLvl ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Display list of Command-line (Option) Aliases
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchA:
  OR   B [DI+2],Alias ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Display Raw Data
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchRaw:
  OR   B [DI+2],RawData ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Uninstall Program from Memory
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchU:
  OR   B [DI],Uninstall ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Install Program in Lower Memory, even if Upper Memory is available
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchL:
  PUSH AX                   ;Save used registers
  MOV  AL,UseLowMemory      ;Bit mask to use
  CALL DoSwitchWithYNPrgFwd ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Display Program Status
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchS:
  OR   B [DI+2],Status ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Display HID Report Descriptor of Last Device found
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchR:
  OR   B [DI+2],Report ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Display list of Bad Devices
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchB:
  OR   B [DI+2],BadDevices ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Display USB Descriptors of Last Device found
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchC:
  OR   B [DI+2],Descriptors ;Set the appropriate Flag
  RET

;------------------------------------------------------------------------------
;Reset BIOS Driver and PS2 Hardware
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchReset:
  OR   B [DI+2],Reset ;Set the appropriate flag
  RET

;------------------------------------------------------------------------------
;Install TSR into Memory (used with Resolution/Scaling/SampleRate/Reset
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchI:
  PUSH AX                   ;Save used registers
  MOV  AL,NoInstall         ;Bit mask to use
  CALL DoSwitchWithYNPrgRvs ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Beep Speaker when "Bad" Devices are discovered
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchBeep:
  PUSH AX                   ;Save used registers
  MOV  AL,NoBeep            ;Bit mask to use
  CALL DoSwitchWithYNTsrRvs ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Use BIOS (vs Hardware) Emulation
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchBIOS:
  PUSH AX                   ;Save used registers
  MOV  AL,EmuHardware       ;Bit mask to use
  CALL DoSwitchWithYNTsrRvs ;Do it
  POP  AX                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Set Hardware Emulation Method (1, 2, or 3)
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchM:
  PUSH AX              ;Save used registers
  CALL SkipColonEquals ;Skip Colon/Equals (if there)
  CALL GetDecimal      ;If not, see if it's a number
  JC  >M70             ;If not, error
  CMP  AX,3            ;More than it should be?
  JA  >M70             ;If so, error
  OR   AL,AL           ;Less than it should be?
  JZ  >M70             ;If so, error
  CMP  ParseTest,Yes   ;Is this just the test run?
  JE  >M90             ;If so, we're done
  MOV  ES:Method,AL    ;If for real, store it
  JMP >M90             ;Done
M70:                   ;Error
  MOV  ParseError,Yes  ;Mark Error Flag
M90:                   ;Done
  POP  AX              ;Restore used registers
  RET

;;------------------------------------------------------------------------------
;;Set Number Of Wheels (0, 1, or 2)
;;Inputs:   DS  = Local Data Area
;;          ES  = TSR Data Area
;;         [DI] = ProgFlags
;;         [SI] = Command Line Pointer
;;         [BP] = TSRFlags
;;          AH  = ParseTest, ZF Set if ParseTest = No
;;------------------------------------------------------------------------------
;DoSwitchW:
;  PUSH AX              ;Save used registers
;  CALL SkipColonEquals ;Skip Colon/Equals (if there)
;  CALL GetDecimal      ;If not, see if it's a number
;  JC  >W70             ;If not, error
;  CMP  AX,2            ;More than it should be?
;  JA  >W70             ;If so, error
;  OR   AL,AL           ;Is it 0?
;  JZ  >W20             ;If so, it's OK like it is
;  ADD  AL,(DvcIDWheel1Yes - 1) ;If not, adjust it to the appropriate value
;W20:                   ;AL contains appropriate value to store
;  CMP  ParseTest,Yes   ;Is this just the test run?
;  JE  >W90             ;If so, we're done
;  MOV  ES:DeviceID,AL  ;If for real, store it
;  JMP >W90             ;Done
;W70:                   ;Error
;  MOV  ParseError,Yes  ;Mark Error Flag
;W90:                   ;Done
;  POP  AX              ;Restore used registers
;  RET

;------------------------------------------------------------------------------
;Disable/Enable individual Devices
;eXchange L/R Buttons on Mice
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchD:
  PUSH CX                                     ;Save used registers
  XOR  CL,CL                                  ;Mark as a Disable
  JMP >D00                                    ;Do it
DoSwitchE:
  PUSH CX                                     ;Save used registers
  MOV  CL,1                                   ;Mark as an Enable
  JMP >D00                                    ;Do it
DoSwitchX:
  PUSH CX                                     ;Save used registers
  MOV  CL,2                                   ;Mark as an Exchange
;  JMP >D00                                    ;Do it
D00:
  PUSH AX,BX,DI                               ;Save used registers
  CALL SkipColonEquals                        ;Skip Colon/Equals (if there)
  MOV  BX,SwitchList2                         ;Point at the list of code words
  CALL GetCode                                ;See if its a match
  JNC >D10                                    ;If so, jump to test it
  CALL GetDecimal                             ;If not, see if it's a number
  JC  >D70                                    ;If not, error
  OR   AH,AH                                  ;Is it more than 255?
  JNZ >D70                                    ;If so, Error
D10:                                          ;Got a number (AL)
  CMP  AL,MaxDevices                          ;Is it valid?
  JBE >D20                                    ;If so, continue
  CMP  CL,2                                   ;Is it Exchange?
  JNE >D70                                    ;If not, Error
  CMP  AL,255                                 ;If so, is it Global?
  JNE >D70                                    ;If not, Error
D20:                                          ;Valid Number
  CMP  ParseTest,Yes                          ;Is this just the Test Run?
  JE  >D90                                    ;If so, don't change anything yet
  CMP  AL,MaxDevices                          ;Is it a single Mouse?
  JBE >D40                                    ;If so, jump to handle it
D30:                                          ;Global
  XOR  ES:TSRFlags,SwapBtns                   ;Toggle Global Button Swap Status
  JMP >D90                                    ;Done
D40:                                          ;Single Mouse
  MOV  BL,AL                                  ;Get the
  CALL DIIndex2OffsetBL                       ;  Info Table pointer (DI)
  CMP  CL,1                                   ;Disable, Enable, or Exchange?
  JE  >D45                                    ;If Enable, handle it
  JA  >D47                                    ;If Exchange, Handle it
D42:                                          ;Disable Mouse
  CALL RlsPressedSysESDI                      ;Release any pending keystrokes
  OR   ES:[DI].DIFlags,DIFlagDisabled         ;Disable it
  AND  ES:[DI].DIFlags,(NOT DIFlagDisconnect) ;Mark Pending keystrokes as done
  JMP >D90                                    ;Done
D45:                                          ;Enable Mouse
  AND  ES:[DI].DIFlags,(NOT DIFlagDisabled)   ;Enable it
  JMP >D90                                    ;Done
D47:                                          ;Swap Mouse Buttons
  XOR  ES:[DI].DIFlags,DIFlagSwapBtns         ;Toggle Button Swap Status
  JMP >D90                                    ;Done
D70:                                          ;Error
  MOV  ParseError,Yes                         ;Mark Error Flag
D90:                                          ;Done
  POP  DI,BX,AX                               ;Restore used registers
  POP  CX                                     ;Restoreused registers
  RET

;------------------------------------------------------------------------------
;Change Resolution of Mice
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchResolution:
  PUSH AX,BX              ;Save used registers
  CALL SkipColonEquals    ;Skip Colon/Equals (if there)
  CALL GetDecimal         ;If not, see if it's a number
  JC  >R70                ;If not, error
  CMP  AX,8               ;More than it should be?
  JA  >R70                ;If so, error
  MOV  BH,Resolution8     ;Assume 8 Counts/mm
  JE  >R50                ;If 8 Counts/mm, Jump to do it
  DEC  BH                 ;Assume 4 Counts/mm
  CMP  AL,4               ;4 Counts/mm?
  JE  >R50                ;If so, Jump to do it
  DEC  BH                 ;Assume 2 Counts/mm
  CMP  AL,2               ;2 Counts/mm?
  JE  >R50                ;If so, Jump to do it
  DEC  BH                 ;Assume 1 Count/mm
  CMP  AL,1               ;1 Count/mm?
  JNE >R70                ;If not, Error
R50:                      ;Resolution is calculated
  MOV  ResolutionValue,BH ;Store the Change Value
  JMP >R90                ;Done
R70:                      ;Error
  MOV  ParseError,Yes     ;Mark Error Flag
R90:                      ;Done
  POP  BX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Change Sampling Rate of Mice
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchSampling:
  PUSH AX,BX            ;Save used registers
  CALL SkipColonEquals  ;Skip Colon/Equals (if there)
  CALL GetDecimal       ;If not, see if it's a number
  JC  >S70              ;If not, error
  CMP  AX,200           ;More than it should be?
  JA  >S70              ;If so, error
  MOV  BH,SampleCode200 ;Assume 200 Hz
  JE  >S50              ;If 200 Hz, Jump to do it
  DEC  BH               ;Assume 100 Hz
  CMP  AL,100           ;100Hz?
  JE  >S50              ;If so, Jump to do it
  DEC  BH               ;Assume 80 Hz
  CMP  AL,80            ;80 Hz?
  JE  >S50              ;If so, Jump to do it
  DEC  BH               ;Assume 60 Hz
  CMP  AL,60            ;60 Hz?
  JE  >S50              ;If so, Jump to do it
  DEC  BH               ;Assume 40 Hz
  CMP  AL,40            ;40 Hz?
  JE  >S50              ;If so, Jump to do it
  DEC  BH               ;Assume 20 Hz
  CMP  AL,20            ;20 Hz?
  JE  >S50              ;If so, Jump to do it
  DEC  BH               ;Assume 10 Hz
  CMP  AL,10            ;10 Hz?
  JNE >S70              ;If not, Error
S50:                    ;Sampling Rate is calculated
  MOV  SamplingValue,BH ;Store the Change Value
  JMP >S90              ;Done
S70:                    ;Error
  MOV  ParseError,Yes   ;Mark Error Flag
S90:                    ;Done
  POP  BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;Change Scaling Factor of Mice
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = ProgFlags
;         [SI] = Command Line Pointer
;         [BP] = TSRFlags
;          AH  = ParseTest, ZF Set if ParseTest = No
;------------------------------------------------------------------------------
DoSwitchScaling:
  PUSH AX,BX           ;Save used registers
  CALL SkipColonEquals ;Skip Colon/Equals (if there)
  CALL GetDecimal      ;If not, see if it's a number
  JC  >C70             ;If not, error
  CMP  AX,2            ;More than it should be?
  JA  >C70             ;If so, error
  OR   AL,AL           ;Less than it should be?
  JZ  >C70             ;If so, error
  MOV  BH,AL           ;If OK, put it in BH for the call
  CALL IsSpaceOrEOX    ;Get the next character
  JZ  >C40             ;If EOL, we're done
  CMP  AL,':'          ;Is it a Colon?
  JNE >C40             ;If not, don't look for the 1
  CALL IsSpaceOrEOX    ;If so, get the next character
  JZ  >C70             ;If EOL, error
  CMP  AL,'1'          ;Is it a 1?
  JNE >C70             ;If not, error
  JMP >C50             ;If so, it's OK
C40:                   ;Done looking for the ":1"
  DEC  SI              ;Point back at the previous character
C50:                   ;Scaling Factor is calculated
  MOV  ScalingValue,BH ;Store the Change Value
  JMP >C90             ;Done
C70:                   ;Error
  MOV  ParseError,Yes  ;Mark Error Flag
C90:                   ;Done
  POP  BX,AX           ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE NEEDED TO HANDLE A NOINSTALL OPTION
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;HANDLE A NO-INSTALL REQUEST FROM THE USER, IF APPROPRIATE
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         ProgFlags.InMemory,ProgFlags.NoInstall
;Outputs: CF = Set if not in Memory yet, and User doesn't want to Install
;              Writes PS/2 Status, if Requested by User
;              DX = Message to write on Program Exit
;            = Set if Already in Memory, or User wants to Install
;              DX = Undefined
;Changes:
;NOTES: The intent of the NoInstall Option is to allow the User to issue
;         the Change Scaling/Change Resolution/Change Sample Rate
;         functions to the "real" PS/2 mouse, without needing USBMOUSE
;         installed in memory.
;------------------------------------------------------------------------------
HandleNoInstall:
  PUSH AX,BX,CX            ;Save used registers
  TEST ProgFlags,InMemory  ;Already installed as a TSR?
  JNZ >I80                 ;If so, continue
  TEST ProgFlags,NoInstall ;User wants to Install?
  JZ  >I80                 ;If so, continue
  CALL ChangeRates         ;If not, do the changes the User requested
  TEST StatusFlags,Status  ;Does the User want the Status?
  JZ  >I70                 ;If not, we're done
  MOV  DX,BIOSErrMsg       ;Assume an Error
  MOV  AX,0C200h+I15ALStatusScaling ;Function = Get Status/Set Scaling
  XOR  BH,BH               ;SubFunction = Get Status
  STC                      ;Preset Error Flag
  INT  15h                 ;Do it (Rtns BL = Bit Flags
                           ;            CL = Resolution Code
                           ;            DL = Sample Rate Code)
  JC  >I75                 ;If Error, Quit
  MOV  BH,DL               ;Put Sample Rate Code in BH
I10:                       ;Write Hardware Enabled Status
  MOV  DX,EnabledMsg       ;Write the
  CALL WriteZPause         ;  Header & Description
  MOV  DX,YesMsg           ;Assume Enabled
  TEST BL,StatusEnabled    ;Is it Enabled?
  JNZ >I15                 ;If so, continue
  MOV  DX,NoMsgS           ;If not, Disabled
I15:                       ;DX points at correct Message
  CALL WriteZPause         ;Write Enabled Status
I20:                       ;Write Number of Enabled Wheels
  MOV  DX,WheelsMsg        ;Write the
  CALL WriteZPause         ;  Description
  CALL GetNumWheels        ;Get the number of Enabled Wheels
  CALL ByteToString        ;Write it
I30:                       ;Write Resolution
  MOV  DX,ResolutionMsg    ;Write the
  CALL WriteZPause         ;  Description
  MOV  AL,1                ;Calculate the
  SHL  AL,CL               ;  Resolution
  CALL ByteToString        ;Write it
I40:                       ;Write Scaling Factor
  MOV  DX,ScalingMsg       ;Write the
  CALL WriteZPause         ;  Description
  MOV  DX,Scaling1Msg      ;Assume 1:1 Scaling
  TEST BL,StatusScaling2   ;Is it 1:1?
  JZ  >I45                 ;If so, continue
  MOV  DX,Scaling2Msg      ;If not, 2:1
I45:                       ;DX points at correct Message
  CALL WriteZPause         ;Write it
I50:                       ;Write Sample Rate
  MOV  DX,SamplesMsg       ;Write the
  CALL WriteZPause         ;  Description
  MOV  AL,BH               ;Write the
  CALL ByteToString        ;  Sample Rate
  CALL WriteCrLfPause      ;Move down
  MOV  DX,NoInstallMsg     ;Point at next message to write
  JMP >I75                 ;Done
I70:                       ;User wants No Installation
  MOV  DX,BIOSUpdateMsg    ;Exit Message to write
I75:                       ;DX points at Exit Message
  STC                      ;Set No-Install flag
  JMP >I90                 ;Done
I80:                       ;Need to Install
  CLC                      ;Set continue flag
I90:                       ;Done
  POP  CX,BX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CALCULATE HOW MANY WHEELS ARE ENABLED IN THE PS/2 MOUSE BIOS
;Inputs:
;Outputs:  AL = Number of Enabled Wheels
;Changes:
;------------------------------------------------------------------------------
GetNumWheels:
  PUSH BX,CX             ;Save used registers
  MOV  CH,AH             ;Save original AH
  MOV  AX,0C200h+I15ALGetDeviceID ;Function = Get Device ID
  INT  15h               ;Do it (returns BH)
  XOR  AL,AL             ;Assume no Wheels Enabled
  CMP  BH,DvcIDWheel1Yes ;How many Wheels are Enabled?
  JB  >W90               ;If none, we're done
  JE  >W20               ;If one, jump to increment wheels by one
  INC  AL                ;Increment number of Enabled Wheels
W20:                     ;One Wheel Enabled
  INC  AL                ;Increment number of Enabled Wheels
W90:                     ;Done
  MOV  AH,CH             ;Restore original AH
  POP  CX,BX             ;Restore Used Registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE AND RESET THE REAL PS2 MOUSE, IF ONE IS INSTALLED
;Inputs:  DS = ES = TSR Data Area
;         CS = Local Data Area
;Outputs: DIPS2Offset contains Data
;Changes:
;NOTES: We will allow the user to use any or all mice at the same time, and
;         "add" them together in a single BIOS Interface.  A call made by
;         the user to change something will change all mice (PS2 & USB).
;       We will pass all appropriate user calls on to the original (BIOS)
;         routine for processing, and then process our code afterwards (we
;         control exactly what gets returned to the caller).
;       Because we Reset the "real" PS2 Mouse driver, the user will need to
;         reset the Int33h Mouse Driver if it was installed before we were.
;         The User Far Call Address will be destroyed by us, and the Driver
;         won't work.
;       Logitech and Microsoft Mouse drivers should still be able to use the
;         PS2 Mouse, but won't be able to use the USB Mice, because they do
;         not exclusively use the BIOS for access to the Mouse.
;------------------------------------------------------------------------------
ResetPS2Mouse:
  PUSH AX,BX,DI                     ;Save used registers
  TEST TSRFlags,EmuHardware         ;Are we emulating Hardware?
  JNZ >P90                          ;If so, don't do anything
  PUSH DX,DS                        ;Save used registers
  MOV  DS,CS                        ;Point DS at the Local Data Area
  MOV  DX,ResetPS2Msg               ;Assume we're installing the program
  TEST ProgFlags,InMemory           ;Are we already installed?
  JZ  >P10                          ;If not, continue
  MOV  DX,ResetPS2Msg2              ;If so, point at correct message
P10:                                ;DX points at Message to Write
  CALL WriteZPause                  ;Write the MEssage
  POP  DS,DX                        ;Restore used registers
  MOV  DI,DIPS2Offset               ;Reset the
  CALL ResetDvcEntryDI              ;  PS2 Mouse Entry
  MOV  AX,0C200h+I15ALReset         ;Function = Reset Mouse
  STC                               ;Preset Error Flag
  CALL DoPS2Int15h                  ;Do it
  JC  >P90                          ;If error, no PS2 Mouse Installed
  OR   CS:ProgFlags,PS2ResetDone    ;Mark the PS2 Reset as done
  MOV  BH,3                         ;Assume not IntelliMouse
  CALL SetIntelliMouseProtocol      ;Try to set the IntelliMouse protocol
  JZ  >P20                          ;If not IntelliMouse, continue
  INC  BH                           ;Is IntelliMouse(2) compatible
P20:                                ;Done setting up IntelliMouse
  CALL InitPS2Structure             ;Initialize the PS2 Data Structure Offsets
  MOV  AX,0C200h+I15ALInitialize    ;Function = Initialize (BH = # Data Bytes)
  CALL DoPS2Int15h                  ;Do it
  CALL SetIntelliMouseProtocol      ;Initialize the IntelliMouse Protocol again
;  MOV  BH,Resolution8               ;Set Resolution
;  MOV  AX,0C200h+I15ALSetResolution ;  at 8 counts/mm
;  CALL DoPS2Int15h                  ;Do it
;  TEST TSRFlags,EmuHardware         ;Are we emulating Hardware?
;  JNZ >P90                          ;If so, don't Enable the PS2 Handler
  MOV  BX,PS2FarCall                ;Point ES:[BX] at our PS2 Call Address
  MOV  AX,0C200h+I15ALSetHandler    ;Function = Set Handler Address
  CALL DoPS2Int15h                  ;Do it
  MOV  BH,1                         ;SubFunction = Enable Handler
  MOV  AX,0C200h+I15ALEnableDisable ;Function = Disable/Enable Handler
  CALL DoPS2Int15h                  ;Do it
P90:                                ;Done
  POP  DI,BX,AX                     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;INITIALIZE THE PS2 STRUCTURE WITH APPROPRIATE OFFSET DATA
;Inputs:  DS = ES = TSR Data Area
;         PS2 Mouse already verified and IntelliMouse Protocol Enabled
;         [DIPS2Offset].NumWheels already filled in
;Outputs: [DIPS2Offset].DIXOffset, .YOffset, .Z1Offset, .Z2Offset
;Changes:
;------------------------------------------------------------------------------
InitPS2Structure:
  PUSH AX,CX,DI                          ;Save used registers
  MOV  DI,DIPS2Offset+(OFFSET DIXOffset) ;ES:[DI] = Where to store PS2 Offsets
  MOV  AX,DIPS2Offset+DIXDataPS2         ;AX = First Offset (PS2 X Data)
  MOV  CX,2                              ;Start with no wheels
  ADD  CL,[DIPS2Offset].DINumWheels      ;Add the actual number of wheels
P10:                                     ;Loop to here for each Offset
  STOSW                                  ;Store the
  INC  AX                                ;  next Offset
  LOOP P10                               ;Keep going until we're done
  POP  DI,CX,AX                          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TRY TO SET THE INTELLIMOUSE PROTOCOL IN THE PS2 BIOS
;Inputs:  DS = ES = TSR Data Area
;         CS = Local Data Area
;         [DI] = PS2 Device Info Entry
;         PS2 Mouse already verified as installed
;Outputs: [DI].DIFlagInUse, [DI].DINumButtons, [DI].DINumWheels
;         ZF = Set if IntelliMouse(s) not supported
;             = Clear if IntelliMouse or IntelliMouse2 is supported
;Changes:
;------------------------------------------------------------------------------
SetIntelliMouseProtocol:
  PUSH AX,BX                      ;Save used registers
  OR   [DI].DIFlags,DIFlagInUse   ;Mark as In Use
  MOV  [DI].DINumButtons,2        ;Default Buttons = 2
  MOV  [DI].DINumWheels,0         ;Default Wheels = 0
  CALL SetSample200               ;Try to
  CALL SetSample100               ;  Enable
  CALL SetSample080               ;  the Wheel
  CALL SetSample100               ;Restore Default Sample Rate
  MOV  AX,0C200h+I15ALGetDeviceID ;Function = Get Device ID
  CALL DoPS2Int15h                ;Do it
  CMP  BH,DvcIDWheel1Yes          ;Does it have a Wheel?
  JNE >I90                        ;If not, continue
  INC  [DI].DINumWheels           ;If so, mark it as having a Wheel
  INC  [DI].DINumButtons          ;  and three buttons
  CALL SetSample200               ;Try to
  CALL SetSample200               ;  Enable
  CALL SetSample080               ;  the second Wheel
  CALL SetSample100               ;Restore Default Sample Rate
  MOV  AX,0C200h+I15ALGetDeviceID ;Function = Get DeviceID
  CALL DoPS2Int15h                ;Do it
  CMP  BH,DvcIDWheel2Yes          ;Does it have two Wheels?
  JNE >I90                        ;If not, continue
  INC  [DI].DINumWheels           ;If so, mark it as having 2 Wheels
  ADD  [DI].DINumButtons,2        ;  and 5 buttons
I90:                              ;Done
  CMP  [DI].DINumWheels,0         ;Set return flag
  POP  BX,AX                      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SET THE PS2 MOUSE SAMPLE RATE
;Inputs:  DS = ES = TSR Data Area
;         CS = Local Data Area
;Outputs:
;Changes:
;------------------------------------------------------------------------------
SetSample200:
  PUSH BX                         ;Save used registers
  MOV  BH,SampleCode200           ;Sample Rate = 200 Hz
  JMP >S00                        ;Do it
SetSample100:
  PUSH BX                         ;Save used registers
  MOV  BH,SampleCode100           ;Sample Rate = 100 Hz
  JMP >S00                        ;Do it
SetSample080:
  PUSH BX                         ;Save used registers
  MOV  BH,SampleCode080           ;Sample Rate = 80 Hz
;  JMP >S00                        ;Do it
S00:
  PUSH AX                         ;Save used registers
  MOV  AX,0C200h+I15ALSetSampling ;Function = Set Sampling Rate
  CALL DoPS2Int15h                ;Do it
  POP  AX                         ;Restore used registers
  POP  BX                         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PERFORM AN INT 15H BIOS FUNCTION ON THE REAL PS2 MOUSE
;Inputs:  DS = ES = TSR Data Area
;         CS = Local Data Area
;         Other Registers (usually AX and BX) as needed for the Call
;         ProgFlags.InMemory
;         If InMemory = True, ES:Int15Hdr.OldVector contains Old Address
;Outputs: Performs INT 15h function on real PS2 BIOS Handler
;         Does NOT perform it on USB Handler
;         Returns flags as issued by the PS2 Handler
;Changes: Resets and Initializes Wheel(s) on the PS2 Mouse
;------------------------------------------------------------------------------
DoPS2Int15h:
  TEST CS:ProgFlags,InMemory   ;Are we already installed as a TSR?
  JZ  >F50                     ;If not, jump to handle it
  PUSHF                        ;If so, call the
  CALL D ES:Int15Hdr.OldVector ;  Original Interrupt Handler
  JMP >F90                     ;Done
F50:                           ;Not a TSR yet
  INT  15h                     ;Just do a regular Interrupt Call
F90:                           ;Done
  RET

;------------------------------------------------------------------------------
;PERFORM THE INT 15h (BIOS MOUSE CONFIGURATION) CHANGES THE USER REQUESTED
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         StatusFlags.Reset
;         SamplingValue
;         ScalingValue
;         ResolutionValue
;         ES:MouseUserFarCall
;Outputs:
;Changes: Modifies Mouse Driver Settings as appropriate
;NOTES: If we are doing a Reset, we do restore the original Mouse Driver
;         Far Call Address (Int 33h), if it existed.  That way the User
;         can Reset the Mouse here without needing to worry about resetting
;         the Int 33h Driver afterwards.
;------------------------------------------------------------------------------
ChangeRates:
  PUSH AX,BX,DX                     ;Save used registers
C10:                                ;Handle Reset
  TEST StatusFlags,Reset            ;Do a Reset?
  JZ  >C40                          ;If not, continue
  TEST ProgFlags,InMemory           ;Are we already Installed in Memory?
  JZ  >C10                          ;If not, see if we're going to install
  TEST ES:TSRFlags,EmuHardware      ;If so, are we emulating the BIOS?
  JZ  >C30                          ;If so, it's OK to Reset
  JMP >C20                          ;If not, we shouldn't reset
C10:                                ;We're not installed yet
  TEST ProgFlags,NoInstall          ;Will we be Installing the program?
  JZ  >C30                          ;If so, continue
C20:                                ;We can't reset - let the user know
  MOV  DX,NoResetMsg                ;Tell User that we
  CALL WriteZPause                  ;  didn't really Reset it
  JMP >C40                          ;Continue
C30:                                ;May be OK to do Reset
  TEST ProgFlags,PS2ResetDone       ;Have we already Reset the Mouse this time?
  JNZ >C40                          ;If so, don't do it again
  PUSH DS                           ;Save used registers
  MOV  DS,ES                        ;Point DS at TSR Data Area
  CALL ResetPS2Mouse                ;Reset the PS2 Mouse
  POP  DS                           ;Restore used registers
  OR   ProgFlags,PS2ResetDone       ;Mark the Reset as complete
C40:                                ;Handle Resolution
  MOV  BH,ResolutionValue           ;Change
  CMP  BH,-1                        ;  Resolution?
  JE  >C50                          ;If not, continue
  MOV  AX,0C200h+I15ALSetResolution ;Function = Set Resolution)
  INT  15h                          ;Do it
C50:                                ;Handle Sampling Rate
  MOV  BH,SamplingValue             ;Change
  CMP  BH,-1                        ;  Sampling Rate?
  JE  >C60                          ;If not, continue
  MOV  AX,0C200h+I15ALSetSampling   ;Function = Set Sampling Rate
  INT  15h                          ;Do it
C60:                                ;Handle Scaling Factor
  MOV  BH,ScalingValue              ;Change
  CMP  BH,-1                        ;  Scaling?
  JE  >C90                          ;If not, continue
  MOV  AX,0C200h+I15ALStatusScaling ;Function = Set Scaling/Get Status
  INT  15h                          ;Do it
C90:                                ;Done
  POP  DX,BX,AX                     ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE NEEDED TO MANIPULATE THE EXTENDED BIOS DATA AREA (PS2 MOUSE BIOS)
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

  XBDASize     EQU      512         ;Number of XBDA Bytes to get
  OldXBDAData:  DB XBDASize DUP (?) ;Original Data in Extended BIOS Data Area
  XBDAMousePtr  DW        0         ;Pointer to Mouse Handler in XBDA

;NOTE: We can't restore unless Original DeviceID = Current
;        (or Mouse Handler will not know what to do with Data)


;Format of Extended BIOS Data Area (PhoenixBIOS 4.0):
;  (Also applies to IBM & AMI BIOS's)
;Offset  Size    Description     (Table M0005)
; 00h    BYTE    length of XBDA in kilobytes
; 01h 33 BYTEs   reserved
; 22h    DWORD   Pointing Device Driver entry point
; 26h    BYTE    Pointing Device Flags 1 (see #M0002)
; 27h    BYTE    Pointing Device Flags 2 (see #M0003)
; 28h  8 BYTEs   Pointing Device Auxiliary Device Data

;Bitfields for Pointing Device Flags 1:
;Bit(s)  Description     (Table M0002)
; 7      command in progress
; 6      resend byte (FAh) received
; 5      acknowledge byte (FEh) received
; 4      error byte (FCh) received
; 3      unexpected value received
; 2-0    index count for auxiliary device data at 28h

;Bitfields for Pointing Device Flags 2:
;Bit(s)  Description     (Table M0003)
; 7      device driver far call flag
; 6-3    reserved
; 2-0    package size (number of bytes received) - 1





;Copy Several Bytes of XBDA Segment (Old XBDA)
;Install ourselves as Mouse Owner
;Look for our address in XBDA (probably offset 22h)
;  Also look for other mouse data (following our address)?
;If found, pull old address from old XBDA and save it
;If the address wasn't zero to start, make sure we set ALL values to defaults
;  (or check what they are) so we know how we're set up.  Shouldn't need
;  to reset the mouse hardware at all unless nothing was installed before us??
;On uninstall, restore old Pointer Address (if it wasn't 0).  If 0, just reset?

;Inputs:
;         CLD already issued


SaveOldMouse:
  PUSH AX,BX,CX,DI,SI,ES    ;Save used registers
  CALL GetXBDASeg           ;Get the Extended BIOS Data Segment (ES)
  JC  >O90                  ;If none found, quit
  SwapDSES                  ;DS:[SI] =
  XOR  SI,SI                ;  XBDA
  MOV  DI,OldXBDAData       ;ES:[DI] = Our Local Buffer
  MOV  CX,(XBDASize/2)      ;CX = Number of Words to Copy
  REP  MOVSW                ;Copy the XBDA
  SwapDSES                  ;DS = Local Data Area, ES = XBDA


;Verify Mouse Driver is installed?
;Save mouse configuration details in TSR (speed, etc.)
;Reset and Point Mouse Driver at Us
;Look for our address in the XBDA (probably offset 22h)
;  Need to make sure where our address is has actually changed, and not just
;    happenstance the data there matches our address.
;If it's our address, save the original address in the TSR, and save the
;  XBDA Offset where the address is stored.

;On exit, if we found and saved everything, just restore it?  What if
;  Mouse Driver was removed from memory so original pointer is no longer
;  valid?
;  Just monitor changes to Mouse Driver Pointer?  That would tell use when
;    a Driver is being installed/uninstalled (setting to 0 indicates an
;    uninstall).  Keep track of things with a Counter (what if User "Resets"
;    mouse driver without uninstalling)?



O90:                        ;Done
  POP  ES,SI,DI,CX,BX,AX    ;Restore used registers
  RET



;------------------------------------------------------------------------------
;GET THE EXTENDED BIOS DATA AREA (XBDA) SEGMENT
;Inputs:   DS  = Local Data Area
;Outputs: CF = Clear if OK (Valid Segment Found)
;              ES = XBDA Segment
;            = Set if Error (Segment Not Found/Invalid Segment)
;              ES unchanged
;Changes:
;------------------------------------------------------------------------------
GetXBDASeg:
  PUSH AX,BX       ;Save used registers
  MOV  BX,ES       ;Save original ES
X10:               ;Use BIOS Call to Get it
  MOV  AH,0C1h     ;Function C1h (Get XBDA Segment)
  STC              ;Preset error flag
  INT  15h         ;Do it (returns ES)
  JNC >X80         ;If OK, we're done
X20:               ;Get it directly from BIOS Data Area
  MOV  AX,40h      ;Point ES at the
  MOV  ES,AX       ;  BIOS Data Area
  MOV  AX,ES:[0Eh] ;Get the (supposed to be) XBDA Segment
  MOV  ES,AX       ;Put it in ES
  CMP  AX,0050h    ;Is it too small to be legitimate?
  JBE >X70         ;If so, quit
  MOV  AL,ES:[0]   ;Get the size of the XBDA (kBytes)
  OR   AL,AL       ;Too small to be legitimate?
  JZ  >X70         ;If so, error
  CMP  AL,10       ;Too big to be legitimate?
  JBE >X80         ;If not, it's OK
X70:               ;Error
  MOV  ES,BX       ;Restore original ES
  STC              ;Set return flag
  JMP >X90         ;Done
X80:               ;OK
  CLC              ;Set return flag
X90:               ;Done
  POP  BX,AX       ;Restore used registers
  RET












;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;SUPPORT ROUTINES FOR PROCESSING SWITCHES
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;GET A YES/NO OPTION FOR A COMMAND-LINE SWITCH
;Inputs:  [SI] = Command Line Pointer
;Outputs: CF = Clear if OK
;              ZF = Set if No
;                   AL = 0 (No)
;                 = Clear if Yes
;                   AL = -1 (Yes)
;            = Set if Error
;              AL = 0
;Changes:
;------------------------------------------------------------------------------
GetYesNo4Switch:
  PUSH BX,CX              ;Save used registers
  MOV  CH,AH              ;Save original AH
  CALL SkipColonEquals    ;Skip Colon/Equals (if there)
  MOV  BX,SwitchListYesNo ;Point at the list of code words
  CALL GetCode            ;See if its a match
  JNC >Y20                ;If so, jump to test it
  CALL GetNextChar        ;If not, get a single character
  JZ  >Y70                ;If end-of-line, error
  CALL Capitalize         ;Capitalize it
Y20:                      ;Got a character
  CMP  AL,'Y'             ;Is it Yes?
  JE  >Y40                ;If so, handle it
  CMP  AL,'N'             ;Is it No?
  JNE >Y70                ;If not, error
Y30:                      ;No
  XOR  AL,AL              ;If so, make it No
  JMP >Y80                ;Done
Y40:                      ;Yes
  MOV  AL,Yes             ;Mark it as a Yes
  JMP >Y80                ;Done
Y70:                      ;Error
  XOR  AL,AL              ;Set AL to 0
  STC                     ;Set Error Flag
  JMP >Y90                ;Done
Y80:                      ;OK
  OR   AL,AL              ;Set Zero Flag for Return
  CLC                     ;Set OK Flag
Y90:                      ;Done
  MOV  AH,CH              ;Restore original AH
  POP  CX,BX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HANDLE A SWITCH WITH A YES/NO OPTION
;Inputs:  [SI] = Command Line Pointer
;         ES = TSR Data Area
;         DS = Local Data Area
;         AL = Bit mask to Set/Unset
;         AH = ParseTest status
;Outputs: Sets/Unsets bit in ES:TSRFlags or DS:ProgFlags as appropriate
;Changes:
;NOTES: If we are changing the resident (TSR) part of the program
;         (ES:TSRFlags), we do not actually change anything unless
;         ParseTest is set to No.  If we are changing the transient
;         part of the program (DS:ProgFlags), we change it no matter
;         what ParseTest is set to.
;       Forward and Reverse refer to Forward and Reverse Logic.  With
;         Forward Logic, if the user says Yes, we set the particular bit
;         to 1.  With Reverse Logic, when the user says Yes, we set
;         to bit to 0.
;------------------------------------------------------------------------------
DoSwitchWithYNTsrRvs:
  PUSH CX                  ;Save used registers
  MOV  CL,-1               ;Mark as Reverse Logic
  JMP >Y000                ;Jump to do ES:TSRFlags
DoSwitchWithYNTsrFwd:
  PUSH CX                  ;Save used registers
  XOR  CL,CL               ;Mark as Forward Logic
  JMP >Y000                ;Jump to do ES:TSRFlags
DoSwitchWithYNPrgRvs:
  PUSH CX                  ;Save used registers
  MOV  CL,-1               ;Mark as Reverse Logic
  JMP >Y001                ;Jump to do DS:ProgFlags
DoSwitchWithYNPrgFwd:
  PUSH CX                  ;Save used registers
  XOR  CL,CL               ;Mark as Forward Logic
  JMP >Y001                ;Jump to do DS:ProgFlags
Y000:                      ;ES:TSRFlags
  PUSH BX                  ;Save used registers
  MOV  BX,OFFSET TSRFlags  ;Point at TSR Flags
  JMP >Y00                 ;Do it
Y001:                      ;DS:ProgFlags
  PUSH BX                  ;Save used registers
  MOV  BX,OFFSET ProgFlags ;Point at TSR Flags
;  JMP >Y00                 ;Do it
Y00:
  PUSH AX                  ;Save used registers
  MOV  CH,AL               ;Save Bit Mask to set
  CALL GetYesNo4Switch     ;Get the Yes/No parameter (AL)
  JC  >Y70                 ;If error, handle it
  OR   AL,AL               ;Is it Yes?
  JNZ >Y20                 ;If so, jump to handle it
Y10:                       ;No
  OR   CL,CL               ;Is it Forward Logic?
  JZ  >Y40                 ;If so, jump to handle it
  JMP >Y50                 ;If not, jump to handle Reverse Logic
Y20:                       ;Yes
  OR   CL,CL               ;Is it Forward Logic?
  JZ  >Y50                 ;If so, jump to handle it
Y40:                       ;Unset the Bit
  NOT  CH                  ;Need to Unset
  CMP  BX,LastTSRByte      ;Is it for ProgFlags?
  JAE >Y45                 ;If so, jump to handle it
Y42:                       ;Unset ES:TSRFlags
  OR   AH,AH               ;Just the Test Run?
  JNZ >Y90                 ;If so, quit
  AND  ES:[BX],CH          ;Unset the appropriate flag
  JMP >Y90                 ;Done
Y45:                       ;Unset DS:ProgFlags
  AND  [BX],CH             ;Unset the appropriate flag
  JMP >Y90                 ;Done
Y50:                       ;Set the Bit
  CMP  BX,LastTSRByte      ;Is it for ProgFlags?
  JAE >Y55                 ;If so, jump to handle it
Y52:                       ;Set ES:TSRFlags
  OR   AH,AH               ;Just the Test Run?
  JNZ >Y90                 ;If so, quit
  OR   ES:[BX],CH          ;Set the appropriate flag
  JMP >Y90                 ;Done
Y55:                       ;Set DS:ProgFlags
  OR   [BX],CH             ;Set the appropriate flag
  JMP >Y90                 ;Done
Y70:                       ;Error
  MOV  ParseError,Yes      ;Mark as an Error
Y90:                       ;Done
  POP  AX                  ;Restore used registers
  POP  BX                  ;Restore used registers
  POP  CX                  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;REPLACE THE CARRIAGE RETURN AT THE END OF THE CMD LINE TAIL WITH A 0
;Inputs:  CS = DS = PSP Segment
;Outputs:
;Changes: Last Byte of Command Tail
;------------------------------------------------------------------------------
FixCmdLineCR:
  PUSH AX,CX,DI,ES   ;Save used registers
  MOV  AL,CR         ;Look for a CR
  MOV  CX,7Fh        ;Maximize Byte Counter
  MOV  ES,DS         ;Point ES:[DI]
  MOV  DI,81h        ;  at the Command Line Tail
  REPNE SCASB        ;Look for the CR
  MOV  B ES:[DI-1],0 ;Replace the CR with a 0
  POP  ES,DI,CX,AX   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NEXT CHARACTER FROM THE ENVIRONMENT OR COMMAND LINE
;Inputs:  [SI] = Pointer (Command Line or Environment)
;Outputs: AL = The next character
;         ZF = Set if End-Of-Line (Character = ASCII 0)
;            = Clear if "real" character
;Changes: SI
;------------------------------------------------------------------------------
GetNextChar:
  PUSH DS                   ;Save used registers
  TEST ProgFlags2,DoStdIn   ;Are we doing StdIn?
  JNZ >N20                  ;If so, jumpto handleit
  TEST ProgFlags2,DoEnvir   ;Are we doing the Program Environment Variable?
  JNZ >N10                  ;If so, handle it
  CMP  CmdLineVarPointer,-1 ;Need to use the CMDLINE Environment variable?
  JE  >N90                  ;If not, just do the "real" command line
N10:                        ;Get next character from the Environment
  MOV  DS,[PSPEnvirSeg]     ;Point at the Environment Segment
  JMP >N90                  ;Continue
N20:                        ;StdIn
  MOV  DS,ExtraMemSegment   ;Poitn DS at the correct Segment
N90:                        ;DS:[SI] points at the next character
  LODSB                     ;Get the character from the "real" command line
  OR   AL,AL                ;Set ZF if EOL
  POP  DS                   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER SPACES TO FIND THE NEXT LEGITIMATE CHARACTER, TESTING FOR EOL
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: ZF = Set if EOL
;               AL  = Unchanged
;              [SI] = End-of-Line Character
;            = Clear if not EOL
;               AL  = First non-space character
;              [SI] = Character AFTER the next non-space character
;Changes:
;------------------------------------------------------------------------------
IsSpaceOrEOX:
  CALL IsSpaceOrEOL ;Skip over the spaces
  JZ  >X90          ;If end-of-line, we're done
  CALL GetNextChar  ;If not, get the next character
  OR   AL,AL        ;Set return flag
X90:                ;Done
  RET

;------------------------------------------------------------------------------
;SKIP OVER SPACES TO FIND LEGITIMATE CHARACTERS, AND TEST FOR EOL
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: ZF = Set if EOL
;            = Clear if legitimate character
;              [SI] = The Next non-Space Character
;Changes:
;------------------------------------------------------------------------------
IsSpaceOrEOL:
  PUSH AX          ;Save used registers
L10:               ;Loop to here to skip over spaces
  CALL GetNextChar ;Get the next character
  CMP  AL,' '      ;Is it a space?
  JE   L10         ;If so, get the next character
  CMP  AL,Tab      ;Is it a Tab?
  JE   L10         ;If so, get the next character
  DEC  SI          ;If not, point back at the character
  OR   AL,AL       ;Set EOL flag
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SKIP OVER A COLON OR EQUALS, IF THERE
;Inputs:  DS:[SI] = Command Line Pointer
;Outputs: CF = Clear if Colon or Equals was found (and skipped)
;              [SI] = First non-Space Character after the Colon/Equals
;            = Set if No Colon or Equals was found
;              [SI] = First non-Space Character after where we were looking
;Changes:
;------------------------------------------------------------------------------
SkipColonEquals:
  PUSH AX           ;Save used registers
  CALL IsSpaceOrEOX ;Skip over the Spaces
  JZ  >Q75          ;If End-of-Line, nothing to get
  CMP  AL,':'       ;Is it a Colon?
  JE  >Q80          ;If so, we're done
  CMP  AL,'='       ;Is it an equals?
  JE  >Q80          ;If so, we're done
Q70:                ;No Colon/Equals Found
  DEC  SI           ;Point back at the character
Q75:                ;[SI] correct
  STC               ;Set return flag
  JMP >Q90          ;Done
Q80:                ;Colon/Equals Found
  CALL IsSpaceOrEOL ;Go to the next character
  CLC               ;Set return flag
Q90:                ;Done
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CONVERT A CODE WORD FROM THE COMMAND LINE TO A BYTE FROM THE LOOKUP TABLE
;Inputs:  DS:[SI] = Command Line Pointer
;         DS:[BX] = List to Test
;Outputs: CF = Clear if a match was found
;              AL = Entry from Table
;              [SI] = Next character after the Code Match
;         CF = Set if no match found (error)
;              AL = Unchanged
;              SI = Unchanged
;Changes: SI if valid match found
;------------------------------------------------------------------------------
GetCode:
  PUSH BX,DX         ;Save used registers
  MOV  DX,AX         ;Save AX in case there's no match
C10:                 ;Loop to here for each list of words
  CMP  B [BX],0      ;Are we at the end of our table?
  JE  >C80           ;If so, we couldn't find a match
  XOR  AH,AH         ;Assume it's a match
  PUSH SI            ;Save our command line pointer for a second
C20:                 ;Loop to here for each character in a word
  CALL GetNextChar   ;Get the next character
  CALL Capitalize    ;Capitalize it
  CALL CmpALBXNoCase ;Is it a match?
  JZ  >C30           ;If so, handle it
  INC  AH            ;If not, mark us as no match
C30:                 ;The character is tested
  INC  BX            ;Point at the next character in our test word
  CMP  B [BX],0      ;Are we at the end of the word?
  JNE  C20           ;If not, keep going until we're done
  INC  BX            ;Point at the next word or code list
  OR   AH,AH         ;Was it a match?
  JZ  >C50           ;If so, jump to handle it
  POP  SI            ;If not, restore our command line pointer
  CMP  B [BX],0      ;Is this the end of our word list?
  JNE  C10           ;If not, just test the next word
  INC  BX,2          ;If so, skip over the codes
  JMP  C10           ;Keep going until we're done with our list
C50:                 ;We've found a match!
  POP  AX            ;Lose our original command line pointer
  DEC  BX            ;Point back at the end-of-word marker
C60:                 ;Loop to here to find the end-of-list
  MOV  AX,[BX]       ;Get the next two characters in our list
  INC  BX            ;Point at the next character for next time
  OR   AX,AX         ;Is this the start of our code list?
  JNE  C60           ;If not, keep looking
  INC  BX            ;If so, point at the character we're looking for
  MOV  DL,[BX]       ;Get the character from the table
  CLC                ;Mark our OK flag
  JMP >C90           ;And we're done
C80:                 ;No match found
  STC                ;Mark us as no match
C90:                 ;We're done
  MOV  AX,DX         ;Set AX to the correct value
  POP  DX,BX         ;Restore used registers
  RET

CmpALBXNoCase:
  PUSH AX         ;Save used registers
  CALL Capitalize ;Capitalize AL
  MOV  AH,AL      ;Store it in AH
  MOV  AL,[BX]    ;Get [BX]
  CALL Capitalize ;Capitalize it
  CMP  AL,AH      ;Compare AL & [BX]
  POP  AX         ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE NEXT SERIES OF NUMERIC CHARACTERS, AND CONVERT THEM TO A NUMBER
;Inputs:  [SI] = Command Line Pointer
;Outputs: AX = decimal character string converted to a number, ?? if not
;         CF = Clear if AX is OK
;            = Set if no decimal found
;Changes:
;------------------------------------------------------------------------------
GetDecimal:
  PUSH BX,CX,DX     ;Save used registers
  XOR  AX,AX        ;Start with zero
  XOR  BX,BX        ;  "    "    "   - accumulator
  MOV  CX,10        ;CX = multiplier & digit limit comparer
  CALL IsSpaceOrEOX ;Skip over the spaces
  JZ  >D70          ;If EOL, Error
  SUB  AL,'0'       ;If not, convert it to a number
  CMP  AL,9         ;Is it really a number?
  JA  >D70          ;If not, there's an error
  DEC  SI           ;If so, step back to the number
D10:                ;Loop to here for each character
  CALL GetNextChar  ;Get the next character
  SUB  AL,'0'       ;Change it to 0-9 range
  XOR  AH,AH        ;Extend byte to word
  CMP  AX,CX        ;Is it a digit?
  JAE >D80          ;If not, we're done
  CMP  BX,6553      ;Is our number getting too big?
  JB  >D20          ;If not, jump to store it
  CMP  AL,5         ;If so, will this number make it too big?
  JA  >D70          ;If so, there's an error
D20:                ;Add the digit to our number
  XCHG AX,BX        ;Swap accumulation into AX, new digit into BX
  MUL  CX           ;Multiply the previous accumulation by 10
  ADD  BX,AX        ;Add in the new digit
  JMP  D10          ;Jump to get next character
D70:                ;There was an error
  STC               ;Mark the error flag
  JMP >D90          ;And quit
D80:                ;We have a good number
  DEC  SI           ;Point back at the non-number
  MOV  AX,BX        ;Put the value where it belongs
  CLC               ;Mark the OK flag
D90:                ;We're done
  POP  DX,CX,BX     ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE THE TELL THE USER WHERE THE COMMAND LINE ERROR OCCURRED
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;WRITE THE COMMAND LINE OR ENVIRONMENT ERROR INFORMATION TO THE SCREEN
;Inputs:  [SI] = Command line Pointer (if Command Line)
;              = StdIn Buffer Pointer (if StdIn String)
;              = Environment Pointer (if Environment Variable)
;         DoEnvir, DoStdIn
;Outputs:
;Changes: Writes information to screen
;------------------------------------------------------------------------------
WriteCmdErr:
  PUSHA                   ;Save all registers
  CALL FindBadChar        ;Find the actual bad character in the string (SI)
  CALL SetupCmdErrStrings ;Set up the String Pointers and Sizes
                          ;DI:[AX] = Command Line String
                          ;DS:[BX] = Program name at beginning of String
                          ;    CX  = Size of BX String
                          ;DS:[DX] = Header String
                          ;DS:[BP] = Footer String
  CALL WriteZErr          ;Write the Header
  CALL WriteCrLfErr       ;Move
  CALL WriteCrLfErr       ;  down
  SUB  SI,AX              ;Calculate the number
  ADD  CX,SI              ;  of spaces
  DEC  CX                 ;  to write
  CALL WriteCmdString     ;Write the Command-line String (may modify CX)
  CALL WriteCrLfErr       ;Move down
  MOV  B SingleMsg[0],' ' ;Want to write spaces
  MOV  DX,SingleMsg       ;Point at the string
E60:                      ;Loop to here to write each space (number in CX)
  CALL WriteZErr          ;Write the space
  LOOP E60                ;Keep writing them until we're done
  MOV  DX,ParseErrMsg     ;Write the Pointer Part
  CALL WriteZErr          ;  of the Error Message
  MOV  DX,BP              ;Write the last part
  CALL WriteZErr          ;  of the message
  POPA                    ;Restore all registers
  RET

;------------------------------------------------------------------------------
;GO BACKWARDS IN AN ERRORED OPTION STRING TO INDICATE TO USER WHERE ERROR WAS
;Inputs:  SI = Option String Pointer (Environment, StdIn, or Command Line)
;                where error was found
;Outputs: SI = Options String Pointer, adjusted to point at a "Real" character
;Changes:
;------------------------------------------------------------------------------
FindBadChar:
  PUSH AX          ;Save used registers
  DEC  SI          ;Go back 1 character to start
B10:               ;Loop to here to find a legitimate character
  CALL GetNextChar ;Get the character
  JZ  >B40         ;If end-of-line, keep going back
  CMP  AL,' '      ;Is it a space?
  JNE >B90         ;If not, we're done
B40:               ;Need to keep going back
  SUB  SI,2        ;Go back to the previous character
  JMP  B10         ;Keep looking
B90:               ;Done
  POP  AX          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SET UP THE STRING POINTERS AND SIZES TO BE ABLE TO WRITE THE COMMAND ERROR
;Inputs:  DS = Local Data Area
;Outputs: DI:[AX] = Command-line String to write
;         DS:[DX] = First Error Message (Header) to write
;         DS:[BX] = Second message to write (first part of Command string)
;             CX  = Number of Bytes to add to String Size
;         DS:[BP] = Third message to write (Footer)
;Changes:
;------------------------------------------------------------------------------
SetupCmdErrStrings:
  TEST ProgFlags2,DoEnvir   ;Working with the Environment?
  JNZ >S40                  ;If so, handle it
  TEST ProgFlags2,DoStdIn   ;Working with StdIn?
  JNZ >S50                  ;If so, handle it
S10:                        ;Command Line
  MOV  AX,81h               ;DI:[AX] =
  MOV  DI,DS                ;  Command-line Tail
  MOV  BX,RealName          ;DS:[BX] = Program Name
  MOV  CX,RealNameSz        ;CX = Size of [BX] String
  CMP  CmdLineVarPointer,-1 ;Real Command Line?
  JE  >S30                  ;If so, continue
S20:                        ;CMDLINE Environment Variable
  MOV  DI,[PSPEnvirSeg]     ;DI:[AX] =
  MOV  AX,CmdLineVarPointer ;  CMDLINE
  SUB  AX,CmdLineVarSize    ;  Environment Variable
S30:                        ;Have Command-line parameters set up
  MOV  DX,ParseCmdMsg       ;DS:[DX] = Header String
  MOV  BP,ParseErrMsg2      ;DS:[BP] = Footer String
  JMP >S90                  ;Done
S40:                        ;Environment
  MOV  DI,[PSPEnvirSeg]     ;DI:[AX] =
  MOV  AX,EnvVarPointer     ;  USBKEYB
  SUB  AX,EnvVarSize        ;  Environment Variable
  MOV  BX,NoMsg             ;DS:[BX] = Second message to write
  XOR  CX,CX                ;CX = Size of [BX] String
  MOV  DX,ParseEnvMsg       ;DS:[DX] = Header String
  MOV  BP,ParseEnvMsg2      ;DS:[BP] = Footer String
  JMP >S90                  ;Done
S50:                        ;StdIn
  MOV  DI,ExtraMemSegment   ;DI:[AX] =
  XOR  AX,AX                ;  StdIn Buffer
  MOV  BX,NoMsg             ;DS:[BX] = Second message to write
  XOR  CX,CX                ;CX = Size of [BX] String
  MOV  DX,ParseStdInMsg     ;DS:[DX] = Header String
  MOV  BP,ParseErrMsg2      ;DS:[BP] = Footer String
S90:                        ;Done
  RET

;------------------------------------------------------------------------------
;WRITE THE CONTENTS OF THE COMMAND STRING, WITH MORE MESSAGES AS NEEDED
;Inputs:  CS = DS = Local Data Area
;         DS:[BX] = First String to write (may not be needed!)
;         DI:[AX] = Second String (main part) to write
;             CX  = Number of Spaces to write at beginning (if normal)
;Outputs: CX = Adjusted number of spaces to write
;Changes:
;------------------------------------------------------------------------------
WriteCmdString:
  PUSH AX,BX,DX,DI,SI      ;Save used registers
  PUSH BP,DS,ES            ;Save used registers
  MOV  BP,CX               ;Save the original Space Counter in BP
  MOV  ES,DI               ;Save the second string Pointer
  MOV  SI,AX               ;  in ES:[SI]
  MOV  DX,BX               ;Save the length
  CALL CalcStrSizeDX       ;  of the first string
  MOV  DI,CX               ;  in DI
  MOV  DS,ES               ;Save the length
  CALL CalcStrSizeSI       ;  of the second string
  MOV  AX,CX               ;  in AX
  CMP  BP,70               ;Are there too many spaces to fit on one line?
  JBE >G20                 ;If not, continue
G10:                       ;Need to lop off the beginning
  SUB  BP,DI               ;We will not be
  XOR  DI,DI               ;  writing the
  MOV  BX,NoMsg            ;  first string
  CMP  BP,70               ;Are there still too many spaces to fit?
  JBE >G20                 ;If not, continue
  MOV  BX,MoreLeftMsg      ;Point at the More Message
  ADD  BP,MoreMsgSize      ;Compensate number of spaces for More Message
  MOV  DI,60               ;We want the number of spaces to be 60
  SUB  DI,BP               ;Calculate how far into the Second string
  NEG  DI                  ;  we need to start to accomplish that
  MOV  BP,59               ;BP = New Number of Spaces
  ADD  SI,DI               ;  where we need to start writing
  SUB  AX,DI               ;  of second string
  MOV  DI,MoreMsgSize      ;DI = Size of new first string (More Message)
G20:                       ;Beginning of line is accounted for
  MOV  DS,CS               ;Write the
  MOV  DX,BX               ;  first
  CALL WriteZErr           ;  string
  MOV  DS,ES               ;Point DS:[DX] at the
  MOV  DX,SI               ;  second string
  ADD  DI,AX               ;Calculate total length of both strings
  CMP  DI,79               ;Will it fit on one line?
  JBE >G40                 ;If so, continue
  MOV  BX,(80-MoreMsgSize) ;Calculate how many characters
  SUB  BX,DI               ;  of the second string
  ADD  BX,AX               ;  we need to write
  MOV  AL,[SI+BX]          ;Save the old character in the string
  MOV  B [SI+BX],0         ;Convert it to an end-of-string character
  CALL WriteZErr           ;Write the truncated string
  MOV  [SI+BX],AL          ;Restore the original character of the string
  MOV  DS,CS               ;Write the
  MOV  DX,MoreRightMsg     ;  More Message
G40:                       ;Write last message
  CALL WriteZErr           ;Write it
G90:                       ;Done
  MOV  CX,BP               ;Return CX = number of spaces to write
  POP  ES,DS,BP            ;Restore used registers
  POP  SI,DI,DX,BX,AX      ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;MISCELLANEOUS COMMAND-LINE RELATED STUFF
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;GET OUR REAL PROGRAM NAME
;Inputs:
;Outputs: RealName, RealNameSz
;Changes:
;------------------------------------------------------------------------------
GetRealName:
  PUSH AX,CX,DI,SI      ;Save used registers
  PUSH DS,ES            ;Save used registers
  MOV  DS,[PSPEnvirSeg] ;Point DS and ES
  MOV  ES,DS            ;  at the environment segment
  XOR  DI,DI            ;Point at the beginning of Environment variable list
  MOV  CX,-1            ;Max out the count limiter
  XOR  AX,AX            ;Need to look for a double 0
N10:                    ;Loop to here to skip over environment variables
  REPNE SCASB           ;Look for the end of this environment variable
  CMP  B [DI],0         ;Is it the end of the variable list (double zero)?
  JNE  N10              ;If not, keep skipping over environment variables
  CMP  W [DI+1],0       ;If so, is there at least one string after the env?
  JE  >N90              ;If not, we're done
  LEA  SI,[DI+3]        ;If so, point DS:[SI] at the True Name
  MOV  ES,CS            ;Point ES (for ES:[DI]) at our local data
N20:                    ;Loop to here to skip over drive/path info
  MOV  DI,RealName      ;Point at the beginning of our data area
  XOR  CX,CX            ;Start the length counter at 0
N30:                    ;Loop to here for each character in the name
  LODSB                 ;Get the next character of the name
  CMP  AL,'.'           ;Is it a dot?
  JE  >N40              ;If so, we may have the name
  CMP  AL,BackSlash     ;Is it a path separator?
  JE   N20              ;If so, start looking for the name again
  CALL Capitalize       ;If part of a name, capitalize the character
  STOSB                 ;Store it in our data area
  INC  CX               ;Increment the size counter
  JMP  N30              ;And keep looking for more characters
N40:                    ;We may have the name
  LODSB                 ;Get the next character of the name
  OR   AL,AL            ;End of the name?
  JZ  >N50              ;If so, it really was our program name
  CMP  AL,BackSlash     ;Was it a Directory with a dotted name?
  JE   N20              ;If so, keep looking for a file name
  JMP  N40              ;If not, keep looking for the end of the name
N50:                    ;Have the name
  XOR  AL,AL            ;Store
  STOSB                 ;  a 0
  MOV  CS:RealNameSz,CX ;Store the Size
N90:                    ;We're done
  POP  ES,DS            ;Restore used registers
  POP  SI,DI,CX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE POINTERS TO THE ENVIRONMENT VARIABLES WE NEED
;Inputs:  CS = DS = Local Data Area (also PSP Data Area)
;Outputs: Various Environment Pointers
;Changes:
;------------------------------------------------------------------------------
GetEnvVars:
  PUSH BX,CX,SI                    ;Save used registers
  MOV  SI,PathVarString1           ;Get
  MOV  BX,OFFSET PathVarPointer    ;  the
  MOV  CX,PathVarSize              ;  PATH
  CALL GetEnvVarPointer            ;  Pointer
  JNZ >V20                         ;If OK, continue
  MOV  SI,PathVarString2           ;If no, try
  CALL GetEnvVarPointer            ;  "Path" instead of "PATH"
V20:                               ;Get Program Variable
  MOV  SI,EnvVarString             ;Get
  MOV  BX,OFFSET EnvVarPointer     ;  our
  MOV  CX,EnvVarSize               ;  Program Variable
  CALL GetEnvVarPointer            ;  Pointer
V50:                               ;Get CMDLINE string
  CMP  B [80h],7Eh                 ;Is command line size maxed out?
  JB  >V90                         ;If not, don't worry about CMDLINE Env Var
  MOV  SI,CmdLineVarString         ;Get
  MOV  BX,OFFSET CmdLineVarPointer ;  the
  MOV  CX,CmdLineVarSize           ;  CMDLINE
  CALL GetEnvVarPointer            ;  Pointer
  CALL AdjustCmdLine               ;Compensate CMDLINE Pointer for Program Name
V90:                               ;Done
  POP  SI,CX,BX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE POINTER TO AN ENVIRONMENT VARIABLE
;Inputs:   CS = DS = Local Data Area (also contains PSP)
;         [BX] = Where to store the Pointer at
;          CX  = Length of String
;         [SI] = Environment Variable String to Look for (ends in =)
;Outputs: ZF = Set if Error (Variable not found)
;               [BX] = -1
;             = Clear if OK (variable was found)
;               [BX] = Pointer to String (in Environment Segment, NOT PSP!!)
;Changes:
;------------------------------------------------------------------------------
GetEnvVarPointer:
  PUSH AX,CX,SI,BP      ;Save used registers
  PUSH DS,ES            ;Save used registers
  MOV  W [BX],-1        ;Default = -1 (does not exist)
  MOV  ES,[PSPEnvirSeg] ;ES:[DI] =
  XOR  DI,DI            ;  Environment Variable List
  MOV  DX,CX            ;Save the Size
  MOV  BP,SI            ;Save the original Pointer
  XOR  AL,AL            ;Need to look for 0's
V10:                    ;Loop to here for each Environment variable
  MOV  CX,DX            ;CX = Number of bytes to test
  MOV  SI,BP            ;DS:[SI] = String to Test for
  REPE CMPSB            ;Is it a match?
  JZ   >V30             ;If so, continue
  MOV  CX,-1            ;Max out the count limiter
  REPNE SCASB           ;Look for the end of this environment variable
  CMP  B ES:[DI],AL     ;End of Environment?
  JNE  V10              ;If not, keep looking
  JMP >V90              ;If so, no variable
V30:                    ;Found it!
  MOV  [BX],DI          ;Store the Pointer
V90:                    ;Done
  CMP  W [BX],-1        ;Set return flag
  POP  ES,DS            ;Restore used registers
  POP  BP,SI,CX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;ADJUST THE CMDLINE ENVIRONMENT VARIABLE POINT TO POINT AT THE COMMAND TAIL
;Inputs:  DS = Local Data Area (and PSP Segment)
;         CmdLineVarPtr (-1 if does not exist)
;           Unadjusted for Program Path/Name
;Outputs: CmdLineVarPtr
;           Adjusted (Program Path/Name skipped over to leave Command Tail)
;Changes:
;NOTES: The CMDLINE environment variable, which is only set by SOME
;         versions of DOS, contains the entire contents of the command
;         line.  The "entire contents" includes our executable file name
;         (including the path, if it was provided), but does not include
;         the redirection and/or piping parameters.
;       This routine skips over the executable file name part and simply
;         points at the command tail (the "Options" part of the command line).
;       The CMDLINE variable is used by some versions of DOS (such as 4DOS/NDOS
;         and DOS 95) to allow the command-line to be longer than the 126
;         characters normally allowed by DOS.
;------------------------------------------------------------------------------
AdjustCmdLine:
  PUSH AX,DX,SI,DS             ;Save used registers
  MOV  SI,CmdLineVarPointer    ;[SI] = CMDLINE environment variable contents
  CMP  SI,-1                   ;Anything there?
  JE  >J90                     ;If not, no need for changes
  MOV  DL,"/"                  ;Assume Get SwitchChar function is unsupported
  MOV  AX,3700h                ;Function 3700h (Get DOS SwitchChar)
  INT  21h                     ;Do it (returns DL)
  MOV  DS,[PSPEnvirSeg]        ;DS:[SI] = CMDLINE contents
J10:                           ;Loop to here to skip initial spaces & tabs
  LODSB                        ;Get the next character
  CMP  AL,' '                  ;Space?
  JE   J10                     ;If so, keep skipping
  CMP  AL,Tab                  ;Tab?
  JE   J10                     ;If so, keep skipping
J20:                           ;Skipped initial Tabs & Spaces
  DEC  SI                      ;Point back at the first character
  XOR  AH,AH                   ;Assume no quotes
  CMP  AL,Quote2               ;Double Quote (part of LFN Path & File Name)?
  JNE >J30                     ;If not, continue
  MOV  AH,AL                   ;If so, mark as a quote
  INC  SI                      ;Update the Pointer
J30:                           ;Loop to here for each character
  LODSB                        ;Get next character
  OR   AH,AH                   ;Are we looking for a Quoted Path/File Name?
  JZ  >J40                     ;If not, continue
J35:                           ;Looking for the Closing Quote
  CMP  AL,AH                   ;Is it the Closing Quote?
  JNE >J50                     ;If not, keep looking
  JMP >J85                     ;If so, we're done
J40:                           ;Not a Quoted Path/File Name
  OR   AL,AL                   ;End of variable?
  JE  >J80                     ;If so, handle it
  CMP  AL,' '                  ;Space?
  JE  >J80                     ;If so, we're done
  CMP  AL,Tab                  ;Tab?
  JE  >J80                     ;If so, we're done
  CMP  AL,'/'                  ;Slash?
  JE  >J80                     ;If so, we're done
  CMP  AL,DL                   ;SwitchChar?
  JE  >J80                     ;If so, we're done
J50:                           ;Not end-of-name
  JMP  J30                     ;Keep looking
J80:                           ;[SI-1] = CMDLINE Pointer
  DEC  SI                      ;Point at beginning of string
J85:                           ;[SI] = New CMDLINE Pointer
  MOV  CS:CmdLineVarPointer,SI ;Store it
J90:                           ;Done
  POP  DS,SI,DX,AX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE ENVIRONMENT VARIABLE CONTENTS TO THE SCREEN
;Inputs:  EnvVarPointer (-1 if no Environment variable exists)
;Outputs:
;Changes: Writes to CON or ERR
;------------------------------------------------------------------------------
WriteEnvCon:
  CMP  EnvVarPointer,-1 ;Is there even an Environment variable to write?
  JE  >W90              ;If not, we're done
  PUSH DX,DS            ;Save used registers
  MOV  DX,EnvVarPointer ;Point at the
  SUB  DX,EnvVarSize    ;  beginning of the Variable
  MOV  DS,[PSPEnvirSeg] ;Point at the Environment Segment
  CALL WriteZPause      ;Write it
  POP  DS,DX            ;Restore used registers
W90:                    ;We're done
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO WRITE SPECIAL MESSAGES TO THE SCREEN (HELP, STATUS, ETC.)
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Program Help Message
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;PRINT THE SYNTAX HELP MESSAGE AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;Outputs: Writes Help Message to the screen
;Changes: Quits program
;------------------------------------------------------------------------------
DoHelp:
  TEST ProgFlags2,Help ;Does the user want help?
  JZ  >H90             ;If not, we're done
  MOV  DX,SyntaxMsgHdr ;Write the first part
  CALL WriteZPause     ;  of the Help Message
  MOV  DX,RealName     ;Write the
  CALL WriteZPause     ;  Real Program Name
  CALL WriteSpace      ;Write a space
  MOV  DX,SyntaxMsg    ;Point at the last part of the Help Message
  XOR  AL,AL           ;ErrorLevel = 0
  JMP  Exit            ;Quit
H90:                   ;No need to do help
  RET


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Bad Device List
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;PRINT THE BAD DEVICE TABLE AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;         ES = TSR Data Area
;Outputs: Writes contents of Bad Device Table to the screen
;Changes: Quits program
;         Lots of Registers
;------------------------------------------------------------------------------
DoBadDevices:
  TEST StatusFlags,BadDevices ;Does the user want the Bad Device List?
  JZ  >B000                   ;If not, we're done
  TEST ProgFlags,InMemory     ;Are we installed in memory yet?
  JNZ >B00                    ;If so, continue
  MOV  DX,BadDvcErrMsg        ;If not, point at the first part of Error Message
  JMP  FinishDataErr          ;Terminate program
B000:
  RET

B00:                               ;Write it
  MOV  DX,BadBeepMsg               ;Write the
  CALL WriteZPause                 ;  Beep Header
  MOV  DX,YesMsg                   ;Assume Yes
  TEST ES:TSRFlags,NoBeep          ;Is it Yes?
  JZ  >B05                         ;If so, continue
  MOV  DX,NoMsgS                   ;If not, point at No Message
B05:                               ;Have Beep Status Message
  CALL WriteZPause                 ;Write the Beep Status
  CALL WriteCrLfPause2             ;Move down
  MOV  DX,BadDeviceHdr             ;Write the
  CALL WriteZPause                 ;  Header
  CALL TestSupportFile             ;Look for the Support File in the Path
  XOR  BL,BL                       ;Start Bad Counter at 0
  MOV  SI,BadDeviceTable           ;Point at Bad Table
  MOV  CX,MaxBadDevices            ;Number of Table Entries
B10:                               ;Loop to here for each table entry
  TEST ES:[SI].BDFlags,BDFlagInUse ;Valid Entry?
  JZ  >B40                         ;If not, skip it
  INC  BL                          ;Increment Bad Counter
  CALL WriteSpace                  ;Write
  MOV  AL,ES:[SI].BDHostIndex      ;  the
  CALL ByteToString                ;  Host Index
  CALL Write3Spaces                ;Write
  MOV  AL,ES:[SI].BDDvcAddress     ;  the
  CALL ByteToString                ;  Device Address
  CALL Write3Spaces                ;Write
  MOV  AL,ES:[SI].BDInterfaceNum   ;  the
  CALL ByteToString                ;  Interface Number
  CALL WriteBadStage               ;Write the Stage details
B40:                               ;Go to next entry
  ADD  SI,BadDeviceStrucSize       ;Point at next Entry
  LOOP B10                         ;Keep going until we're done
  OR   BL,BL                       ;Were there any Bad Devices?
  JNZ >B70                         ;If so, we're done
  MOV  DX,NoneFoundMsg             ;If not, write
  CALL WriteZPause                 ;  "None Found" Message
B70:                               ;Done
  MOV  DX,NoMsg                    ;Write nothing when done
  XOR  AL,AL                       ;ErrorLevel=0
  JMP  Exit                        ;And QUIT

;------------------------------------------------------------------------------
;WRITE THE BAD STAGE INFORMATION TO THE SCREEN
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [SI] = Bad Device Table Entry
;                Already checked for Validity (BDFlagInUse = True)
;                Host Index, Device Address, & Interface Number alread written
;Outputs: Write Try, Stage, and Error Code Numbers to the Table
;Changes:
;------------------------------------------------------------------------------
WriteBadStage:
  PUSH AX,CX                       ;Save used registers
  CALL Write2Spaces                ;Move over
  XOR  AH,AH                       ;Write the
  MOV  AL,ES:[SI].BDNewDeviceStage ;  Stage
  CALL WordToString                ;  Number
  CALL Write2Spaces                ;Move over
  MOV  CX,ES:[SI].BDNewDeviceError ;Write the
  CALL WriteBadError               ;  Error Code
  CALL WriteCrLfPause              ;Move down
  POP  CX,AX                       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE ERROR CODE AND APPROPRIATE DESCRIPTION TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         AL = Stage Number
;         CX = Error Code to write
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
WriteBadError:
  PUSH AX,DX            ;Save used registers
  XCHG AX,CX            ;AX = Error Code, CL = Stage
  CALL WriteWordHex     ;Write the Error Code
  CALL WriteHexEnd      ;Write an h
  CMP  CL,200           ;Is the stage more than 200?
  JAE >E90              ;If so, the Error Code is meaningless
  CALL WriteSpace       ;Move over
  TEST ProgFlags,FoundSuptFile ;Is there a Support File in the Path?
  JNZ >E20              ;If so, continue
E10:                    ;No Support File
  MOV  DX,NoSuptFileMsg ;If not, write first part
  CALL WriteZPause      ;  of Error Message
  MOV  DX,SupportFile   ;Write the Support
  CALL WriteZPause      ;  File Name
  JMP >E90              ;Done
E20:                    ;OK to call support File
  TEST CL,1             ;Is it even (TD Status) or odd (Int 14h Error)?
  JZ  >E50              ;If Even, jump to handle it
E40:                    ;Int 14h Error Code (Stage is Odd)
  CALL WriteInt14Err    ;Write the Int 14h Error Code
  JMP >E90              ;Done
E50:                    ;TD Status Code (Stage is Even)
  CALL WriteTDStsErr    ;Write the TD Status Error Code
E90:                    ;Done
  POP  DX,AX            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE TD STATS OR INT 14H ERROR DESCRIPTION TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         AX = TD Status Code to write
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
WriteTDStsErr:
  PUSH BX,CX,DX           ;Save used registers
  MOV  BX,TDStatusCode    ;[BX] = Where to write Code Number
  MOV  CX,TDStatusCallBk  ;[CX] = Where to write Screen-writing Address
  MOV  DX,TDStatusMsg     ;[DX] = Start of command-tail string
  JMP >R00                ;Do it
WriteInt14Err:
  PUSH BX,CX,DX           ;Save used registers
  MOV  BX,Int14ErrCode    ;[BX] = Where to write Code Number
  MOV  CX,Int14ErrCallBk  ;[CX] = Where to write Screen-writing Address
  MOV  DX,Int14ErrMsg     ;[DX] = Start of command-tail string
;  JMP >R00               ;Do it
R00:
  PUSH AX                 ;Save used registers
  CALL WriteWordToString  ;Write the Error Code to our string
  MOV  BX,CX              ;Write our
  MOV  AX,CS              ;  screen-writing
  MOV  CX,WriteZPauseFar  ;  code address
  CALL WriteSegmentOffset ;  to the string
  MOV  BX,DX              ;Copy our String
  CALL CopyToCmdTail      ;  to the Command Tail String
  CALL DoExec             ;Write the Description
  POP  AX                 ;Restore used registers
  POP  DX,CX,BX           ;Restore used registers
  RET


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Raw Mouse Data
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;PRINT THE RAW DATA TO THE SCREEN AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;         ES = TSR Data Area
;         TSRFlags
;Outputs: Writes Raw Data to Screen
;Changes: Quits program
;         Lots of Registers
;------------------------------------------------------------------------------
DoRawData:
  TEST StatusFlags,RawData ;Does the user want the Raw Data?
  JZ  >W000                ;If not, we're done
  TEST ProgFlags,InMemory  ;If so, are we installed in memory yet?
  JNZ >W00                 ;If so, show the data
  MOV  DX,RawDataErrMsg    ;Point at first part of Error Message
  JMP  FinishDataErr       ;Termiante Program
W000:
  RET

W00:
  MOV  AL,ErLvlNoDevice     ;Assume no
  MOV  DX,NoDeviceMsg       ;  Devices installed yet
  TEST ES:TSRFlags,EmuHardware ;Emulating BIOS?
  JZ  >W03                  ;If so, handle it
W02:                        ;Emulating Hardware
  CALL GotAMouseNoPS2       ;If not, don't look for PS2 mice
  JMP >W04                  ;Jump to process the result
W03:                        ;Emulating BIOS
  CALL GotAMouse            ;Any Mice installed?
W04:                        ;Carry flag set
  JC  >W80                  ;If no mice, error
W05:                        ;Have at least one keyboard
  OR   ProgFlags2,Write2Err ;Write to Error instead of CON/Pause
  OR   ES:TSRFlags2,DisableAll ;Don't allow mice to do anything
;  CALL HideCursor           ;Hide the Cursor
  MOV  DX,RawDataHdr        ;Write the
  CALL WriteZErr            ;  Header
  CALL GetCursor            ;Get Cursor Position (BH=Row,BL=Col)
  SUB  BH,(MaxDevices+1)    ;Go
  CALL PutCursor            ;  back up
  MOV  BP,BX                ;Save starting Cursor position
  CALL FlushKbdBuff         ;Flush the Keyboard Buffer
  CALL CopyAllRawData       ;Copy (Initialize) ALL Raw Data from TSR
W10:                        ;Loop to here each time we update the status
  XOR  CL,CL                ;Start with Index 0
  MOV  BX,BP                ;Put the Cursor
  CALL PutCursor            ;  at the top of the Table
W20:                        ;Loop to here for each Device
  CALL TestRawData          ;Check Raw Data for changes to this Device
  CALL WriteRawData         ;Write the Raw Data for this Device
  INC  CL                   ;Increment Index
  CMP  CL,(MaxDevices+1)    ;Done all of the Devices yet?
  JB   W20                  ;If not, continue
  CALL Write2Spaces         ;Overwrite possible ^C
W60:                        ;Loop to here to flush keystrokes
  CALL GetKey               ;Any Keys from real Keyboard?
  JZ  >W65                  ;If not, continue
  CALL FlushKbdBuff         ;If so, get rid of the rest of keystrokes
  JMP >W70                  ;Done
W65:                        ;No keystroke
  JMP  W10                  ;Keep writing
W70:                        ;Done
;  CALL ShowCursor           ;Show the Cursor
  AND  ES:TSRFlags2,(NOT DisableAll) ;Enable Mice again
  AND  ProgFlags2,(NOT Write2Err)    ;Write to Pause again
  XOR  AL,AL                ;ErrorLevel=0
  MOV  DX,NoMsg             ;Write nothing
W80:                        ;Done, quit program
  JMP  Exit                 ;And QUIT
W90:                        ;No need to do help
  RET

;------------------------------------------------------------------------------
;COPY ALL RAW MOUSE DATA FROM THE TSR TO THE LOCAL DATA AREA
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         CLD already issued
;Outputs: DeviceInfoTable in Local (non-TSR) Data Area
;Changes:
;------------------------------------------------------------------------------
CopyAllRawData:
  PUSH CX             ;Save used registers
  MOV  CL,MaxDevices  ;CL = Maximum Device Info Index (Index for PS2 Mouse)
L10:                  ;Loop to here for each entry
  CALL CopyRawDataOld ;Copy the Data
  SUB  CL,1           ;Go to the next Index
  JNC  L10            ;If not done yet, keep copying
  POP  CX             ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A SINGLE DEVICE INFO ENTRY FROM THE TSR TO THE LOCAL DATA AREA
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         CL = Device Info Index Number to Copy
;         CLD already issued
;Outputs: Copies Device Info Entry from TSR to Local Data Area
;Changes: DS:DeviceInfoTable
;------------------------------------------------------------------------------
CopyRawDataOld:
  PUSH CX,DI,SI,DS,ES ;Save used registers
  PUSH DS             ;Exchange
  PUSH ES             ;  DS
  POP  DS             ;  and
  POP  ES             ;  ES
  CLI                 ;Disable Interrupts
  CALL DIIndex2Offset ;Convert Index (CL) to Offset (DI)
  MOV  SI,DI          ;Point DS:[SI] at TSR Data, ES:[DI] at Local Data
  MOV  CX,(DeviceInfoStrucSize/2) ;CX = Number of Words to Copy
  REP  MOVSW          ;Copy the Data
  STI                 ;Enable Interrupts
  POP  ES,DS,SI,DI,CX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST MOUSE DATA TO SEE IF IT HAS CHANGED, OR IF WE SHOULD JUST USE OLD DATA
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         CL = Device Info Index Number to Test
;         CLD already issued
;Outputs: DS:DeviceInfoTable
;Changes:
;NOTES: This test to see if anything on the mouse has changed at a level
;         where we need to display it.  The new Mouse data is in the TSR,
;         while the old Mouse data is in the Local Data Area.  We test
;         to see if the new mouse data has changed to anything other than
;         0 (to anything other than no button presses or movements).
;         If it has changed appropriately, we copy the new data into the
;         old for display.
;       We do this becuase immediately after a mouse has stopped moving,
;         all of the movement statuses return to 0 right away, many times
;         too quickly for us to see what they really were on the screen
;         before they disappeared.  Using this method, we have a "memory"
;         of the last mouse movement that was measured immediately before
;         the movement stopped (before the movement counter went to 0).
;         While it could be slightloy confusing, it makes it much easier
;         to see what the mouse is really doing.
;------------------------------------------------------------------------------
TestRawData:
  PUSH AX,CX,DI,SI    ;Save used registers
  CALL DIIndex2Offset ;Point ES:[DI] at the TSR (New) Data
  MOV  AH,CL          ;Save the Index
  TEST ES:[DI].DIFlags,DIFlagInUse ;Valid Mouse?
  JZ  >T90            ;If not, just quit
  ADD  DI,(OFFSET DIStsData) ;If OK, point ES:[DI] at the New Data
  MOV  SI,DI          ;Point DS:[SI] at the Old Data
  MOV  CX,8           ;CX = Number of Bytes to Test
T10:                  ;Loop to here for each status byte
  LODSB               ;Get the Old Data Byte
  SCASB               ;Has it changed?
  JE  >T30            ;If not, continue testing
T20:                  ;The Byte has changed
  CMP  B ES:[DI-1],0  ;Did it change to 0?
  JNE >T50            ;If not, it's a change
T30:                  ;Byte did not change, or changed to 0
  LOOP T10            ;Keep testing until we're done
  JMP >T90            ;If not bytes changed, we're done
T50:                  ;It changed
  MOV  CL,AH          ;Copy the New Data
  CALL CopyRawDataOld ;  into the Old Buffer
T90:                  ;Done
  POP  SI,DI,CX,AX    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE RAW DATA FOR A SINGLE DEVICE
;Inputs:  ES = TSR Data Area
;         DS = Local Data Area
;         CL = Device Info Index
;         Local Device Info Entry updated as appropriate
;         ProgFlags.Write2Err already set
;Outputs:
;Changes:
;------------------------------------------------------------------------------
WriteRawData:
  PUSH AX,CX,DX,DI,SI              ;Save used registers
  CALL DIIndex2Offset              ;Point DS:[SI] at the
  MOV  SI,DI                       ;   Local Device Info Entry
;  TEST ES:[DI].DIFlags,DIFlagInUse ;A valid mouse?
  TEST ES:[SI].DIFlags,DIFlagInUse ;A valid mouse?
  JZ  >W90                         ;If not, quit
  TEST ES:TSRFlags,EmuHardware     ;If so, should we write the PS2 Mouse?
  JZ  >W10                         ;If so, continue
  CMP  CL,MaxDevices               ;If not, is this the PS2 Mouse?
  JE  >W90                         ;If so, quit
W10:                               ;Valid Mouse
  CALL WriteSpace                  ;Move over
  MOV  AL,CL                       ;Put the Device Index in AL
  CMP  AL,MaxDevices               ;Is it the PS2 Mouse?
  JE  >W30                         ;If so, write "PS2" instead of the Number
W20:                               ;Write Index Number
  CALL ByteToString                ;Write the Device Index Number
  JMP >W40                         ;Continue
W30:                               ;Write "PS2"
  MOV  DX,PS2Msg                   ;Write
  CALL WriteZErr                   ;  "PS2"
W40:                               ;Device Index written
  CALL Write2Spaces                ;Move over
;  MOV  AL,ES:[DI].DIHostIndex      ;Write the
  MOV  AL,[SI].DIHostIndex         ;Write the
  CALL ByteToString                ;  Host Index
  CALL WriteSpace                  ;Move over
;  MOV  AL,ES:[DI].DIDvcAddress     ;Write the
  MOV  AL,[SI].DIDvcAddress        ;Write the
  CALL ByteToString                ;  Device Address
  CALL WriteSpace                  ;Move over
;  MOV  AL,ES:[DI].DIInterfaceNum   ;Write the
  MOV  AL,[SI].DIInterfaceNum      ;Write the
  CALL ByteToString                ;  Interface Number
  CALL Write2Spaces                ;Move over
  ADD  SI,(OFFSET DIStsData)       ;Point DS:[SI] at Raw Status Data
;  MOV  AL,ES:[DI]                  ;Write the
  MOV  AL,[SI]                     ;Write the
  CALL WriteRawBtns                ;  button data
  INC  SI                          ;Point at the next byte of data
  MOV  CX,7                        ;CX = Number of Bytes to write
W50:                               ;Loop to here for each raw key
  CALL WriteSpace                  ;Move over
  LODSB                            ;Write the
;  MOV  AL,ES:[DI]                  ;Write
;  INC  DI                          ;  the
  CALL WriteInteger                ;  Value
  LOOP W50                         ;Keep writing until we're done
  CALL WriteCrLfErr                ;Start a new line
W90:                               ;Done
  POP  SI,DI,DX,CX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE RAW BUTTON DATA TO THE SCREEN
;Inputs:  AL = Button Data to write
;         DS:RawBtnMsg = Translation Table
;         ProgFlags.Write2Err already set
;         CLD already issued
;Outputs:
;Changes:
;------------------------------------------------------------------------------
WriteRawBtns:
  PUSH AX,CX,SI     ;Save used registers
  MOV  AH,AL        ;Put buitton mask in AH
  MOV  BL,1         ;Start with Button 1
  MOV  CX,8         ;Write 8 characters
  MOV  SI,RawBtnMsg ;Point at the ASCII translation Table
B10:                ;Loop to here for each button
  LODSB             ;Get the character to write
  SHR  AH,1         ;Is the lowest bit set?
  JC  >B20          ;If so, write the character
  MOV  AL,'.'       ;If not, write a space
B20:                ;AL contains character to write
  CALL WriteAL      ;Write the appropriate character
  LOOP B10          ;Keep going until we're done
  POP  SI,CX,AX     ;Restore used registers

;------------------------------------------------------------------------------
;GET CURRENT VIDEO PAGE
;Inputs:
;Outputs: BH = Current video Page
;Changes: VPage, if not already calcualted done yet
;------------------------------------------------------------------------------
GetVideoPage:
  PUSH AX           ;Save used registers
  MOV  AL,VideoPage ;Get the current stored page number
  CMP  AL,-1        ;Have we already calculated the Video Page?
  JNE >V90          ;If so, we're done
  PUSH BX           ;If not, save used registers
  MOV  AH,0Fh       ;Service 0F (Get current video state)
                    ;Returns AH = Screen Width, AL = Display Mode, BH = Active Page
  INT  10h          ;Do It
  MOV  AL,BH        ;Store the video page number
  POP  BX           ;Restore used registers
V90:                ;DOne
  MOV  BH,AL        ;Set the value
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;READ CURSOR POSITION
;Inputs:
;Outputs: BH = Cursor Row
;         BL = Cursor Column
;Changes:
;------------------------------------------------------------------------------
GetCursor:
  PUSH AX,CX,DX     ;Save used register
  CALL GetVideoPage ;Get the Video Page (BH)
  MOV  AH,3         ;Service 3 (Get Cursor Position & Size)
                    ;Returns DX = Cursor Position, CX = Cursor Size
  INT  10h          ;Do it
  MOV  BX,DX        ;Put cursor info in BX where we want it
  POP  DX,CX,AX     ;Restore used register
  RET

;------------------------------------------------------------------------------
;SET CURSOR POSITION
;Inputs:  BH = Cursor Row
;         BL = Cursor Column
;Outputs:
;Changes:
;------------------------------------------------------------------------------
PutCursor:
  PUSH AX,BX,DX     ;Save used register
  MOV  DX,BX        ;Put cursor position in DX where it belongs
  CALL GetVideoPage ;Get the Video Page (BH)
  MOV  AH,2         ;Service 2 (Set Cursor Position)
  INT  10h          ;Do It
  POP  DX,BX,AX     ;Restore used register
  RET

;;------------------------------------------------------------------------------
;;SAVE CURSOR SIZE, HIDE THE CURSOR
;;Inputs:
;;Outputs: DS:CursorSize
;;Changes:
;;------------------------------------------------------------------------------
;HideCursor:
;  PUSH AX,BX,CX,DX   ;Save used register
;  CALL GetVideoPage  ;Get the Video Page (BH)
;  MOV  AH,3          ;Service 3 (Get Cursor Position & Size)
;                     ;Returns DX = Cursor Position, CX = Cursor Size
;  INT  10h           ;Do it
;  MOV  CursorSize,CX ;Put cursor info in BX where we want it
;;  XOR  CX,CX         ;Hide Cursor
;  MOV  CX,0F0Fh      ;Hide Cursor
;  MOV  AH,1          ;Function 1 (Set Cursor Size)
;  INT  10h           ;Do it
;  POP  DX,CX,BX,AX   ;Restore used register
;  RET
;
;;------------------------------------------------------------------------------
;;RESTORE ORIGINAL CURSOR SIZE
;;Inputs:  DS:CursorSize
;;Outputs:
;;Changes:
;;------------------------------------------------------------------------------
;ShowCursor:
;  PUSH AX,BX,CX      ;Save used register
;  CALL GetVideoPage  ;Get the Video Page (BH)
;  MOV  CX,CursorSize ;CX = Cursor Size
;  MOV  AH,1          ;Service 1 (Set Cursor Size)
;  INT  10h           ;Do it
;  POP  CX,BX,AX      ;Restore used register
;  RET


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Command-Line Aliases
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;PRINT THE COMMAND LINE ALIASES AND QUIT
;Inputs:  DS:[SI] = Current command line pointer
;Outputs: Writes Aliases to the screen
;Changes: Quits program
;------------------------------------------------------------------------------
DoAliases:
  TEST StatusFlags,Alias  ;Does user want to see the Aliases?
  JZ  >A95                ;If not, we're done
  PUSH BX,DX,SI           ;Save used registers

  CALL WriteCrLfPause     ;Move down
  MOV  DX,AliasHdrMsg     ;Set up
  CALL SetupPause         ;  the Header
  MOV  SI,SwitchList      ;Point at Tbl of Aliases
  MOV  BX,'?'+('Z' SHL 8) ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum   ;Write the Aliases
  MOV  BX,1+(31 SHL 8)    ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum   ;Write the Aliases
  CALL WriteCrLfPause2    ;Move down

  MOV  DX,MouseIdxHdrMsg  ;Set up
  CALL SetupPause         ;  the Header
  MOV  SI,SwitchList2     ;Point at Tbl of Aliases
  MOV  BX,MaxDevices+(MaxDevices SHL 8) ;BL = Min Value, BH = Max Value
  CALL DoAliasesGoNum     ;Write the Aliases
  MOV  BX,255+(255 SHL 8) ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNum     ;Write the Aliases
  CALL WriteCrLfPause2    ;Move down

  MOV  DX,YesNoHdrMsg     ;Set up
  CALL SetupPause         ;  the Header
  MOV  SI,SwitchListYesNo ;Point at Table of Aliases
  MOV  BX,'N'+('Y' SHL 8) ;BL = Min Value, BH = Max Value to look for
  CALL DoAliasesGoNoNum   ;Write the Aliases

A90:                      ;Done
  POP  SI,DX,BX           ;Restore used registers
  MOV  DX,NoMsg           ;Don't write anything
  XOR  AL,AL              ;ErrorLevel = 0
  JMP  Exit               ;Quit
A95:                      ;No need to write Aliases
  RET

;------------------------------------------------------------------------------
;WRITE THE LIST OF SWITCH CHARACTERS TO THE SCREEN
;Inputs:  DS = Local Data Area
;         ES = TSR Data Area
;         BL = Minimum Value to look for in the Table
;         DH = Maximum Value to look for in the Table
;         SI = Switch List ("Code Word") Table
;Outputs: Writes List of Switch Characters to the Screen
;Changes:
;NOTES:   Sets CH =  0 if we write Blocks for Switches < " "
;                 = -1 if we write numbers for the all Switches (0-255)
;                 = -2 if we subtract 128 from the numbers (128-255)
;------------------------------------------------------------------------------
;DoAliasesGoNum80h:
;  PUSH CX               ;Save used registers
;  MOV  CH,2             ;Mark as writing Numbers, needing to subtract 128
;  JMP >G00              ;Continue
DoAliasesGoNum:
  PUSH CX               ;Save used registers
  MOV  CH,1             ;Mark as writing Numbers, not needing to modify
  JMP >G00              ;Continue
DoAliasesGoNoNum:
  PUSH CX               ;Save used registers
  XOR  CH,CH            ;Mark as not writing Numbers
;  JMP >G00              ;Continue
G00:
  PUSH AX,DI            ;Save used registers
  MOV  DI,AliasSortTbl  ;Point at the Alias Sort Table
  MOV  AL,BL            ;Start with the Lower Limit
G10:                    ;Loop to here for each Alias
  CALL GetAliasPointers ;Get the Alias Pointers
  JZ  >G60              ;If none for this entry, go to the next one
G20:                    ;Have at least one valid Alias
  CALL WriteCrLfPause   ;If valid, move down
  OR   CH,CH            ;Writing Single characters?
  JNZ >G40              ;If not, handle full numbers
  MOV  CL,78            ;Write this many characters per line
  CMP  AL,' '           ;Is there a valid single-character equivalent?
  JB  >G30              ;If not, don't write it
  CALL WriteAL          ;If so, write it
  JMP >G50              ;Continue
G30:                    ;Not a writable character
  CALL WriteBlock       ;Write a Block Character
  JMP >G50              ;Continue
G40:                    ;Need to write Numbers
  MOV  CL,76            ;Write this many characters per line
  PUSH AX               ;Save the number for a second
  CMP  CH,2             ;Do we need to modify the Number?
  JNE >G45              ;If not, continue
  AND  AL,(NOT 80h)     ;Subtract 128 from it
G45:                    ;Write the Number
  CALL ByteToString     ;Write the Switch as a Number
  POP  AX               ;Restore the Number
G50:                    ;Done writing single character
  CALL WriteAliases     ;Write the Alias List
G60:                    ;Done with this Code
  CMP  AL,BH            ;Have we done the whole list yet?
  JE  >G90              ;If so, we're done
  INC  AL               ;If not, go to the next possibility
  JMP  G10              ;Keep going until we're done
G90:                    ;Done
  POP  DI,AX            ;Restore used registers
  POP  CX               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET STRING POINTERS FOR ALL ALIASES FOR A SINGLE CHARACTER OPTION
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;          AL  = Switch Table entry to look for (not 0)
;         [SI] = Switch Table to look through, sorted backwards
;         [DI] = Alias Sort Table
;Outputs:  ZF = Clear if OK
;               [DI] = Filled with String Pointers, sorted backwards
;                      Last Entry is always a 0
;             = Set if no matches found
;                      First Entry is 0
;Changes:
;------------------------------------------------------------------------------
GetAliasPointers:
  PUSH AX,BX,CX,DX      ;Save used registers
  PUSH DI,SI            ;Save used registers
  MOV  CX,DI            ;Sve original pointer
P10:                    ;Loop to here for each Table Entry
  MOV  DX,SI            ;Save beginning Alias Pointer
P20:                    ;Loop to here to find the end of an Alias List Entry
  CMP  B [SI],0         ;End of Alias List?
  JE  >P30              ;If so, handle it
  CALL SkipOverStringSI ;If not, skip over the string
  JMP  P20              ;Keep looking
P30:                    ;End of Alias List Entry
  MOV  BX,SI            ;Save End-of-Entry Pointer
  CMP  AL,[SI+1]        ;Is this entry a match?
  JNE >P50              ;If not, go to the next one
  MOV  SI,DX            ;Point at Alias String
P40:                    ;Loop to here for each Alias String
  CMP  B [SI],0         ;End of the entry?
  JE  >P50              ;If so, we're done
  MOV  [DI],SI          ;If not, store it in the sort table
  INC  DI,2             ;Update the Table Pointer
  CALL SkipOverStringSI ;Skip over the string
  JMP  P40              ;Keep going until we have all of the strings
P50:                    ;Done with this entry
  MOV  SI,BX            ;Get End-of-Entry Pointer
  INC  SI,2             ;Skip over End-of-Entry stuff
  CMP  B [SI],0         ;End of Table?
  JNE  P10              ;If not, keep looking
P90:                    ;Done
  MOV  W [DI],0         ;Make sure last entry is a 0
  CMP  CX,DI            ;Set return flag
  POP  SI,DI            ;Restore used registers
  POP  DX,CX,BX,AX      ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE ALL ALIAS STRINGS FOR A SINGLE CHARACTER OPTION
;Inputs:   DS  = Local Data Area
;          ES  = TSR Data Area
;         [DI] = Alias String Pointer Table (Sorted backwards)
;                  Last Entry is a 0
;          CL  = Number of characters left available to write on first line
;                 (76 if Writing Numbers, 78 if Writing Characters)
;         CLD already issued
;Outputs:
;Changes:
;------------------------------------------------------------------------------
WriteAliases:
  PUSHA               ;Save all registers
  PUSH ES             ;Save used registers
  MOV  ES,DS          ;Point ES at the Local Data Area
  MOV  BL,CL          ;BL = Character Countdown Measurer
  MOV  BH,CL          ;BH = Original Counter
  MOV  DX,DI          ;Save start-of-table Pointer
  MOV  CX,-1          ;Maximize counter
  XOR  AX,AX          ;Find the last
  REPNE SCASW         ;  entry in
  SUB  DI,4           ;  the Table
W10:                  ;Loop to here for each Alias String
  CMP  DI,DX          ;Are we done writing?
  JB  >W90            ;If so, quit
  MOV  SI,[DI]        ;Get the next string
  DEC  DI,2           ;Update the table pointer
  CALL CalcStrSizeSI  ;See how long the string is
  OR   BL,BL          ;Is the character counter down to 0?
  JZ  >W20            ;If so, we need to reset it
  CMP  CL,BL          ;Is this string too long to fit on this line?
  JB  >W40            ;If not, go ahead and write it
W20:                  ;Need to start a new line
  CALL WriteCrLfPause ;Start a new line
  PUSH CX             ;Save String Size for a second
  MOV  CL,79          ;Calculate how many
  SUB  CL,BH          ;  spaces to write
  CALL WriteSpaces    ;Write the spaces
  POP  CX             ;Restore String Size
  MOV  BL,BH          ;Reset the character counter
W40:                  ;Write the Alias String
  SUB  BL,CL          ;Update the
  DEC  BL             ;  character counter
  CALL WriteSpace     ;Write a Space
  CALL WriteZPauseSI  ;Write the string
  JMP  W10            ;Keep writing
W90:                  ;Done
  CALL WriteCrLfPause ;Move down
  POP  ES             ;Restore used registers
  POPA                ;Restore all registers
  RET


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write the ErrorLevel Table
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;WRITE ERRORLEVEL TABLE TO THE SCREEN AND QUIT
;Inputs:  CS = DS = Local Data Area
;Outputs: Writes ErrorLevel Table to the Screen
;Changes: Quits Program
;         Several registers
;------------------------------------------------------------------------------
DoErrLvls:
  TEST StatusFlags,ErrLvl   ;Does the user want to see the ErrorLevels?
  JZ  >V95                  ;If not quit
  MOV  DX,ErrLvlHdr         ;Write the
  CALL SetupPause           ;  Table Header
  MOV  AL,1                 ;Start with ErrorLevel 1
V10:                        ;Loop to here for each table entry
  MOV  BX,ErrLvlTbl         ;Table to search
  CALL SearchByte2StringTbl ;Valid ErrorLevel?
  JC  >V50                  ;If not, keep looking
  CALL WriteSpace           ;Move Over
  CALL ByteToString         ;If valid, write the Number
  CALL WriteSpace           ;Write a space
  MOV  DX,BX                ;Write the
  CALL WriteZPause          ;  Description String
  CALL WriteCrLfPause       ;Write a New Line
V50:                        ;Done with this entry
  INC  AL                   ;Increment ErrorLevel
  JNZ  V10                  ;If not rolled over yet, keep looking
V90:                        ;Done
  MOV  DX,NoMsg             ;Don't write anything
  XOR  AL,AL                ;ErrorLevel = 0
  JMP  Exit                 ;Quit
V95:                        ;No need to write Aliases
  RET


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Program/Mice Status
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;WRITE THE CURRENT STATUS TO THE SCREEN
;Inputs:  ES = TSR Data Area
;         Status (Flag)
;Outputs: Writes a bunch of stuff to the screen if Status Flag is True
;Changes:
;------------------------------------------------------------------------------
DoStatus:
  TEST StatusFlags,Status ;Need to to anything?
  JZ  >S000               ;If not, quit
  CALL ChangeRates        ;If so, change Rates, if Appropriate
  TEST ProgFlags,InMemory ;Are we installed in memory yet?
  JNZ >S00                ;If so, go ahead and do it
  MOV  DX,StatusErrMsg    ;If not, point at the first part of Error Message
  JMP  FinishDataErr      ;Terminate program
S000:
  RET

S00:                                  ;Need to do Status
  CALL GotAMouse                      ;Any USB Mice installed?
  JNC >S001                           ;If so, continue
  PUSH DX                             ;If not,
  MOV  DX,NoMouseMsg                  ;  just write
  CALL WriteZPause                    ;  the "No Mouse"
  POP  DX                             ;  message
  RET

S001:                                  ;Do the Status thing!
  PUSH AX,BX,CX,DX                    ;Save used registers
  MOV  BX,0500h                       ;BL = Index (0), BH = Indent Spaces (5)

S10:                                  ;Write INT 33h Enabled Status
  MOV  DX,StatusMsg1
  CALL WriteZPause
  MOV  DX,YesMsg
  TEST ES:TSRFlags,Int15Enabled
  JNZ >S15
  MOV  DX,NoMsgS
S15:
  CALL WriteZPause

S20:                                  ;Write number of Enabled Wheels
  MOV  DX,StatusMsg2
  CALL WriteZPause
  XOR  AL,AL
  CMP  ES:DeviceID,DvcIDWheel1Yes
  JB  >S25
  JE  >S22
  INC  AL
S22:
  INC  AL
S25:
  CALL ByteToString

  MOV  DX,StatusMsg3                  ;Write Resolution
  CALL WriteZPause
  MOV  CL,ES:Resolution
  MOV  AL,1
  SHL  AL,CL
  CALL ByteToString
  CALL DoIndividualStatus             ;Do Individual Status for Index 0

S30:                                  ;Write Scaling Factor
  MOV  DX,StatusMsg4
  CALL WriteZPause
  MOV  DX,Scaling1Msg
  TEST ES:TSRFlags,ScalingFactor2
  JZ  >S35
  MOV  DX,Scaling2Msg
S35:
  CALL WriteZPause
  CALL DoIndividualStatus             ;Do Individual Status for Index 1

S40:                                  ;Write Sample Rate
  MOV  DX,StatusMsg5
  CALL WriteZPause
  MOV  AL,ES:SampleRateCode
  CMP  AL,SampleCode200
  JNE >S42
  MOV  AL,200
  JMP >S45
S42:
  PUSH BX
  MOV  BX,SampleRateTable
  CALL SearchByteToWordTbl
  MOV  AL,BL
  POP  BX
S45:
  CALL ByteToString
  CALL DoIndividualStatus             ;Do Individual Status for Index 2

S50:                                  ;Write Global Swap L/R Button Status
  MOV  DX,StatusMsg6
  CALL WriteZPause
  MOV  DX,YesMsg
  TEST ES:TSRFlags,SwapBtns
  JNZ >S55
  MOV  DX,NoMsgS
S55:
  CALL WriteZPause
  CALL DoIndividualStatus             ;Do Individual Status for Index 3

  MOV  DX,Status1EndMsg               ;Write last line
  CALL WriteZPause                    ;  of Combined Table
  CALL DoIndividualStatus             ;Do Individual Status for Index 4

S60:                                  ;Write Emulation Level
  MOV  DX,EmulationMsg
  CALL WriteZPause
  MOV  DX,BIOSMsg
  TEST ES:TSRFlags,EmuHardware
  JZ  >S65
  MOV  DX,HardwareMsg
S65:
  CALL WriteZPause
  CALL DoIndividualStatus             ;Do Individual Status for Index 5

  MOV  DX,MethodMsg                   ;Write HW Emulation Method
  CALL WriteZPause
  MOV  AL,ES:Method
  CALL ByteToString
  CALL DoIndividualStatus             ;Do Individual Status for Index 6

  MOV  DX,Status1EndMsg               ;Write last line
  CALL WriteZPause                    ;  of Left-hand Table
  CALL DoIndividualStatus             ;Do Individual Status for Index 7
  MOV  BH,37                          ;Increase Indent
  CALL DoPS2Status                    ;Do the PS2 Status
  MOV  DX,Status2EndMsg               ;Write the last line of the
  CALL WriteZPause                    ;  Individual Table
S90:                                  ;Done
  CALL WriteCrLfPause                 ;Write a New Line
  POP  DX,CX,BX,AX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE STATUS OF AN INDIVIDUAL DEVICE TO THE SCREEN
;Inputs:  BL = Device Index to write
;         BH = Number of spaces to write at beginning of line (Indent)
;         ES = TSR Data Area
;Outputs: To Screen
;Changes: BL is incremented
;------------------------------------------------------------------------------
DoIndividualStatus:
  PUSH AX,CX,DX,DI                    ;Save used registers
  MOV  CL,BH                          ;Do the
  CALL WriteSpaces                    ;  Indent
  MOV  AL,BL                          ;Write the Index Number
  CALL WriteSpace
  CALL WriteNibbleHex
  CALL WriteSpace
  CALL DIIndex2OffsetBL               ;Convert Index to Offset (DI)

  CALL Write2Spaces                   ;Write Enabled Status
  MOV  DX,YesMsg
  TEST ES:[DI].DIFlags,DIFlagDisabled
  JZ  >I15
  MOV  DX,NoMsgS
I15:
  CALL WriteZPause

  CALL Write2Spaces                   ;Write L/R Button Swap Status
  MOV  DX,YesMsg
  TEST ES:[DI].DIFlags,DIFlagSwapBtns
  JNZ >I25
  MOV  DX,NoMsgS
I25:
  CALL WriteZPause

  TEST ES:[DI].DIFlags,DIFlagInUse    ;Valid Table Entry?
  JZ  >I90                            ;If not, we're done with this one

  CALL Write2Spaces                   ;Write Number of Buttons
  MOV  AL,ES:[DI].DINumButtons
  CALL ByteToString

  CALL Write4Spaces                   ;Write Wheel Status
  MOV  AL,ES:[DI].DINumWheels
  CALL ByteToString

  CALL Write2Spaces                   ;Write Host Index
  MOV  AL,ES:[DI].DIHostIndex
  CALL ByteToString

  CALL Write2Spaces                   ;Write Device Address
  MOV  AL,ES:[DI].DIDvcAddress
  CALL ByteToString

  CALL Write2Spaces                   ;Write Interface Number
  MOV  AL,ES:[DI].DIInterfaceNum
  CALL ByteToString

I90:                                  ;Done
  CALL WriteCrLfPause                 ;Start a new line
  INC  BL                             ;Increment Index
  POP  DI,DX,CX,AX                    ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF WE HAVE EVEN A SINGLE MOUSE (EITHER USB OR PS2)
;Inputs:  ES = TSR Data Area
;         DeviceInfoTbl
;Outputs: CF = Clear if at least one Mouse/Wheel on USB Devices
;            = Set if not
;Changes:
;------------------------------------------------------------------------------
GotAMouseNoPS2:
  PUSH CX                          ;Save used registers
  MOV  CX,MaxDevices               ;Number of entries to test (exclude PS2)
  JMP >G00                         ;Do it
GotAMouse:
  PUSH CX                          ;Save used registers
  MOV  CX,(MaxDevices+1)           ;Number of entries to test (include PS2)
;  JMP >G00                         ;Do it
G00:
  PUSH DI                          ;Save used registers
  MOV  DI,DeviceInfoTable          ;Point at Table
G10:                               ;Loop to here for each Table Entry
  TEST ES:[DI].DIFlags,DIFlagInUse ;Valid Device here?
  JNZ >G80                         ;If so, we have a mouse
  ADD  DI,DeviceInfoStrucSize      ;Point at next table entry
  LOOP G10                         ;Keep going
G70:                               ;No Device Installed
  STC                              ;Set No Device Flag
  JMP >G90                         ;Done
G80:                               ;At least one Device
  CLC                              ;Set OK Flag
G90:                               ;Done
  POP  DI                          ;Restore used registers
  POP  CX                          ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE THE STATUS OF THE PS2 MOUSE
;Inputs:  BH = Number of spaces to write at beginning of line (Indent)
;         ES = TSR Data Area
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
DoPS2Status:
  PUSH AX,CX,DX,DI                    ;Save used registers
  MOV  CL,BH                          ;Do the
  CALL WriteSpaces                    ;  Indent
  MOV  DX,PS2Msg                      ;Write the
  CALL WriteZPause                    ;  "Index"
  CALL Write2Spaces

  MOV  DX,YesMsg                      ;Write Enabled Status
  TEST ES:[DIPS2Offset].DIFlags,DIFlagDisabled
  JZ  >P10
  MOV  DX,NoMsgS
P10:
  CALL WriteZPause

  CALL Write2Spaces                   ;Write Button L/R Exchange Status
  MOV  DX,YesMsg
  TEST ES:[DIPS2Offset].DIFlags,DIFlagSwapBtns
  JNZ >P20
  MOV  DX,NoMsgS
P20:
  CALL WriteZPause

  TEST ES:[DIPS2Offset].DIFlags,DIFlagInUse ;PS2 Mouse even installed?
  JNZ >P25                            ;If so, continue
  MOV  DX,NoPS2Msg                    ;If not, write
  CALL WriteZPause                    ;  "not installed" message
  JMP >P90                            ;Done
P25:                                  ;PS2 Mouse is installed
  CALL Write2Spaces                   ;Write Btns & Wheels
  CMP  ES:[DIPS2Offset].DINumWheels,1 ;How many Wheels does the PS2 Mouse have?
  JA  >P30                            ;If 2, jump to handle it
  JB  >P40                            ;If none, jump to handle it
  MOV  DX,Maybe3Msg                   ;1 Wheel
  CALL WriteZPause
  MOV  AL,1
  JMP >P50
P30:                                  ;IntelliMouse2
  MOV  DX,Maybe5Msg
  CALL WriteZPause
  CALL Write4Spaces
  MOV  DX,Maybe2Msg
  CALL WriteZPause
  JMP >P60
P40:                                  ;Not IntelliMouse or IntelliMouse2
  MOV  DX,BtnsUnknownMsg
  CALL WriteZPause
  XOR  AL,AL
P50:                                  ;Write number of wheels
  CALL Write4Spaces
  CALL ByteToString

P60:
  CALL Write2Spaces                   ;Write Host Index
  MOV  DX,NAMsg
  CALL WriteZPause
  CALL Write2Spaces                   ;Write Device Address
  CALL WriteZPause
  CALL Write2Spaces                   ;Write Interface Number
  CALL WriteZPause
P90:                                  ;Done
  CALL WriteCrLfPause                 ;Start a new line
  INC  BL                             ;Increment Index
  POP  DI,DX,CX,AX                    ;Restore used registers
  RET


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Device/Config Descriptors
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;WRITE THE DESCRIPTORS OF THE LAST DEVICE TO THE SCREEN, IF NECESSARY
;Inputs:  ES:DescriptorData
;Outputs: To Screen
;Changes:
;------------------------------------------------------------------------------
DoDescriptors:
  TEST StatusFlags,Descriptors ;Does the user want to see the Descriptors?
  JZ  >D000                    ;If not, quit
  TEST ProgFlags,InMemory      ;Are we installed in memory yet?
  JNZ >D00                     ;If so, go ahead and do it
  MOV  DX,DescrErrMsg          ;If not, point at first part of Error Message
  JMP  FinishDataErr           ;Terminate program
D000:
  RET

D00:
  CALL TestLastDevice          ;Anything to do (quits if not)?
  MOV  DI,ES:LastDeviceFound   ;Get Info Table Pointer
  MOV  AX,ES                   ;Write the address
  MOV  CX,DvcDescrData         ;  of the Descriptor Data
  MOV  BX,DescriptorMsgAddr    ;  to the
  CALL WriteSegmentOffset      ;  Command Line Parameters
  MOV  BX,DescriptorMsgHost    ;Write
  MOV  AL,ES:[DI].DIHostIndex  ;  the
  CALL WriteByteToString       ;  Host Index
  INC  BX                      ;  and the
  MOV  AL,ES:[DI].DIDvcAddress ;  Device Address
  CALL WriteByteToString       ;  to the string
  MOV  AX,CS                   ;Write our
  MOV  CX,WriteZPauseFar       ;  Call-Back Address
  MOV  BX,DescriptorCallBk     ;  to the
  CALL WriteSegmentOffset      ;  Command Tail String
  MOV  BX,DescriptorMsg        ;Copy our String
  CALL CopyToCmdTail           ;  to the Command Tail String
  CALL DoExec                  ;Write the Descriptors
  MOV  DX,NoMsg                ;Write nothing
  XOR  AL,AL                   ;ErrorLevel = 0
  JMP  Exit                    ;Quit


;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ
;Write Report Descriptor
;ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ

;------------------------------------------------------------------------------
;PRINT OUT THE REPORT DESCRIPTOR DATA, IF NECESSARY
;Inputs:  ES:ReportData
;Outputs:
;Changes:
;------------------------------------------------------------------------------
DoReport:
  TEST StatusFlags,Report ;Does the user want to see the Descriptors?
  JZ  >R000               ;If not, quit
  TEST ProgFlags,InMemory ;Are we installed in memory yet?
  JNZ >R00                ;If so, go ahead and do it
  MOV  DX,ReportErrMsg    ;If not, point at the first part of Error Message
  JMP  FinishDataErr      ;Terminate program
R000:
  RET

R00:
  CALL TestLastDevice        ;Anything to do (quits if not)?
  MOV  DI,ES:LastDeviceFound ;Get Info Table Pointer
  MOV  AX,ES                 ;Write the address
  MOV  CX,ReportData         ;  of the Report Data
  MOV  BX,ReportDescrMsgAddr ;  to the
  CALL WriteSegmentOffset    ;  Command Tail String
  MOV  AX,CS                 ;Write our
  MOV  CX,WriteZPauseFar     ;  Call-Back Address
  MOV  BX,ReportDescrCallBk  ;  to the
  CALL WriteSegmentOffset    ;  Command Tail String
  MOV  BX,ReportDescrMsg     ;Copy our String
  CALL CopyToCmdTail         ;  to the Command Tail String
  CALL DoExec                ;Call the Support Program
  MOV  DX,NoMsg                ;Write nothing
  XOR  AL,AL                   ;ErrorLevel = 0
  JMP  Exit                    ;Quit


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Support Code needed to Write USB Descriptors
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;TEST AND SEE IF WE CAN PRINT A DESCRIPTOR (LASTDEVICEFOUND & SUPPORTFILE)
;Inputs:  ES = TSR Data Area
;         LastDeviceFound
;Outputs: Quits program if error
;         Initializes PathToUse, Prints Header if OK
;Changes:
;------------------------------------------------------------------------------
TestLastDevice:
  PUSH AX,DX,SI                    ;Save used registers
  MOV  SI,ES:LastDeviceFound       ;Get the Device Pointer that this is for
  TEST ES:[SI].DIFlags,DIFlagInUse ;Device still there?
  JNZ >L10                         ;If so, continue
  MOV  DX,NoDescrDataMsg           ;Point at Error Message
  MOV  AL,ErLvlNoData              ;ErrorLevel = No Data
  JMP  Exit                        ;Quit
L10:                               ;Have some Data
  CALL TestSupportFile             ;Look for the Support File in the Path
  JNZ >L20                         ;If found, continue
  MOV  DX,NoSuptFileMsg            ;If not, write first part
  CALL WriteZErr                   ;  of Error Message
  MOV  DX,SupportFile              ;Write the
  CALL WriteZErr                   ;  File Name
  JMP  Exit                        ;Quit

L20:                               ;Everything is OK
  MOV  DX,HostIndexMsg             ;Write
  CALL WriteZPause                 ;  the
  MOV  AL,ES:[SI].DIHostIndex      ;  Host
  CALL ByteToString                ;  Index
  CALL WriteCrLfPause              ;Write a new line
  MOV  DX,DvcAddressMsg            ;Write
  CALL WriteZPause                 ;  the
  MOV  AL,ES:[SI].DIDvcAddress     ;  Device
  CALL ByteToString                ;  Address
  CALL WriteCrLfPause2             ;Write 2 new lines
L90:                               ;Done
  POP  SI,DX,AX                    ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO DELAY FOR A LITTLE WHILE
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;DELAY FOR A GIVEN NUMBER OF MILLISECONDS
;Inputs:  AX = number of milliseconds to wait (0-65,535)
;Outputs:
;Changes:
;------------------------------------------------------------------------------
DelayMSAX:
  PUSH AX,BX,DX  ;Save used registers
  OR   AX,AX     ;Is there anything to do?
  JZ  >D90       ;If not, just quit
  MOV  DX,1000   ;Convert
  MUL  DX        ;  to
  MOV  BX,AX     ;  microseconds (DX:BX)
D10:             ;Loop to here for each 65535 uS
  OR   DX,DX     ;Counted down high word yet?
  JZ  >D30       ;If so, handle it
  DEC  DX        ;Decrement high word
  MOV  AX,-1     ;Maximum microseconds
  JMP >D50       ;Do it
D30:             ;Counted down high word
  OR   BX,BX     ;Anything to do?
  JZ  >D90       ;If not, we're done
  MOV  AX,BX     ;Get Low Word
  XOR  BX,BX     ;Make sure it's zero next time
D50:             ;AX = Microseconds to wait
  CALL DelayUSAX ;Delay the number of microseconds specified
  JMP  D10       ;See if we need to wait some more
D90:             ;We're done
  POP  DX,BX,AX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DELAY FOR A GIVEN NUMBER OF MICROSECONDS
;Inputs:  AX = number of microseconds to wait (0-65535)
;Outputs:
;Changes:
;   NOTE: Delays will be slightly longer than what's asked for, never shorter.
;         Accuracy decreases with small delays or slow computers!
;------------------------------------------------------------------------------
DelayUSAX:
  PUSH AX,BX           ;Save used registers
  OR   AX,AX           ;Is there anything to do?
  JZ  >D90             ;If not, just quit
  MOV  BX,AX           ;Put it in BX
  MOV  AX,(0FFFFh/3)+1 ;Maximum time for each sub-delay
D10:                   ;Loop to here for each sub-delay
  CMP  BX,AX           ;Is it more than the maximum?
  JA  >D40             ;If so, just do the sub-delay
  MOV  AX,BX           ;If not, just do the remainder that's left
D40:                   ;Do the delay
  CALL DoDelayUS       ;Do the US Delay
  SUB  BX,AX           ;SUbtract out how long we just waited
  JNZ  D10             ;If not 0 yet, do the sub-delay again
D90:                   ;We're done
  POP  BX,AX           ;Restore used registers
  RET

DoDelayUS:
  PUSH AX,BX,CX,DX ;Save used registers
  PUSHF            ;Save Flags
  SHL  AX,1        ;Multiply by 2
  MOV  BX,11932    ;Compensate for the fact
  MUL  BX          ;  that the clock frequency
  MOV  BX,10000    ;  is actually
  DIV  BX          ;  1.19318 MHz
  MOV  DX,AX       ;Save it
  CLI              ;Disable interrupts
  CALL GetTimer    ;Get the starting timer tick counter
  MOV  CX,AX       ;Save it
D20:               ;Keep looping to here until we've waited long enough
  MOV  BX,CX       ;Get the starting timer tick counter
  CALL GetTimer    ;Get the current timer tick counter
  SUB  BX,AX       ;Calculate the elapsed time
  CMP  BX,DX       ;Has it been long enough?
  JB   D20         ;If not, keep waiting
D90:               ;We're done
  POPF             ;Restore Flags
  POP  DX,CX,BX,AX ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET CURRENT TIMER COUNTER FROM PORT 40h (THE CLOCK INTERRUPT)
;Inputs:
;Outputs: AX = Current timer counter word
;Changes:
;------------------------------------------------------------------------------
GetTimer:
  PUSHF       ;Save flags
  CLI         ;DIsable interrupts
  MOV  AL,06h ;Bits 7:6 =  00 = Timer 0
              ;Bits 5:4 =  00 = Latch Counter
              ;Bits 3:1 = 011 = Mode 3 (Square Wave)
              ;Bit    0 =   0 = Binary Counter (16 bits)
  OUT  43h,AL ;Tell the PIT what wer'e going to do
  IN   AL,40h ;Get LSB of timer counter
  MOV  AH,AL  ;Save it
  IN   AL,40h ;Get MSB of timer counter
  XCHG AH,AL  ;Put things in the right order
  POPF        ;Restore flags
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code to test and see if the computer is compatible or not.
;Tests DOS Version, Hardware installed, etc. as appropriate to make sure
;  we don't do something that could crash the computer.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;TEST THE COMPUTER TO SEE IF IT'S COMPATIBLE (CPU, DOS VERSION, WHATEVER)
;Inputs:  InMemory
;Outputs: Quits the program if Computer is Incompatible
;         Returns if Computer is OK
;         Stores all kinds of values in memory variables
;Changes:
;------------------------------------------------------------------------------
TestCompatibility:
  PUSH AX,DX              ;Save used registers
  TEST ProgFlags,InMemory ;Are we already installed?
  JNZ >C90                ;If so, no need to test again
  MOV  AL,ErLvlBadCPU     ;Assume incorrect
  MOV  DX,BadCPUMsg       ;  CPU type
  CALL Test8086           ;Is the CPU at least an 80286?
  JNC >C70                ;If not, quit
  MOV  AL,ErLvlDOSVer     ;Assume incorrect
  MOV  DX,DOSVerMsg       ;  DOS Version
  CALL TestDOSVer         ;  new enough?
  JC  >C70                ;If not, quit
  MOV  AL,ErLvlNoHost     ;Assume Host Driver
  MOV  DX,NoHostMsg       ;  is not installed
  CALL TestUSBHost        ;Is there a USB Host Driver installed?
  JNC >C90                ;If not, we're OK
C70:                      ;The computer is incompatible for some reason
  JMP  Exit               ;Quit!
C90:                      ;The computer is compatible!
  POP  DX,AX              ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF THE CPU IS AN 8086/8088
;Inputs:
;Outputs: CF = Clear if the CPU is an 8086/8088
;            = Set if 80286+
;Changes:
;------------------------------------------------------------------------------
Test8086:
  PUSH AX,BX     ;Save used registers
  PUSHF          ;Save flags
  MOV  BX,0F000h ;Flags mask for testing (these bits always set on 8086/8088)
  PUSHF          ;Put the current flags
  POP  AX        ;  in AX
  AND  AX,0FFFh  ;Try to clear the
  PUSH AX        ;  four high bits
  POPF           ;  of the flags
  PUSHF          ;Put the current flags
  POP  AX        ;  in AX
  AND  AX,BX     ;Are the four high bits
  CMP  AX,BX     ;  of the flags set?
  JE  >E80       ;If so, it's an 8086/8088
  POPF           ;Restore flags
E70:             ;It is not an 8086/8088
  STC            ;Set the not 8086/8088 flag
  JMP >E90       ;We're done
E80:             ;It is an 8086/8088
  POPF           ;Restore flags
  CLC            ;Set the 8086/8088 flag
E90:             ;We're done
  POP  BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST THE CURRENT DOS VERSION
;Inputs:  MinDOSVer (EQUate)
;Outputs: CF = Clear if DOS version >= MinDOSVer
;         CF = Set if DOS version < MinDOSVer
;Changes:
;------------------------------------------------------------------------------
TestDOSVer:
  PUSH AX,BX,CX     ;Save used registers
  MOV  AX,3000h     ;Service 30h (get DOS version number)
                    ;  DOS 5.0 requires AL = 00h, may change BX and CX
  INT  21h          ;Do it (Returns AL= Major, AH = Minor)
  XCHG AL,AH        ;Swap Major and Minor versions for testing
  CMP  AX,MinDOSVer ;Is the version new enough?
  JAE >D80          ;If so, it's OK
D70:                ;DOS is older than the minimum
  STC               ;Set the "too old" flag
  JMP >D90          ;We're done
D80:                ;DOS is >= the minimum
  CLC               ;Set the OK flag
D90:                ;We're done
  POP  CX,BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST AND SEE IF A COMPATIBLE USB HOST DRIVER IS INSTALLED
;Inputs:
;Outputs: CF = Set if error (No Host Driver Installed)
;            = Clear if OK
;Changes:
;------------------------------------------------------------------------------
TestUSBHost:
  PUSH AX,BX,CX ;Save used registers
  MOV  AX,5000h ;Function 5000h (Install Check)
  MOV  BX,'US'  ;Set
  MOV  CX,'B!'  ;  registers
  STC           ;Preset Error Flag
  INT  14h      ;Do it
  JC  >U70      ;If Error, USB Host can't be installed
  OR   AX,AX    ;Is AX Correct?
  JNZ >U70      ;If not, Error
  CMP  BX,'B!'  ;Is BX correct?
  JNE >U70      ;If not, Error
  CMP  CX,'US'  ;Is CX correct?
  JE  >U80      ;If so, it's installed
U70:            ;Not installed
  STC           ;Set Error Flag
  JMP >U90      ;Done
U80:            ;Installed
  CLC           ;Set OK flag
U90:            ;Done
  POP  CX,BX,AX ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code to test and see which hardware emulation method to use.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;TEST TO SEE WHICH HARDWARE EMULATION METHOD TO USE
;Inputs: CS = DS = ES = Local Data Area
;        (Program is not installed yet as TSR,
;           and we haven't yet copied ourselves into a UMB)
;Outputs: Method = 1, 2, or 3 (picks the lowest number that works OK)
;Changes:
;NOTES: This does not test the Method if the Method is already known (<> 0)
;------------------------------------------------------------------------------
TestMethod:
  PUSH DI               ;Save used registers
  MOV  DI,OFFSET Method ;Point [DI] at the Method
  CMP  B [DI],0         ;Did the user provide us a Method to use?
  JNE >M90              ;If so, we don't need a Test
  MOV  B [DI],1         ;Use Method 1
  CALL TestMethodGo     ;Test it
  JZ  >M90              ;If it worked, we're done
  INC  B [DI]           ;If not, use Method 2
;  CALL TestMethodGo     ;Test it
;  JZ  >M90              ;If it worked, we're done
  INC  B [DI]           ;If not, use Method 3
M90:                    ;We've calculated a Method to use
  POP  DI               ;Restore used registers
  RET

;------------------------------------------------------------------------------
;TEST A PARTICULAR HARDWARE EMULATION METHOD
;Inputs:  Method = 1 or 2 (The method to test)
;Outputs: ZF = Set if it worked
;            = Clear if it didn't
;Changes:
;------------------------------------------------------------------------------
TestMethodGo:
  PUSH AX,BX,CX,DI,SI     ;Save used registers
  MOV  AH,-1              ;Assume the test doesn't work
  CALL DisableMousePort   ;Disable the Real Mouse
  CALL DisableKeybIRQ     ;Disable the Keyboard Interrupt
  MOV  AL,74h             ;Change Int 74h
  MOV  DI,Int74Hdr        ;  (IRQ 12/PS2 Mouse)
  CALL ChangeInt          ;  to our code
  MOV  SI,OFFSET TestByte ;Point [SI] at the TestByte
  MOV  BL,11              ;Start by trying to send an 11
M10:                      ;Loop to here for each test byte
  MOV  [SI],BL            ;Start by setting TestByte to something
  INC  B [SI]             ;  other than the byte we're sending
  CALL FakeIt             ;Send the Byte
  MOV  CX,-1              ;Set the Loop Counter
M40:                      ;Loop to here to wait for the Code
  CMP  [SI],BL            ;Has the TestByte changed?
  LOOPNE M40              ;If not, keep waiting
  JCXZ >M90               ;If it didn't work, we're done
  SUB  BL,1               ;If it worked, change the Test Byte
  JNC  M10                ;If we haven't tested all possibilities, keep testing
  INC  AH                 ;If we have, set the "it worked" flag
M90:                      ;Done
  CALL RestoreInt         ;Restore the original IRQ 12 Code (uses AL, ES:[DI])
  CALL EnableKeybIRQ      ;Enable the Keyboard Interrupt
  CALL EnableMousePort    ;Enable the Real Mouse
  OR   AH,AH              ;Set the return flag
  POP  SI,DI,CX,BX,AX     ;Restore used registers
  RET

;------------------------------------------------------------------------------
;HAVE INTERRUPT 74h (PS2 MOUSE INTERRUPT) RUN THIS EVERY TIME IT OCCURS
;Inputs:
;Outputs: TestByte
;Changes:
;NOTES: This is only used during the TestMethod Routine
;------------------------------------------------------------------------------
Int74Hdr:
  DB TYPE IntHdr DUP (0) ;Header for removable interrupt vector code

Int74:
  PUSH AX             ;Save used registers
  STI                 ;Enable interrupts
  IN   AL,60h         ;Get the Mouse Data Byte
  MOV  CS:TestByte,AL ;Store it
  CALL IssueEOI2      ;Issue the End-of-Interrupt
  POP  AX             ;Restore used registers
  IRET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;Code to save/restore IRQ 8 (INT 70h)
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;ENABLE IRQ 8 (INT 70h)
;Inputs:  ES = TSR Data Area
;Outputs: OldA1
;Changes: Enables IRQ 8 (INT 70h), if not already enabled
;------------------------------------------------------------------------------
StartIRQ8:
  PUSH AX           ;Save used registers
  PUSHF             ;Save flags
  CLI               ;Disable Interrupts
  IN   AL,0A1h      ;Get the PIC2 IRQ Mask
  MOV  ES:OldA1,AL  ;Save it
  AND  AL,(NOT 01h) ;Enable IRQ8
  OUT  0A1h,AL      ;  (Int 70h)
  POPF              ;Restore flags
  POP  AX           ;Restore used registers
  RET

;------------------------------------------------------------------------------
;DISABLE IRQ 8 (INT 70h) IF WE ENABLED IT
;Inputs:  ES = TSR Data Area
;         OldA1
;Outputs:
;Changes: Restores IRQ 8 Enabled status to its original state
;------------------------------------------------------------------------------
StopIRQ8:
  PUSH AX           ;Save used registers
  PUSHF             ;Save flags
  CLI               ;Disable Interrupts
  TEST ES:OldA1,01h ;Was IRQ 8 Enabled when we started?
  JZ  >Q90          ;If so, we're done
  IN   AL,0A1h      ;Get the current PIC2 status
  OR   AL,01h       ;Disable IRQ8
  OUT  0A1h,AL      ;  (Int 70h)
Q90:                ;Done
  POPF              ;Restore flags
  POP  AX           ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO SET UP COMMAND TAIL WITH PROPER PARAMETERS FOR EXEC CALL
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;WRITE A SEGMENT:OFFSET COMBINATION TO A STRING VARIABLE
;Inputs:  AX   = Segment to Write
;         CX   = Offset to Write
;         [BX] = String to Write to
;Outputs: Converts AX:CX to a Hex String and stores at [BX]
;Changes:
;------------------------------------------------------------------------------
WriteSegmentOffset:
  PUSH AX,DI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at Local Data Area
  MOV  DI,BX          ;Point DI at String
  CALL Word2HexString ;Store the Segment
  MOV  AL,':'         ;Store the
  STOSB               ;  Colon
  MOV  AX,CX          ;Store
  CALL Word2HexString ;  the Offset
  POP  ES,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE A WORD AS HEX TO A STRING
;Inputs:   AX  = Word to Write
;         [BX] = String to write to
;Outputs:
;Changes: BX
;------------------------------------------------------------------------------
WriteWordToString:
  PUSH AX,DI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at Local Data Area
  MOV  DI,BX          ;Point DI at String
  CALL Word2HexString ;Store the Word
  MOV  AL,'h'         ;Write the
  STOSB               ;  'h'
  ADD  BX,5           ;Update Pointer
  POP  ES,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;WRITE A BYTE AS HEX TO A STRING
;Inputs:  AL   = Byte to Write
;         [BX] = String to write to
;Outputs:
;Changes: BX
;------------------------------------------------------------------------------
WriteByteToString:
  PUSH AX,DI,ES       ;Save used registers
  MOV  ES,DS          ;Point ES at Local Data Area
  MOV  DI,BX          ;Point DI at String
  CALL Byte2HexString ;Store the Byte
  MOV  AL,'h'         ;Write the
  STOSB               ;  'h'
  ADD  BX,3           ;Update Pointer
  POP  ES,DI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;COPY A STRING INTO THE COMMAND TAIL
;Inputs:  [BX] = ASCIIZ String to Copy
;Outputs: To CmdTail
;Changes:
;------------------------------------------------------------------------------
CopyToCmdTail:
  PUSH AX,CX,DX,DI,SI,ES ;Save used registers
  MOV  ES,DS             ;Point ES at Local Data Area
  MOV  CX,(CmdTailMax/2) ;Number of Words to Copy
  MOV  AX,CR+(CR SHL 8)  ;Fill with CR's
  MOV  DI,CmdTail        ;Point at Command Tail
  PUSH DI                ;Save Pointer
  REP  STOSW             ;Fill the Command Tail
  POP  DI                ;Restore Pointer
  MOV  DX,BX             ;Calculate number of bytes
  CALL CalcStrSizeDX     ;  to copy
  MOV  CmdTailSize,CL    ;Store the Command Tail Size
  MOV  SI,BX             ;Copy the string
  REP  MOVSB             ;  to the Command Tail
  POP  ES,SI,DI,DX,CX,AX ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO EXEC ANOTHER PROGRAM
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;SETUP PARAMETERS TO BE ABLE TO PERFORM THE EXEC FUNCTION
;Inputs:
;Outputs:
;Changes: Lots of stuff
;------------------------------------------------------------------------------
SetupExec:
  PUSH AX,SI                       ;Save used registers
  MOV  SI,ParamBlock               ;Point at Parameter Block
  MOV  AX,CS                       ;Use our data segment
  MOV  [SI].PBEnvirSegment,0       ;Use Copy of our Environment
  MOV  W [SI].PBCmdTailPtr[0],OFFSET CmdTailSize
  MOV  W [SI].PBCmdTailPtr[2],AX   ;Command Tail Pointer
  MOV  W [SI].PBFCB1Offset[0],FCB1
  MOV  W [SI].PBFCB1Offset[2],AX   ;FCB #1 Pointer
  MOV  W [SI].PBFCB2Offset[0],FCB2
  MOV  W [SI].PBFCB2Offset[2],AX   ;FCB #2 Pointer
  POP  SI,AX                       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;PERFORM THE EXEC FUNCTION
;Inputs:  ParamBlock set up with appropriate data
;         CmdTail/CmdTailSize set up with appropriate Info
;         PathToUse contains Child Program to Call
;         DS = Our data Area
;Outputs: CF = Clear if Program Returned ErrorLevel 0
;Changes: Performs EXEC Function
;------------------------------------------------------------------------------
DoExec:
  PUSH ES            ;Save used register
  MOV  ES,DS         ;Point ES at our Data Area
  PUSHA              ;Save all registers
  MOV  OldSPExec,SP  ;Save the Stack Pointer (may be destroyed by Call)
  MOV  DX,PathToUse  ;DS:[DX] = Child Program to Run
  MOV  BX,ParamBlock ;ES:BX = Parameter Block
  MOV  AX,4B00h      ;Function 4B00h (Load and Execute child program)
  INT  21h           ;Do it (May Destroy BX & DX)
  JC  >X70           ;If Error, quit
X00:                 ;When the Child Program Exits, here we are!
  CLD                ;Go forward with string functions
  CLI                ;Disable interrupts
  MOV  DS,CS         ;Restore
  MOV  ES,DS         ;  all
  MOV  SS,CS         ;  Segments
  MOV  SP,OldSPExec  ;Restore Stack Pointer
  STI                ;Enable interrupts
  MOV  AH,4Dh        ;Function 4Dh (Get ErrorLevel)
  INT  21h           ;Do it (Returns AH = Term Type, AL = ErrorLevel)
  OR   AL,AL         ;ErrorLevel 0?
  JZ  >X80           ;If so, it was OK
X70:                 ;Error
  STC                ;Set Error Flag
  JMP >X90           ;Done
X80:                 ;OK
  CLC                ;Set OK Flag
X90:                 ;Done
  POPA               ;Restore all registers
  POP  ES            ;Restore used registers
  RET

;------------------------------------------------------------------------------
;LOOK FOR THE SUPPORT FILE SOMEHWERE IN THE PATH
;Inputs:  DS = Local Data Area
;         SupportFile
;         ProgFlags.FoundSuptFile = False
;Outputs: ZF = Clear if Support File Found
;              ProgFlags.FoundSuptFile = True
;Outputs: ZF = Set if Support File Not Found
;              ProgFlags.FoundSuptFile = False
;Changes:
;------------------------------------------------------------------------------
TestSupportFile:
  PUSH DI,SI                ;Save used registers
  MOV  SI,OFFSET ProgFlags  ;Point [SI] at ProgFlags
  MOV  DI,SupportFile       ;Look for the
  CALL FindSupportFile      ;  Support File
  JC  >S90                  ;If not found, quit
  OR   B [SI],FoundSuptFile ;If found, mark it as found
S90:                        ;Done
  TEST B [SI],FoundSuptFile ;Set return flag
  POP  SI,DI                ;Restore used registers
  RET

;------------------------------------------------------------------------------
;FIND THE LOCATION OF THE SUPPORT FILE
;Inputs:  [DI] = FIleName to Look for
;Outputs: CF = Clear if OK (File Found)
;              PathToUse = Path & FileName Concatenated Together
;            = Set if Error (File not Found)
;              PathToUse = Undefined
;Changes: Various Path pointer Registers (OurPathPointer, PathVarPointer, etc.)
;------------------------------------------------------------------------------
FindSupportFile:
  PUSH AX,SI,DS,ES       ;Save used registers
F10:                     ;Do Our Path
  MOV  SI,OurPathPointer ;Point SI at our Path pointer
  CMP  SI,-1             ;Valid?
  JE  >F20               ;If not, skip it
  MOV  AX,[PSPEnvirSeg]  ;Point ES at
  MOV  ES,AX             ;  Environment Segment
  CALL CopyPathName      ;Copy it
  CALL FindFile          ;File exists?
  JNC >F80               ;If so, we're done
F20:                     ;Do Current Path
  MOV  ES,CS             ;Point ES at us
  MOV  SI,CurrentPath    ;If not, try the Current Path
  CALL CopyPathName      ;Copy it
  CALL FindFile          ;File exists?
  JNC >F80               ;If so, we're done
F30:                     ;Do Environment PATH
  MOV  SI,PathVarPointer ;SI = Environment PATH Pointer
  CMP  SI,-1             ;Is it valid?
  JE  >F70               ;If not, we're done
  MOV  AX,[PSPEnvirSeg]  ;Point ES at Path
  MOV  ES,AX             ;  (Environment) Segment
F35:                     ;Loop to here for each element in PATH
  CALL CopyPathName      ;Copy the PATH element
  JC  >F70               ;If End of Path, we're done
  CALL FindFile          ;File exists?
  JNC >F80               ;If so, we're done
F40:                     ;Loop to here to skip over PATH Element
  MOV  AL,ES:[SI]        ;Get next character
  INC  SI                ;Increment Pointer
  OR   AL,AL             ;End of PATH?
  JZ  >F70               ;If so, we're done
  CMP  AL,';'            ;End of PATH Element?
  JNE  F40               ;If not, Keep looking
  JMP  F35               ;Try the next Element
F70:                     ;Error
  STC                    ;Set Error Flag
  JMP >F90               ;Done
F80:                     ;Found our File
  CLC                    ;Set OK flag
F90:                     ;Done
  POP  ES,DS,SI,AX       ;Restore used registers
  RET

;------------------------------------------------------------------------------
;SEE IF THE SUPPORT FILE EXISTS
;Inputs:  PathToUse
;Outputs: CF = Clear if OK (File Found)
;            = Set if Error (File not Found)
;Changes:
;------------------------------------------------------------------------------
FindFile:
  PUSH AX,BX,CX,DX   ;Save used registers
  PUSH DS,ES         ;Save used registers
  MOV  AH,2Fh        ;Function 2Fh (Get DTA Address)
  INT  21h           ;Do it (returns ES:BX)
  MOV  AH,1Ah        ;Function 1Ah (Set DTA Address)
  MOV  DX,FCB1       ;DS:DX = New DTA Address
  INT  21h           ;Do it
  MOV  AX,4E00h      ;Function 4Eh (Find First Matching File)
  MOV  CX,FAttrToUse ;Search for any file, including Hidden
  MOV  DX,PathToUse  ;DS:DX = ASCIIZ FileName to search for
  INT  21h           ;Do it (returns CF)
  PUSHF              ;Save return flag
  MOV  DS,ES         ;Point DS:DX
  MOV  DX,BX         ;  at the old DTA
  MOV  AH,1Ah        ;Function 1Ah (Set DTA Address)
  INT  21h           ;Do it
  POPF               ;Restore return flag
  POP  ES,DS         ;Restore used registers
  POP  DX,CX,BX,AX   ;Restore used registers
  RET

;------------------------------------------------------------------------------
;CREATE A COMPLETE PATH NAME GIVEN A PATH AND A FILE NAME
;Inputs:  ES:[SI] = Path name to Copy
;         DS:[DI] = FileName to Append to the Path
;Outputs: PathToUse = Path & File Concatenated Together
;NOTES: The Path String can either end with a 0 (ASCIIZ String) or a SemiColon
;         (the way strings are stored in the PATH Environment variable).
;       This code will properly handle an empty Path String, which will
;         simply set PathToUse to the FileName.
;       The FileName must be an ASCIIZ String.
;------------------------------------------------------------------------------
CopyPathName:
  PUSH AX,BX,CX,DI,SI,DS,ES  ;Save used registers
  PUSH DS                    ;Point ES
  PUSH ES                    ;  at our data,
  POP  DS                    ;  DS at
  POP  ES                    ;  Path Data
  MOV  BX,DI                 ;Save File Name Pointer
  XOR  AX,AX                 ;Fill with Zeroes
  MOV  CX,(MaxPathSize/2)    ;Number of Words to Write
  MOV  DI,PathToUse          ;Where to write them
  PUSH DI                    ;Save Pointer
  REP  STOSW                 ;Reset the Buffer
  POP  DI                    ;Restore Pointer
  XOR  CL,CL                 ;Start String Length Counter at 0
N10:                         ;Loop to here for each character in Path
  LODSB                      ;Get next character
  OR   AL,AL                 ;End of Path?
  JZ  >N20                   ;If so, do Filename
  CMP  AL,';'                ;End of Path (Environment PATH)?
  JE  >N20                   ;If so, do FileName
  INC  CL                    ;Increment String Length Counter
  STOSB                      ;If not, store the character
  JMP  N10                   ;Keep going
N20:                         ;Done with Path
  OR   CL,CL                 ;Any characters at all in the Path?
  JZ  >N30                   ;If so, is Current Path, and don't want backslash!
  CMP  B ES:[DI-1],Backslash ;Path ends in a Backslash?
  JE  >N30                   ;If so, it's OK
  MOV  AL,Backslash          ;If not, store
  STOSB                      ;  a Backslash
N30:                         ;Path is OK
  MOV  DS,ES                 ;Point DS:[SI]
  MOV  SI,BX                 ;  at the File Name
N40:                         ;Loop to here for each character in File name
  LODSB                      ;Get next character
  OR   AL,AL                 ;End of File Name?
  JZ  >N90                   ;If so, we're done
  STOSB                      ;If not, store it
  JMP  N40                   ;Keepgoing
N90:                         ;Done
  POP  ES,DS,SI,DI,CX,BX,AX  ;Restore used registers
  RET

;------------------------------------------------------------------------------
;GET THE PATH THAT OUR PROGRAM IS IN
;Inputs:
;Outputs: OurPathPointer = Pointer to Our Path String (in Environment Segment)
;                        = -1 if we couldn't find our own Path
;Changes:
;------------------------------------------------------------------------------
GetOurPath:
  PUSH AX,CX,DI,SI,DS,ES    ;Save used registers
  MOV  AX,[PSPEnvirSeg]     ;Point DS & ES
  MOV  DS,AX                ;  at the
  MOV  ES,AX                ;  Environment segment
  XOR  DI,DI                ;Point at the beginning of Environment list
  MOV  CX,-1                ;Max out the count limiter
  XOR  AX,AX                ;Need to look for a double 0
N10:                        ;Loop to here to skip over Environment vars
  REPNE SCASB               ;Look for the end of this environment variable
  CMP  [DI],AL              ;Is it the end of the variable list (double 0)?
  JNE  N10                  ;If not, keep skipping over Environment vars
  CMP  [DI+1],AX            ;If so, is there a string after the Envir?
  JE  >N70                  ;If not, we're done
  ADD  DI,3                 ;Point DI and SI at the
  MOV  SI,DI                ;  True Name
  MOV  CS:OurPathPointer,DI ;Store the Pointer
N20:                        ;Loop to here to find the end of the PATH
  LODSB                     ;Get the next character of the name
  CMP  AL,':'               ;Is it a Colon?
  JE  >N30                  ;If so, possible end-of-path
  CMP  AL,Backslash         ;Is it backslash?
  JE  >N30                  ;If so, possible end-of-path
  OR   AL,AL                ;End of File Name?
  JZ  >N50                  ;If so, we're done
  JMP >N40                  ;If not, keep looking
N30:                        ;Update end-of-path Pointer
  MOV  DI,SI                ;Save possible end-of-path pointer
N40:                        ;Not end-of-path yet
  JMP  N20                  ;Keep looking
N50:                        ;At end of Path
  CMP  DI,CS:OurPathPointer ;Was there a real path?
  JE  >N70                  ;If not, error
  MOV  B [DI],0             ;If OK, put 0 at end of Path
  JMP >N90                  ;Done
N70:                        ;No Path
  MOV  CS:OurPathPointer,-1 ;Set Pointer
N90:                        ;Done
  POP  ES,DS,SI,DI,CX,AX    ;Restore used registers
  RET


;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;CODE TO AUTOMATICALLY MOVE THE PROGRAM INTO UPPER MEMORY
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

;------------------------------------------------------------------------------
;TRY TO ALLOCATE A UMB, AND COPY THE PROGRAM TO THE UMB SEGMENT
;Inputs:  DS = ES = Current Data Segment
;Outputs: ES = New Data Segment (in UMB or Low Memory Hole)
;              If no UMB or Low Memory Hole is available, ES remains unchanged
;         Entire TSR Portion of Program is copied to new Segment
;Changes:
;------------------------------------------------------------------------------
MoveProgramToUMB:
  PUSHA                       ;Save all registers
  MOV  AX,DS                  ;Save original Data Segment
  CMP  AX,0A000h              ;Already in Upper Memory?
  JAE >U90                    ;If so, just quit
  TEST ProgFlags,UseLowMemory ;Does user want us in Low Memory?
  JNZ >U90                    ;If so, just quit
  MOV  BX,(LastTSRByte SHR 4) ;Number of Paragraphs to Allocate
  CALL AllocateUMBDOS         ;Try to get a UMB from DOS (Returns ES)
  JZ  >U90                    ;If it didn't work, quit
  MOV  DX,ES                  ;Save new Segment
  PUSH DS                     ;Save data Segment
  MOV  AX,DS                  ;Point DS
  DEC  AX                     ;  at current
  MOV  DS,AX                  ;  MCB
  MOV  AX,ES                  ;Point ES
  DEC  AX                     ;  at new
  MOV  ES,AX                  ;  MCB
  MOV  SI,OFFSET MCBOwnerName ;Copy
  MOV  DI,SI                  ;  the
  MOV  CX,4                   ;  Owner
  REP  MOVSW                  ;  Name
  POP  DS                     ;Restore Data Segment
  MOV  ES:[MCBOwnerID],DX     ;Make itself the Owner
  MOV  AH,26h                 ;Function 26h (Create new PSP, DX = Segment)
  INT  21h                    ;Do it
  MOV  ES,DX                  ;Point ES at new Segment again
  ADD  DX,BX                  ;Calculate where next Segment will be
  MOV  ES:[PSPNextSegment],DX ;Store it in the PSP
  MOV  CL,3                   ;Calculate
  SHL  BX,CL                  ;  number
  MOV  CX,BX                  ;  of words
  SUB  CX,(PSPStrucSize/2)    ;  to Copy
  MOV  SI,PSPStrucSize        ;SI = Old Data
  MOV  DI,SI                  ;DI = New Data
  REP  MOVSW                  ;Copy Program to new Segment
  OR   ProgFlags,MemoryMoved  ;Mark memory as having been moved
U90:                          ;Done
  POPA                        ;Restore all registers
  RET

;------------------------------------------------------------------------------
;TRY TO ALLOCATE A UMB THROUGH DOS
;Inputs:  BX = Number of Paragraphs to Allocate
;Outputs: ZF = Clear if OK
;              ES = Segment of Allocated UMB
;            = Set if Error
;              ES = Unchanged
;Changes:
;NOTES: Allocating USB's through DOS normally requires a
;         DOS=HIGH,UMB line in CONFIG.SYS
;------------------------------------------------------------------------------
AllocateUMBDOS:
  PUSHA              ;Save all registers
  MOV  CX,ES         ;Save original ES
  MOV  DI,BX         ;Save Number of Paragraphs to Allocate
  MOV  AX,5800h      ;Function 5800h (Get Memory Allocation Strategy)
  INT  21h           ;Do it (returns AX)
  JC  >D90           ;If error, quit
  MOV  SI,AX         ;If OK, save it
  MOV  AX,5802h      ;Function 5802h (Get UMB Link State - DOS 5+ Only)
  INT  21h           ;Do it (Returns AL)
  JC  >D90           ;If error, quit
  MOV  DL,AL         ;If OK, Save it
  MOV  BX,UMBLinkYes ;Enable USB's in DOS Memory Chain
  MOV  AX,5803h      ;Function 5803h (Set UMB Link State)
  INT  21h           ;Do it
  JC  >D90           ;If error, quit
  MOV  BX,StrategyHighLowBest ;Strategy = Try High, then Low, Best Fit
  MOV  AX,5801h      ;Do
  INT  21h           ;  it
  JC  >D80           ;If error, quit
D20:                 ;Strategy is set
  MOV  BX,DI         ;BX = # of Paragraphs to Allocate
  MOV  AH,48h        ;Function 48h (Allocate Memory)
  INT  21h           ;Do it (returns AX = Segment)
  JC  >D80           ;If error, quit
  MOV  ES,AX         ;If OK, put Segment in ES
  CMP  AX,0A000h     ;Is it really a UMB?
  JAE >D80           ;If so, continue
  MOV  DI,CS         ;If not, is it a memory "hole" lower in memory
  CMP  AX,DI         ;  than we are (from a previously removed TSR)?
  JB  >D80           ;If so, continue
  MOV  AH,49h        ;Function 49h (Free Memory, Segment in ES)
  INT  21h           ;Do it
  MOV  ES,CX         ;Restore original ES
D80:                 ;Restore original UMB Link State & Strategy
  XOR  BH,BH         ;Restore
  MOV  BL,DL         ;  original
  MOV  AX,5803h      ;  USB
  INT  21h           ;  Link State
  MOV  BX,SI         ;Restore original
  MOV  AX,5801h      ;  Memory Allocation
  INT  21h           ;  Strategy
D90:                 ;Done
  MOV  AX,ES         ;Set
  CMP  CX,AX         ;  Return Flag
  POPA               ;Restore all registers
  RET



;  TestIt:
;;    MOV  BX,DeviceInfoTable
;    MOV  BX,TestDescr
;    CALL DecipherReportDescr
;    RET
;
;
;TestDescr:
;
;;UltraMarc U-HID Special Firmware for Compu-Weigh
;
;  DB 005h, 001h       ;Usage Page:     Generic Desktop
;  DB 009h, 002h       ;Usage:          Mouse
;  DB 0A1h, 001h       ;Collection:     Application
;  DB 009h, 001h       ; Usage:          Pointer
;  DB 0A1h, 000h       ; Collection:     Physical
;  DB 005h, 009h       ;  Usage Page:     Buttons
;  DB 019h, 001h       ;  Usage Min:      Button #1 (Primary)
;  DB 029h, 003h       ;  Usage Max:      Button #3 (Tertiary)
;  DB 015h, 000h       ;  Logical Min:    00h  0  0
;  DB 025h, 001h       ;  Logical Max:    01h  1  +1
;  DB 095h, 003h       ;  Report Count:   3
;  DB 075h, 001h       ;  Report Size:    1
;  DB 081h, 002h       ;  INPUT:          Data  Variable  Absolute
;                      ;                  No_Wrap  Linear  Preferred_State
;                      ;                  No_Null_State  Non_Volatile  Bit_Field
;  DB 095h, 001h       ;  Report Count:   1
;  DB 075h, 005h       ;  Report Size:    5
;  DB 081h, 001h       ;  INPUT:          Constant  Array  Absolute
;                      ;                  No_Wrap  Linear  Preferred_State
;                      ;                  No_Null_State  Non_Volatile  Bit_Field
;  DB 005h, 001h       ;  Usage Page:     Generic Desktop
;  DB 009h, 030h       ;  Usage:          X-axis
;  DB 009h, 031h       ;  Usage:          Y-axis
;  DB 009h, 032h       ;  Usage:          Z-axis
;  DB 016h, 000h, 080h ;  Logical Min:    8000h  32,768  -32,768
;  DB 026h, 0FFh, 07Fh ;  Logical Max:    7FFFh  32,767  +32,767
;  DB 075h, 010h       ;  Report Size:    16
;  DB 095h, 003h       ;  Report Count:   3
;  DB 081h, 006h       ;  INPUT:          Data  Variable  Relative
;                      ;                  No_Wrap  Linear  Preferred_State
;                      ;                  No_Null_State  Non_Volatile  Bit_Field
;  DB 0C0h             ;  End Collection
;  DB 0C0h             ; End Collection
;
;
;  DB 00h


;Logitech MX Revolution: TESTED OK
;  DB 005,001,009,002,0A1,001,009,001,0A1,000,005,009,019,001,029,010
;  DB 015,000,025,001,095,010,075,001,081,002,005,001,016,001,080,026
;  DB 0FF,07F,075,010,095,002,009,030,009,031,081,006,015,081,025,07F
;  DB 075,008,095,001,009,038,081,006,005,00C,00A,038,002,095,001,081
;  DB 006,0C0,0C0
;  DB 000,000,000

;Apple BlueTooth Mouse (Only 1 button, with Battery Status): TESTED OK
;  DB 005,001,009,002,0a1,001,009,001,0a1,000,005,009,085,002,019,001
;  DB 029,001,015,000,025,001,095,001,075,001,081,002,095,001,075,007
;  DB 081,001,005,001,009,030,009,031,015,081,025,07f,095,002,075,008
;  DB 081,006,005,006,009,020,015,000,025,064,075,008,095,001,085,047
;  DB 0b1,022,0c0,0c0
;  DB 000,000,000

;Apple Mighty Mouse: TESTED OK
;  DB 005,001,009,002,0a1,001,005,009,019,001,029,004,015,000,025,001
;  DB 095,004,075,001,081,002,095,001,075,004,081,001,005,001,009,001
;  DB 0a1,000,009,030,009,031,009,032,009,038,015,081,025,07f,075,008
;  DB 095,004,081,006,0c0,005,0ff,009,0c0,075,008,095,001,081,002,0c0
;  DB 000,000,000

;Windows Vista sample Report Descriptor for Horizontal Scroll Wheel:
;  DOES NOT WORK!!

;  DB 005,001,009,002,0a1,001,005,001,009,002,0a1,002,085,001,009,001
;  DB 0a1,000,005,009,019,001,029,005,095,005,075,001,025,001,081,002
;  DB 075,003,095,001,081,001,005,001,009,030,009,031,095,002,075,008
;  DB 015,081,025,07f,081,006,0a1,002,085,002,009,048,095,001,075,002
;  DB 015,000,025,001,035,001,045,004,0B1,002,085,001,009,038,035,000
;  DB 045,000,015,081,025,07f,075,008,081,006,0c0,0a1,002,085,002,009
;  DB 048,075,002,015,000,025,001,035,001,045,004,0B1,002,035,000,045
;  DB 000,075,004,0B1,001,085,001,005,00c,015,081,025,07f,075,008,00A
;  DB 038,002,082,006,000,0c0,0c0,0c0,0c0
;  DB 000,000,000

;  0x05, 0x01,       ;Usage Page Generic Desktop
;  0x09, 0x02,       ;Usage Mouse
;  0xa1, 0x01,       ;Collection Application
;  0x05, 0x01,       ; Usage Page Generic Desktop
;  0x09, 0x02,       ; Usage Mouse
;  0xa1, 0x02        ; Collection Logical
;  0x85, 0x01        ;  Report ID 1
;  0x09, 0x01,       ;  Usage Pointer
;  0xa1, 0x00,       ;   Collection Physical
;  0x05, 0x09,       ;   Usage Page Buttons
;  0x19, 0x01,       ;   Usage Minimum 1
;  0x29, 0x05,       ;   Usage Maximum 5
;  0x95, 0x05,       ;   Report Count 5
;  0x75, 0x01,       ;   Report Size 1
;  0x25, 0x01,       ;   Logical Maximum 1
;  0x81, 0x02,       ;   Input Data, Variable, Absolute
;
;  0x75, 0x03,       ;   Report Size 3
;  0x95, 0x01,       ;   Report Count 1
;  0x81, 0x01,       ;   Input Constant, Array, Absolute
;  0x05, 0x01,       ;   Usage Page Generic Desktop
;  0x09, 0x30,       ;   Usage X-Axis
;  0x09, 0x31,       ;   Usage Y-Axis
;  0x95, 0x02,       ;   Report Count 2
;  0x75, 0x08,       ;   Report Size 8
;  0x15, 0x81,       ;   Logical Minimum -127
;  0x25, 0x7f,       ;   Logical Maximum +127
;  0x81, 0x06,       ;   Input Data, Variable, Relative

;  0xa1, 0x02        ;   Collection Logical
;  0x85, 0x02        ;    Report ID 2
;  0x09, 0x48,       ;    Usage Resolution Multiplier
;  0x95, 0x01,       ;    Report Count 1
;  0x75, 0x02,       ;    Report Size 2  OS requires > 1
;  0x15, 0x00,       ;    Logical Minimum 0
;  0x25, 0x01,       ;    Logical Maximum 1
;  0x35, 0x01        ;    Physical Minimum 1  OS requires > 1
;  0x45, 0x04        ;    Physical Maximum 4
;  0xB1, 0x02        ;    Feature Variable

;  0x85, 0x01        ;    Report ID 1
;  0x09, 0x38,       ;    Usage Wheel
;  0x35, 0x00        ;    Physical Minimum 0
;  0x45, 0x00        ;    Physical Maximum 0
;  0x15, 0x81,       ;    Logical Minimum -127
;  0x25, 0x7f,       ;    Logical Maximum +127
;  0x75, 0x08,       ;    Report Size 8
;  0x81, 0x06,       ;    Input Data, Variable, Relative

;  0xc0,             ;    End Collection

;  0xa1, 0x02        ;   Collection Logical
;  0x85, 0x02        ;    Report ID 2
;  0x09, 0x48,       ;    Usage Resolution Multiplier
;  0x75, 0x02,       ;    Report Size 2  OS requires > 1
;  0x15, 0x00,       ;    Logical Minimum 0
;  0x25, 0x01,       ;    Logical Maximum 1
;  0x35, 0x01        ;    Physical Minimum 1
;  0x45, 0x04        ;    Physical Maximum 4
;  0xB1, 0x02        ;    Feature Variable
;  0x35, 0x00        ;    Physical Minimum 0
;  0x45, 0x00        ;    Physical Maximum 0
;  0x75, 0x04,       ;    Report Size 4
;  0xB1, 0x01        ;    Feature Constant

;  0x85, 0x01        ;    Report ID 1
;  0x05, 0x0c        ;    Usage Page Consumer Devices
;  0x15, 0x81,       ;    Logical Minimum -127
;  0x25, 0x7f,       ;    Logical Maximum +127
;  0x75, 0x08,       ;    Report Size 8
;  0x0A, 0x38, 0x02, ;    Usage AC Pan
;  0x82, 0x06, 0x00, ;    Input Data, Variable, Relative ;Horizontal Wheel

;  0xc0,             ;    End Collection

;  0xc0,             ;   End Collection

;  0xc0,             ;  End Collection

;  0xc0,             ; End Collection



;The P5 USB Glove is recognized as a mouse device under OS X, but not under
;OS 9. I've included the USB Prober report in case anyone can see anything in
;the report that looks suspect:
;
;
;96 (0x60): Composite device (Demo P5  USB)
;    Device Descriptor
;        Descriptor Version Number:. 0x0110
;        Device Class:.............. 0   (Composite)
;        Device Subclass:........... 0
;        Device Protocol:........... 0
;        Device MaxPacketSize:...... 8
;        Device VendorID/ProductID:. 0x0d7f/0x0100
;        Device Version Number:..... 0x0400
;        Number of Configurations:.. 0x0001
;        Manufacturer String:....... 3   (Essential Reality )
;        Product String:............ 1   (Demo P5  USB)
;        Serial Number String:...... 2   (P5)
;    Configuration Descriptor
;        Total Length of Descriptor: 59 (0x3b)
;        Number of Interfaces:...... 2
;        Configuration Value:....... 1
;        Attributes:................ 0x80 (Bus Powered)
;        Max Power:................. 500 ma
;        Interface #0 - HID
;            Alternate Setting:... 0
;            Number of Endpoints:. 1
;            Interface Class:..... 3   (HID)
;            Interface Subclass:.. 0
;            Interface Protocol:.. 0
;            HID Descriptor
;                Descriptor Version Number: 0x0110
;                Country Code:..... 33
;                Descriptor Count:. 1
;                Descriptor 1
;                    Type:... 0x22 (0x22 = Report Descriptor)
;                    Length:. 138 (0x8a)
;                    Parsed Report Descriptor
;                        Usage Page (140)
;                        Usage 1 (0x1)
;                        Collection (Application)
;                          ReportID........... (1)
;                          Usage 4 (0x4)
;                          Logical Minimum.... (-1)
;                          Logical Maximum.... (1)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (2)
;                          Usage 2 (0x2)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (3)
;                          Usage 3 (0x3)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (7)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (4)
;                          Usage 5 (0x5)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (5)
;                          Usage 6 (0x6)
;                          Logical Minimum.... (-1)
;                          Logical Maximum.... (1)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (6)
;                          Usage 7 (0x7)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (4)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (1)
;                          Usage 3 (0x3)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (23)
;                          Input (Data, Variable, Absolute, No Wrap, Linear,
;Preferred State, No Null Position, Bitfield)
;                          ReportID........... (3)
;                          Usage 4 (0x4)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Input (Data, Variable, Absolute, No Wrap, Linear,
;Preferred State, No Null Position, Bitfield)
;                          ReportID........... (8)
;                          Usage 8 (0x8)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (23)
;                          Input (Data, Variable, Absolute, No Wrap, Linear,
;Preferred State, No Null Position, Bitfield)
;                        End Collection
;                    Contents (hex)
;                        0000:05 8C 09 01 A1 01 85 01 09 04 15 FF 25 01 75 08
;                        0010:95 01 B1 82 85 02 09 02 15 00 26 FF 00 75 08 95
;                        0020:01 B1 82 85 03 09 03 15 00 26 FF 00 75 08 95 07
;                        0030:B1 82 85 04 09 05 15 00 26 FF 00 75 08 95 01 B1
;                        0040:82 85 05 09 06 15 FF 25 01 75 08 95 01 B1 82 85
;                        0050:06 09 07 15 00 26 FF 00 75 08 95 04 B1 82 85 01
;                        0060:09 03 15 00 26 FF 00 75 08 95 17 81 82 85 03 09
;                        0070:04 15 00 26 FF 00 75 08 81 82 85 08 09 08 15 00
;                        0080:26 FF 00 75 08 95 17 81 82 C0
;            Endpoint - Interrupt input
;                Endpoint Address:.... 0x81  (IN)
;                Attributes:.......... 0x03  (Interrupt)
;                Max Packet Size:..... 8
;                Polling Interval:.... 10 ms
;        Interface #1 - HID
;            Alternate Setting:... 0
;            Number of Endpoints:. 1
;            Interface Class:..... 3   (HID)
;            Interface Subclass:.. 1
;            Interface Protocol:.. 2   (Mouse - Boot Protocol)
;            HID Descriptor
;                Descriptor Version Number: 0x0110
;                Country Code:..... 33
;                Descriptor Count:. 1
;                Descriptor 1
;                    Type:... 0x22 (0x22 = Report Descriptor)
;                    Length:. 50 (0x32)
;                    Parsed Report Descriptor
;                        Usage Page (Generic Desktop)
;                        Usage (Mouse)
;                        Collection (Application)
;                          Usage (Pointer)
;                          Collection (Physical)
;                            Usage Page (Button)
;                            Usage Minimum...... (1)
;                            Usage Maximum...... (2)
;                            Logical Minimum.... (0)
;                            Logical Maximum.... (1)
;                            Report Count....... (2)
;                            Report Size........ (1)
;                            Input (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Bitfield)
;                            Report Count....... (1)
;                            Report Size........ (6)
;                            Input (Constant, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Bitfield)
;                            Usage Page (Generic Desktop)
;                            Usage (X)
;                            Usage (Y)
;                            Logical Minimum.... (-127)
;                            Logical Maximum.... (127)
;                            Report Size........ (8)
;                            Report Count....... (2)
;                            Input (Data, Variable, Relative, No Wrap,
;Linear, Preferred State, No Null Position, Bitfield)
;                          End Collection
;                        End Collection
;                    Contents (hex)
;                        0000:05 01 09 02 A1 01 09 01 A1 00 05 09 19 01 29 02
;                        0010:15 00 25 01 95 02 75 01 81 02 95 01 75 06 81 03
;                        0020:05 01 09 30 09 31 15 81 25 7F 75 08 95 02 81 06
;                        0030:C0 C0
;            Endpoint - Interrupt input
;                Endpoint Address:.... 0x82  (IN)
;                Attributes:.......... 0x03  (Interrupt)
;                Max Packet Size:..... 3
;                Polling Interval:.... 10 ms
;    Endpoint Status
;        Endpoint 0 (Control): Idle
;    Driver Information
;        Driver Descriptor Version:. 0x0000
;        Driver Name:............... USBCompositeDevice1.5.6
;        Driver Version:............ 1.5.6
;        Driver VendorID/ProductID:. 0x0000/0x0000
;        Driver Class:.............. 0   (Composite)
;        Driver SubClass:........... 0
;        Driver Protocol:........... 0
;        Driver Type:............... Generic
;        Driver Loading Options
;          Match Generic Device:.... yes (driver will support other Composite
;devices)
;          Allow interface match:... no
;          Protocol match required:. no


;;Windows Vista sample Report Descriptor for Horizontal Scroll Wheel
;
;USAGE PAGE (Generic Desktop)
;USAGE (Mouse)
;COLLECTION (Application)
;  USAGE PAGE (Generic Desktop)
;  USAGE (Mouse)
;  COLLECTION (Logical)
;    REPORT_ID (0x01)
;    USAGE (Pointer)
;    COLLECTION (Physical)
;      USAGE PAGE (Buttons)
;      USAGE MINIMUM (1)
;      USAGE MAXIMUM (5)
;      REPORT COUNT (5)
;      REPORT SIZE (1)
;      LOGICAL MAXIMUM (1)
;      INPUT (Var)
;      REPORT SIZE (3)
;      REPORT COUNT (1)
;      INPUT (Constant)
;      USAGE PAGE (Generic Desktop)
;      USAGE (X)
;      USAGE (Y)
;      REPORT COUNT (2)
;      REPORT SIZE (8)
;      LOGICAL MINIMUM (-127)
;      LOGICAL MAXIMUM (127)
;      INPUT (Var, Rel)
;      COLLECTION (Logical)
;        REPORT ID (0x02)
;        USAGE (Resolution Multiplier)
;        REPORT COUNT (1)
;        REPORT SIZE (2) 			;OS requires > 1
;        LOGICAL MINIMUM (0)
;        LOGICAL MAXIMUM (1)
;        PHYSICAL MINIMUM (1)		;OS requires > 1
;        PHYSICAL MAXIMUM (4)
;        FEATURE (Var)
;        REPORT ID (0x01)
;        USAGE (Wheel)
;        PHYSICAL MINIMUM (0) 		;Resets the global value
;        PHYSICAL MAXIMUM (0)
;        LOGICAL MINIMUM (-127)
;        LOGICAL MAXIMUM (127)
;        REPORT SIZE (8)
;        INPUT (Var, Rel)			;Vertical wheel scroll
;      END COLLECTION (Logical)
;      COLLECTION (Logical)
;        REPORT ID (0x02)
;        USAGE (Resolution Multiplier)
;        REPORT SIZE (2) 			;OS requires > 1
;        LOGICAL MINIMUM (0)
;        LOGICAL MAXIMUM (1)
;        PHYSICAL MINIMUM (1)
;        PHYSICAL MAXIMUM (4)
;        FEATURE (Var)
;        PHYSICAL MINIMUM (0) 		;Resets the global value
;        PHYSICAL MAXIMUM (0)
;        REPORT SIZE (4)
;        FEATURE (Constant)
;        REPORT ID (0x01)
;        USAGE PAGE (Consumer Devices)
;        LOGICAL MINIMUM (-127)
;        LOGICAL MAXIMUM (127)
;        REPORT SIZE (8)
;        USAGE (AC Pan)
;        INPUT (Var, Rel)			;Horizontal wheel scroll
;      END COLLECTION (Logical)
;    END COLLECTION (Linked)
;  END COLLECTION (Logical)
;END COLLECTION (Application)
;
;
;
;;The hid report descriptor received from the "Apple Mighty Mouse":
;;unsigned char hid_report_desc[] = {
;        0x5, 0x1,              /* Usage Page (Generic Desktop)             */
;        0x9, 0x2,              /* Usage (0x2)                              */
;        0xa1, 0x1,             /* Collection (Application)                 */
;        0x5, 0x9,              /*     Usage Page (Button)                  */
;        0x19, 0x1,             /*     Usage Minimum (0x1)                  */
;        0x29, 0x4,             /*     Usage Maximum (0x4)                  */
;        0x15, 0,               /*     Logical Minimum (0)                  */
;        0x25, 0x1,             /*     Logical Maximum (0x1)                */
;        0x95, 0x4,             /*     Report Count (0x4)                   */
;        0x75, 0x1,             /*     Report Size (0x1)                    */
;        0x81, 0x2,             /*     Input (Data, Variable, Absolute)     */
;        0x95, 0x1,             /*     Report Count (0x1)                   */
;        0x75, 0x4,             /*     Report Size (0x4)                    */
;        0x81, 0x1,             /*     Input (Constant, Array, Absolute)    */
;        0x5, 0x1,              /*     Usage Page (Generic Desktop)         */
;        0x9, 0x1,              /*     Usage (0x1)                          */
;        0xa1, 0,               /*     Collection (Physical)                */
;        0x9, 0x30,             /*         Usage (0x30)      X-Axis         */
;        0x9, 0x31,             /*         Usage (0x31)      Y-Axis         */
;        0x9, 0x32,             /*         Usage (0x32)      Z-Axis         */
;        0x9, 0x38,             /*         Usage (0x38)      Wheel          */
;        0x15, 0x81,            /*         Logical Minimum (0x81)           */
;        0x25, 0x7f,            /*         Logical Maximum (0x7f)           */
;        0x75, 0x8,             /*         Report Size (0x8)                */
;        0x95, 0x4,             /*         Report Count (0x4)               */
;        0x81, 0x6,             /*         Input (Data, Variable, Relative) */
;        0xc0,                  /*     End Collection                       */
;        0x5, 0xff,             /*     Usage Page ()                        */
;        0x9, 0xc0,             /*     Usage (0xc0)                         */
;        0x75, 0x8,             /*     Report Size (0x8)                    */
;        0x95, 0x1,             /*     Report Count (0x1)                   */
;        0x81, 0x2,             /*     Input (Data, Variable, Absolute)     */
;        0xc0,                  /* End Collection                           */
;
;
;
;
;The P5 USB Glove is recognized as a mouse device under OS X, but not under
;OS 9. I've included the USB Prober report in case anyone can see anything in
;the report that looks suspect:
;
;
;96 (0x60): Composite device (Demo P5  USB)
;    Device Descriptor
;        Descriptor Version Number:. 0x0110
;        Device Class:.............. 0   (Composite)
;        Device Subclass:........... 0
;        Device Protocol:........... 0
;        Device MaxPacketSize:...... 8
;        Device VendorID/ProductID:. 0x0d7f/0x0100
;        Device Version Number:..... 0x0400
;        Number of Configurations:.. 0x0001
;        Manufacturer String:....... 3   (Essential Reality )
;        Product String:............ 1   (Demo P5  USB)
;        Serial Number String:...... 2   (P5)
;    Configuration Descriptor
;        Total Length of Descriptor: 59 (0x3b)
;        Number of Interfaces:...... 2
;        Configuration Value:....... 1
;        Attributes:................ 0x80 (Bus Powered)
;        Max Power:................. 500 ma
;        Interface #0 - HID
;            Alternate Setting:... 0
;            Number of Endpoints:. 1
;            Interface Class:..... 3   (HID)
;            Interface Subclass:.. 0
;            Interface Protocol:.. 0
;            HID Descriptor
;                Descriptor Version Number: 0x0110
;                Country Code:..... 33
;                Descriptor Count:. 1
;                Descriptor 1
;                    Type:... 0x22 (0x22 = Report Descriptor)
;                    Length:. 138 (0x8a)
;                    Parsed Report Descriptor
;                        Usage Page (140)
;                        Usage 1 (0x1)
;                        Collection (Application)
;                          ReportID........... (1)
;                          Usage 4 (0x4)
;                          Logical Minimum.... (-1)
;                          Logical Maximum.... (1)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (2)
;                          Usage 2 (0x2)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (3)
;                          Usage 3 (0x3)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (7)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (4)
;                          Usage 5 (0x5)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (5)
;                          Usage 6 (0x6)
;                          Logical Minimum.... (-1)
;                          Logical Maximum.... (1)
;                          Report Size........ (8)
;                          Report Count....... (1)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (6)
;                          Usage 7 (0x7)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (4)
;                          Feature (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Volatile, Bitfield)
;                          ReportID........... (1)
;                          Usage 3 (0x3)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (23)
;                          Input (Data, Variable, Absolute, No Wrap, Linear,
;Preferred State, No Null Position, Bitfield)
;                          ReportID........... (3)
;                          Usage 4 (0x4)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Input (Data, Variable, Absolute, No Wrap, Linear,
;Preferred State, No Null Position, Bitfield)
;                          ReportID........... (8)
;                          Usage 8 (0x8)
;                          Logical Minimum.... (0)
;                          Logical Maximum.... (255)
;                          Report Size........ (8)
;                          Report Count....... (23)
;                          Input (Data, Variable, Absolute, No Wrap, Linear,
;Preferred State, No Null Position, Bitfield)
;                        End Collection
;                    Contents (hex)
;                        0000:05 8C 09 01 A1 01 85 01 09 04 15 FF 25 01 75 08
;                        0010:95 01 B1 82 85 02 09 02 15 00 26 FF 00 75 08 95
;                        0020:01 B1 82 85 03 09 03 15 00 26 FF 00 75 08 95 07
;                        0030:B1 82 85 04 09 05 15 00 26 FF 00 75 08 95 01 B1
;                        0040:82 85 05 09 06 15 FF 25 01 75 08 95 01 B1 82 85
;                        0050:06 09 07 15 00 26 FF 00 75 08 95 04 B1 82 85 01
;                        0060:09 03 15 00 26 FF 00 75 08 95 17 81 82 85 03 09
;                        0070:04 15 00 26 FF 00 75 08 81 82 85 08 09 08 15 00
;                        0080:26 FF 00 75 08 95 17 81 82 C0
;            Endpoint - Interrupt input
;                Endpoint Address:.... 0x81  (IN)
;                Attributes:.......... 0x03  (Interrupt)
;                Max Packet Size:..... 8
;                Polling Interval:.... 10 ms
;        Interface #1 - HID
;            Alternate Setting:... 0
;            Number of Endpoints:. 1
;            Interface Class:..... 3   (HID)
;            Interface Subclass:.. 1
;            Interface Protocol:.. 2   (Mouse - Boot Protocol)
;            HID Descriptor
;                Descriptor Version Number: 0x0110
;                Country Code:..... 33
;                Descriptor Count:. 1
;                Descriptor 1
;                    Type:... 0x22 (0x22 = Report Descriptor)
;                    Length:. 50 (0x32)
;                    Parsed Report Descriptor
;                        Usage Page (Generic Desktop)
;                        Usage (Mouse)
;                        Collection (Application)
;                          Usage (Pointer)
;                          Collection (Physical)
;                            Usage Page (Button)
;                            Usage Minimum...... (1)
;                            Usage Maximum...... (2)
;                            Logical Minimum.... (0)
;                            Logical Maximum.... (1)
;                            Report Count....... (2)
;                            Report Size........ (1)
;                            Input (Data, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Bitfield)
;                            Report Count....... (1)
;                            Report Size........ (6)
;                            Input (Constant, Variable, Absolute, No Wrap,
;Linear, Preferred State, No Null Position, Bitfield)
;                            Usage Page (Generic Desktop)
;                            Usage (X)
;                            Usage (Y)
;                            Logical Minimum.... (-127)
;                            Logical Maximum.... (127)
;                            Report Size........ (8)
;                            Report Count....... (2)
;                            Input (Data, Variable, Relative, No Wrap,
;Linear, Preferred State, No Null Position, Bitfield)
;                          End Collection
;                        End Collection
;                    Contents (hex)
;                        0000:05 01 09 02 A1 01 09 01 A1 00 05 09 19 01 29 02
;                        0010:15 00 25 01 95 02 75 01 81 02 95 01 75 06 81 03
;                        0020:05 01 09 30 09 31 15 81 25 7F 75 08 95 02 81 06
;                        0030:C0 C0
;            Endpoint - Interrupt input
;                Endpoint Address:.... 0x82  (IN)
;                Attributes:.......... 0x03  (Interrupt)
;                Max Packet Size:..... 3
;                Polling Interval:.... 10 ms
;    Endpoint Status
;        Endpoint 0 (Control): Idle
;    Driver Information
;        Driver Descriptor Version:. 0x0000
;        Driver Name:............... USBCompositeDevice1.5.6
;        Driver Version:............ 1.5.6
;        Driver VendorID/ProductID:. 0x0000/0x0000
;        Driver Class:.............. 0   (Composite)
;        Driver SubClass:........... 0
;        Driver Protocol:........... 0
;        Driver Type:............... Generic
;        Driver Loading Options
;          Match Generic Device:.... yes (driver will support other Composite
;devices)
;          Allow interface match:... no
;          Protocol match required:. no
;
;

;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
;SPACE AND CODE TO PROCESS OUR PROGRAM STACK
;  This code and must be physically at the END of the Program,
;    but must be one of the FIRST procedures called by the Program.
;  In addition, the Stack Pointer (SP) must be set properly
;    (to LastProgByte - 2) BEFORE this code is called.
;±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±

COMMENT 

  By default, DOS allocates all available conventional memory
    to a .COM program (like this one).
  We don't need all that memory, and in fact don't need more than the
    actual size of our program, plus some room for a stack.  DOS normally
    puts the stack at the very end of our 64k segment (DS = ES = SS,
    with SP = FFFEh).  If our program is relatively small (like this one
    is), there is a huge space between the end of our program and the
    stack that never gets used.  For most programs, this isn't a problem,
    since the memory is automatically deallocated when the program terminates.
  Howver, this program may end up needing to call a support program while it
    is running.  In preparation to do this, we will need to reallocate our
    memory requirements to free up some memory for the support program
    to use.
  Once we start using the default DOS stack, we will need to allocate to
    our program a full 64k of memory or the stack will get corrupted when
    we call the support program.  If we resize our memory before we
    start using the stack, however, we can reallocate to only the amount
    of memory we actually need.  This will leave more room for our
    support program to run in, and for any "sub-support" programs
    that it may use.

END COMMENT ;

;------------------------------------------------------------------------------
;RESIZE OUR MEMORY ALLOCATION TO THE MINIMUM SIZE NEEDED
;Inputs:  SP = LastProgByte - 2
;         ES = Local Segment
;Outputs: Rellocates Memory to the minimum size required to run the program
;Changes:
;------------------------------------------------------------------------------
ResizeMemory:
  PUSH AX,BX  ;Save used registers
  MOV  AH,4Ah ;Function 4Ah (Resize Memory Block pointed at by ES)
  MOV  BX,(LastProgByte SHR 4) ;BX = Number of Paragraphs to Resize to
  INT  21h    ;Do it
  POP  BX,AX  ;Restore used registers
  RET

  EVEN 16
  DB (ProgStackSize/8) DUP ('PrgStack')

LastProgByte:
